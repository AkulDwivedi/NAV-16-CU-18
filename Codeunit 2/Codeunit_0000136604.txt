OBJECT Codeunit 136604 ERM RS Package Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LibraryRapidStart@1002 : Codeunit 131903;
      LibraryUtility@1003 : Codeunit 131000;
      LibraryERM@1023 : Codeunit 131300;
      LibraryDim@1006 : Codeunit 131001;
      ConfigPackageMgt@1016 : Codeunit 8611;
      ConfigXMLExchange@1015 : Codeunit 8614;
      Assert@1004 : Codeunit 130000;
      DimSetLessThanExpectedError@1000 : TextConst 'ENU=Dimension Set ID should be more than %1 in Gen. Jnl Line 4. Current value is %2.';
      IncorrectDimSetError@1001 : TextConst 'ENU=Dimension Set ID is not correct.';
      LibraryRandom@1005 : Codeunit 130440;
      DimValueNotUpdatedErr@1007 : TextConst 'ENU=Dimension Set Entry was not updated on apply.';

    LOCAL PROCEDURE Cleanup@60(PackageCode@1001 : Code[20]);
    VAR
      ConfigPackage@1000 : Record 8623;
    BEGIN
      ConfigPackage.SETRANGE(Code,PackageCode);
      ConfigPackage.DELETEALL(TRUE);
    END;

    [Test]
    PROCEDURE ApplyJnlLineWithNewDimension@11();
    VAR
      ConfigPackage@1011 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      DimSetEntry@1003 : Record 480;
      GenJnlLine@1006 : Record 81;
      DimValue@1009 : Record 349;
      GenJnlTemplate@1005 : Record 80;
      GenJnlBatch@1004 : Record 232;
      LastDimSetID@1008 : Integer;
      LineNo@1002 : Integer;
    BEGIN
      // Test apply of data with dimensions
      HideDialog;
      CreatePackageAndFields(ConfigPackage,ConfigPackageTable);

      // 1. Setup: Create dimension setup
      LibraryDim.CreateDimensionValue(DimValue,LibraryERM.GetShortcutDimensionCode(3));
      CreateDimSetEntryPackageData(1,LibraryERM.GetShortcutDimensionCode(3),DimValue.Code,3,ConfigPackage.Code);

      // 2. Create Gen. Journal Lines
      LineNo := 10000;
      CreateGenJnlLines(ConfigPackage,ConfigPackageTable,LineNo,GenJnlTemplate,GenJnlBatch);

      // 3. Apply data
      LastDimSetID := 0;
      IF DimSetEntry.FINDLAST THEN
        LastDimSetID := DimSetEntry."Dimension Set ID";

      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);
      Cleanup(ConfigPackage.Code);

      // 4. Check encoded dimension set ID's
      GenJnlLine.GET(GenJnlTemplate.Name,GenJnlBatch.Name,LineNo);
      Assert.IsTrue(GenJnlLine."Dimension Set ID" >= LastDimSetID,
        STRSUBSTNO(DimSetLessThanExpectedError,LastDimSetID,GenJnlLine."Dimension Set ID"));

      // 5. Clean up test data
      LibraryERM.ClearGenJournalLines(GenJnlBatch);
      DimSetEntry.RESET;
      DimSetEntry.SETRANGE("Dimension Value Code",DimValue.Code);
      DimSetEntry.DELETEALL;
      DimValue.DELETE(TRUE);
      GenJnlBatch.DELETE;
    END;

    [Test]
    PROCEDURE ApplyJnlLineWithExistingDimension@28();
    VAR
      ConfigPackage@1003 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      DimSetEntry@1007 : Record 480;
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1005 : Record 232;
      GenJnlLine@1006 : Record 81;
      ExistingDimSetID@1009 : Integer;
      DimNumber@1010 : Integer;
      LineNo@1002 : Integer;
    BEGIN
      // Test apply of data with dimensions
      HideDialog;

      // 1. Setup: Create dimension setup
      CreatePackageAndFields(ConfigPackage,ConfigPackageTable);

      DimNumber := 0;
      DimSetEntry.NEXT(LibraryRandom.RandInt(DimSetEntry.COUNT));
      ExistingDimSetID := DimSetEntry."Dimension Set ID";
      LibraryDim.FindDimensionSetEntry(DimSetEntry,DimSetEntry."Dimension Set ID");
      REPEAT
        DimNumber += 1;
        CreateDimSetEntryPackageData(1,DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code",DimNumber,ConfigPackage.Code);
      UNTIL DimSetEntry.NEXT = 0;

      // 2. Create Gen. Journal Lines
      LineNo := 10000;
      CreateGenJnlLines(ConfigPackage,ConfigPackageTable,LineNo,GenJnlTemplate,GenJnlBatch);

      // 3. Apply data
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);

      // 4. Check encoded dimension set ID's
      GenJnlLine.GET(GenJnlTemplate.Name,GenJnlBatch.Name,LineNo);
      Assert.AreEqual(ExistingDimSetID,GenJnlLine."Dimension Set ID",IncorrectDimSetError);

      // 5. Clean up test data
      LibraryERM.ClearGenJournalLines(GenJnlBatch);
      GenJnlBatch.DELETE;
    END;

    [Test]
    PROCEDURE ApplyDimSetId_ApplyWithDifferentValueCode_DataApplied@7();
    VAR
      DimensionSetEntry@1000 : Record 480;
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      Dimension@1004 : Record 348;
      DimensionValue@1005 : Record 349;
      NewDimSetID@1003 : Integer;
    BEGIN
      DimensionSetEntry.FINDLAST;
      NewDimSetID := DimensionSetEntry."Dimension Set ID" + 1;
      LibraryDim.FindDimension(Dimension);

      CLEAR(DimensionSetEntry);
      DimensionSetEntry."Dimension Set ID" := NewDimSetID;
      DimensionSetEntry."Dimension Code" := Dimension.Code;
      DimensionSetEntry.INSERT;

      DimensionValue.SETRANGE("Dimension Code",Dimension.Code);
      DimensionValue.FINDFIRST;

      CreatePackageAndFields(ConfigPackage,ConfigPackageTable);
      CreateDimSetEntryPackageData(NewDimSetID,Dimension.Code,DimensionValue.Code,0,ConfigPackage.Code);

      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);

      DimensionSetEntry.GET(NewDimSetID,Dimension.Code);
      Assert.AreEqual(DimensionValue.Code,DimensionSetEntry."Dimension Value Code",DimValueNotUpdatedErr);

      DimensionSetEntry.DELETE;
    END;

    [Test]
    PROCEDURE ApplyPackageToJnlLineWithoutDimension@5();
    VAR
      ConfigPackage@1006 : Record 8623;
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1002 : Record 81;
      LineNo@1001 : Integer;
    BEGIN
      // Verify that there is no need to define "Dimension Set ID" field for package without dimensions

      HideDialog;
      LibraryRapidStart.CreatePackage(ConfigPackage);

      LineNo := 10000;
      CreateGenJnlLinePackageDataWithoutDimSetID(ConfigPackage,LineNo,GenJnlTemplate,GenJnlBatch);
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);
      ExportImportPackage(ConfigPackage.Code);
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);
      GenJnlLine.GET(GenJnlTemplate.Name,GenJnlBatch.Name,LineNo);
      Assert.AreEqual(0,GenJnlLine."Dimension Set ID",IncorrectDimSetError);

      LibraryERM.ClearGenJournalLines(GenJnlBatch);
      GenJnlBatch.DELETE;
    END;

    [Test]
    PROCEDURE ApplyBlankDimSetIDToJnlLineWithDefDimension@2();
    VAR
      ConfigPackage@1000 : Record 8623;
      ConfigPackageTable@1001 : Record 8613;
      GenJnlTemplate@1004 : Record 80;
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1006 : Record 81;
      LineNo@1002 : Integer;
      DimSetID@1005 : Integer;
    BEGIN
      // Verify that default dimensions are inherited from the G/L Account if "Dimension Set ID" from package is zero.

      HideDialog;
      CreatePackageAndFields(ConfigPackage,ConfigPackageTable);

      LineNo := 10000;
      CreateGenJnlLineWithAccNoAndDefDimensions(ConfigPackage,ConfigPackageTable,LineNo,GenJnlTemplate,GenJnlBatch,DimSetID);
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);
      ExportImportPackage(ConfigPackage.Code);
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);
      GenJnlLine.GET(GenJnlTemplate.Name,GenJnlBatch.Name,LineNo);
      Assert.AreEqual(DimSetID,GenJnlLine."Dimension Set ID",IncorrectDimSetError);

      LibraryERM.ClearGenJournalLines(GenJnlBatch);
      GenJnlBatch.DELETE;
    END;

    LOCAL PROCEDURE CreatePackageAndFields@1(VAR ConfigPackage@1002 : Record 8623;VAR ConfigPackageTable@1001 : Record 8613);
    BEGIN
      LibraryRapidStart.CreatePackage(ConfigPackage);
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Dimension Set Entry");
    END;

    LOCAL PROCEDURE CreateGenJnlLines@3(VAR ConfigPackage@1001 : Record 8623;VAR ConfigPackageTable@1000 : Record 8613;LineNo@1002 : Integer;VAR GenJnlTemplate@1009 : Record 80;VAR GenJnlBatch@1008 : Record 232);
    VAR
      GenJnlLine@1003 : Record 81;
    BEGIN
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Journal Line");

      CreateGenJnlBatchPackageData(GenJnlTemplate,GenJnlBatch);
      CreateGenJnlLinePackageData(
        GenJnlTemplate.Name,GenJnlBatch.Name,LineNo,WORKDATE,
        COPYSTR(
          LibraryUtility.GenerateRandomCode(GenJnlLine.FIELDNO("Document No."),DATABASE::"Gen. Journal Line"),
          1,LibraryUtility.GetFieldLength(DATABASE::"Gen. Journal Line",GenJnlLine.FIELDNO("Document No."))),
        0,1,1,ConfigPackage.Code);
    END;

    LOCAL PROCEDURE CreateGenJnlLinePackageDataWithoutDimSetID@14(VAR ConfigPackage@1005 : Record 8623;LineNo@1003 : Integer;VAR GenJnlTemplate@1002 : Record 80;VAR GenJnlBatch@1001 : Record 232);
    VAR
      GLAccount@1000 : Record 15;
      ConfigPackageTable@1004 : Record 8613;
      ConfigPackageField@1007 : Record 8616;
      GenJnlLine@1006 : Record 81;
    BEGIN
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Journal Line");
      ConfigPackageField.GET(ConfigPackageTable."Package Code",ConfigPackageTable."Table ID",DATABASE::"Dimension Set Entry");
      ConfigPackageField.VALIDATE("Include Field",FALSE);
      ConfigPackageField.MODIFY(TRUE);

      CreateGenJnlBatchPackageData(GenJnlTemplate,GenJnlBatch);
      LibraryERM.FindGLAccount(GLAccount);
      PrepareGenJnlLinePackageData(
        ConfigPackage.Code,1,GenJnlTemplate.Name,GenJnlBatch.Name,LineNo,WORKDATE,
        COPYSTR(
          LibraryUtility.GenerateRandomCode(GenJnlLine.FIELDNO("Document No."),DATABASE::"Gen. Journal Line"),
          1,LibraryUtility.GetFieldLength(DATABASE::"Gen. Journal Line",GenJnlLine.FIELDNO("Document No."))),
        GLAccount."No.",0);
    END;

    LOCAL PROCEDURE CreateGenJnlLineWithAccNoAndDefDimensions@4(VAR ConfigPackage@1004 : Record 8623;VAR ConfigPackageTable@1003 : Record 8613;LineNo@1002 : Integer;VAR GenJnlTemplate@1001 : Record 80;VAR GenJnlBatch@1000 : Record 232;VAR DimSetID@1005 : Integer);
    VAR
      GenJnlLine@1007 : Record 81;
      AccNo@1006 : Code[20];
    BEGIN
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Journal Line");

      CreateGLAccountWithDefaultDimensions(AccNo,DimSetID);

      CreateGenJnlBatchPackageData(GenJnlTemplate,GenJnlBatch);
      PrepareGenJnlLinePackageData(
        ConfigPackage.Code,1,GenJnlTemplate.Name,GenJnlBatch.Name,LineNo,WORKDATE,
        COPYSTR(
          LibraryUtility.GenerateRandomCode(GenJnlLine.FIELDNO("Document No."),DATABASE::"Gen. Journal Line"),
          1,LibraryUtility.GetFieldLength(DATABASE::"Gen. Journal Line",GenJnlLine.FIELDNO("Document No."))),
        AccNo,0);
    END;

    LOCAL PROCEDURE CreateGenJnlBatchPackageData@24(VAR GenJnlTemplate@1001 : Record 80;VAR GenJnlBatch@1000 : Record 232);
    BEGIN
      GenJnlTemplate.SETRANGE(Type,GenJnlTemplate.Type::General);
      GenJnlTemplate.FINDFIRST;
      LibraryERM.CreateGenJournalBatch(GenJnlBatch,GenJnlTemplate.Name);
    END;

    LOCAL PROCEDURE CreateGenJnlLinePackageData@31(GenJnlTemplate@1009 : Code[20];GenJnlBatch@1010 : Code[20];LineNo@1011 : Integer;PostingDate@1002 : Date;DocumentNo@1003 : Code[20];Amount@1004 : Decimal;DimSetID@1005 : Integer;RecordNo@1007 : Integer;PackageCode@1001 : Code[20]);
    VAR
      GLAccount@1000 : Record 15;
      GenJnlLine@1008 : Record 81;
    BEGIN
      LibraryERM.FindGLAccount(GLAccount);
      PrepareGenJnlLinePackageData(
        PackageCode,RecordNo,GenJnlTemplate,GenJnlBatch,LineNo,PostingDate,DocumentNo,GLAccount."No.",Amount);
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Dimension Set ID"),FORMAT(DimSetID));
    END;

    LOCAL PROCEDURE PrepareGenJnlLinePackageData@15(PackageCode@1008 : Code[20];RecordNo@1009 : Integer;GenJnlTemplate@1006 : Code[20];GenJnlBatch@1005 : Code[20];LineNo@1004 : Integer;PostingDate@1003 : Date;DocumentNo@1002 : Code[20];AccNo@1001 : Code[20];Amount@1000 : Decimal);
    VAR
      GenJnlLine@1007 : Record 81;
    BEGIN
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Journal Template Name"),FORMAT(GenJnlTemplate));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Journal Batch Name"),FORMAT(GenJnlBatch));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Line No."),FORMAT(LineNo));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Posting Date"),FORMAT(PostingDate));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Document No."),FORMAT(DocumentNo));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,
        GenJnlLine.FIELDNO("Account Type"),FORMAT(GenJnlLine."Account Type"::"G/L Account"));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO("Account No."),FORMAT(AccNo));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Gen. Journal Line",RecordNo,GenJnlLine.FIELDNO(Amount),FORMAT(Amount));
    END;

    LOCAL PROCEDURE CreateDimSetEntryPackageData@21(DimSetID@1001 : Integer;DimCode@1002 : Code[20];DimValueCode@1003 : Code[20];RecordNo@1004 : Integer;PackageCode@1005 : Code[20]);
    VAR
      DimSetEntry@1000 : Record 480;
    BEGIN
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Dimension Set Entry",RecordNo,DimSetEntry.FIELDNO("Dimension Set ID"),FORMAT(DimSetID));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Dimension Set Entry",RecordNo,DimSetEntry.FIELDNO("Dimension Code"),FORMAT(DimCode));
      LibraryRapidStart.CreatePackageData(
        PackageCode,DATABASE::"Dimension Set Entry",RecordNo,DimSetEntry.FIELDNO("Dimension Value Code"),FORMAT(DimValueCode));
    END;

    LOCAL PROCEDURE CreateGLAccountWithDefaultDimensions@10(VAR AccNo@1000 : Code[20];VAR DimSetID@1001 : Integer);
    VAR
      GLAccount@1002 : Record 15;
      DefaultDimension@1003 : Record 352;
      DimSetEntry@1004 : Record 480;
    BEGIN
      LibraryERM.CreateGLAccount(GLAccount);
      AccNo := GLAccount."No.";
      DimSetEntry.NEXT(LibraryRandom.RandInt(DimSetEntry.COUNT));
      DimSetID := DimSetEntry."Dimension Set ID";
      LibraryDim.FindDimensionSetEntry(DimSetEntry,DimSetEntry."Dimension Set ID");
      REPEAT
        LibraryDim.CreateDefaultDimension(
          DefaultDimension,DATABASE::"G/L Account",AccNo,DimSetEntry."Dimension Code",DimSetEntry."Dimension Value Code");
      UNTIL DimSetEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE ExportImportPackage@63(ConfigPackageCode@1002 : Code[20]);
    VAR
      ConfigPackage@1004 : Record 8623;
      ConfigPackageTable@1003 : Record 8613;
      ConfigXMLExchange@1001 : Codeunit 8614;
      PackageXML@1000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      ConfigPackage.GET(ConfigPackageCode);
      ConfigPackageTable.SETRANGE("Package Code",ConfigPackage.Code);

      PackageXML := PackageXML.XmlDocument;
      ConfigXMLExchange.SetExcelMode(TRUE);
      ConfigXMLExchange.SetExportFromWksht(TRUE);
      ConfigXMLExchange.ExportPackageXMLDocument(PackageXML,ConfigPackageTable,ConfigPackage,FALSE);
      ConfigXMLExchange.ImportPackageXMLDocument(PackageXML);
    END;

    LOCAL PROCEDURE HideDialog@56();
    BEGIN
      ConfigPackageMgt.SetHideDialog(TRUE);
      ConfigXMLExchange.SetCalledFromCode(TRUE);
      ConfigXMLExchange.SetHideDialog(TRUE);
    END;

    BEGIN
    {
      // [FEATURE] [Dimension] [Rapid Start]
      ------------------------------------------------------------------------------------
      Test Function Name                                                            TFS ID
      ------------------------------------------------------------------------------------
      ApplyPackageToJnlLineWithoutDimension                                         59046
      ApplyBlankDimSetIDToJnlLineWithDefDimension                                   59046
    }
    END.
  }
}

