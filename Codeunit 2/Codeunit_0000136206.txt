OBJECT Codeunit 136206 Marketing Profiling
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LibraryMarketing@1001 : Codeunit 131900;
      LibraryUtility@1004 : Codeunit 131000;
      LibrarySales@1017 : Codeunit 130509;
      LibraryPurchase@1014 : Codeunit 130512;
      LibraryInventory@1015 : Codeunit 132201;
      LibraryVariableStorage@1003 : Codeunit 131004;
      LibraryRandom@1008 : Codeunit 130440;
      Assert@1010 : Codeunit 130000;
      isInitialized@1000 : Boolean;
      DateFormulaCurrentDayTok@1005 : TextConst 'ENU=<CD>';
      IncorrectFieldValueErr@1012 : TextConst '@@@=%1 - field name;ENU=Field %1 contains incorrect value';
      ContactClassificationErr@1011 : TextConst 'ENU=Contact Classification was not updated';
      DateFormulaStartDayTok@1016 : TextConst 'ENU=<CY-1Y+1D>';
      IncorrectQuestionnaireLineCountErr@1013 : TextConst 'ENU=Incorrect count questionnaire lines.';

    [Test]
    PROCEDURE CreateQuestionnairesWithRating@3();
    VAR
      ProfileQuestionnaireLine@1001 : Record 5088;
    BEGIN
      // Covers document number TC0015 - refer to TFS ID 21734.
      // Test that creating profile questionnaires with the list of questions and answers with Contact Class as Rating,
      // On Running Update Classification Updates No. of Contact Assigned.

      ProfileQuestionnaires(ProfileQuestionnaireLine."Contact Class. Field"::Rating);
    END;

    [Test]
    PROCEDURE CreateQuestionnairesWithNoFilter@51();
    VAR
      ProfileQuestionnaireLine@1000 : Record 5088;
      ProfileQuestionnaireCode@1001 : Code[10];
    BEGIN
      // 1. Setup: Create Profile Questionnaires Header and Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      CreateQuestionnairesLines(ProfileQuestionnaireCode,ProfileQuestionnaireLine."Contact Class. Field"::Rating);

      // 2. Exercise: Run the Update Classification Report with no filter.
      UpdateClassification('');

      // 3. Verify: Check No. of Contact Assigned to the Questionnaire Lines.
      VerifyNoOfContacts(ProfileQuestionnaireCode);
    END;

    [Test]
    PROCEDURE QuestionnairesInteractionQty@12();
    VAR
      ProfileQuestionnaireLine@1001 : Record 5088;
    BEGIN
      // Covers document number TC0015 - refer to TFS ID 21734.
      // Test that creating profile questionnaires with the list of questions and answers with Contact Class as Interaction Quantity,
      // On Running Update Classification Updates No. of Contact Assigned.

      ProfileQuestionnaires(ProfileQuestionnaireLine."Contact Class. Field"::"Interaction Quantity")
    END;

    LOCAL PROCEDURE ProfileQuestionnaires@17(ContactClass@1006 : Option);
    VAR
      ProfileQuestionnaireCode@1001 : Code[10];
    BEGIN
      // 1. Setup: Create Profile Questionnaires Header and Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      CreateQuestionnairesLines(ProfileQuestionnaireCode,ContactClass);

      // 2. Exercise: Run the Update Classification Report to update the No of Contact.
      UpdateClassification(ProfileQuestionnaireCode);

      // 3. Verify: Check No. of Contact Assigned to the Questionnaire Lines.
      VerifyNoOfContacts(ProfileQuestionnaireCode);
    END;

    [Test]
    [HandlerFunctions(ModalFormHandlerCreateRating,ModalFormHandlerAnswerPoints)]
    PROCEDURE CreatingContactRatingAuto@15();
    VAR
      ProfileQuestionnaireLine@1003 : Record 5088;
      TempProfileQuestionnaireLine@1004 : TEMPORARY Record 5088;
      ProfileQuestionnaireCode@1001 : Code[10];
      WizardFromValue@1002 : Decimal;
      WizardToValue@1000 : Decimal;
    BEGIN
      // Covers document number TC0016 - refer to TFS ID 21734.
      // Test that it is possible to create the contact rating automatically and assigning answer points.

      // 1. Setup: Create Profile Questionnaires Header.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;

      // Set global variables for Form Handler.
      WizardFromValue := LibraryRandom.RandInt(10);  // Wizard From Value and Wizard To Value is not important for the test Case.
      WizardToValue := WizardFromValue + LibraryRandom.RandInt(10);
      LibraryVariableStorage.Enqueue(WizardFromValue);
      LibraryVariableStorage.Enqueue(WizardToValue);

      // 2. Exercise: Create Contact Rating automatically, assigning answer points.
      ProfileQuestionnaireLine.VALIDATE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      TempProfileQuestionnaireLine.CreateRatingFromProfQuestnLine(ProfileQuestionnaireLine);

      // 3. Verify: Check Answers are created with values of From Value and To Value. Check No. of Contact Assigned
      // to the Questionnaire Lines.
      VerifyAnswerRating(ProfileQuestionnaireCode,WizardFromValue,WizardToValue);
    END;

    [Test]
    [HandlerFunctions(ModalFormContactProfileAnswers)]
    PROCEDURE CreateContactProfileManually@19();
    VAR
      ProfileQuestionnaireCode@1003 : Code[10];
    BEGIN
      // Covers document number TC0017 - refer to TFS ID 21734.
      // Test that filling in a contact profile manually on a contact card updates the No. of Contact on Profile Questionnaires Line.

      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      QuestionnairesLinesWOClass(ProfileQuestionnaireCode);

      // 2. Exercise: Manually set the Answer on Contact Profile.
      ManuallySetAnswerOnContProfile(ProfileQuestionnaireCode);

      // 3. Verify: Check No. of Contact assigned to the Profile Questionnaire Line.
      VerifyNoOfContactsManualAssign(ProfileQuestionnaireCode);
    END;

    [Test]
    [HandlerFunctions(ModalFormContactProfileAnswers,ModalFormHandlerSegCriteria,ModalFrmHandleSavedSegCriteria)]
    PROCEDURE ContactToSegmentUsingProfile@65();
    VAR
      SegmentHeader@1003 : Record 5076;
      ProfileQuestionnaireCode@1004 : Code[10];
      SegmentNo@1005 : Code[20];
    BEGIN
      // Covers document number TC0018 - refer to TFS ID 21734.
      // Test that contacts can be added to the segments using the questionnaire profiles.

      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      // Manually set the Answer on Contact Profile.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      QuestionnairesLinesWOClass(ProfileQuestionnaireCode);
      ManuallySetAnswerOnContProfile(ProfileQuestionnaireCode);

      // 2. Exercise: Create Segment Header. Assign Contact to the Segment with Add Contact Report. Save Criteria, Create another
      // Segment and Reuse the Saved Criteria.
      LibraryMarketing.CreateSegmentHeader(SegmentHeader);
      SegmentNo := SegmentHeader."No.";
      AddContact(SegmentHeader,ProfileQuestionnaireCode);
      SegmentHeader.SaveCriteria;

      CLEAR(SegmentHeader);
      LibraryMarketing.CreateSegmentHeader(SegmentHeader);
      SegmentHeader.ReuseCriteria;

      // 3. Verify: Contacts added to the segments Header and Segment Line.
      VerifyCriteriaActionOnSegment(SegmentNo);
      VerifyContactsOnSegmentLine(SegmentNo,ProfileQuestionnaireCode);
      VerifyCriteriaActionOnSegment(SegmentHeader."No.");
      VerifyContactsOnSegmentLine(SegmentHeader."No.",ProfileQuestionnaireCode);
    END;

    [Test]
    PROCEDURE QuestionnairesCustomerSaleFreq@21();
    VAR
      ProfileQuestionnaireLine@1003 : Record 5088;
    BEGIN
      // Covers document number TC0019 - refer to TFS ID 21734.
      // Test that creating profile questionnaires with the list of questions and answers with Customer Class to
      // Sales Frequency (Invoices/Year) On Running Update Classification Updates No. of Contact Assigned.

      QuestionnairesCustomerClass(ProfileQuestionnaireLine."Customer Class. Field"::"Sales Frequency (Invoices/Year)");
    END;

    [Test]
    PROCEDURE QuestionnairesCustomerProfit@33();
    VAR
      ProfileQuestionnaireLine@1001 : Record 5088;
    BEGIN
      // Covers document number TC0019 - refer to TFS ID 21734.
      // Test that creating profile questionnaires with the list of questions and answers with Customer Class to
      // Profit (LCY) On Running Update Classification Updates No. of Contact Assigned.

      QuestionnairesCustomerClass(ProfileQuestionnaireLine."Customer Class. Field"::"Profit (LCY)");
    END;

    LOCAL PROCEDURE QuestionnairesCustomerClass@4(CustomerClass@1003 : Option);
    VAR
      ProfileQuestionnaireCode@1001 : Code[10];
    BEGIN
      // Covers document number TC0019 - refer to TFS ID 21734.
      // Test that creating profile questionnaires with the list of questions and answers with Customer Class to
      // Profit (LCY) On Running Update Classification Updates No. of Contact Assigned.

      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      QuestionnairesLinesCustClass(ProfileQuestionnaireCode,CustomerClass);

      // 2. Exercise: Run the Update Classification Report to update the No of Contact.
      UpdateClassification(ProfileQuestionnaireCode);

      // 3. Verify: Check No. of Contact Assigned to the Questionnaire.
      VerifyNoOfContacts(ProfileQuestionnaireCode);
    END;

    [Test]
    PROCEDURE QuestionnairesTestReport@22();
    VAR
      ProfileQuestionnaireHeader@1004 : Record 5087;
      ProfileQuestionnaireLine@1000 : Record 5088;
      QuestionnaireTest@1003 : Report 5067;
      ProfileQuestionnaireCode@1001 : Code[10];
      FilePath@1002 : Text[1024];
    BEGIN
      // Covers document number TC0020 - refer to TFS ID 21734.
      // Test that profile questionnaires Test Report generates correct output.

      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;

      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Question,0);
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0);

      // 2. Exercise: Save Test Report as XML and XLSX in local Temp folder.
      ProfileQuestionnaireHeader.SETRANGE(Code,ProfileQuestionnaireHeader.Code);
      QuestionnaireTest.SETTABLEVIEW(ProfileQuestionnaireHeader);
      FilePath := TEMPORARYPATH + ProfileQuestionnaireHeader.Code + '.xlsx';
      QuestionnaireTest.SAVEASEXCEL(FilePath);

      // 3. Verify: Verify that saved file has some data.
      LibraryUtility.CheckFileNotEmpty(FilePath);
    END;

    [Test]
    PROCEDURE HandoutsWithoutClassification@23();
    BEGIN
      // Covers document number TC0020 - refer to TFS ID 21734.
      // Test that profile questionnaires Handouts Report without print classification set generates correct output.

      QuestionnairesHandoutsReport(FALSE);
    END;

    [Test]
    PROCEDURE HandoutsWithClassification@25();
    BEGIN
      // Covers document number TC0020 - refer to TFS ID 21734.
      // Test that profile questionnaires Handouts Report with print classification set generates correct output.

      QuestionnairesHandoutsReport(TRUE);
    END;

    LOCAL PROCEDURE QuestionnairesHandoutsReport@30(PrintClassification@1004 : Boolean);
    VAR
      ProfileQuestionnaireHeader@1003 : Record 5087;
      ProfileQuestionnaireLine@1002 : Record 5088;
      QuestionnaireHandouts@1001 : Report 5066;
      ProfileQuestionnaireCode@1005 : Code[10];
      FilePath@1000 : Text[1024];
    BEGIN
      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Question,0);
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0);

      // 2. Exercise: Save Handouts Report as XML and XLSX in local Temp folder.
      ProfileQuestionnaireHeader.SETRANGE(Code,ProfileQuestionnaireHeader.Code);
      QuestionnaireHandouts.SETTABLEVIEW(ProfileQuestionnaireHeader);
      IF PrintClassification THEN
        QuestionnaireHandouts.InitializeRequest(TRUE);

      FilePath := TEMPORARYPATH + ProfileQuestionnaireHeader.Code + '.xlsx';
      QuestionnaireHandouts.SAVEASEXCEL(FilePath);

      // 3. Verify: Verify that saved file has some data.
      LibraryUtility.CheckFileNotEmpty(FilePath);
    END;

    [Test]
    [HandlerFunctions(SettingTwoAnswerUnCheckOneOnQuestionnaireHandler)]
    PROCEDURE UncheckTheSecondOneAfterTwoAnswersBeSelectedOnQuestionnaire@40();
    VAR
      ProfileQuestionnaireLine@1004 : Record 5088;
      Contact@1000 : Record 5050;
      ProfileManagement@1002 : Codeunit 5059;
      ProfileQuestionnaireCode@1001 : Code[10];
    BEGIN
      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      QuestionnairesLinesWOClassWithMultipleAnswerOptions(ProfileQuestionnaireCode);

      // Create contact and set the Type in the Contact Card.
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      LibraryMarketing.CreateContact(Contact);
      Contact.VALIDATE(Type,Contact.Type::Person);
      Contact.MODIFY(TRUE);

      // 2. Exercise: Check the 1st answer and check the 2nd answer, then uncheck the 2nd answer.
      ProfileManagement.ShowContactQuestionnaireCard(Contact,ProfileQuestionnaireCode,ProfileQuestionnaireLine."Line No.");
      // 3. Verify: No error pops up after uncheck the 2nd answer.
    END;

    [Test]
    [HandlerFunctions(CheckQuestionThenUndoOnQuestionnaireHandler)]
    PROCEDURE UncheckQuestionAfterQuestionBeSelectedOnQuestionnaire@35();
    VAR
      ProfileQuestionnaireLine@1004 : Record 5088;
      Contact@1000 : Record 5050;
      ProfileManagement@1002 : Codeunit 5059;
      ProfileQuestionnaireCode@1001 : Code[10];
    BEGIN
      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      QuestionnairesLinesWOClassWithMultipleAnswerOptions(ProfileQuestionnaireCode);

      // Create contact and set the Type in the Contact Card.
      FindFirstQuestionOnProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      LibraryMarketing.CreateContact(Contact);
      Contact.VALIDATE(Type,Contact.Type::Person);
      Contact.MODIFY(TRUE);

      // 2. Exercise: Check the question, then uncheck it.
      ProfileManagement.ShowContactQuestionnaireCard(Contact,ProfileQuestionnaireCode,ProfileQuestionnaireLine."Line No.");
      // 3. Verify: No error pops up after uncheck the question.
    END;

    [Test]
    [HandlerFunctions(ProfileQuestionnaireSetupPageHandler)]
    PROCEDURE ProfileQuestionnaireSetupPage@32();
    VAR
      ProfileQuestionnaireLine@1002 : Record 5088;
      ProfileQuestionnaireCode@1000 : Code[10];
      FromValue@1003 : Decimal;
      ToValue@1004 : Decimal;
    BEGIN
      // 1. Setup: Create Profile Questionnaires - Questionnaires Header and Questionnaires Line.
      Initialize;
      ProfileQuestionnaireCode := CreateQuestionnairesHeader;
      CreateQuestionnairesLinesWithValues(ProfileQuestionnaireCode,FromValue,ToValue);

      // 2. Exercise: Set values via Profile Questionnaire Setup
      COMMIT;
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      PAGE.RUNMODAL(PAGE::"Profile Questionnaire Setup",ProfileQuestionnaireLine);

      // 3. Verify: No error pops up after uncheck the question and stored values are correct
      VerifyQuestionariesLineFromValueAndToValue(ProfileQuestionnaireCode,FromValue,ToValue);
    END;

    [Test]
    PROCEDURE QuestionnairesVendorPurchaseLCY@145();
    VAR
      ProfileQuestionnaireHeader@1003 : Record 5087;
      ProfileQuestionnaireLine@1000 : Record 5088;
      ContactProfileAnswer@1004 : Record 5089;
      InvoiceAmount@1001 : Decimal;
      ProfileQuestionnaireCode@1002 : Code[10];
      VendorNo@1007 : Code[20];
      ContactNo@1005 : Code[20];
    BEGIN
      // [FEATURE] [Purchase]
      // [SCENARIO 375341] A Contact having posted Purchase Invoice with positive amount should be included in Answer with positive range of total value
      Initialize;
      InvoiceAmount := LibraryRandom.RandIntInRange(100,200);
      ProfileQuestionnaireCode := CreateQuestionnairesHeaderWithContactType(ProfileQuestionnaireHeader."Contact Type"::Companies);

      // [GIVEN] Question with 2 answers
      CreateQuestionnairesLineQuestion(
        ProfileQuestionnaireLine,ProfileQuestionnaireCode,ProfileQuestionnaireLine."Contact Class. Field"::" ");
      UpdateVendorClass(ProfileQuestionnaireLine,ProfileQuestionnaireLine."Vendor Class. Field"::"Purchase (LCY)");

      // [GIVEN] "Answer 1" with total value range = (-<unlimited>;-50]
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0,InvoiceAmount / 2);
      // [GIVEN] "Answer 2" with total value range = [50;<unlimited>)
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,InvoiceAmount / 2,0);

      // [GIVEN] Vendor "V" with linked Contact "C"
      CreateVendorWithContact(VendorNo,ContactNo);

      // [GIVEN] Purchase Invoice with Amount 100 for Vendor "V"
      PostPurchaseInvoice(VendorNo,InvoiceAmount);

      // [WHEN] Run the Update Classification Report to update answers
      UpdateClassification(ProfileQuestionnaireCode);

      // [THEN] Contact "C" is not included in "Answer 1"
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      ContactProfileAnswer.INIT;
      ContactProfileAnswer.SETRANGE("Contact No.",ContactNo);
      ContactProfileAnswer.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      ContactProfileAnswer.SETRANGE("Line No.",ProfileQuestionnaireLine."Line No.");
      Assert.RecordIsEmpty(ContactProfileAnswer);

      // [THEN] Contact "C" is included in "Answer 2"
      ProfileQuestionnaireLine.NEXT;
      ContactProfileAnswer.SETRANGE("Line No.",ProfileQuestionnaireLine."Line No.");
      Assert.RecordIsNotEmpty(ContactProfileAnswer);
    END;

    [Test]
    [HandlerFunctions(AnswerPointsModalPageHandler)]
    PROCEDURE UT_FinishWizardWithMultipleQuestionnareLinesAfterPerformLastWizStep@44();
    VAR
      ProfileQuestionnaireHeader@1001 : Record 5087;
      ProfileQuestionnaireLine@1000 : Record 5088;
    BEGIN
      // [FEATURE] [UT] [UI]
      // [SCENARIO 375354] When finish "Create Rating" wizard with multiple profile questionnaire lines all the lines are added to Questionnaire setup

      Initialize;
      // [GIVEN] Questionnaire with one heading line and two answers
      LibraryMarketing.CreateQuestionnaireHeader(ProfileQuestionnaireHeader);
      ProfileQuestionnaireLine."Profile Questionnaire Code" := ProfileQuestionnaireHeader.Code;
      ProfileQuestionnaireLine."Answer Option" := ProfileQuestionnaireLine."Answer Option"::HighLow;
      ProfileQuestionnaireLine.ValidateAnswerOption;

      // [GIVEN] Handled answers by the last Wizard step
      ProfileQuestionnaireLine."Wizard Step" := 3;
      ProfileQuestionnaireLine.PerformNextWizardStatus;

      // [WHEN] Finish "Create Rating" Wizard
      ProfileQuestionnaireLine.FinishWizard;

      // [THEN] The count of questionnaire lines added is 3 (heading + two answers)
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireLine."Profile Questionnaire Code");
      Assert.AreEqual(3,ProfileQuestionnaireLine.COUNT,IncorrectQuestionnaireLineCountErr);
    END;

    [Test]
    PROCEDURE QuestionnairesClassificationMethodPercentageOfValue@46();
    VAR
      ProfileQuestionnaireHeader@1003 : Record 5087;
      ProfileQuestionnaireLine@1000 : Record 5088;
      ContactProfileAnswer@1004 : Record 5089;
      InvoiceAmount@1001 : Decimal;
      ProfileQuestionnaireCode@1002 : Code[10];
      VendorNo@1007 : ARRAY [3] OF Code[20];
      ContactNo@1005 : ARRAY [3] OF Code[20];
      Index@1006 : Integer;
      OldWorkDate@1008 : Date;
    BEGIN
      // [FEATURE] [Purchase]
      // [SCENARIO 381660] Stan calls "Update Contacts Classification" report when question's classification method is ÊPercentage of ValueË
      Initialize;
      InvoiceAmount := LibraryRandom.RandIntInRange(10,20);
      OldWorkDate := WORKDATE;
      WORKDATE := CALCDATE('<2Y>',WORKDATE); // move to clean year.
      ProfileQuestionnaireCode := CreateQuestionnairesHeaderWithContactType(ProfileQuestionnaireHeader."Contact Type"::Companies);

      // [GIVEN] Question with "Classification Method" = "Percentage of Value"
      CreateQuestionnairesLineQuestion(
        ProfileQuestionnaireLine,ProfileQuestionnaireCode,ProfileQuestionnaireLine."Contact Class. Field"::" ");
      UpdateVendorClass(ProfileQuestionnaireLine,ProfileQuestionnaireLine."Vendor Class. Field"::"Purchase (LCY)");
      UpdateQuestionClassificationMethod(
        ProfileQuestionnaireLine,ProfileQuestionnaireLine."Classification Method"::"Percentage of Value");

      // [GIVEN] "Answer 1" with range = [0;20]
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(
        ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0,20);
      // [GIVEN] "Answer 2" with range = [21;40]
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(
        ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,21,40);
      // [GIVEN] "Answer 3" with range = [41;100]
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(
        ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,41,100);

      // [GIVEN] Posted invoice with Amount = 100 for contact "C1"
      // [GIVEN] Posted invoice with Amount = 200 for contact "C2"
      // [GIVEN] Posted invoice with Amount = 300 for contact "C3"
      FOR Index := 1 TO ARRAYLEN(VendorNo) DO BEGIN
        CreateVendorWithContact(VendorNo[Index],ContactNo[Index]);
        PostPurchaseInvoice(VendorNo[Index],InvoiceAmount * Index);
      END;

      // [WHEN] Run the Update Classification Report to update answers
      UpdateClassification(ProfileQuestionnaireCode);

      WORKDATE := OldWorkDate;

      // [THEN] Total amount = 600
      // [THEN] Contact "C1" is only included in "Answer 1" => 100 / 600 => 0% .. 16.67% .. 20%
      // [THEN] Contact "C2" is only included in "Answer 2" => 200 / 600 => 21% .. 33.33% .. 40%
      // [THEN] Contact "C3" is only included in "Answer 3" => 300 / 600 => 41% .. 50% .. 100%
      ContactProfileAnswer.INIT;
      ContactProfileAnswer.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      FOR Index := 1 TO ARRAYLEN(ContactNo) DO BEGIN
        ContactProfileAnswer.SETRANGE("Contact No.",ContactNo[Index]);
        ContactProfileAnswer.SETRANGE("Line No.",ProfileQuestionnaireLine."Line No.");
        Assert.RecordIsNotEmpty(ContactProfileAnswer);
        ContactProfileAnswer.SETFILTER("Line No.",'<>%1',ProfileQuestionnaireLine."Line No.");
        Assert.RecordIsEmpty(ContactProfileAnswer);
        ProfileQuestionnaireLine.NEXT;
      END;
    END;

    LOCAL PROCEDURE Initialize@20();
    VAR
      LibraryERMCountryData@1000 : Codeunit 131305;
    BEGIN
      IF isInitialized THEN
        EXIT;

      LibrarySales.SetCreditWarningsToNoWarnings;

      LibraryERMCountryData.CreateVATData;
      LibraryERMCountryData.UpdateGeneralPostingSetup;

      isInitialized := TRUE;
      COMMIT;
    END;

    LOCAL PROCEDURE AddContact@10(SegmentHeader@1000 : Record 5076;ProfileQuestionnaireCode@1003 : Code[10]);
    VAR
      ContactProfileAnswer@1002 : Record 5089;
      AddContacts@1001 : Report 5198;
    BEGIN
      SegmentHeader.SETRANGE("No.",SegmentHeader."No.");
      AddContacts.SETTABLEVIEW(SegmentHeader);
      ContactProfileAnswer.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      AddContacts.SETTABLEVIEW(ContactProfileAnswer);
      AddContacts.USEREQUESTPAGE(FALSE);
      AddContacts.RUNMODAL;
    END;

    LOCAL PROCEDURE InitializeFromValueAndToValue@63(VAR NoOfDecimals@1000 : Integer;VAR FromValue@1001 : Decimal;VAR ToValue@1002 : Decimal);
    BEGIN
      NoOfDecimals := LibraryRandom.RandInt(7);
      FromValue := LibraryRandom.RandDec(50,NoOfDecimals);
      ToValue := FromValue + LibraryRandom.RandDec(50,NoOfDecimals);
    END;

    LOCAL PROCEDURE CreateQuestionnairesHeader@18() : Code[10];
    VAR
      ProfileQuestionnaireHeader@1000 : Record 5087;
    BEGIN
      EXIT(CreateQuestionnairesHeaderWithContactType(ProfileQuestionnaireHeader."Contact Type"::People));
    END;

    LOCAL PROCEDURE CreateQuestionnairesHeaderWithContactType@49(ContactType@1001 : Option) : Code[10];
    VAR
      ProfileQuestionnaireHeader@1000 : Record 5087;
    BEGIN
      LibraryMarketing.CreateQuestionnaireHeader(ProfileQuestionnaireHeader);
      ProfileQuestionnaireHeader.VALIDATE(Priority,ProfileQuestionnaireHeader.Priority::Normal);
      ProfileQuestionnaireHeader.VALIDATE("Contact Type",ContactType);
      ProfileQuestionnaireHeader.MODIFY(TRUE);
      EXIT(ProfileQuestionnaireHeader.Code);
    END;

    LOCAL PROCEDURE CreateQuestionnairesLines@13(ProfileQuestionnaireCode@1000 : Code[10];ContactClass@1003 : Option);
    VAR
      ProfileQuestionnaireLine@1001 : Record 5088;
    BEGIN
      // Create Questionnaires Lines of Type Question and Answer.
      CreateQuestionnairesLineQuestion(ProfileQuestionnaireLine,ProfileQuestionnaireCode,ContactClass);

      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(
        ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,LibraryRandom.RandInt(10));
    END;

    LOCAL PROCEDURE CreateQuestionnairesLineQuestion@47(VAR ProfileQuestionnaireLine@1000 : Record 5088;ProfileQuestionnaireCode@1001 : Code[10];ContactClass@1002 : Option);
    BEGIN
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Question,0);
      UpdateQuestionnairesLineClass(ProfileQuestionnaireLine,TRUE,ContactClass);
    END;

    LOCAL PROCEDURE CreateVendorWithContact@55(VAR VendorNo@1000 : Code[20];VAR ContactNo@1001 : Code[20]);
    VAR
      Vendor@1004 : Record 23;
      ContactBusinessRelation@1002 : Record 5054;
      CreateContsFromVendors@1003 : Report 5194;
    BEGIN
      LibraryPurchase.CreateVendor(Vendor);
      Vendor.SETRECFILTER;
      CreateContsFromVendors.USEREQUESTPAGE(FALSE);
      CreateContsFromVendors.SETTABLEVIEW(Vendor);
      CreateContsFromVendors.RUN;

      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Vendor);
      ContactBusinessRelation.SETRANGE("No.",Vendor."No.");
      ContactBusinessRelation.FINDFIRST;
      ContactNo := ContactBusinessRelation."Contact No.";
      VendorNo := Vendor."No.";
    END;

    LOCAL PROCEDURE PerformNextOnWizard@37(VAR TempProfileQuestionnaireLine@1002 : TEMPORARY Record 5088;FromValue@1000 : Decimal;ToValue@1001 : Decimal);
    BEGIN
      TempProfileQuestionnaireLine.VALIDATE("Wizard From Value",FromValue);
      TempProfileQuestionnaireLine.VALIDATE("Wizard To Value",ToValue);
      NextStepToDoWizard(TempProfileQuestionnaireLine);
    END;

    LOCAL PROCEDURE PostPurchaseInvoice@48(VendorNo@1000 : Code[20];InvoiceAmount@1003 : Decimal);
    VAR
      PurchaseHeader@1001 : Record 38;
      PurchaseLine@1002 : Record 39;
    BEGIN
      LibraryPurchase.CreatePurchHeader(PurchaseHeader,PurchaseHeader."Document Type"::Invoice,VendorNo);
      LibraryPurchase.CreatePurchaseLine(PurchaseLine,PurchaseHeader,PurchaseLine.Type::Item,LibraryInventory.CreateItemNo,1);
      PurchaseLine.VALIDATE("Direct Unit Cost",InvoiceAmount);
      PurchaseLine.MODIFY(TRUE);
      LibraryPurchase.PostPurchaseDocument(PurchaseHeader,TRUE,TRUE);
    END;

    LOCAL PROCEDURE QuestionnairesLinesCustClass@52(ProfileQuestionnaireCode@1001 : Code[10];CustomerClass@1002 : Option);
    VAR
      ProfileQuestionnaireLine@1000 : Record 5088;
    BEGIN
      // Create Questionnaires Lines of Type Question and Answer. Update Customer Class as per parameter on Type Question.
      CreateQuestionnairesLineQuestion(
        ProfileQuestionnaireLine,ProfileQuestionnaireCode,ProfileQuestionnaireLine."Contact Class. Field"::" ");

      UpdateCustomerClass(ProfileQuestionnaireLine,CustomerClass);

      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(
        ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,LibraryRandom.RandIntInRange(100,200));
    END;

    LOCAL PROCEDURE QuestionnairesLinesWOClass@29(ProfileQuestionnaireCode@1000 : Code[10]);
    VAR
      ProfileQuestionnaireLine@1002 : Record 5088;
    BEGIN
      // Create Questionnaires Lines of Type Question and Answer.
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Question,0);
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0);
    END;

    LOCAL PROCEDURE QuestionnairesLinesWOClassWithMultipleAnswerOptions@14(ProfileQuestionnaireCode@1000 : Code[10]);
    VAR
      ProfileQuestionnaireLine@1002 : Record 5088;
    BEGIN
      // Create Questionnaires Lines of Type Question and two Answer options.
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Question,0);  // The question does not allow Multiple Answers.
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0);
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,0);
    END;

    LOCAL PROCEDURE CreateQuestionnairesLinesWithValues@41(ProfileQuestionnaireCode@1000 : Code[10];VAR FromValue@1001 : Decimal;VAR ToValue@1003 : Decimal);
    VAR
      ProfileQuestionnaireLine@1002 : Record 5088;
      NoOfDecimals@1004 : Integer;
    BEGIN
      // Create Questionnaires Lines of Type Question and two Answer options.
      InitializeFromValueAndToValue(NoOfDecimals,FromValue,ToValue);

      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Question,0);

      ProfileQuestionnaireLine.VALIDATE("No. of Decimals",NoOfDecimals);
      ProfileQuestionnaireLine.MODIFY(TRUE);

      LibraryVariableStorage.Enqueue(FromValue);
      LibraryVariableStorage.Enqueue(ToValue);

      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,FromValue,ToValue);
      LibraryMarketing.CreateProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      UpdateQuestionnairesLineWithFromValue(ProfileQuestionnaireLine,ProfileQuestionnaireLine.Type::Answer,FromValue,ToValue);
    END;

    LOCAL PROCEDURE FindProfileQuestionnaireLine@61(VAR ProfileQuestionnaireLine@1002 : Record 5088;ProfileQuestionnaireCode@1000 : Code[20]);
    BEGIN
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      ProfileQuestionnaireLine.SETRANGE(Type,ProfileQuestionnaireLine.Type::Answer);
      ProfileQuestionnaireLine.FINDFIRST;
    END;

    LOCAL PROCEDURE FindFirstQuestionOnProfileQuestionnaireLine@28(VAR ProfileQuestionnaireLine@1002 : Record 5088;ProfileQuestionnaireCode@1000 : Code[20]);
    BEGIN
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      ProfileQuestionnaireLine.SETRANGE(Type,ProfileQuestionnaireLine.Type::Question);
      ProfileQuestionnaireLine.FINDFIRST;
    END;

    LOCAL PROCEDURE FinishStepToDoWizard@6(VAR TempProfileQuestionnaireLine@1000 : TEMPORARY Record 5088);
    BEGIN
      TempProfileQuestionnaireLine.MODIFY;
      TempProfileQuestionnaireLine.CheckStatus;
      TempProfileQuestionnaireLine.FinishWizard;
    END;

    LOCAL PROCEDURE ManuallySetAnswerOnContProfile@38(ProfileQuestionnaireCode@1000 : Code[10]);
    VAR
      Contact@1003 : Record 5050;
      ProfileQuestionnaireLine@1001 : Record 5088;
      ContactProfileAnswers@1002 : Page 5114;
    BEGIN
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);

      Contact.SETRANGE(Type,Contact.Type::Person);
      Contact.SETFILTER("Company No.",'<>''''');
      Contact.FINDFIRST;

      ContactProfileAnswers.SETTABLEVIEW(ProfileQuestionnaireLine);
      ContactProfileAnswers.SetRunFromForm(ProfileQuestionnaireLine,Contact,ProfileQuestionnaireCode);
      ContactProfileAnswers.RUNMODAL;
    END;

    LOCAL PROCEDURE NextStepToDoWizard@2(VAR TempProfileQuestionnaireLine@1000 : TEMPORARY Record 5088);
    BEGIN
      TempProfileQuestionnaireLine.MODIFY;
      TempProfileQuestionnaireLine.CheckStatus;
      TempProfileQuestionnaireLine.PerformNextWizardStatus;
    END;

    LOCAL PROCEDURE UpdateClassification@53(ProfileQuestionnaireCode@1002 : Code[10]);
    VAR
      ProfileQuestionnaireHeader@1000 : Record 5087;
      UpdateContactClassification@1001 : Report 5199;
    BEGIN
      ProfileQuestionnaireHeader.SETRANGE(Code,ProfileQuestionnaireCode);
      UpdateContactClassification.SETTABLEVIEW(ProfileQuestionnaireHeader);
      UpdateContactClassification.USEREQUESTPAGE(FALSE);
      UpdateContactClassification.RUNMODAL;
    END;

    LOCAL PROCEDURE UpdateCustomerClass@31(ProfileQuestionnaireLine@1001 : Record 5088;CustomerClass@1000 : Option);
    BEGIN
      ProfileQuestionnaireLine.VALIDATE("Customer Class. Field",CustomerClass);
      EVALUATE(ProfileQuestionnaireLine."Starting Date Formula",DateFormulaStartDayTok);
      EVALUATE(ProfileQuestionnaireLine."Ending Date Formula",DateFormulaCurrentDayTok);
      ProfileQuestionnaireLine.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE UpdateVendorClass@45(VAR ProfileQuestionnaireLine@1001 : Record 5088;VendorClass@1000 : Option);
    BEGIN
      WITH ProfileQuestionnaireLine DO BEGIN
        VALIDATE("Vendor Class. Field",VendorClass);
        EVALUATE("Starting Date Formula",DateFormulaStartDayTok);
        EVALUATE("Ending Date Formula",DateFormulaCurrentDayTok);
        MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateQuestionClassificationMethod@54(VAR ProfileQuestionnaireLine@1001 : Record 5088;ClassificationMethod@1000 : Option);
    BEGIN
      WITH ProfileQuestionnaireLine DO BEGIN
        VALIDATE("Classification Method",ClassificationMethod);
        MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE UpdateQuestionnairesLineType@1(VAR ProfileQuestionnaireLine@1001 : Record 5088;Type@1000 : Option;ToValue@1002 : Decimal);
    BEGIN
      ProfileQuestionnaireLine.VALIDATE(
        Description,
        COPYSTR(
          LibraryUtility.GenerateRandomCode(ProfileQuestionnaireLine.FIELDNO(Description),DATABASE::"Profile Questionnaire Line"),
          1,LibraryUtility.GetFieldLength(DATABASE::"Profile Questionnaire Line",ProfileQuestionnaireLine.FIELDNO(Description))));

      ProfileQuestionnaireLine.VALIDATE(Type,Type);
      ProfileQuestionnaireLine.VALIDATE("To Value",ToValue);
      ProfileQuestionnaireLine.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE UpdateQuestionnairesLineClass@11(VAR ProfileQuestionnaireLine@1001 : Record 5088;AutoContactClassification@1002 : Boolean;ContactClassField@1003 : Option);
    BEGIN
      ProfileQuestionnaireLine.VALIDATE("Auto Contact Classification",AutoContactClassification);
      ProfileQuestionnaireLine.VALIDATE("Contact Class. Field",ContactClassField);
      EVALUATE(ProfileQuestionnaireLine."Starting Date Formula",DateFormulaStartDayTok);
      EVALUATE(ProfileQuestionnaireLine."Ending Date Formula",DateFormulaCurrentDayTok);
      ProfileQuestionnaireLine.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE UpdateQuestionnairesLineWithFromValue@42(ProfileQuestionnaireLine@1001 : Record 5088;Type@1000 : Option;FromValue@1003 : Decimal;ToValue@1002 : Decimal);
    BEGIN
      UpdateQuestionnairesLineType(ProfileQuestionnaireLine,Type,ToValue);
      WITH ProfileQuestionnaireLine DO BEGIN
        VALIDATE("From Value",FromValue);
        MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE VerifyAnswerRating@26(ProfileQuestionnaireCode@1000 : Code[10];FromValue@1002 : Decimal;ToValue@1003 : Decimal);
    VAR
      ProfileQuestionnaireLine@1001 : Record 5088;
    BEGIN
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      ProfileQuestionnaireLine.TESTFIELD("From Value",FromValue);
      ProfileQuestionnaireLine.TESTFIELD("To Value",ToValue);
    END;

    LOCAL PROCEDURE VerifyContactsOnSegmentLine@36(SegmentNo@1000 : Code[20];ProfileQuestionnaireCode@1003 : Code[10]);
    VAR
      SegmentLine@1002 : Record 5077;
      ContactProfileAnswer@1005 : Record 5089;
    BEGIN
      SegmentLine.SETRANGE("Segment No.",SegmentNo);
      SegmentLine.FINDFIRST;
      ContactProfileAnswer.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      ContactProfileAnswer.FINDFIRST;
      SegmentLine.TESTFIELD("Contact No.",ContactProfileAnswer."Contact No.");
    END;

    LOCAL PROCEDURE VerifyCriteriaActionOnSegment@78(SegmentNo@1000 : Code[20]);
    VAR
      SegmentHeader@1001 : Record 5076;
    BEGIN
      SegmentHeader.GET(SegmentNo);
      SegmentHeader.CALCFIELDS("No. of Criteria Actions");
      SegmentHeader.TESTFIELD("No. of Criteria Actions",1);  // Validates that Critria has been set on the Segment.
    END;

    LOCAL PROCEDURE VerifyNoOfContacts@5(ProfileQuestionnaireCode@1001 : Code[10]);
    VAR
      ProfileQuestionnaireLine@1002 : Record 5088;
      ContactProfileAnswer@1005 : Record 5089;
    BEGIN
      ContactProfileAnswer.SETRANGE("Profile Questionnaire Code",ProfileQuestionnaireCode);
      Assert.IsFalse(ContactProfileAnswer.ISEMPTY,ContactClassificationErr);
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      ProfileQuestionnaireLine.CALCFIELDS("No. of Contacts");
      ProfileQuestionnaireLine.TESTFIELD("No. of Contacts",ContactProfileAnswer.COUNT);
    END;

    LOCAL PROCEDURE VerifyNoOfContactsManualAssign@27(ProfileQuestionnaireCode@1001 : Code[10]);
    VAR
      ProfileQuestionnaireLine@1002 : Record 5088;
    BEGIN
      FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);
      ProfileQuestionnaireLine.CALCFIELDS("No. of Contacts");
      ProfileQuestionnaireLine.TESTFIELD("No. of Contacts",1);  // Contact Assigned to Profile Questionnaire line.
    END;

    LOCAL PROCEDURE VerifyQuestionariesLineFromValueAndToValue@50(ProfileQuestionnaireCode@1003 : Code[10];FromValue@1001 : Variant;ToValue@1002 : Variant);
    VAR
      ProfileQuestionnaireLine@1000 : Record 5088;
    BEGIN
      WITH ProfileQuestionnaireLine DO BEGIN
        FindProfileQuestionnaireLine(ProfileQuestionnaireLine,ProfileQuestionnaireCode);

        Assert.AreEqual(
          FromValue,
          "From Value",
          STRSUBSTNO(IncorrectFieldValueErr,FIELDCAPTION("From Value")));
        Assert.AreEqual(
          ToValue,
          "To Value",
          STRSUBSTNO(IncorrectFieldValueErr,FIELDCAPTION("To Value")));
      END;
    END;

    [ModalPageHandler]
    PROCEDURE ModalFormHandlerCreateRating@24(VAR CreateRating@1003 : Page 5189;VAR Response@1000 : Action);
    VAR
      TempProfileQuestionnaireLine@1002 : TEMPORARY Record 5088;
      WizardFromValue@1001 : Decimal;
      WizardToValue@1004 : Decimal;
    BEGIN
      TempProfileQuestionnaireLine.INIT;
      CreateRating.GETRECORD(TempProfileQuestionnaireLine);
      TempProfileQuestionnaireLine.INSERT;  // Use of Insert in case of Temporary Table.

      TempProfileQuestionnaireLine.VALIDATE(Description,TempProfileQuestionnaireLine."Profile Questionnaire Code");
      TempProfileQuestionnaireLine.VALIDATE("Min. % Questions Answered",LibraryRandom.RandInt(10));  // Value is not important for the test Case.
      NextStepToDoWizard(TempProfileQuestionnaireLine);

      TempProfileQuestionnaireLine.VALIDATE("Answer Option",TempProfileQuestionnaireLine."Answer Option"::ABC);
      TempProfileQuestionnaireLine.ValidateAnswerOption;
      NextStepToDoWizard(TempProfileQuestionnaireLine);

      WizardFromValue := LibraryVariableStorage.DequeueDecimal;
      WizardToValue := LibraryVariableStorage.DequeueDecimal;

      PerformNextOnWizard(TempProfileQuestionnaireLine,WizardFromValue,WizardToValue);
      PerformNextOnWizard(TempProfileQuestionnaireLine,WizardFromValue,WizardToValue);
      PerformNextOnWizard(TempProfileQuestionnaireLine,WizardFromValue,WizardToValue);

      FinishStepToDoWizard(TempProfileQuestionnaireLine);
    END;

    [ModalPageHandler]
    PROCEDURE ModalFormHandlerAnswerPoints@8(VAR AnswerPoints@1003 : Page 5172;VAR Response@1000 : Action);
    BEGIN
    END;

    [ModalPageHandler]
    PROCEDURE ModalFormHandlerSegCriteria@9(VAR SaveSegmentCriteria@1003 : Page 5142;VAR Response@1000 : Action);
    VAR
      SavedSegmentCriteria@1001 : Record 5098;
      SegmentCriteria@1002 : Code[10];
    BEGIN
      SegmentCriteria :=
        COPYSTR(
          LibraryUtility.GenerateRandomCode(SavedSegmentCriteria.FIELDNO(Code),DATABASE::"Saved Segment Criteria"),
          1,LibraryUtility.GetFieldLength(DATABASE::"Profile Questionnaire Line",SavedSegmentCriteria.FIELDNO(Code)));

      LibraryVariableStorage.Enqueue(SegmentCriteria);

      SaveSegmentCriteria.SetValues(ACTION::OK,SegmentCriteria,SegmentCriteria);
    END;

    [ModalPageHandler]
    PROCEDURE ModalFrmHandleSavedSegCriteria@16(VAR SavedSegmentCriteriaList@1003 : Page 5141;VAR Response@1000 : Action);
    VAR
      SavedSegmentCriteria@1002 : Record 5098;
    BEGIN
      SavedSegmentCriteria.SETRANGE(Code,LibraryVariableStorage.DequeueText);
      SavedSegmentCriteria.FINDFIRST;
      SavedSegmentCriteriaList.SETRECORD(SavedSegmentCriteria);
      Response := ACTION::LookupOK;
    END;

    [ModalPageHandler]
    PROCEDURE ModalFormContactProfileAnswers@43(VAR ContactProfileAnswers@1003 : Page 5114;VAR Response@1000 : Action);
    BEGIN
      ContactProfileAnswers.UpdateProfileAnswer;
    END;

    [ModalPageHandler]
    PROCEDURE SettingTwoAnswerUnCheckOneOnQuestionnaireHandler@34(VAR ContactProfAnswers@1000 : TestPage 5114);
    BEGIN
      ContactProfAnswers.Set.SETVALUE(TRUE);
      ContactProfAnswers.NEXT;
      ASSERTERROR ContactProfAnswers.Set.SETVALUE(TRUE);
      ContactProfAnswers.Set.SETVALUE(FALSE);
      ContactProfAnswers.OK.INVOKE; // No error pops up after uncheck the 2nd answer.
    END;

    [ModalPageHandler]
    PROCEDURE CheckQuestionThenUndoOnQuestionnaireHandler@39(VAR ContactProfAnswers@1000 : TestPage 5114);
    BEGIN
      ASSERTERROR ContactProfAnswers.Set.SETVALUE(TRUE);
      ContactProfAnswers.Set.SETVALUE(FALSE); // No error pops up after uncheck the question.
      ContactProfAnswers.OK.INVOKE;
    END;

    [ModalPageHandler]
    PROCEDURE ProfileQuestionnaireSetupPageHandler@7(VAR TestPage@1000 : TestPage 5110);
    VAR
      FromValue@1002 : Variant;
      ToValue@1003 : Variant;
    BEGIN
      LibraryVariableStorage.Dequeue(FromValue);
      LibraryVariableStorage.Dequeue(ToValue);

      TestPage.FIRST;
      TestPage.NEXT;
      TestPage."From Value".SETVALUE(FromValue);
      TestPage."To Value".SETVALUE(ToValue);

      TestPage.OK.INVOKE;
    END;

    [ModalPageHandler]
    PROCEDURE AnswerPointsModalPageHandler@76(VAR AnswerPoints@1000 : TestPage 5172);
    BEGIN
      AnswerPoints.OK.INVOKE;
    END;

    BEGIN
    {
      // [FEATURE] [Profile Questionnaire] [Marketing]
      Test for Profiling and Classification:
        1. Creating a profile questionnaire.
        2. Creating contact rating.
        3. Filling in a Contact Profile Manually on a Contact Card.
        4. Adding Contacts to Segments Using the Profile Information.
        5. Updating Profile Questionnaires by Using the Update Contact Classification Batch Job.
        6. Viewing the Questionnaire Test and the Questionnaire Handouts Reports.
        7. Setting value to "To Value" and "From Value" columns in Profile Questionnaire Setup page

      TFS_TS_ID = 21734
      Covers Test case:
        TC0015 TFS_TC_ID=11963 and 31766
        TC0016 TFS_TC_ID=11964 and 31767
        TC0017 TFS_TC_ID=11965 and 31768
        TC0018 TFS_TC_ID=5896
        TC0019 TFS_TC_ID=5897
        TC0020 TFS_TC_ID=5898

      Test uncheck something he/she selected on Questionnaire by error:
        1. Verify the second answer can be unchecked after 2 answers be selected for Contact Questionnaires which does not allow Multiple answers.
        2. Verify the question can be unchecked after the question be selected for Contact Questionnaires.

      TFS_TS_ID = 48706
      Covers Test case:
       --------------------------------------------------------------------------------------------------------------------------------------------
       Test Function Name                                                                                                                    TFS ID
       --------------------------------------------------------------------------------------------------------------------------------------------
      UncheckTheSecondOneAfterTwoAnswersBeSelectedOnQuestionnaire
      UncheckQuestionAfterQuestionBeSelectedOnQuestionnaire
      ProfileQuestionnaireSetupPage                                                                                                          346354
    }
    END.
  }
}

