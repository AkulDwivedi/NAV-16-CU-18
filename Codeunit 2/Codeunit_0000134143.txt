OBJECT Codeunit 134143 ERM Source Code Setup Reversal
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LibraryERM@1002 : Codeunit 131300;
      LibraryRandom@1001 : Codeunit 130440;

    [Test]
    [HandlerFunctions(ConfirmHandler,MessageHandler)]
    PROCEDURE SourceCodeReversal@17();
    VAR
      GenJournalBatch@1005 : Record 232;
      GenJournalLine@1003 : Record 81;
      GLRegister@1002 : Record 45;
      ReversalEntry@1001 : Record 179;
      SourceCodeSetup@1000 : Record 242;
      SourceCode@1006 : Record 230;
      OldSourceCode@1004 : Code[10];
    BEGIN
      // Create and post General Journal Line using Random Values and Reversal of General Ledger Entries to test Source Code.

      // Setup: Modify Source Code Setup.
      LibraryERM.CreateSourceCode(SourceCode);
      OldSourceCode := ModifySourceCodeSetup(SourceCodeSetup,SourceCode.Code);

      // Create and Post General Journal Line using Random Values.
      LibraryERM.SelectGenJnlBatch(GenJournalBatch);
      LibraryERM.ClearGenJournalLines(GenJournalBatch);
      CreateGenJournalLine(
        GenJournalLine,GenJournalBatch,LibraryERM.CreateGLAccountNo,LibraryRandom.RandDec(5,2));
      CreateGenJournalLine(
        GenJournalLine,GenJournalBatch,LibraryERM.CreateGLAccountNo,-LibraryRandom.RandDec(5,2));
      LibraryERM.PostGeneralJnlLine(GenJournalLine);

      // Exercise: Reversal of General Ledger Entries.
      GLRegister.FINDLAST;
      ReversalEntry.SetHideDialog(TRUE);
      ReversalEntry.ReverseRegister(GLRegister."No.");

      // Verify: Verify GL Register Source Code.
      VerifySourceCode(SourceCodeSetup.Reversal);

      // Tear Down: Roll back the Previous Source Code Setup.
      ModifySourceCodeSetup(SourceCodeSetup,OldSourceCode);
      DeleteSourceCode(SourceCode.Code);
    END;

    LOCAL PROCEDURE CreateGenJournalLine@9(VAR GenJournalLine@1000 : Record 81;GenJournalBatch@1001 : Record 232;AccountNo@1003 : Code[20];Amount@1002 : Decimal);
    BEGIN
      LibraryERM.CreateGeneralJnlLine(
        GenJournalLine,GenJournalBatch."Journal Template Name",GenJournalBatch.Name,
        GenJournalLine."Document Type"::" ",GenJournalLine."Account Type"::"G/L Account",AccountNo,Amount);
    END;

    LOCAL PROCEDURE DeleteSourceCode@1(SourceCodeNo@1001 : Code[10]);
    VAR
      SourceCode@1000 : Record 230;
    BEGIN
      SourceCode.SETFILTER(Code,SourceCodeNo);
      SourceCode.FINDFIRST;
      SourceCode.DELETE(TRUE);
    END;

    LOCAL PROCEDURE ModifySourceCodeSetup@2(VAR SourceCodeSetup@1000 : Record 242;SourceCode@1002 : Code[10]) OldSourceCode : Code[10];
    BEGIN
      SourceCodeSetup.GET;
      OldSourceCode := SourceCodeSetup.Reversal;
      SourceCodeSetup.VALIDATE(Reversal,SourceCode);
      SourceCodeSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE VerifySourceCode@30(Reversal@1003 : Code[10]);
    VAR
      GLRegister@1000 : Record 45;
      Assert@1002 : Codeunit 130000;
    BEGIN
      // Verify Source Code from Source Code Setup and GL Register must be Same.
      GLRegister.FINDLAST;
      Assert.AreEqual(Reversal,GLRegister."Source Code",'Source Code Must be equal');
    END;

    [ConfirmHandler]
    PROCEDURE ConfirmHandler@78(Question@1000 : Text[1024];VAR Reply@1001 : Boolean);
    BEGIN
      // Handler for confirmation messages, always send positive reply.
      Reply := TRUE;
    END;

    [MessageHandler]
    PROCEDURE MessageHandler@3(Message@1000 : Text[1024]);
    BEGIN
      // Message Handler.
    END;

    BEGIN
    {
      // [FEATURE] [Reverse] [G/L Register] [Source Code]
      Unsupported version tags:
      ES: Unable to Execute

      Test Source code setup after Reversal.
        1. Test Source code in GL Register and Source Code Setup after Reversal.

      Covers Test Cases:
        -------------------------------------------------
        Test Function Name   TFS ID
        -------------------------------------------------
        SourceCodeReversal   123608,123609,123610,123611
    }
    END.
  }
}

