OBJECT Codeunit 136401 Create G/L Acc. Journal lines
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1002 : Codeunit 130000;
      UnknownError@1003 : TextConst 'ENU=Unexpected Error.';
      NoBatchError@1005 : TextConst 'ENU=Gen. Journal Batch name is blank.';
      NoTemplateError@1004 : TextConst 'ENU=Gen. Journal Template name is blank.';

    [Test]
    [HandlerFunctions(HandleMessage)]
    PROCEDURE CreateGLAccJnlLine@1();
    VAR
      GenJnlLine@1004 : Record 81;
      StandardGenJnlLine@1005 : Record 751;
      GLAcc@1003 : Code[20];
      JnlTemplate@1002 : Code[10];
      JnlBatch@1001 : Code[10];
      StandJnlCode@1007 : Code[20];
    BEGIN
      PrepareParameter(GLAcc,JnlTemplate,JnlBatch,StandJnlCode);
      ClearGenJnlLine(GenJnlLine);
      RunCreateGLAccJnl(GenJnlLine,WORKDATE,JnlTemplate,JnlBatch,StandJnlCode,GLAcc);

      // Validate generated general journal line against standard journal line
      GenJnlLine.FINDFIRST;
      StandardGenJnlLine.SETRANGE("Standard Journal Code",StandJnlCode);
      StandardGenJnlLine.FINDSET;
      REPEAT
        GenJnlLine.TESTFIELD("Journal Template Name",JnlTemplate);
        GenJnlLine.TESTFIELD("Journal Batch Name",JnlBatch);
        GenJnlLine.TESTFIELD("Posting Date",WORKDATE);
        GenJnlLine.TESTFIELD("Document Type",GenJnlLine."Document Type"::Invoice);
        GenJnlLine.TESTFIELD("Account No.",GLAcc);
        GenJnlLine.TESTFIELD(Amount,StandardGenJnlLine.Amount);
        GenJnlLine.NEXT;
      UNTIL StandardGenJnlLine.NEXT = 0;
    END;

    [Test]
    PROCEDURE CreateGLAccJnlLineNoTemplate@9();
    VAR
      GenJnlLine@1004 : Record 81;
      GLAcc@1003 : Code[20];
      JnlTemplate@1002 : Code[10];
      JnlBatch@1001 : Code[10];
      StandJnlCode@1007 : Code[20];
    BEGIN
      PrepareParameter(GLAcc,JnlTemplate,JnlBatch,StandJnlCode);
      RunBatchAndHandleError(GenJnlLine,WORKDATE,'',JnlBatch,StandJnlCode,GLAcc,NoTemplateError);
    END;

    [Test]
    PROCEDURE CreateGLAccJnlLineNoBatch@10();
    VAR
      GenJnlLine@1004 : Record 81;
      GLAcc@1003 : Code[20];
      JnlTemplate@1002 : Code[10];
      JnlBatch@1001 : Code[10];
      StandJnlCode@1007 : Code[20];
    BEGIN
      PrepareParameter(GLAcc,JnlTemplate,JnlBatch,StandJnlCode);
      RunBatchAndHandleError(GenJnlLine,WORKDATE,JnlTemplate,'',StandJnlCode,GLAcc,NoBatchError);
    END;

    LOCAL PROCEDURE ClearGenJnlLine@7(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlLine.INIT;
      GenJnlLine.DELETEALL;
      COMMIT;
    END;

    LOCAL PROCEDURE PrepareParameter@12(VAR GLAcc@1004 : Code[20];VAR JnlTemplate@1003 : Code[10];VAR JnlBatch@1002 : Code[10];VAR StandJnlCode@1001 : Code[20]);
    VAR
      StandardGenJnl@1000 : Record 750;
    BEGIN
      GLAcc := FindGLAcc;
      StandardGenJnl.FINDFIRST;
      StandJnlCode := StandardGenJnl.Code;
      JnlTemplate := StandardGenJnl."Journal Template Name";
      JnlBatch := FindGenJnlBatch(JnlTemplate);
    END;

    LOCAL PROCEDURE RunBatchAndHandleError@5(VAR GenJnlLine@1005 : Record 81;PostingDate@1004 : Date;JnlTemplate@1003 : Text[10];JnlBatch@1002 : Code[10];StandardJnlCode@1001 : Code[20];GLAcc@1000 : Code[20];ExpectError@1006 : Text[250]);
    BEGIN
      ASSERTERROR RunCreateGLAccJnl(GenJnlLine,PostingDate,JnlTemplate,JnlBatch,StandardJnlCode,GLAcc);
      Assert.AreEqual(ExpectError,GETLASTERRORTEXT,UnknownError);
    END;

    LOCAL PROCEDURE FindGenJnlBatch@6(TemplateName@1000 : Code[10]) : Code[10];
    VAR
      GenJnlBatch@1001 : Record 232;
      LibraryERM@1002 : Codeunit 131300;
    BEGIN
      LibraryERM.FindGenJournalBatch(GenJnlBatch,TemplateName);
      EXIT(GenJnlBatch.Name);
    END;

    LOCAL PROCEDURE FindGLAcc@3() : Code[20];
    VAR
      GLAccount@1000 : Record 15;
      LibraryERM@1002 : Codeunit 131300;
    BEGIN
      LibraryERM.FindGLAccount(GLAccount);
      EXIT(GLAccount."No.");
    END;

    LOCAL PROCEDURE RunCreateGLAccJnl@2(VAR GenJnlLine@1008 : Record 81;PostingDate@1001 : Date;JournalTemplate@1002 : Text[10];BatchName@1003 : Code[10];StandardTemplate@1004 : Code[20];AccNo@1005 : Code[20]);
    VAR
      GLAccount@1007 : Record 15;
      CreateGLAccJnlLines@1000 : Report 8610;
    BEGIN
      CLEAR(CreateGLAccJnlLines);
      GLAccount.SETRANGE("No.",AccNo);
      CreateGLAccJnlLines.SETTABLEVIEW(GLAccount);
      // Invoice is used here since document type is not important.
      CreateGLAccJnlLines.InitializeRequest(
        GenJnlLine."Document Type"::Invoice,PostingDate,JournalTemplate,BatchName,StandardTemplate);
      CreateGLAccJnlLines.USEREQUESTPAGE(FALSE);
      CreateGLAccJnlLines.RUN;
    END;

    [MessageHandler]
    PROCEDURE HandleMessage@4(Message@1000 : Text[1024]);
    BEGIN
    END;

    BEGIN
    {
      // [FEATURE] [Create G/L Acc. Journal lines] [General Journal]
    }
    END.
  }
}

