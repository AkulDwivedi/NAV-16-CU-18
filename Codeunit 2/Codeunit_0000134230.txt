OBJECT Codeunit 134230 ERM - Dimensions 7.0 Test
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Permissions=TableData 480=rimd;
    Subtype=Test;
    OnRun=BEGIN
            isInitialized := FALSE;
          END;

  }
  CODE
  {
    VAR
      Dim1@1000 : TextConst 'ENU=DEPT';
      Dim2@1001 : TextConst 'ENU=AREA51';
      Dim3@1002 : TextConst 'ENU=PROJ';
      DimX@1003 : TextConst 'ENU=X';
      DimY@1004 : TextConst 'ENU=Y';
      LibraryDimension@1006 : Codeunit 131001;
      LibraryERM@1011 : Codeunit 131300;
      Assert@1008 : Codeunit 130000;
      LibraryRandom@1009 : Codeunit 130440;
      LibraryUtility@1010 : Codeunit 131000;
      isInitialized@1005 : Boolean;
      DimensionMismatchError@1012 : TextConst 'ENU=Value in %1 field in Item Journal Line does not match Dimension Set Entry.';
      WrongConsolidatedDimFilterErr@1013 : TextConst 'ENU=Wrong consolidated Dimension filter value';

    [Test]
    PROCEDURE CreateDimension@7();
    VAR
      Dimension@1001 : Record 348;
      DimensionCount@1000 : Integer;
    BEGIN
      // TestCreateDimension
      // Tests the creation of a dimension by using Library - Dimension functionality
      // Verifies that the new dimension exists and that the total amount of dimensions was increased

      // Setup
      DimensionCount := Dimension.COUNT;

      // Exercise
      LibraryDimension.CreateDimension(Dimension);

      // Verify
      Assert.IsTrue(Dimension.GET(Dimension.Code),'Expected dimension was not created');
      Assert.AreEqual(DimensionCount + 1,Dimension.COUNT,'Expected dimension count to increase');
    END;

    [Test]
    PROCEDURE CreateAndAssignDimValue@9();
    VAR
      Dimension@1000 : Record 348;
      DimensionValue@1001 : Record 349;
      DimensionValueCount@1002 : Integer;
    BEGIN
      // TestCreateAndAssignDimensionValue
      // Tests adding a new dimension value and assigning it to a dimension by using Library - Dimension functionality
      // Verifies that the new value exists, that the new value belongs to the given dimension code and that the total amount
      // of dimension values was increased

      // Setup
      LibraryDimension.CreateDimension(Dimension);
      DimensionValueCount := DimensionValue.COUNT;

      // Exercise
      LibraryDimension.CreateDimensionValue(DimensionValue,Dimension.Code);

      // Verify
      Assert.IsTrue(DimensionValue.GET(Dimension.Code,DimensionValue.Code),'Expected dimension value was not found');
      Assert.AreEqual(DimensionValue."Dimension Code",Dimension.Code,'Expected dimension value was not assigned correctly');
      Assert.AreEqual(DimensionValueCount + 1,DimensionValue.COUNT,'Expected dimension value count to increase');
    END;

    [Test]
    PROCEDURE DeleteDimensionValue@42();
    VAR
      Dimension@1000 : Record 348;
      DimensionValue@1001 : Record 349;
    BEGIN
      // TestDeleteDimensionValue
      // Tests the deletion of unused dimension values.

      // Setup
      LibraryDimension.CreateDimension(Dimension);
      LibraryDimension.CreateDimensionValue(DimensionValue,Dimension.Code);

      // Exercise & Verify
      Assert.IsTrue(DimensionValue.DELETE(TRUE),'Dimension value was not deleted');
      DimensionValue.SETRANGE("Dimension Code",Dimension.Code);
      Assert.AreEqual(0,DimensionValue.COUNT,'Expected dimension value count must be zero');
    END;

    [Test]
    PROCEDURE DeleteDimension@43();
    VAR
      Dimension@1000 : Record 348;
    BEGIN
      // TestDeleteDimension
      // Tests the deletion of an unused dimension w/o dimension values

      // Setup
      LibraryDimension.CreateDimension(Dimension);

      // Exercise & Verify
      Assert.IsTrue(Dimension.DELETE(TRUE),'Dimension was not deleted');
    END;

    [Test]
    PROCEDURE DeleteUsedDimension@45();
    VAR
      Dimension@1002 : Record 348;
      DefaultDimension@1000 : Record 352;
      DimensionValue@1005 : Record 349;
      GenJournalLine@1008 : Record 81;
      GLAccNo@1001 : Code[20];
    BEGIN
      // TestDeleteUsedDimension
      // Tests that it is not allowed to delete dimensions and dimension values which are still
      // in use. Verification expects an apropriate exception.

      // Setup
      LibraryDimension.CreateDimension(Dimension);
      LibraryDimension.CreateDimensionValue(DimensionValue,Dimension.Code);
      GLAccNo := LibraryERM.CreateGLAccountNoWithDirectPosting;

      // Create a default dimension for GL Account
      LibraryDimension.CreateDefaultDimensionGLAcc(DefaultDimension,GLAccNo,
        Dimension.Code,DimensionValue.Code);

      // Create a general journal line using new default dimension
      CreateGenJournalLine(GenJournalLine,
        GenJournalLine."Document Type"::Invoice,
        GenJournalLine."Account Type"::"G/L Account",
        GLAccNo);

      // Exercise & Verify
      ASSERTERROR DimensionValue.DELETE(TRUE);
      ASSERTERROR Dimension.DELETE(TRUE);
    END;

    [Test]
    PROCEDURE Test_InsertDimVal@5();
    VAR
      DimVal@1003 : Record 349;
      DimSetEntry@1002 : Record 480;
      TempDimSetEntry@1000 : TEMPORARY Record 480;
      DimSetTreeNode@1001 : Record 481;
      Assert@1005 : Codeunit 130000;
      NewDimID@1004 : Integer;
      NewDimID2@1006 : Integer;
    BEGIN
      InitDimSetup;
      DimVal.GET(Dim1,'B');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry.INSERT(TRUE);

      NewDimID := DimSetEntry.GetDimensionSetID(TempDimSetEntry);
      Assert.AreNotEqual(NewDimID,0,'1 : No Dimension Set Entry was inserted');

      DimSetEntry.SETRANGE("Dimension Set ID",NewDimID);
      Assert.AreEqual(DimSetEntry.COUNT,1,'2 : Not exactly one Dimension Set Entry was inserted');

      DimSetEntry.FINDFIRST; // only one exists
      Assert.AreEqual(DimSetEntry."Dimension Value ID",DimVal."Dimension Value ID",'3 : Dimension Value ID is wrong');

      DimSetTreeNode.SETRANGE("Dimension Set ID",NewDimID);
      DimSetTreeNode.FINDFIRST; // only one exists
      Assert.AreEqual(DimSetTreeNode."Parent Dimension Set ID",0,'4 : Parent Dimension ID should be zero');
      Assert.AreEqual(DimSetTreeNode."Dimension Value ID",DimVal."Dimension Value ID",'5 : Dimension Value ID in Tree Node is wrong');
      Assert.AreEqual(DimSetTreeNode."Dimension Set ID",DimSetEntry."Dimension Set ID",'6 : Dimension Set ID in Tree Node is wrong');
      Assert.IsTrue(DimSetTreeNode."In Use",'7 : Tree Node must be in use');

      DimVal.GET(Dim3,'C');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
      TempDimSetEntry.INSERT;

      DimSetEntry.SETRANGE("Dimension Set ID");

      TempDimSetEntry.DELETEALL;
      NewDimID := DimSetEntry.GetDimensionSetID(TempDimSetEntry);
      Assert.AreEqual(NewDimID,0,'11 : The empty set should return 0');

      DimVal.GET(Dim2,'A');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
      TempDimSetEntry.INSERT;

      NewDimID := DimSetEntry.GetDimensionSetID(TempDimSetEntry);
      Assert.AreNotEqual(NewDimID,0,'12 : No Dimension Set Entry was inserted');
      Assert.AreNotEqual(NewDimID,NewDimID2,'13 : The Dimension Set ID is wrong.');
      DimSetEntry.SETRANGE("Dimension Set ID",NewDimID);
      Assert.AreEqual(DimSetEntry.COUNT,1,'14 : Not exactly one Dimension Set Entry was inserted');

      TempDimSetEntry.DELETEALL;
      DimVal.GET(DimX,'A');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
      TempDimSetEntry.INSERT;
      DimVal.GET(DimY,'C');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
      TempDimSetEntry.INSERT;
      NewDimID := DimSetEntry.GetDimensionSetID(TempDimSetEntry);
      TempDimSetEntry.DELETEALL;
      DimVal.GET(DimX,'A');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
      TempDimSetEntry.INSERT;
      NewDimID2 := DimSetEntry.GetDimensionSetID(TempDimSetEntry);
      Assert.IsTrue(NewDimID2 < NewDimID,'15 : New Dim ID should be less than previous')
    END;

    [Test]
    PROCEDURE Test_EditDimVal@4();
    VAR
      DimVal@1003 : Record 349;
      TempDimSetEntry@1000 : TEMPORARY Record 480;
      Assert@1001 : Codeunit 130000;
    BEGIN
      InitDimSetup;
      DimVal.GET(Dim1,'A');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry.INSERT(TRUE); // should trigger assignment of "Dimension Value ID"
      Assert.AreEqual(DimVal."Dimension Value ID",TempDimSetEntry."Dimension Value ID",'1 : Dimension Value ID is wrong.');
      DimVal.GET(Dim1,'B');
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry.MODIFY(TRUE); // should trigger new assignment of "Dimension Value ID"
      Assert.AreEqual(DimVal."Dimension Value ID",TempDimSetEntry."Dimension Value ID",'2 : Dimension Value ID is wrong.');

      DimVal.GET(Dim2,'A');
      TempDimSetEntry.INIT;
      TempDimSetEntry."Dimension Code" := '';
      TempDimSetEntry.VALIDATE("Dimension Code",DimVal."Dimension Code");
      Assert.AreEqual('',TempDimSetEntry."Dimension Value Code",'3: Dimension Value Code must be blank');
      Assert.AreEqual(0,TempDimSetEntry."Dimension Value ID",'4 : Dimension Value ID must be zero.');
      TempDimSetEntry.VALIDATE("Dimension Value Code",DimVal.Code);
      Assert.AreEqual(DimVal."Dimension Value ID",TempDimSetEntry."Dimension Value ID",'5 : Dimension Value ID is wrong.');
      DimVal.GET(Dim2,'X');
      ASSERTERROR TempDimSetEntry.VALIDATE("Dimension Value Code",DimVal.Code);

      DimVal.GET(DimX,'X');
      TempDimSetEntry.INIT;
      TempDimSetEntry."Dimension Code" := '';
      ASSERTERROR TempDimSetEntry.VALIDATE("Dimension Code",DimVal."Dimension Code");
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      ASSERTERROR TempDimSetEntry.VALIDATE("Dimension Value Code",DimVal.Code);
    END;

    [Test]
    PROCEDURE Test_COD408_Get_Set_DimSetID@8();
    VAR
      DimVal@1002 : Record 349;
      TempDimSetEntry@1001 : TEMPORARY Record 480;
      Assert@1000 : Codeunit 130000;
      DimMgt@1003 : Codeunit 408;
      DimSetID@1004 : Integer;
    BEGIN
      InitDimSetup;
      DimVal.GET(Dim1,'B');
      TempDimSetEntry."Dimension Code" := DimVal."Dimension Code";
      TempDimSetEntry."Dimension Value Code" := DimVal.Code;
      TempDimSetEntry.INSERT(TRUE);
      DimSetID := DimMgt.GetDimensionSetID(TempDimSetEntry);
      Assert.AreNotEqual(
        DimSetID,
        0,'1 : No Dimension Set Entry was inserted');

      TempDimSetEntry.DELETEALL;
      DimMgt.GetDimensionSet(TempDimSetEntry,DimSetID);
      Assert.AreEqual(1,TempDimSetEntry.COUNT,'2: Only one record was expected.');
      TempDimSetEntry.FINDFIRST;
      Assert.AreEqual(DimVal."Dimension Code",TempDimSetEntry."Dimension Code",'3: Wrong Dimension Code.');
      Assert.AreEqual(DimVal.Code,TempDimSetEntry."Dimension Value Code",'4: Wrong Dimension Value Code.');
    END;

    [Test]
    PROCEDURE ValidateDimensionsSorting@1();
    VAR
      TempDimSelectionBuf@1001 : TEMPORARY Record 368;
      ObjectType@1004 : Integer;
      ObjectID@1003 : Integer;
      AnalysisViewCode@1002 : Code[10];
      SelectedDimText@1006 : Text[250];
      DimTextFieldName@1005 : Text[100];
    BEGIN
      ObjectType := 0;
      ObjectID := 0;
      AnalysisViewCode := '';
      SelectedDimText := '';
      DimTextFieldName := '';

      CreateDimensionSelectionBuffer(TempDimSelectionBuf,TempDimSelectionBuf.Level::" ",'VERKŽUFER');
      CreateDimensionSelectionBuffer(TempDimSelectionBuf,TempDimSelectionBuf.Level::" ",'VERKAUFSKAMPAGNE');

      TempDimSelectionBuf.SetDimSelection(ObjectType,ObjectID,AnalysisViewCode,SelectedDimText,TempDimSelectionBuf);
      TempDimSelectionBuf.CompareDimText(ObjectType,ObjectID,AnalysisViewCode,SelectedDimText,DimTextFieldName);
    END;

    [Test]
    PROCEDURE ValidateDimensionsSortingLevel@15();
    VAR
      TempDimSelectionBuf@1001 : TEMPORARY Record 368;
      ObjectType@1004 : Integer;
      ObjectID@1003 : Integer;
      AnalysisViewCode@1002 : Code[10];
      SelectedDimText@1006 : Text[250];
      DimTextFieldName@1005 : Text[100];
    BEGIN
      ObjectType := 0;
      ObjectID := 0;
      AnalysisViewCode := '';
      SelectedDimText := '';
      DimTextFieldName := '';

      CreateDimensionSelectionBuffer(TempDimSelectionBuf,LibraryRandom.RandIntInRange(1,5) - 1,LibraryUtility.GenerateGUID);
      CreateDimensionSelectionBuffer(TempDimSelectionBuf,LibraryRandom.RandIntInRange(1,5) - 1,LibraryUtility.GenerateGUID);

      TempDimSelectionBuf.SetDimSelection(ObjectType,ObjectID,AnalysisViewCode,SelectedDimText,TempDimSelectionBuf);
      TempDimSelectionBuf.CompareDimText(ObjectType,ObjectID,AnalysisViewCode,SelectedDimText,DimTextFieldName);
    END;

    [Test]
    PROCEDURE TestCopyDimItemJournal@10();
    VAR
      Global1DimVal@1001 : Record 349;
      Global2DimVal@1003 : Record 349;
      DimSetEntry@1000 : Record 480;
      GeneralLedgerSetupOld@1005 : Record 98;
      GeneralLedgerSetup@1002 : Record 98;
      ItemJournalLine@1004 : Record 83;
    BEGIN
      InsertDim(Dim1,FALSE);
      InsertDim(Dim2,FALSE);
      Global1DimVal.SETRANGE("Dimension Code",Dim1);
      Global1DimVal.FINDFIRST;
      Global2DimVal.SETRANGE("Dimension Code",Dim2);
      Global2DimVal.FINDFIRST;
      GeneralLedgerSetup.GET;
      GeneralLedgerSetupOld := GeneralLedgerSetup;
      GeneralLedgerSetup."Global Dimension 1 Code" := Global1DimVal."Dimension Code";
      GeneralLedgerSetup."Global Dimension 2 Code" := Global2DimVal."Dimension Code";
      GeneralLedgerSetup.MODIFY;
      DimSetEntry.FINDLAST;
      DimSetEntry."Dimension Set ID" += 1;
      DimSetEntry."Dimension Code" := Global1DimVal."Dimension Code";
      DimSetEntry."Dimension Value Code" := Global1DimVal.Code;
      DimSetEntry.INSERT;
      DimSetEntry."Dimension Code" := Global2DimVal."Dimension Code";
      DimSetEntry."Dimension Value Code" := Global2DimVal.Code;
      DimSetEntry.INSERT;

      ItemJournalLine.INIT;
      ItemJournalLine.CopyDim(DimSetEntry."Dimension Set ID");

      Assert.AreEqual(Global1DimVal.Code,ItemJournalLine."Shortcut Dimension 1 Code",
        STRSUBSTNO(DimensionMismatchError,ItemJournalLine.FIELDCAPTION("Shortcut Dimension 1 Code")));
      Assert.AreEqual(Global2DimVal.Code,ItemJournalLine."Shortcut Dimension 2 Code",
        STRSUBSTNO(DimensionMismatchError,ItemJournalLine.FIELDCAPTION("Shortcut Dimension 2 Code")));

      // Tear-Down
      GeneralLedgerSetup."Global Dimension 1 Code" := GeneralLedgerSetupOld."Global Dimension 1 Code";
      GeneralLedgerSetup."Global Dimension 2 Code" := GeneralLedgerSetupOld."Global Dimension 2 Code";
      GeneralLedgerSetup.MODIFY;
    END;

    [Test]
    PROCEDURE ConsolidatedDimFilter@12();
    VAR
      Dimension@1001 : Record 348;
      DimMgt@1007 : Codeunit 408;
      DimensionFilter@1002 : Text;
      DimConsolidationCodeFilter@1003 : Text;
      ConsolidatedFilter@1006 : Text;
    BEGIN
      // [FEATURE] [Consolidation]
      // [SCENARIO 363011] GetConsolidatedDimFilterByDimFilter run with Consolidated Codes Filter adds Dimension Codes to Filter
      // [GIVEN] Dimension X with Consolidation Code A, Dimension Y with Consolidation Code B
      CreateDimensionsWithConsolidationCodes(DimensionFilter,DimConsolidationCodeFilter);
      // [WHEN] DimMgt.GetConsolidatedDimFilterByDimFilter run with Filter(Consolidation Code = A|B)
      ConsolidatedFilter := DimMgt.GetConsolidatedDimFilterByDimFilter(Dimension,DimConsolidationCodeFilter);
      // [THEN] Return Value is equal to (A|B|X|Y).
      Assert.AreEqual(
        DimConsolidationCodeFilter + '|' + DimensionFilter,
        ConsolidatedFilter,
        WrongConsolidatedDimFilterErr);
    END;

    LOCAL PROCEDURE CreateDimensionSelectionBuffer@16(VAR TempDimensionSelectionBuffer@1001 : TEMPORARY Record 368;LevelValue@1002 : Option;CodeValue@1000 : Code[20]);
    BEGIN
      WITH TempDimensionSelectionBuffer DO BEGIN
        INIT;
        Level := LevelValue;
        Code := CodeValue;
        Selected := TRUE;
        INSERT;
      END;
    END;

    LOCAL PROCEDURE CreateDimensionsWithConsolidationCodes@20(VAR DimensionFilter@1000 : Text;VAR DimConsolidationCodeFilter@1001 : Text);
    VAR
      Dimension@1009 : Record 348;
      I@1007 : Integer;
      Separator@1003 : Text;
    BEGIN
      Separator := '|';
      FOR I := 1 TO LibraryRandom.RandInt(10) DO BEGIN
        LibraryDimension.CreateDimension(Dimension);
        WITH Dimension DO BEGIN
          VALIDATE(
            "Consolidation Code",
            LibraryUtility.GenerateRandomCode(FIELDNO("Consolidation Code"),DATABASE::Dimension));
          MODIFY(TRUE);
          DimensionFilter += Code + Separator;
          DimConsolidationCodeFilter += "Consolidation Code" + Separator;
        END;
      END;
      DimensionFilter := DELCHR(DimensionFilter,'>',Separator);
      DimConsolidationCodeFilter := DELCHR(DimConsolidationCodeFilter,'>',Separator);
    END;

    LOCAL PROCEDURE InsertDim@6(DimCode@1000 : Code[20];IsBlocked@1003 : Boolean);
    VAR
      Dim@1001 : Record 348;
      DimVal@1002 : Record 349;
    BEGIN
      IF Dim.GET(DimCode) THEN
        Dim.DELETE;
      Dim.VALIDATE(Code,DimCode);
      Dim.VALIDATE(Blocked,IsBlocked);
      Dim.INSERT(TRUE);
      DimVal.SETRANGE("Dimension Code",Dim.Code);
      DimVal.DELETEALL;
      InsertDimValues(Dim.Code,'A',FALSE);
      InsertDimValues(Dim.Code,'B',FALSE);
      InsertDimValues(Dim.Code,'C',FALSE);
      InsertDimValues(Dim.Code,'X',TRUE);
    END;

    LOCAL PROCEDURE InsertDimValues@3(DimCode@1002 : Code[20];DimValCode@1001 : Code[20];IsBlocked@1003 : Boolean);
    VAR
      DimVal@1000 : Record 349;
    BEGIN
      DimVal.INIT;
      DimVal."Dimension Code" := DimCode;
      DimVal.VALIDATE(Code,DimValCode);
      DimVal.VALIDATE(Blocked,IsBlocked);
      DimVal.INSERT;
    END;

    LOCAL PROCEDURE CreateGenJournalLine@55(VAR GenJournalLine@1000 : Record 81;DocumentType@1003 : Option;AccountType@1002 : Option;AccountNo@1004 : Code[20]);
    VAR
      GenJournalBatch@1001 : Record 232;
    BEGIN
      // CreateGenJournalLine
      // Creates a general journal line based on the given document type, account type and account no.

      LibraryERM.SelectGenJnlBatch(GenJournalBatch);
      LibraryERM.CreateGeneralJnlLine(
        GenJournalLine,
        GenJournalBatch."Journal Template Name",
        GenJournalBatch.Name,
        DocumentType,
        AccountType,
        AccountNo,
        LibraryRandom.RandInt(100));
    END;

    LOCAL PROCEDURE InitDimSetup@2();
    BEGIN
      IF isInitialized THEN
        EXIT;

      InsertDim(Dim1,FALSE);
      InsertDim(Dim2,FALSE);
      InsertDim(Dim3,FALSE);
      InsertDim(DimX,TRUE);
      InsertDim(DimY,FALSE);

      isInitialized := TRUE;
      COMMIT;
    END;

    BEGIN
    {
      // [FEATURE] [Dimension]
      // This test codeunit tests generic dimensions behavior as implemented in the NAV 7.0 project.
      // Does not depend on any prior data setup.
    }
    END.
  }
}

