OBJECT Codeunit 131352 Library - Document Approvals
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LibrarySales@1000 : Codeunit 130509;
      LibraryUtility@1001 : Codeunit 131000;
      LibraryRandom@1002 : Codeunit 130440;

    PROCEDURE CreateMockupUserSetup@6(VAR UserSetup@1000 : Record 91);
    VAR
      SalespersonPurchaser@1006 : Record 13;
      User@1003 : Record 2000000120;
    BEGIN
      LibrarySales.CreateSalesperson(SalespersonPurchaser);
      UserSetup."Salespers./Purch. Code" := SalespersonPurchaser.Code;
      UserSetup."User ID" :=
        COPYSTR(LibraryUtility.GenerateRandomCode(User.FIELDNO("User Name"),DATABASE::User),
          1,LibraryUtility.GetFieldLength(DATABASE::User,User.FIELDNO("User Name")));
      UserSetup."Approver ID" := '';
      UserSetup."Unlimited Sales Approval" := TRUE;
      UserSetup."Unlimited Purchase Approval" := TRUE;
      UserSetup."Unlimited Request Approval" := TRUE;
      UserSetup."Sales Amount Approval Limit" := 0;
      UserSetup."Purchase Amount Approval Limit" := 0;
      UserSetup."Request Amount Approval Limit" := 0;
      UserSetup."E-Mail" := 'someone@example.com';
      UserSetup.INSERT;
    END;

    PROCEDURE CreateUser@26(UserName@1001 : Code[50];WindowsUserName@1003 : Text[208]);
    VAR
      UserCard@1000 : TestPage 9807;
    BEGIN
      UserCard.OPENNEW;
      UserCard."User Name".SETVALUE(UserName);
      UserCard."Windows User Name".SETVALUE(WindowsUserName);
      UserCard.Permissions.NEW;
      UserCard.Permissions."<Permission Set>".SETVALUE('SUPER');
      UserCard.OK.INVOKE;
    END;

    PROCEDURE CreateNonWindowsUser@8(UserName@1001 : Code[50]);
    VAR
      UserCard@1000 : TestPage 9807;
    BEGIN
      UserCard.OPENNEW;
      UserCard."User Name".SETVALUE(UserName);
      UserCard.Permissions.NEW;
      UserCard.Permissions."<Permission Set>".SETVALUE('SUPER');
      UserCard.OK.INVOKE;
    END;

    PROCEDURE CreateUserSetup@57(VAR UserSetup@1000 : Record 91;UserID@1001 : Code[50];ApproverID@1002 : Code[50]);
    VAR
      SalespersonPurchaser@1006 : Record 13;
    BEGIN
      LibrarySales.CreateSalesperson(SalespersonPurchaser);
      UserSetup.VALIDATE("Salespers./Purch. Code",SalespersonPurchaser.Code);
      UserSetup."User ID" := UserID;
      UserSetup."Approver ID" := ApproverID;
      UserSetup.INSERT(TRUE);
    END;

    PROCEDURE DeleteUserSetup@12(VAR UserSetup@1001 : Record 91;WindowsUserName@1002 : Text[208]);
    VAR
      User@1000 : Record 2000000120;
    BEGIN
      GetUser(User,WindowsUserName);
      UserSetup.SETRANGE("User ID",User."User Name");
      UserSetup.DELETEALL;
    END;

    PROCEDURE GetUser@10(VAR User@1000 : Record 2000000120;WindowsUserName@1001 : Text[208]) : Boolean;
    BEGIN
      User.SETRANGE("Windows Security ID",SID(WindowsUserName));
      EXIT(User.FINDFIRST);
    END;

    PROCEDURE GetNonWindowsUser@9(VAR User@1000 : Record 2000000120;UserName@1001 : Text[208]) : Boolean;
    BEGIN
      User.SETRANGE("User Name",UserName);
      EXIT(User.FINDSET);
    END;

    PROCEDURE GetUserSetup@7(VAR UserSetup@1001 : Record 91;WindowsUserName@1002 : Text[208]) : Boolean;
    VAR
      User@1000 : Record 2000000120;
    BEGIN
      IF GetUser(User,WindowsUserName) THEN
        UserSetup.SETRANGE("User ID",User."User Name")
      ELSE
        UserSetup.SETRANGE("User ID",WindowsUserName);
      EXIT(UserSetup.FINDFIRST);
    END;

    PROCEDURE UpdateApprovalLimits@2(VAR UserSetup@1000 : Record 91;UnlimitedSalesApproval@1001 : Boolean;UnlimitedPurchaseApproval@1002 : Boolean;UnlimitedRequestApproval@1003 : Boolean;SalesAmountApprovalLimit@1004 : Integer;PurchaseAmountApprovalLimit@1005 : Integer;RequestAmountApprovalLimit@1006 : Integer);
    BEGIN
      UserSetup.VALIDATE("Unlimited Sales Approval",UnlimitedSalesApproval);
      UserSetup.VALIDATE("Unlimited Purchase Approval",UnlimitedPurchaseApproval);
      UserSetup.VALIDATE("Unlimited Request Approval",UnlimitedRequestApproval);

      UserSetup.VALIDATE("Sales Amount Approval Limit",SalesAmountApprovalLimit);
      UserSetup.VALIDATE("Purchase Amount Approval Limit",PurchaseAmountApprovalLimit);
      UserSetup.VALIDATE("Request Amount Approval Limit",RequestAmountApprovalLimit);

      UserSetup.MODIFY(TRUE);
    END;

    PROCEDURE UserExists@1(WindowsUserName@1001 : Text[208]) : Boolean;
    VAR
      User@1000 : Record 2000000120;
    BEGIN
      User.SETRANGE("Windows Security ID",SID(WindowsUserName));
      EXIT(NOT User.ISEMPTY);
    END;

    PROCEDURE UserSetupExists@3(WindowsUserName@1002 : Text[208]) : Boolean;
    VAR
      User@1000 : Record 2000000120;
      UserSetup@1001 : Record 91;
    BEGIN
      GetUser(User,WindowsUserName);
      UserSetup.SETRANGE("User ID",User."User Name");
      EXIT(NOT UserSetup.ISEMPTY);
    END;

    PROCEDURE SetupUsersForApprovals@18(VAR IntermediateApproverUserSetup@1003 : Record 91);
    VAR
      CurrentUserSetup@1000 : Record 91;
      FinalApproverUserSetup@1002 : Record 91;
    BEGIN
      CreateOrFindUserSetup(CurrentUserSetup,USERID);
      CreateMockupUserSetup(IntermediateApproverUserSetup);
      CreateMockupUserSetup(FinalApproverUserSetup);

      SetApprover(CurrentUserSetup,IntermediateApproverUserSetup);
      SetApprover(IntermediateApproverUserSetup,FinalApproverUserSetup);
      SetSubstitute(CurrentUserSetup,FinalApproverUserSetup);
    END;

    PROCEDURE SetupUsersForApprovalsWithLimits@42(VAR CurrentUserSetup@1002 : Record 91;VAR IntermediateApproverUserSetup@1001 : Record 91;VAR FinalApproverUserSetup@1000 : Record 91);
    BEGIN
      SetupUsersForApprovals(IntermediateApproverUserSetup);

      CurrentUserSetup.GET(USERID);
      SetSalesAmountApprovalLimits(CurrentUserSetup,LibraryRandom.RandIntInRange(1,100));
      SetLimitedSalesApprovalLimits(CurrentUserSetup);
      SetPurchaseAmountApprovalLimits(CurrentUserSetup,LibraryRandom.RandIntInRange(1,100));
      SetLimitedPurchaseApprovalLimits(CurrentUserSetup);

      SetSalesAmountApprovalLimits(IntermediateApproverUserSetup,LibraryRandom.RandIntInRange(101,1000));
      SetLimitedSalesApprovalLimits(IntermediateApproverUserSetup);
      SetPurchaseAmountApprovalLimits(IntermediateApproverUserSetup,LibraryRandom.RandIntInRange(101,1000));
      SetLimitedPurchaseApprovalLimits(IntermediateApproverUserSetup);

      FinalApproverUserSetup.GET(IntermediateApproverUserSetup."Approver ID");
      SetUnlimitedSalesApprovalLimits(FinalApproverUserSetup);
      SetUnlimitedPurchaseApprovalLimits(FinalApproverUserSetup);
    END;

    PROCEDURE CreateOrFindUserSetup@46(VAR UserSetup@1000 : Record 91;UserName@1001 : Text[208]);
    BEGIN
      IF NOT GetUserSetup(UserSetup,COPYSTR(UserName,1,50)) THEN
        CreateUserSetup(UserSetup,COPYSTR(UserName,1,50),'');
    END;

    PROCEDURE SetApprover@45(VAR UserSetup@1000 : Record 91;ApproverUserSetup@1001 : Record 91);
    BEGIN
      UserSetup."Approver ID" := ApproverUserSetup."User ID";
      UserSetup.MODIFY(TRUE);
    END;

    PROCEDURE SetSubstitute@34(VAR UserSetup@1000 : Record 91;VAR SubstituteUserSetup@1001 : Record 91);
    BEGIN
      UserSetup.Substitute := SubstituteUserSetup."User ID";
      UserSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetSalesAmountApprovalLimits@25(VAR UserSetup@1000 : Record 91;SalesApprovalLimit@1004 : Integer);
    BEGIN
      UserSetup."Sales Amount Approval Limit" := SalesApprovalLimit;
      UserSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetUnlimitedSalesApprovalLimits@27(VAR UserSetup@1000 : Record 91);
    BEGIN
      UserSetup."Unlimited Sales Approval" := TRUE;
      UserSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetLimitedSalesApprovalLimits@28(VAR UserSetup@1000 : Record 91);
    BEGIN
      UserSetup."Unlimited Sales Approval" := FALSE;
      UserSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetPurchaseAmountApprovalLimits@11(VAR UserSetup@1000 : Record 91;PurchaseApprovalLimit@1004 : Integer);
    BEGIN
      UserSetup."Purchase Amount Approval Limit" := PurchaseApprovalLimit;
      UserSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetUnlimitedPurchaseApprovalLimits@5(VAR UserSetup@1000 : Record 91);
    BEGIN
      UserSetup."Unlimited Purchase Approval" := TRUE;
      UserSetup.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE SetLimitedPurchaseApprovalLimits@4(VAR UserSetup@1000 : Record 91);
    BEGIN
      UserSetup."Unlimited Purchase Approval" := FALSE;
      UserSetup.MODIFY(TRUE);
    END;

    PROCEDURE GetApprovalEntries@53(VAR ApprovalEntry@1003 : Record 454;RecordID@1000 : RecordID);
    BEGIN
      ApprovalEntry.SETRANGE("Record ID to Approve",RecordID);
      ApprovalEntry.FINDSET;
    END;

    PROCEDURE GetPostedApprovalEntries@13(VAR PostedApprovalEntry@1003 : Record 456;RecordID@1000 : RecordID);
    BEGIN
      PostedApprovalEntry.SETRANGE("Posted Record ID",RecordID);
      PostedApprovalEntry.FINDSET;
    END;

    PROCEDURE GetPostedApprovalComments@14(VAR PostedApprovalCommentLine@1003 : Record 457;RecordID@1000 : RecordID);
    BEGIN
      PostedApprovalCommentLine.SETRANGE("Posted Record ID",RecordID);
      PostedApprovalCommentLine.FINDSET;
    END;

    PROCEDURE UpdateApprovalEntryWithCurrUser@65(RecordID@1001 : RecordID);
    VAR
      ApprovalEntry@1000 : Record 454;
      UserSetup@1002 : Record 91;
    BEGIN
      GetApprovalEntries(ApprovalEntry,RecordID);
      UserSetup.GET(USERID);
      ApprovalEntry.MODIFYALL("Sender ID",UserSetup."Approver ID",TRUE);
      ApprovalEntry.MODIFYALL("Approver ID",UserSetup."User ID",TRUE);
    END;

    BEGIN
    END.
  }
}

