OBJECT Codeunit 132214 Library - Permissions
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE GetMyUser@8(VAR User@1000 : Record 2000000120);
    BEGIN
      Initialize;
      User.SETRANGE("User Name",USERID);
      User.FINDFIRST;
      User.SETRANGE("User Name");
    END;

    PROCEDURE CreateUser@7(VAR User@1002 : Record 2000000120;NewUserName@1000 : Text[50];IsWindowsUser@1001 : Boolean);
    BEGIN
      User.INIT;
      User."User Security ID" := CREATEGUID;
      IF NewUserName = '' THEN
        User."User Name" := COPYSTR(GetGuidString,1,MAXSTRLEN(User."User Name"))
      ELSE
        User."User Name" := NewUserName;
      User."Full Name" := User."User Name";
      IF IsWindowsUser THEN
        User."Windows Security ID" := SID(User."User Name");
      User.INSERT(TRUE);
    END;

    PROCEDURE CreateUserGroup@6(VAR UserGroup@1000 : Record 9000;NewCode@1001 : Code[20]);
    BEGIN
      UserGroup.INIT;
      IF NewCode = '' THEN
        UserGroup.Code := COPYSTR(GetGuidString,1,MAXSTRLEN(UserGroup.Code))
      ELSE
        UserGroup.Code := NewCode;
      UserGroup.Name := UserGroup.Code;
      UserGroup.INSERT(TRUE);
    END;

    PROCEDURE CreateUserGroupMember@1(VAR UserGroup@1000 : Record 9000;VAR UserGroupMember@1001 : Record 9001);
    VAR
      User@1002 : Record 2000000120;
    BEGIN
      GetMyUser(User);
      UserGroupMember.INIT;
      UserGroupMember."User Group Code" := UserGroup.Code;
      UserGroupMember."User Security ID" := User."User Security ID";
      IF UserGroupMember.INSERT THEN;
    END;

    PROCEDURE CreatePermissionSet@11(VAR PermissionSet@1000 : Record 2000000004;NewCode@1001 : Code[20]);
    BEGIN
      IF NewCode <> '' THEN
        PermissionSet."Role ID" := NewCode
      ELSE
        PermissionSet."Role ID" := COPYSTR(GetGuidString,1,MAXSTRLEN(PermissionSet."Role ID"));
      PermissionSet.Name := PermissionSet."Role ID" + ' Name';
      PermissionSet.INSERT;
    END;

    PROCEDURE CreateTenantPermissionSet@16(VAR TenantPermissionSet@1000 : Record 2000000165;NewCode@1001 : Code[20];AppID@1002 : GUID);
    BEGIN
      IF NewCode <> '' THEN
        TenantPermissionSet."Role ID" := NewCode
      ELSE
        TenantPermissionSet."Role ID" := COPYSTR(GetGuidString,1,MAXSTRLEN(TenantPermissionSet."Role ID"));
      TenantPermissionSet.Name := TenantPermissionSet."Role ID" + ' Name';
      TenantPermissionSet."App ID" := AppID;
      TenantPermissionSet.INSERT;
    END;

    PROCEDURE CreateUsersUserGroupsPermissionSets@29();
    VAR
      User@1000 : Record 2000000120;
      UserGroup@1001 : Record 9000;
      PermissionSet@1002 : Record 2000000004;
      i@1003 : Integer;
      NewCode@1004 : Text[20];
    BEGIN
      // Creates a batch of test data, using other functions in this library
      UserGroup.SETFILTER(Code,'TEST*');
      UserGroup.DELETEALL(TRUE);
      UserGroup.SETRANGE(Code);
      PermissionSet.SETFILTER("Role ID",'TEST*');
      PermissionSet.DELETEALL(TRUE);
      Initialize;
      FOR i := 1 TO 15 DO BEGIN
        NewCode := STRSUBSTNO('TEST%1',i);
        User.SETRANGE("User Name",NewCode);
        IF User.ISEMPTY THEN
          CreateUser(User,NewCode,FALSE);
        IF NOT UserGroup.GET(NewCode) THEN
          CreateUserGroup(UserGroup,NewCode);
        IF NOT PermissionSet.GET(NewCode) THEN
          CreatePermissionSet(PermissionSet,NewCode);
      END;
    END;

    PROCEDURE AddUserToUserGroup@10(VAR UserGroup@1000 : Record 9000;VAR User@1001 : Record 2000000120;NewCompanyName@1003 : Text[30]);
    VAR
      UserGroupMember@1002 : Record 9001;
    BEGIN
      UserGroupMember.INIT;
      UserGroupMember."User Group Code" := UserGroup.Code;
      UserGroupMember."User Security ID" := User."User Security ID";
      UserGroupMember."Company Name" := NewCompanyName;
      UserGroupMember.INSERT
    END;

    PROCEDURE AddPermissionSetToUserGroup@35(VAR UserGroup@1000 : Record 9000;VAR PermissionSet@1001 : Record 2000000004);
    VAR
      UserGroupPermissionSet@1002 : Record 9003;
    BEGIN
      UserGroupPermissionSet.INIT;
      UserGroupPermissionSet."User Group Code" := UserGroup.Code;
      UserGroupPermissionSet."Role ID" := PermissionSet."Role ID";
      UserGroupPermissionSet.INSERT
    END;

    PROCEDURE AddPermissionSetToUser@3(VAR User@1000 : Record 2000000120;VAR PermissionSet@1001 : Record 2000000004);
    VAR
      AccessControl@1003 : Record 2000000053;
    BEGIN
      AccessControl.INIT;
      AccessControl."Role ID" := PermissionSet."Role ID";
      AccessControl.Scope := AccessControl.Scope::System;
      AccessControl."User Security ID" := User."User Security ID";

      AccessControl.INSERT
    END;

    PROCEDURE AddPermission@12(RoleID@1000 : Code[20];ObjectType@1001 : Option;ObjectID@1002 : Integer);
    VAR
      Permission@1003 : Record 2000000005;
    BEGIN
      Permission.INIT;
      Permission."Role ID" := RoleID;
      Permission."Object Type" := ObjectType;
      Permission."Object ID" := ObjectID;
      IF ObjectType = Permission."Object Type"::"Table Data" THEN BEGIN
        Permission."Read Permission" := Permission."Read Permission"::Yes;
        Permission."Insert Permission" := Permission."Insert Permission"::" ";
        Permission."Modify Permission" := Permission."Modify Permission"::" ";
        Permission."Delete Permission" := Permission."Delete Permission"::" ";
      END ELSE
        Permission."Execute Permission" := Permission."Execute Permission"::" ";
      Permission.INSERT;
    END;

    LOCAL PROCEDURE GetGuidString@14() : Text;
    BEGIN
      EXIT(DELCHR(FORMAT(CREATEGUID),'=','{-}'));
    END;

    LOCAL PROCEDURE Initialize@2();
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"Users - Create Super User");
    END;

    BEGIN
    END.
  }
}

