OBJECT Table 16445 GST Distribution Line
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.49326;
  }
  PROPERTIES
  {
    CaptionML=[ENU=GST Distribution Line;
               ENN=GST Distribution Line];
  }
  FIELDS
  {
    { 1   ;   ;Distribution No.    ;Code20        ;CaptionML=[ENU=Distribution No.;
                                                              ENN=Distribution No.];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENN=Line No.];
                                                   Editable=No }
    { 6   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ENN=Posting Date];
                                                   Editable=No }
    { 17  ;   ;From GSTIN No.      ;Code15        ;TableRelation="GST Registration Nos." WHERE (Input Service Distributor=FILTER(Yes));
                                                   CaptionML=[ENU=GSTIN No.;
                                                              ENN=GSTIN No.];
                                                   Editable=No }
    { 19  ;   ;Rcpt. Credit Type   ;Option        ;OnValidate=BEGIN
                                                                GSTDistributionHeader.GET("Distribution No.");
                                                                IF GSTDistributionHeader.Reversal AND ("Rcpt. Credit Type" <> xRec."Rcpt. Credit Type") THEN
                                                                  ERROR(ChangeRcptCreditTypeErr);

                                                                TESTFIELD("To Location Code");
                                                                IF "Rcpt. Credit Type" <> "Rcpt. Credit Type"::" " THEN BEGIN
                                                                  GSTDistributionLine.RESET;
                                                                  GSTDistributionLine.SETRANGE("Distribution No.","Distribution No.");
                                                                  GSTDistributionLine.SETRANGE("To Location Code","To Location Code");
                                                                  GSTDistributionLine.SETRANGE("Rcpt. Credit Type","Rcpt. Credit Type");
                                                                  GSTDistributionLine.SETFILTER("Line No.",'<>%1',"Line No.");
                                                                  IF GSTDistributionLine.FINDFIRST THEN
                                                                    ERROR(SameToLocationErr,"To Location Code","Rcpt. Credit Type",GSTDistributionLine."Line No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Rcpt. Credit Type;
                                                              ENN=Rcpt. Credit Type];
                                                   OptionCaptionML=[ENU=" ,Availment,Non-Availment";
                                                                    ENN=" ,Availment,Non-Availment"];
                                                   OptionString=[ ,Availment,Non-Availment] }
    { 20  ;   ;From Location Code  ;Code10        ;CaptionML=[ENU=From Location Code;
                                                              ENN=From Location Code];
                                                   Editable=No }
    { 21  ;   ;To Location Code    ;Code10        ;TableRelation=Location WHERE (GST Input Service Distributor=FILTER(No));
                                                   OnValidate=VAR
                                                                Location@1500000 : Record 14;
                                                                Location1@1500001 : Record 14;
                                                              BEGIN
                                                                CLEAR("To GSTIN No.");
                                                                GSTDistributionHeader.GET("Distribution No.");
                                                                GSTDistributionHeader.TESTFIELD("From Location Code");
                                                                GSTDistributionHeader.TESTFIELD("Posting Date");
                                                                IF GSTDistributionHeader."Dist. Credit Type" = GSTDistributionHeader."Dist. Credit Type"::" " THEN
                                                                  ERROR(DistCreditTypeErr);
                                                                "From Location Code" := GSTDistributionHeader."From Location Code";
                                                                "From GSTIN No." := GSTDistributionHeader."From GSTIN No.";
                                                                "Posting Date" := GSTDistributionHeader."Posting Date";
                                                                "Rcpt. Credit Type" := GSTDistributionHeader."Dist. Credit Type";
                                                                Location.GET("From Location Code");
                                                                IF Location1.GET("To Location Code") THEN BEGIN
                                                                  Location1.TESTFIELD("State Code");
                                                                  "To GSTIN No." := Location1."GST Registration No.";
                                                                  IF Location."State Code" = Location1."State Code" THEN
                                                                    "Distribution Jurisdiction" := "Distribution Jurisdiction"::Intrastate
                                                                  ELSE
                                                                    "Distribution Jurisdiction" := "Distribution Jurisdiction"::Interstate;
                                                                END;

                                                                IF GSTDistributionHeader.Reversal THEN
                                                                  IF "To Location Code" <> xRec."To Location Code" THEN
                                                                    ERROR(ChangeToLocErr);
                                                              END;

                                                   CaptionML=[ENU=To Location Code;
                                                              ENN=To Location Code] }
    { 22  ;   ;To GSTIN No.        ;Code15        ;CaptionML=[ENU=To GSTIN No.;
                                                              ENN=To GSTIN No.];
                                                   Editable=No }
    { 23  ;   ;Distribution Jurisdiction;Option   ;CaptionML=[ENU=Distribution Jurisdiction;
                                                              ENN=Distribution Jurisdiction];
                                                   OptionCaptionML=[ENU=Intrastate,Interstate;
                                                                    ENN=Intrastate,Interstate];
                                                   OptionString=Intrastate,Interstate;
                                                   Editable=No }
    { 24  ;   ;Distribution %      ;Decimal       ;OnValidate=VAR
                                                                GSTDistributionLine@1500000 : Record 16445;
                                                              BEGIN
                                                                TESTFIELD("To Location Code");
                                                                GeneralLedgerSetup.GET;
                                                                GSTDistributionHeader.GET("Distribution No.");
                                                                GSTDistributionLine.SETRANGE("Distribution No.","Distribution No.");
                                                                GSTDistributionLine.SETFILTER("Line No.",'<>%1',"Line No.");
                                                                GSTDistributionLine.CALCSUMS("Distribution %");
                                                                IF GSTDistributionLine."Distribution %" + "Distribution %" > 100 THEN
                                                                  ERROR(DistPercentTotalErr,FIELDNAME("Distribution %"));
                                                                IF ("Distribution %" < 0) OR ("Distribution %" > 100) THEN
                                                                  ERROR(DistPercentRangeErr,FIELDNAME("Distribution %"));
                                                                IF "Distribution %" <> 0 THEN
                                                                  "Distribution Amount" :=
                                                                    ROUND(GSTDistributionHeader."Total Amout Applied for Dist." * "Distribution %" / 100,GeneralLedgerSetup."Amount Rounding Precision")
                                                                ELSE
                                                                  "Distribution Amount" := 0;

                                                                IF GSTDistributionHeader.Reversal THEN
                                                                  IF "Distribution %" <> xRec."Distribution %" THEN
                                                                    ERROR(ChangeDistPerErr);
                                                              END;

                                                   CaptionML=[ENU=Distribution %;
                                                              ENN=Distribution %] }
    { 25  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ENN=Shortcut Dimension 1 Code];
                                                   CaptionClass='1,2,1' }
    { 26  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ENN=Shortcut Dimension 2 Code];
                                                   CaptionClass='1,2,2' }
    { 27  ;   ;Distribution Amount ;Decimal       ;CaptionML=[ENU=Distribution Amount;
                                                              ENN=Distribution Amount];
                                                   Editable=No }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              ENN=Dimension Set ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Distribution No.,Line No.               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GSTDistributionHeader@1500001 : Record 16444;
      GSTDistributionLine@1500043 : Record 16445;
      GeneralLedgerSetup@1500006 : Record 98;
      DimMgt@1500000 : Codeunit 408;
      SameToLocationErr@1500002 : TextConst '@@@="%1 = To Location Code, %2 = Rcpt. Credit Type, %3 = Line No.";ENU=To Location Code: %1 and Rcpt. Credit Type: %2 already exists for Line No. %3.;ENN=To Location Code: %1 and Rcpt. Credit Type: %2 already exists for Line No. %3.';
      DistPercentRangeErr@1500004 : TextConst '@@@="%1 = Field Name";ENU=%1 must be in between 0 and 100.;ENN=%1 must be in between 0 and 100.';
      DistPercentTotalErr@1500003 : TextConst '@@@="%1 = Field Name";ENU=Sum of %1 cannot be more than 100.;ENN=Sum of %1 cannot be more than 100.';
      DistCreditTypeErr@1500005 : TextConst 'ENU=Dist. Credit Type cannot be blank.;ENN=Dist. Credit Type cannot be blank.';
      ChangeToLocErr@1500007 : TextConst 'ENU=You cannot change To Location Code for GST Distribution Reversal.;ENN=You cannot change To Location Code for GST Distribution Reversal.';
      ChangeDistPerErr@1500008 : TextConst 'ENU=You cannot change Distribution % for GST Distribution Reversal.;ENN=You cannot change Distribution % for GST Distribution Reversal.';
      ChangeRcptCreditTypeErr@1500009 : TextConst 'ENU=You cannot change Rcpt. Credit Type for GST Distribution Reversal.;ENN=You cannot change Rcpt. Credit Type for GST Distribution Reversal.';

    PROCEDURE ShowDimensions@1500000();
    BEGIN
      TESTFIELD("Distribution No.");
      TESTFIELD("Line No.");
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2',"Distribution No.","Line No."));
      DimMgt.UpdateGlobalDimFromDimSetID("Dimension Set ID","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@19(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    BEGIN
    END.
  }
}

