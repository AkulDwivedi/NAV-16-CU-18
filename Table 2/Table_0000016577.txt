OBJECT Table 16577 Distribution Header
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 GenLedgSetup.GET;
                 GenLedgSetup.TESTFIELD("Service Tax Distribution Nos.");
                 NoSeries.GET(GenLedgSetup."Service Tax Distribution Nos.");
                 "No." := NoSeriesMgt.GetNextNo(NoSeries.Code,WORKDATE,TRUE);
                 "No. Series" := GenLedgSetup."Service Tax Distribution Nos.";
               END;
               "Posting No. Series" := GenLedgSetup."S.T. Posted Distribution Nos.";
               "Creation Date" := WORKDATE;
               "User ID" := USERID;
               "Posting Date" := WORKDATE;
             END;

    OnDelete=VAR
               SvcTaxEntryDetail2@1500000 : Record 16474;
             BEGIN
               DistributionLine.RESET;
               DistributionLine.SETRANGE("Document No.","No.");
               DistributionLine.DELETEALL;

               SvcTaxEntryDetail.RESET;
               SvcTaxEntryDetail.SETRANGE("Distribution No.","No.");
               IF SvcTaxEntryDetail.FINDSET THEN
                 REPEAT
                   SvcTaxEntryDetail2 := SvcTaxEntryDetail;
                   SvcTaxEntryDetail2."Distribution No." := '';
                   SvcTaxEntryDetail2."Dist. Input Service Tax Credit" := FALSE;
                   SvcTaxEntryDetail2.MODIFY(TRUE);
                 UNTIL SvcTaxEntryDetail.NEXT = 0;
             END;

    CaptionML=[ENU=Distribution Header;
               ENN=Distribution Header];
    LookupPageID=Page16337;
    DrillDownPageID=Page16337;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  GenLedgSetup.GET;
                                                                  NoSeriesMgt.TestManual(GenLedgSetup."Service Tax Distribution Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ENN=No.];
                                                   NotBlank=No }
    { 2   ;   ;From Location       ;Code10        ;TableRelation=Location.Code WHERE (Use As In-Transit=FILTER(No));
                                                   OnValidate=BEGIN
                                                                UpdateSvcTaxEntryDetail;
                                                                DistributionLine.RESET;
                                                                DistributionLine.SETRANGE("Document No.","No.");
                                                                DistributionLine.MODIFYALL("From Location","From Location");
                                                              END;

                                                   CaptionML=[ENU=From Location;
                                                              ENN=From Location] }
    { 3   ;   ;Distribution Basis  ;Text50        ;CaptionML=[ENU=Distribution Basis;
                                                              ENN=Distribution Basis] }
    { 4   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ENN=Posting Date] }
    { 7   ;   ;No. Series          ;Code20        ;CaptionML=[ENU=No. Series;
                                                              ENN=No. Series] }
    { 8   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ENN=Creation Date] }
    { 9   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1500000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ENN=User ID] }
    { 10  ;   ;Posting No. Series  ;Code20        ;CaptionML=[ENU=Posting No. Series;
                                                              ENN=Posting No. Series] }
    { 11  ;   ;Service Tax Group Code;Code20      ;TableRelation="Service Tax Groups";
                                                   OnValidate=BEGIN
                                                                UpdateSvcTaxEntryDetail;
                                                              END;

                                                   CaptionML=[ENU=Service Tax Group Code;
                                                              ENN=Service Tax Group Code] }
    { 12  ;   ;Total Applied Amount;Decimal       ;CaptionML=[ENU=Total Applied Amount;
                                                              ENN=Total Applied Amount] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GenLedgSetup@1500002 : Record 98;
      NoSeries@1500001 : Record 308;
      NoSeriesMgt@1500000 : Codeunit 396;
      SvcTaxEntryDetail@1500010 : Record 16474;
      SvcTaxEntryDetail2@1500004 : Record 16474;
      DistributionLine@1500003 : Record 16578;

    PROCEDURE AssistEdit@1(OldDsmnHeader@1000 : Record 16577) : Boolean;
    VAR
      DsmnHeader@1500000 : Record 16577;
    BEGIN
      WITH DsmnHeader DO BEGIN
        COPY(Rec);
        GenLedgSetup.GET;
        GenLedgSetup.TESTFIELD("Service Tax Distribution Nos.");
        IF NoSeriesMgt.SelectSeries(GenLedgSetup."Service Tax Distribution Nos.","No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := DsmnHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE UpdateSvcTaxEntryDetail@1500000();
    BEGIN
      SvcTaxEntryDetail.RESET;
      SvcTaxEntryDetail.SETRANGE("Service Tax Group Code",xRec."Service Tax Group Code");
      SvcTaxEntryDetail.SETRANGE("Distribution No.","No.");
      IF SvcTaxEntryDetail.FINDSET THEN
        REPEAT
          SvcTaxEntryDetail2 := SvcTaxEntryDetail;
          SvcTaxEntryDetail2."Dist. Input Service Tax Credit" := FALSE;
          SvcTaxEntryDetail2."Distribution No." := '';
          SvcTaxEntryDetail2.MODIFY;
        UNTIL SvcTaxEntryDetail.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

