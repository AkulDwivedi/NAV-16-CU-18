OBJECT Table 16433 GST Reconcilation
{
  OBJECT-PROPERTIES
  {
    Date=29-06-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.48822;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               GSTReconcilationLines.SETRANGE("GSTIN No.","GSTIN No.");
               GSTReconcilationLines.SETRANGE(Month,Month);
               GSTReconcilationLines.SETRANGE(Year,Year);
               GSTReconcilationLines.DELETEALL(TRUE);
               PeriodicGSTR2AData.SETRANGE("GSTIN No.","GSTIN No.");
               PeriodicGSTR2AData.SETRANGE(Month,Month);
               PeriodicGSTR2AData.SETRANGE(Year,Year);
               PeriodicGSTR2AData.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=GST Reconcilation;
               ENN=GST Reconcilation];
    LookupPageID=Page16431;
  }
  FIELDS
  {
    { 1   ;   ;GSTIN No.           ;Code15        ;TableRelation="GST Registration Nos.";
                                                   OnValidate=VAR
                                                                GSTRegistrationNos@1500000 : Record 16400;
                                                              BEGIN
                                                                "Input Service Distributor" := FALSE;
                                                                IF GSTRegistrationNos.GET("GSTIN No.") THEN
                                                                  "Input Service Distributor" := GSTRegistrationNos."Input Service Distributor";
                                                                IF CheckReconciliationLine(xRec."GSTIN No.",Month,Year) THEN
                                                                  ERROR(GSTRegNoErr,"GSTIN No.");
                                                              END;

                                                   CaptionML=[ENU=GSTIN No.;
                                                              ENN=GSTIN No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Month               ;Integer       ;OnValidate=BEGIN
                                                                IF CheckReconciliationLine("GSTIN No.",xRec.Month,Year) THEN
                                                                  ERROR(MonthModifyErr,Month,"GSTIN No.");
                                                              END;

                                                   CaptionML=[ENU=Month;
                                                              ENN=Month];
                                                   MinValue=1;
                                                   MaxValue=12;
                                                   NotBlank=Yes }
    { 3   ;   ;Year                ;Integer       ;OnValidate=BEGIN
                                                                GeneralLedgerSetup.GET;
                                                                "GST Recon. Tolerance" := GeneralLedgerSetup."GST Recon. Tolerance";
                                                                IF STRLEN(FORMAT(Year)) <= 3 THEN
                                                                  ERROR(YearFormatErr);
                                                                IF CheckReconciliationLine("GSTIN No.",Month,xRec.Year) THEN
                                                                  ERROR(ModifyYearErr,Year,"GSTIN No.");
                                                              END;

                                                   CaptionML=[ENU=Year;
                                                              ENN=Year];
                                                   NotBlank=Yes }
    { 4   ;   ;Document No         ;Code20        ;CaptionML=[ENU=Document No;
                                                              ENN=Document No];
                                                   NotBlank=Yes }
    { 5   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ENN=Posting Date];
                                                   NotBlank=Yes }
    { 6   ;   ;GST Recon. Tolerance;Decimal       ;CaptionML=[ENU=GST Recon. Tolerance;
                                                              ENN=GST Recon. Tolerance] }
    { 7   ;   ;Dimension Set ID    ;Integer       ;CaptionML=[ENU=Dimension Set ID;
                                                              ENN=Dimension Set ID] }
    { 8   ;   ;Input Service Distributor;Boolean  ;CaptionML=[ENU=Input Service Distributor;
                                                              ENN=Input Service Distributor];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;GSTIN No.,Month,Year                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GSTReconcilationLines@1500003 : Record 16430;
      GeneralLedgerSetup@1500001 : Record 98;
      PeriodicGSTR2AData@1500000 : Record 16431;
      DimensionManagement@1500002 : Codeunit 408;
      YearFormatErr@1500004 : TextConst 'ENU=Year Format must be YYYY.;ENN=Year Format must be YYYY.';
      MonthModifyErr@1500034 : TextConst '@@@=You can not modify the Month,since GST Reconciliation Lines already has records for %1 Month and for GST Registion No. %2.;ENU=You can not modify the Month,since GST Reconciliation Lines already has records for %1 Month and for GST Registion No. %2.;ENN=You can not modify the Month,since GST Reconciliation Lines already has records for %1 Month and for GST Registion No. %2.';
      ModifyYearErr@1500008 : TextConst '@@@=You can not modify the Year,since GST Reconciliation Lines already has records for %1 Year and for GST Registion No. %2.;ENU=You can not modify the Year,since GST Reconciliation Lines already has records for %1 Year and for GST Registion No. %2.;ENN=You can not modify the Year,since GST Reconciliation Lines already has records for %1 Year and for GST Registion No. %2.';
      GSTRegNoErr@1500005 : TextConst '@@@=You can not modify the GSTIN No.,since GST Reconciliation Lines already has records for GST Registion No. %1.;ENU=You can not modify the GSTIN No.,since GST Reconciliation Lines already has records for GST Registion No. %1.;ENN=You can not modify the GSTIN No.,since GST Reconciliation Lines already has records for GST Registion No. %1.';

    PROCEDURE ShowDimensions@1500003();
    BEGIN
      "Dimension Set ID" :=
        DimensionManagement.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1',"GSTIN No."));
    END;

    LOCAL PROCEDURE CheckReconciliationLine@1500777(GSTINNo@15000001 : Code[15];InputMonth@15000002 : Integer;InputYear@1170000003 : Integer) : Boolean;
    VAR
      GSTReconcilationLines2@15000000 : Record 16430;
    BEGIN
      GSTReconcilationLines2.SETRANGE("GSTIN No.",GSTINNo);
      GSTReconcilationLines2.SETRANGE(Month,InputMonth);
      GSTReconcilationLines2.SETRANGE(Year,InputYear);
      IF GSTReconcilationLines2.FINDFIRST THEN
        EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

