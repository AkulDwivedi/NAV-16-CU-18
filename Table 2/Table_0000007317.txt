OBJECT Table 7317 Warehouse Receipt Line
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45778,NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    OnDelete=VAR
               WhseRcptHeader@1000 : Record 7316;
               OrderStatus@1001 : Option;
             BEGIN
               IF (Quantity <> "Qty. Outstanding") AND ("Qty. Outstanding" <> 0) THEN
                 IF NOT CONFIRM(Text004,FALSE,TABLECAPTION,"Line No.") THEN
                   ERROR(Text003);

               WhseRcptHeader.GET("No.");
               OrderStatus := WhseRcptHeader.GetHeaderStatus("Line No.");
               IF OrderStatus <> WhseRcptHeader."Document Status" THEN BEGIN
                 WhseRcptHeader.VALIDATE("Document Status",OrderStatus);
                 WhseRcptHeader.MODIFY;
               END;
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Warehouse Receipt Line;
               ENN=Warehouse Receipt Line];
    LookupPageID=Page7342;
    DrillDownPageID=Page7342;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ENN=No.];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENN=Line No.];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              ENN=Source Type];
                                                   Editable=No }
    { 4   ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              ENN=Source Subtype];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ENN=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10;
                                                   Editable=No }
    { 6   ;   ;Source No.          ;Code20        ;CaptionML=[ENU=Source No.;
                                                              ENN=Source No.];
                                                   Editable=No }
    { 7   ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              ENN=Source Line No.];
                                                   Editable=No }
    { 9   ;   ;Source Document     ;Option        ;CaptionML=[ENU=Source Document;
                                                              ENN=Source Document];
                                                   OptionCaptionML=[ENU=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer;
                                                                    ENN=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer];
                                                   OptionString=,Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer;
                                                   Editable=No }
    { 10  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ENN=Location Code];
                                                   Editable=No }
    { 11  ;   ;Shelf No.           ;Code10        ;CaptionML=[ENU=Shelf No.;
                                                              ENN=Shelf No.] }
    { 12  ;   ;Bin Code            ;Code20        ;TableRelation=IF (Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code))
                                                                 ELSE IF (Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                  Zone Code=FIELD(Zone Code));
                                                   OnValidate=VAR
                                                                Bin@1000 : Record 7354;
                                                                WhseIntegrationMgt@1001 : Codeunit 7317;
                                                              BEGIN
                                                                IF xRec."Bin Code" <> "Bin Code" THEN
                                                                  IF "Bin Code" <> '' THEN BEGIN
                                                                    GetLocation("Location Code");
                                                                    WhseIntegrationMgt.CheckBinTypeCode(DATABASE::"Warehouse Receipt Line",
                                                                      FIELDCAPTION("Bin Code"),
                                                                      "Location Code",
                                                                      "Bin Code",0);
                                                                    IF Location."Directed Put-away and Pick" THEN BEGIN
                                                                      Bin.GET("Location Code","Bin Code");
                                                                      "Zone Code" := Bin."Zone Code";
                                                                      CheckBin(FALSE);
                                                                    END;
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Bin Code;
                                                              ENN=Bin Code] }
    { 13  ;   ;Zone Code           ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF xRec."Zone Code" <> "Zone Code" THEN BEGIN
                                                                  IF "Zone Code" <> '' THEN BEGIN
                                                                    GetLocation("Location Code");
                                                                    Location.TESTFIELD("Directed Put-away and Pick");
                                                                  END;
                                                                  "Bin Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Zone Code;
                                                              ENN=Zone Code] }
    { 14  ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ENN=Item No.];
                                                   Editable=No }
    { 15  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                InitOutstandingQtys;
                                                                "Qty. (Base)" := CalcBaseQty(Quantity);
                                                              END;

                                                   CaptionML=[ENU=Quantity;
                                                              ENN=Quantity];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 16  ;   ;Qty. (Base)         ;Decimal       ;CaptionML=[ENU=Qty. (Base);
                                                              ENN=Qty. (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 19  ;   ;Qty. Outstanding    ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Outstanding (Base)" := CalcBaseQty("Qty. Outstanding");
                                                                VALIDATE("Qty. to Receive","Qty. Outstanding");
                                                              END;

                                                   CaptionML=[ENU=Qty. Outstanding;
                                                              ENN=Qty. Outstanding];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 20  ;   ;Qty. Outstanding (Base);Decimal    ;CaptionML=[ENU=Qty. Outstanding (Base);
                                                              ENN=Qty. Outstanding (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 21  ;   ;Qty. to Receive     ;Decimal       ;OnValidate=VAR
                                                                WMSMgt@1000 : Codeunit 7302;
                                                              BEGIN
                                                                IF "Qty. to Receive" > "Qty. Outstanding" THEN
                                                                  ERROR(
                                                                    Text002,
                                                                    "Qty. Outstanding");

                                                                GetLocation("Location Code");
                                                                IF Location."Directed Put-away and Pick" THEN BEGIN
                                                                  WMSMgt.CalcCubageAndWeight(
                                                                    "Item No.","Unit of Measure Code","Qty. to Receive",Cubage,Weight);

                                                                  IF (CurrFieldNo <> 0) AND ("Qty. to Receive" > 0) THEN
                                                                    CheckBin(TRUE);
                                                                END;

                                                                "Qty. to Cross-Dock" := 0;
                                                                "Qty. to Cross-Dock (Base)" := 0;
                                                                "Qty. to Receive (Base)" := CalcBaseQty("Qty. to Receive");

                                                                Item.CheckSerialNoQty("Item No.",FIELDCAPTION("Qty. to Receive (Base)"),"Qty. to Receive (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Receive;
                                                              ENN=Qty. to Receive];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 22  ;   ;Qty. to Receive (Base);Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Receive","Qty. to Receive (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Receive (Base);
                                                              ENN=Qty. to Receive (Base)];
                                                   DecimalPlaces=0:5 }
    { 23  ;   ;Qty. Received       ;Decimal       ;OnValidate=BEGIN
                                                                "Qty. Received (Base)" := CalcBaseQty("Qty. Received");
                                                              END;

                                                   CaptionML=[ENU=Qty. Received;
                                                              ENN=Qty. Received];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 24  ;   ;Qty. Received (Base);Decimal       ;CaptionML=[ENU=Qty. Received (Base);
                                                              ENN=Qty. Received (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 29  ;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ENN=Unit of Measure Code];
                                                   Editable=No }
    { 30  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ENN=Qty. per Unit of Measure];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 31  ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ENN=Variant Code];
                                                   Editable=No }
    { 32  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENN=Description];
                                                   Editable=No }
    { 33  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ENN=Description 2];
                                                   Editable=No }
    { 34  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ENN=Status];
                                                   OptionCaptionML=[ENU=" ,Partially Received,Completely Received";
                                                                    ENN=" ,Partially Received,Completely Received"];
                                                   OptionString=[ ,Partially Received,Completely Received];
                                                   Editable=No }
    { 35  ;   ;Sorting Sequence No.;Integer       ;CaptionML=[ENU=Sorting Sequence No.;
                                                              ENN=Sorting Sequence No.];
                                                   Editable=No }
    { 36  ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              ENN=Due Date] }
    { 37  ;   ;Starting Date       ;Date          ;CaptionML=[ENU=Starting Date;
                                                              ENN=Starting Date] }
    { 38  ;   ;Cubage              ;Decimal       ;CaptionML=[ENU=Cubage;
                                                              ENN=Cubage];
                                                   DecimalPlaces=0:5 }
    { 39  ;   ;Weight              ;Decimal       ;CaptionML=[ENU=Weight;
                                                              ENN=Weight];
                                                   DecimalPlaces=0:5 }
    { 48  ;   ;Not upd. by Src. Doc. Post.;Boolean;CaptionML=[ENU=Not upd. by Src. Doc. Post.;
                                                              ENN=Not upd. by Src. Doc. Post.];
                                                   Editable=No }
    { 49  ;   ;Posting from Whse. Ref.;Integer    ;CaptionML=[ENU=Posting from Whse. Ref.;
                                                              ENN=Posting from Whse. Ref.];
                                                   Editable=No }
    { 50  ;   ;Qty. to Cross-Dock  ;Decimal       ;OnValidate=BEGIN
                                                                CrossDockMgt.GetUseCrossDock(UseCrossDock,"Location Code","Item No.");
                                                                IF NOT UseCrossDock THEN
                                                                  ERROR(Text006,Item.TABLECAPTION,Location.TABLECAPTION);
                                                                IF "Qty. to Cross-Dock" > "Qty. to Receive" THEN
                                                                  ERROR(
                                                                    Text005,
                                                                    "Qty. to Receive");

                                                                "Qty. to Cross-Dock (Base)" := CalcBaseQty("Qty. to Cross-Dock");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Cross-Dock;
                                                              ENN=Qty. to Cross-Dock];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 51  ;   ;Qty. to Cross-Dock (Base);Decimal  ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE("Qty. to Cross-Dock","Qty. to Cross-Dock (Base)");
                                                              END;

                                                   CaptionML=[ENU=Qty. to Cross-Dock (Base);
                                                              ENN=Qty. to Cross-Dock (Base)];
                                                   DecimalPlaces=0:5 }
    { 52  ;   ;Cross-Dock Zone Code;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code),
                                                                                  Cross-Dock Bin Zone=CONST(Yes));
                                                   CaptionML=[ENU=Cross-Dock Zone Code;
                                                              ENN=Cross-Dock Zone Code] }
    { 53  ;   ;Cross-Dock Bin Code ;Code20        ;TableRelation=IF (Cross-Dock Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                      Cross-Dock Bin=CONST(Yes))
                                                                                                                      ELSE IF (Cross-Dock Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                                                                                  Zone Code=FIELD(Cross-Dock Zone Code),
                                                                                                                                                                                  Cross-Dock Bin=CONST(Yes));
                                                   CaptionML=[ENU=Cross-Dock Bin Code;
                                                              ENN=Cross-Dock Bin Code] }
  }
  KEYS
  {
    {    ;No.,Line No.                            ;SumIndexFields=Qty. to Receive (Base);
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Source Type,Source Subtype,Source No.,Source Line No.;
                                                   SumIndexFields=Qty. Outstanding (Base);
                                                   MaintainSIFTIndex=No }
    {    ;No.,Source Type,Source Subtype,Source No.,Source Line No.;
                                                   MaintainSQLIndex=No }
    {    ;No.,Sorting Sequence No.                ;MaintainSQLIndex=No }
    {    ;No.,Shelf No.                           ;MaintainSQLIndex=No }
    {    ;No.,Item No.                            ;MaintainSQLIndex=No }
    {    ;No.,Source Document,Source No.          ;MaintainSQLIndex=No }
    {    ;No.,Due Date                            ;MaintainSQLIndex=No }
    {    ;No.,Bin Code                            ;MaintainSQLIndex=No }
    {    ;Item No.,Location Code,Variant Code     ;SumIndexFields=Qty. Outstanding (Base);
                                                   MaintainSIFTIndex=No }
    {    ;Bin Code,Location Code                  ;SumIndexFields=Cubage,Weight;
                                                   MaintainSIFTIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1002 : Record 14;
      Item@1012 : Record 27;
      Bin@1009 : Record 7354;
      WhsRecptHeader@1500005 : Record 7316;
      PostedGateEntryLine@1500004 : Record 16556;
      GateEntryAttachment@1500003 : Record 16557;
      GateEntryAttachment2@1500002 : Record 16557;
      PostedGateEntryLineList@1500001 : Page 16480;
      GateEntryAttachmentList@1500000 : Page 16481;
      CrossDockMgt@1005 : Codeunit 5780;
      UseCrossDock@1006 : Boolean;
      Text001@1022 : TextConst 'ENU=You cannot rename a %1.;ENN=You cannot rename a %1.';
      Text002@1021 : TextConst 'ENU=You cannot handle more than the outstanding %1 units.;ENN=You cannot handle more than the outstanding %1 units.';
      Text003@1001 : TextConst 'ENU=Cancelled.;ENN=Cancelled.';
      Text004@1004 : TextConst 'ENU=%1 %2 is not completely received.\Do you really want to delete the %1?;ENN=%1 %2 is not completely received.\Do you really want to delete the %1?';
      Text005@1008 : TextConst 'ENU=You cannot Cross-Dock  more than the %1 units to be received.;ENN=You cannot Cross-Dock  more than the %1 units to be received.';
      Text006@1007 : TextConst 'ENU=Cross-Docking is disabled for this %1 and/or %2;ENN=Cross-Docking is disabled for this %1 and/or %2';
      IgnoreErrors@1003 : Boolean;
      ErrorOccured@1010 : Boolean;

    LOCAL PROCEDURE CalcBaseQty@14(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(Qty * "Qty. per Unit of Measure",0.00001));
    END;

    LOCAL PROCEDURE CalcQty@5(QtyBase@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(QtyBase / "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE AutofillQtyToReceive@10(VAR WhseReceiptLine@1000 : Record 7317);
    BEGIN
      WITH WhseReceiptLine DO BEGIN
        IF FIND('-') THEN
          REPEAT
            VALIDATE("Qty. to Receive","Qty. Outstanding");
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE DeleteQtyToReceive@11(VAR WhseReceiptLine@1000 : Record 7317);
    BEGIN
      WITH WhseReceiptLine DO BEGIN
        IF FIND('-') THEN
          REPEAT
            VALIDATE("Qty. to Receive",0);
            MODIFY;
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE GetItem@15();
    BEGIN
      IF Item."No." <> "Item No." THEN
        Item.GET("Item No.");
    END;

    PROCEDURE GetLineStatus@1() : Integer;
    BEGIN
      IF "Qty. Outstanding" = 0 THEN
        Status := Status::"Completely Received"
      ELSE
        IF Quantity = "Qty. Outstanding" THEN
          Status := Status::" "
        ELSE
          Status := Status::"Partially Received";

      EXIT(Status);
    END;

    LOCAL PROCEDURE GetLocation@2(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        Location.GetLocationSetup(LocationCode,Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE GetBin@4(LocationCode@1000 : Code[10];BinCode@1001 : Code[20]);
    BEGIN
      GetLocation(LocationCode);
      IF NOT Location."Bin Mandatory" THEN
        CLEAR(Bin)
      ELSE
        IF (Bin."Location Code" <> LocationCode) OR
           (Bin.Code <> BinCode)
        THEN
          Bin.GET(LocationCode,BinCode);
    END;

    LOCAL PROCEDURE CheckBin@3(CalledFromQtytoReceive@1002 : Boolean);
    VAR
      BinContent@1001 : Record 7302;
      DeductCubage@1006 : Decimal;
      DeductWeight@1005 : Decimal;
    BEGIN
      IF CalledFromQtytoReceive THEN BEGIN
        DeductCubage := xRec.Cubage;
        DeductWeight := xRec.Weight;
      END;

      IF BinContent.GET(
           "Location Code","Bin Code",
           "Item No.","Variant Code","Unit of Measure Code")
      THEN BEGIN
        IF NOT BinContent.CheckIncreaseBinContent(
             "Qty. to Receive",xRec."Qty. to Receive",
             DeductCubage,DeductWeight,Cubage,Weight,FALSE,IgnoreErrors)
        THEN
          ErrorOccured := TRUE;
      END ELSE BEGIN
        GetBin("Location Code","Bin Code");
        IF NOT Bin.CheckIncreaseBin(
             "Bin Code","Item No.","Qty. to Receive",
             DeductCubage,DeductWeight,Cubage,Weight,FALSE,IgnoreErrors)
        THEN
          ErrorOccured := TRUE;
      END;
      IF ErrorOccured THEN
        "Bin Code" := '';
    END;

    PROCEDURE OpenItemTrackingLines@6500();
    VAR
      PurchaseLine@1000 : Record 39;
      SalesLine@1001 : Record 37;
      TransferLine@1002 : Record 5741;
      ReservePurchLine@1003 : Codeunit 99000834;
      ReserveSalesLine@1004 : Codeunit 99000832;
      ReserveTransferLine@1005 : Codeunit 99000836;
      SecondSourceQtyArray@1007 : ARRAY [3] OF Decimal;
      Direction@1006 : 'Outbound,Inbound';
    BEGIN
      TESTFIELD("No.");
      TESTFIELD("Qty. (Base)");

      GetItem;
      Item.TESTFIELD("Item Tracking Code");

      SecondSourceQtyArray[1] := DATABASE::"Warehouse Receipt Line";
      SecondSourceQtyArray[2] := "Qty. to Receive (Base)";
      SecondSourceQtyArray[3] := 0;

      CASE "Source Type" OF
        DATABASE::"Purchase Line":
          BEGIN
            IF PurchaseLine.GET("Source Subtype","Source No.","Source Line No.") THEN
              ReservePurchLine.CallItemTracking2(PurchaseLine,SecondSourceQtyArray);
          END;
        DATABASE::"Sales Line":
          BEGIN
            IF SalesLine.GET("Source Subtype","Source No.","Source Line No.") THEN
              ReserveSalesLine.CallItemTracking2(SalesLine,SecondSourceQtyArray);
          END;
        DATABASE::"Transfer Line":
          BEGIN
            Direction := Direction::Inbound;
            IF TransferLine.GET("Source No.","Source Line No.") THEN
              ReserveTransferLine.CallItemTracking2(TransferLine,Direction,SecondSourceQtyArray);
          END
      END;
    END;

    PROCEDURE SetIgnoreErrors@6();
    BEGIN
      IgnoreErrors := TRUE;
    END;

    PROCEDURE HasErrorOccured@7() : Boolean;
    BEGIN
      EXIT(ErrorOccured);
    END;

    PROCEDURE GetGateEntryLines@1500000();
    BEGIN
      PostedGateEntryLine.MODIFYALL(Mark,FALSE);
      WhsRecptHeader.GET("No.");
      PostedGateEntryLine.RESET;
      PostedGateEntryLine.SETCURRENTKEY("Entry Type","Source Type","Source No.",Status);
      CASE "Source Type" OF
        DATABASE::"Sales Line":
          IF "Source Subtype" = 5 THEN
            PostedGateEntryLine.SETRANGE("Source Type",PostedGateEntryLine."Source Type"::"Sales Return Order");
        DATABASE::"Purchase Line":
          IF "Source Subtype" = 1 THEN
             PostedGateEntryLine.SETRANGE("Source Type",PostedGateEntryLine."Source Type"::"Purchase Order");
        DATABASE::"Transfer Line":
          IF "Source Subtype" = 1 THEN
            PostedGateEntryLine.SETRANGE("Source Type",PostedGateEntryLine."Source Type"::"Transfer Receipt");
      END;
      PostedGateEntryLine.SETRANGE("Entry Type",PostedGateEntryLine."Entry Type"::Inward);
      PostedGateEntryLine.SETRANGE("Source No.","Source No.");
      PostedGateEntryLine.SETRANGE(Status,PostedGateEntryLine.Status::Open);
      GateEntryAttachment.SETCURRENTKEY("Source Type","Source No.","Entry Type","Gate Entry No.","Line No.");
      IF PostedGateEntryLine.FINDSET THEN
        REPEAT
          GateEntryAttachment.SETRANGE("Source No.",PostedGateEntryLine."Source No.");
          GateEntryAttachment.SETRANGE("Gate Entry No.",PostedGateEntryLine."Gate Entry No.");
          GateEntryAttachment.SETRANGE("Line No.",PostedGateEntryLine."Line No.");
          IF NOT GateEntryAttachment.FINDFIRST THEN BEGIN
            PostedGateEntryLine.Mark := TRUE;
            PostedGateEntryLine.MODIFY;
            COMMIT;
          END;
      UNTIL PostedGateEntryLine.NEXT = 0;

      PostedGateEntryLine.RESET;
      PostedGateEntryLine.SETCURRENTKEY("Entry Type","Source Type","Source No.",Status);
      PostedGateEntryLine.SETRANGE(Mark,TRUE);


      IF PostedGateEntryLine.FINDFIRST THEN BEGIN
        PostedGateEntryLineList.SETTABLEVIEW(PostedGateEntryLine);
        IF PAGE.RUNMODAL(PAGE::"Posted Gate Entry Line List",PostedGateEntryLine) = ACTION::LookupOK THEN BEGIN
          GateEntryAttachment.INIT;
          GateEntryAttachment."Source Type" := PostedGateEntryLine."Source Type";
          GateEntryAttachment."Source No." := PostedGateEntryLine."Source No.";
          GateEntryAttachment."Entry Type" := PostedGateEntryLine."Entry Type";
          GateEntryAttachment."Gate Entry No." := PostedGateEntryLine."Gate Entry No.";
          GateEntryAttachment."Line No." := PostedGateEntryLine."Line No.";
          GateEntryAttachment."Warehouse Recpt. No." := "No.";
          GateEntryAttachment.INSERT;
        END;
      END;
    END;

    PROCEDURE ShowAttachedGE@1500003(SourceDocType@1500000 : ',Sales Order,,,Sales Return Order,Purchase Order,,,Purchase Return Order,Inbound Transfer');
    BEGIN
      GateEntryAttachment2.RESET;
      GateEntryAttachment2.SETCURRENTKEY("Source Type","Source No.","Entry Type","Gate Entry No.","Line No.");
      CASE SourceDocType OF
       SourceDocType::"Sales Return Order":
         GateEntryAttachment2.SETRANGE("Source Type",GateEntryAttachment2."Source Type"::"Sales Return Order");
       SourceDocType::"Purchase Order":
         GateEntryAttachment2.SETRANGE("Source Type",GateEntryAttachment2."Source Type"::"Purchase Order");
       SourceDocType::"Inbound Transfer":
         GateEntryAttachment2.SETRANGE("Source Type",GateEntryAttachment2."Source Type"::"Transfer Receipt");
      END;
      GateEntryAttachment2.SETRANGE("Entry Type",GateEntryAttachment2."Entry Type"::Inward);
      GateEntryAttachment2.SETRANGE("Source No.","Source No.");
      CLEAR(GateEntryAttachmentList);
      GateEntryAttachmentList.SETTABLEVIEW(GateEntryAttachment2);
      GateEntryAttachmentList.LOOKUPMODE := TRUE;
      GateEntryAttachmentList.RUNMODAL;
    END;

    PROCEDURE InitOutstandingQtys@8();
    BEGIN
      VALIDATE("Qty. Outstanding",Quantity - "Qty. Received");
      "Qty. Outstanding (Base)" := "Qty. (Base)" - "Qty. Received (Base)";
    END;

    PROCEDURE GetWhseRcptLine@9(VAR WhseRcptLine@1000 : Record 7317;WhseRcptHeaderNo@1001 : Code[20];SourceType@1002 : Integer;SourceSubType@1003 : Option;SourceNo@1004 : Code[20];SourceLineNo@1005 : Integer);
    BEGIN
      WhseRcptLine.SETRANGE("No.",WhseRcptHeaderNo);
      WhseRcptLine.SETRANGE("Source Type",SourceType);
      WhseRcptLine.SETRANGE("Source Subtype",SourceSubType);
      WhseRcptLine.SETRANGE("Source No.",SourceNo);
      WhseRcptLine.SETRANGE("Source Line No.",SourceLineNo);
      WhseRcptLine.FINDFIRST;
    END;

    BEGIN
    END.
  }
}

