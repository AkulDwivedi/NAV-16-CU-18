OBJECT Table 16403 GST Claim Setoff
{
  OBJECT-PROPERTIES
  {
    Date=23-02-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.48067;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Priority := GetLastPriority("GST Component Code");
               IF IsZeroPriority("GST Component Code") THEN
                 ERROR(ZeroPriorityErr);
             END;

    CaptionML=[ENU=GST Claim Setoff;
               ENN=GST Claim Setoff];
    LookupPageID=Page16403;
    DrillDownPageID=Page16403;
  }
  FIELDS
  {
    { 1   ;   ;GST Component Code  ;Code10        ;TableRelation="GST Component";
                                                   CaptionML=[ENU=GST Component Code;
                                                              ENN=GST Component Code];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Set Off Component Code;Code10      ;TableRelation="GST Component";
                                                   OnValidate=BEGIN
                                                                IF "GST Component Code" = "Set Off Component Code" THEN
                                                                  ERROR(SameComponentErr);
                                                              END;

                                                   CaptionML=[ENU=Set Off Component Code;
                                                              ENN=Set Off Component Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Priority            ;Integer       ;OnValidate=BEGIN
                                                                IF (xRec.Priority <> Priority) AND IsSamePriority("GST Component Code",Priority) THEN
                                                                  ERROR(SamePriorityErr);
                                                              END;

                                                   CaptionML=[ENU=Priority;
                                                              ENN=Priority];
                                                   MinValue=1;
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;GST Component Code,Set Off Component Code;
                                                   Clustered=Yes }
    {    ;Priority                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SamePriorityErr@1500000 : TextConst 'ENU=Priority Cannot be duplicate.;ENN=Priority Cannot be duplicate.';
      SameComponentErr@1500001 : TextConst 'ENU=You cannot select same GST Component in GST Claim Setoff.;ENN=You cannot select same GST Component in GST Claim Setoff.';
      ZeroPriorityErr@1500003 : TextConst 'ENU=Priority Cannot be Zero.;ENN=Priority Cannot be Zero.';

    LOCAL PROCEDURE IsSamePriority@1500000(GSTComponentCode@1500001 : Code[10];GSTPriority@1500002 : Integer) : Boolean;
    VAR
      GSTClaimSetoff@1500000 : Record 16403;
    BEGIN
      GSTClaimSetoff.SETCURRENTKEY(Priority);
      GSTClaimSetoff.SETRANGE("GST Component Code",GSTComponentCode);
      GSTClaimSetoff.SETRANGE(Priority,GSTPriority);
      EXIT(NOT GSTClaimSetoff.ISEMPTY);
    END;

    LOCAL PROCEDURE IsZeroPriority@1500001(GSTComponentCode@1500001 : Code[10]) : Boolean;
    VAR
      GSTClaimSetoff@1500000 : Record 16403;
    BEGIN
      GSTClaimSetoff.SETRANGE("GST Component Code",GSTComponentCode);
      GSTClaimSetoff.SETRANGE(Priority,0);
      EXIT(NOT GSTClaimSetoff.ISEMPTY);
    END;

    LOCAL PROCEDURE GetLastPriority@1500002(GSTComponentCode@1500001 : Code[10]) : Integer;
    VAR
      GSTClaimSetoff@1500000 : Record 16403;
    BEGIN
      GSTClaimSetoff.SETCURRENTKEY(Priority);
      GSTClaimSetoff.SETRANGE("GST Component Code",GSTComponentCode);
      IF GSTClaimSetoff.FINDLAST THEN
        EXIT(GSTClaimSetoff.Priority + 1);
      EXIT(1);
    END;

    BEGIN
    END.
  }
}

