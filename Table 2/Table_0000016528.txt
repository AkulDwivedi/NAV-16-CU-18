OBJECT Table 16528 FA Retrospective Book
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    Permissions=TableData 5601=r,
                TableData 5625=r;
    OnInsert=BEGIN
               LOCKTABLE;
               FA.LOCKTABLE;
               DeprBook.LOCKTABLE;
               IF "FA No." <> '' THEN
                 FA.GET("FA No.");
               DeprBook.GET("Depreciation Book Code");
               Description := FA.Description;
               IF ("No. of Depreciation Years" <> 0) OR ("No. of Depreciation Months" <> 0) THEN
                 DeprBook.TESTFIELD("Fiscal Year 365 Days",FALSE);
             END;

    OnModify=BEGIN
               LOCKTABLE;
               DeprBook.LOCKTABLE;
               DeprBook.GET("Depreciation Book Code");
               IF ("No. of Depreciation Years" <> 0) OR ("No. of Depreciation Months" <> 0) THEN
                 DeprBook.TESTFIELD("Fiscal Year 365 Days",FALSE);
               EXIT;
             END;

    OnDelete=BEGIN
               MoveEntries;
             END;

    CaptionML=[ENU=FA Retrospective Book;
               ENN=FA Retrospective Book];
  }
  FIELDS
  {
    { 1   ;   ;FA No.              ;Code20        ;OnValidate=BEGIN
                                                                IF "FA No." <> '' THEN
                                                                  FADeprBook.GET("FA No.","Depreciation Book Code");
                                                                "FA Posting Group" := FADeprBook."FA Posting Group";
                                                                "Depreciation Starting Date" := FADeprBook."Depreciation Starting Date";
                                                              END;

                                                   OnLookup=VAR
                                                              FixedAsset@1500000 : Record 5600;
                                                              ProposedDeprMethodHeader@1500001 : Record 16527;
                                                            BEGIN
                                                              FixedAsset.CLEARMARKS;
                                                              FixedAsset.RESET;
                                                              ProposedDeprMethodHeader.GET("Depreciation Book Code","Proposed Depreciation Method",
                                                                "FA Class Code","FA Subclass Code");
                                                              IF ProposedDeprMethodHeader."FA Filter" <> '' THEN
                                                                FixedAsset.SETFILTER("No.",ProposedDeprMethodHeader."FA Filter");
                                                              IF "FA Class Code" <> '' THEN
                                                                FixedAsset.SETRANGE("FA Class Code","FA Class Code");
                                                              IF "FA Subclass Code" <> '' THEN
                                                                FixedAsset.SETRANGE("FA Subclass Code","FA Subclass Code");
                                                              IF FixedAsset.FINDSET THEN
                                                                REPEAT
                                                                  IF FADeprBook.GET(FixedAsset."No.","Depreciation Book Code") THEN
                                                                    FixedAsset.MARK := TRUE;
                                                                UNTIL FixedAsset.NEXT = 0;
                                                              FixedAsset.MARKEDONLY(TRUE);

                                                              IF PAGE.RUNMODAL(PAGE::"Fixed Asset List",FixedAsset) = ACTION::LookupOK THEN
                                                                VALIDATE("FA No.",FixedAsset."No.");
                                                            END;

                                                   CaptionML=[ENU=FA No.;
                                                              ENN=FA No.] }
    { 2   ;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   OnValidate=BEGIN
                                                                IF "Depreciation Book Code" <> DeprBook.Code THEN
                                                                  DeprBook.GET("Depreciation Book Code");
                                                                "FA Book Type" := DeprBook."FA Book Type";
                                                              END;

                                                   CaptionML=[ENU=Depreciation Book Code;
                                                              ENN=Depreciation Book Code];
                                                   NotBlank=No;
                                                   Editable=No }
    { 3   ;   ;Proposed Depreciation Method;Option;OnValidate=BEGIN
                                                                CASE "Proposed Depreciation Method" OF
                                                                  "Proposed Depreciation Method"::"Straight-Line":
                                                                    BEGIN
                                                                      "Proposed Declining-Balance %" := 0;
                                                                      "Retrospective Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                    END;
                                                                  "Proposed Depreciation Method"::"Declining-Balance 1",
                                                                  "Proposed Depreciation Method"::"Declining-Balance 2":
                                                                    BEGIN
                                                                      "Proposed Straight-Line %" := 0;
                                                                      "No. of Depreciation Years" := 0;
                                                                      "No. of Depreciation Months" := 0;
                                                                      "Fixed Depr. Amount" := 0;
                                                                      "Retrospective Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                    END;
                                                                  "Proposed Depreciation Method"::"DB1/SL",
                                                                  "Proposed Depreciation Method"::"DB2/SL":
                                                                    BEGIN
                                                                      "Retrospective Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                    END;
                                                                  "Proposed Depreciation Method"::"User-Defined":
                                                                    BEGIN
                                                                      "Proposed Straight-Line %" := 0;
                                                                      "No. of Depreciation Years" := 0;
                                                                      "No. of Depreciation Months" := 0;
                                                                      "Fixed Depr. Amount" := 0;
                                                                      "Proposed Declining-Balance %" := 0;
                                                                    END;
                                                                  "Proposed Depreciation Method"::Manual:
                                                                    BEGIN
                                                                      "Proposed Straight-Line %" := 0;
                                                                      "No. of Depreciation Years" := 0;
                                                                      "No. of Depreciation Months" := 0;
                                                                      "Fixed Depr. Amount" := 0;
                                                                      "Proposed Declining-Balance %" := 0;
                                                                      "Retrospective Table Code" := '';
                                                                      "First User-Defined Depr. Date" := 0D;
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Proposed Depreciation Method;
                                                              ENN=Proposed Depreciation Method];
                                                   OptionCaptionML=[ENU=Straight-Line,Declining-Balance 1,Declining-Balance 2,DB1/SL,DB2/SL,User-Defined,Manual;
                                                                    ENN=Straight-Line,Declining-Balance 1,Declining-Balance 2,DB1/SL,DB2/SL,User-Defined,Manual];
                                                   OptionString=Straight-Line,Declining-Balance 1,Declining-Balance 2,DB1/SL,DB2/SL,User-Defined,Manual;
                                                   Editable=No }
    { 4   ;   ;Depreciation Starting Date;Date    ;OnValidate=BEGIN
                                                                CalcDeprPeriod;
                                                              END;

                                                   CaptionML=[ENU=Depreciation Starting Date;
                                                              ENN=Depreciation Starting Date];
                                                   Editable=Yes }
    { 5   ;   ;Proposed Straight-Line %;Decimal   ;OnValidate=BEGIN
                                                                TESTFIELD("FA Book Type","FA Book Type"::" ");
                                                                IF ("Proposed Straight-Line %" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                AdjustLinearMethod("No. of Depreciation Years","Fixed Depr. Amount");
                                                              END;

                                                   CaptionML=[ENU=Proposed Straight-Line %;
                                                              ENN=Proposed Straight-Line %];
                                                   DecimalPlaces=2:8;
                                                   MinValue=0 }
    { 6   ;   ;No. of Depreciation Years;Decimal  ;OnValidate=VAR
                                                                DeprBook2@1000 : Record 5611;
                                                              BEGIN
                                                                DeprBook2.GET("Depreciation Book Code");
                                                                DeprBook2.TESTFIELD("Fiscal Year 365 Days",FALSE);

                                                                TESTFIELD("Depreciation Starting Date");
                                                                IF ("No. of Depreciation Years" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;

                                                                "No. of Depreciation Months" := ROUND("No. of Depreciation Years" * 12,0.00000001);
                                                                AdjustLinearMethod("Proposed Straight-Line %","Fixed Depr. Amount");
                                                              END;

                                                   CaptionML=[ENU=No. of Depreciation Years;
                                                              ENN=No. of Depreciation Years];
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 7   ;   ;No. of Depreciation Months;Decimal ;OnValidate=VAR
                                                                DeprBook2@1000 : Record 5611;
                                                              BEGIN
                                                                DeprBook2.GET("Depreciation Book Code");
                                                                DeprBook2.TESTFIELD("Fiscal Year 365 Days",FALSE);

                                                                TESTFIELD("Depreciation Starting Date");
                                                                IF ("No. of Depreciation Months" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                "No. of Depreciation Years" := ROUND("No. of Depreciation Months" / 12,0.00000001);
                                                                AdjustLinearMethod("Proposed Straight-Line %","Fixed Depr. Amount");
                                                              END;

                                                   CaptionML=[ENU=No. of Depreciation Months;
                                                              ENN=No. of Depreciation Months];
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 8   ;   ;Fixed Depr. Amount  ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Fixed Depr. Amount" <> 0) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                AdjustLinearMethod("Proposed Straight-Line %","No. of Depreciation Years");
                                                              END;

                                                   CaptionML=[ENU=Fixed Depr. Amount;
                                                              ENN=Fixed Depr. Amount];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 9   ;   ;Proposed Declining-Balance %;Decimal;
                                                   OnValidate=BEGIN
                                                                TESTFIELD("FA Book Type","FA Book Type"::" ");
                                                                IF "Proposed Declining-Balance %" >= 100 THEN
                                                                  FIELDERROR("Proposed Declining-Balance %",Text001);
                                                                IF ("Proposed Declining-Balance %" <> 0) AND NOT DecliningMethod THEN
                                                                  DeprMethodError;
                                                              END;

                                                   CaptionML=[ENU=Proposed Declining-Balance %;
                                                              ENN=Proposed Declining-Balance %];
                                                   DecimalPlaces=2:8;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 10  ;   ;Retrospective Table Code;Code10    ;TableRelation="Depreciation Table Header";
                                                   OnValidate=BEGIN
                                                                IF ("Retrospective Table Code" <> '') AND NOT UserDefinedMethod THEN
                                                                  DeprMethodError;
                                                              END;

                                                   CaptionML=[ENU=Retrospective Table Code;
                                                              ENN=Retrospective Table Code] }
    { 11  ;   ;FA Posting Group    ;Code10        ;OnLookup=VAR
                                                              FAPostingGroup@1500000 : Record 5606;
                                                              FALedEntry@1500001 : Record 5601;
                                                              ProposedDeprMethodHeader@1500002 : Record 16527;
                                                            BEGIN
                                                              FAPostingGroup.RESET;
                                                              ProposedDeprMethodHeader.GET("Depreciation Book Code","Proposed Depreciation Method",
                                                                "FA Class Code","FA Subclass Code");
                                                              IF FAPostingGroup.FINDSET THEN
                                                                REPEAT
                                                                  FALedEntry.RESET;
                                                                  FALedEntry.SETCURRENTKEY("Depreciation Book Code","FA Class Code","FA Subclass Code","FA No.","FA Posting Group");
                                                                  FALedEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
                                                                  IF "FA Class Code" <> '' THEN
                                                                    FALedEntry.SETRANGE("FA Class Code","FA Class Code");
                                                                  IF "FA Subclass Code" <> '' THEN
                                                                    FALedEntry.SETRANGE("FA Subclass Code","FA Subclass Code");
                                                                  IF ProposedDeprMethodHeader."FA Filter" <> '' THEN
                                                                    FALedEntry.SETFILTER("FA No.",ProposedDeprMethodHeader."FA Filter");
                                                                  FALedEntry.SETRANGE("FA Posting Group",FAPostingGroup.Code);
                                                                  IF FALedEntry.FINDFIRST THEN
                                                                    FAPostingGroup.MARK := TRUE;
                                                                UNTIL FAPostingGroup.NEXT = 0;
                                                              FAPostingGroup.MARKEDONLY(TRUE);
                                                              IF PAGE.RUNMODAL(PAGE::"FA Posting Groups",FAPostingGroup) = ACTION::LookupOK THEN
                                                                VALIDATE("FA Posting Group",FAPostingGroup.Code);
                                                            END;

                                                   CaptionML=[ENU=FA Posting Group;
                                                              ENN=FA Posting Group] }
    { 12  ;   ;Depreciation Ending Date;Date      ;OnValidate=BEGIN
                                                                TESTFIELD("FA Book Type","FA Book Type"::" ");
                                                                TESTFIELD("Depreciation Starting Date");
                                                                IF ("Depreciation Ending Date" <> 0D) AND NOT LinearMethod THEN
                                                                  DeprMethodError;
                                                                CalcDeprPeriod;
                                                              END;

                                                   CaptionML=[ENU=Depreciation Ending Date;
                                                              ENN=Depreciation Ending Date] }
    { 13  ;   ;First User-Defined Depr. Date;Date ;OnValidate=BEGIN
                                                                IF ("First User-Defined Depr. Date" <> 0D) AND NOT UserDefinedMethod THEN
                                                                  DeprMethodError;
                                                              END;

                                                   CaptionML=[ENU=First User-Defined Depr. Date;
                                                              ENN=First User-Defined Depr. Date] }
    { 14  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ENN=Description];
                                                   Editable=No }
    { 15  ;   ;FA Book Type        ;Option        ;CaptionML=[ENU=FA Book Type;
                                                              ENN=FA Book Type];
                                                   OptionCaptionML=[ENU=" ,Income Tax";
                                                                    ENN=" ,Income Tax"];
                                                   OptionString=[ ,Income Tax] }
    { 16  ;   ;FA Class Code       ;Code10        ;TableRelation="FA Class";
                                                   CaptionML=[ENU=FA Class Code;
                                                              ENN=FA Class Code];
                                                   Editable=No }
    { 17  ;   ;FA Subclass Code    ;Code10        ;TableRelation="FA Subclass";
                                                   CaptionML=[ENU=FA Subclass Code;
                                                              ENN=FA Subclass Code];
                                                   Editable=No }
    { 18  ;   ;Use DB% First Fiscal Year;Boolean  ;OnValidate=BEGIN
                                                                IF "Use DB% First Fiscal Year" THEN
                                                                  IF NOT (("Proposed Depreciation Method" = "Proposed Depreciation Method"::"DB1/SL") OR
                                                                          ("Proposed Depreciation Method" = "Proposed Depreciation Method"::"DB2/SL"))
                                                                  THEN
                                                                    DeprMethodError;
                                                              END;

                                                   CaptionML=[ENU=Use DB% First Fiscal Year;
                                                              ENN=Use DB% First Fiscal Year] }
  }
  KEYS
  {
    {    ;Depreciation Book Code,Proposed Depreciation Method,FA Class Code,FA Subclass Code,FA Posting Group,FA No.,Depreciation Starting Date;
                                                   Clustered=Yes }
    {    ;Depreciation Book Code,Proposed Depreciation Method,FA Class Code,FA Subclass Code,Depreciation Starting Date,FA Posting Group,FA No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=must not be 100;ENN=must not be 100';
      Text002@1002 : TextConst 'ENU=%1 is later than %2.;ENN=%1 is later than %2.';
      Text003@1003 : TextConst 'ENU=must not be %1;ENN=must not be %1';
      Text004@1004 : TextConst 'ENU=untitled;ENN=untitled';
      FA@1005 : Record 5600;
      DeprBook@1006 : Record 5611;
      DepreciationCalc@1009 : Codeunit 5616;
      FADeprBook@1500001 : Record 5612;
      RetroEntry@1500002 : Record 16526;

    LOCAL PROCEDURE AdjustLinearMethod@1(VAR Amount1@1000 : Decimal;VAR Amount2@1001 : Decimal);
    BEGIN
      Amount1 := 0;
      Amount2 := 0;
      IF "No. of Depreciation Years" = 0 THEN BEGIN
        "No. of Depreciation Months" := 0;
        "Depreciation Ending Date" := 0D;
      END;
    END;

    PROCEDURE CalcDeprPeriod@4();
    VAR
      DeprBook2@1000 : Record 5611;
    BEGIN
      IF "Depreciation Starting Date" = 0D THEN BEGIN
        "Depreciation Ending Date" := 0D;
        "No. of Depreciation Years" := 0;
        "No. of Depreciation Months" := 0;
      END;
      IF ("Depreciation Starting Date" = 0D) OR ("Depreciation Ending Date" = 0D) THEN BEGIN
        "No. of Depreciation Years" := 0;
        "No. of Depreciation Months" := 0;
      END ELSE BEGIN
        IF "Depreciation Starting Date" > "Depreciation Ending Date" THEN
          ERROR(
            Text002,
            FIELDCAPTION("Depreciation Starting Date"),FIELDCAPTION("Depreciation Ending Date"));
        DeprBook2.GET("Depreciation Book Code");
        IF DeprBook2."Fiscal Year 365 Days" THEN BEGIN
          "No. of Depreciation Months" := 0;
          "No. of Depreciation Years" := 0;
        END;
        IF NOT DeprBook2."Fiscal Year 365 Days" THEN BEGIN
          "No. of Depreciation Months" :=
            DepreciationCalc.DeprDays("Depreciation Starting Date","Depreciation Ending Date",FALSE) / 30;
          "No. of Depreciation Months" := ROUND("No. of Depreciation Months",0.00000001);
          "No. of Depreciation Years" := ROUND("No. of Depreciation Months" / 12,0.00000001);
        END;
        "Proposed Straight-Line %" := 0;
        "Fixed Depr. Amount" := 0;
      END;
    END;

    PROCEDURE LinearMethod@9() : Boolean;
    BEGIN
      EXIT(
        "Proposed Depreciation Method" IN
        ["Proposed Depreciation Method"::"Straight-Line",
         "Proposed Depreciation Method"::"DB1/SL",
         "Proposed Depreciation Method"::"DB2/SL"]);
    END;

    PROCEDURE DecliningMethod@10() : Boolean;
    BEGIN
      EXIT(
        "Proposed Depreciation Method" IN
        ["Proposed Depreciation Method"::"Declining-Balance 1",
         "Proposed Depreciation Method"::"Declining-Balance 2",
         "Proposed Depreciation Method"::"DB1/SL",
         "Proposed Depreciation Method"::"DB2/SL"]);
    END;

    PROCEDURE UserDefinedMethod@11() : Boolean;
    BEGIN
      EXIT("Proposed Depreciation Method" = "Proposed Depreciation Method"::"User-Defined");
    END;

    PROCEDURE DeprMethodError@7();
    BEGIN
      FIELDERROR("Proposed Depreciation Method",STRSUBSTNO(Text003,"Proposed Depreciation Method"));
    END;

    PROCEDURE Caption@6() : Text[120];
    VAR
      FA@1000 : Record 5600;
      DeprBook@1001 : Record 5611;
    BEGIN
      IF "FA No." = '' THEN
        EXIT(Text004);
      FA.GET("FA No.");
      DeprBook.GET("Depreciation Book Code");
      EXIT(
        STRSUBSTNO(
          '%1 %2 %3 %4',"FA No.",FA.Description,"Depreciation Book Code",DeprBook.Description));
    END;

    PROCEDURE MoveEntries@1500001();
    BEGIN
      RetroEntry.RESET;
      RetroEntry.SETCURRENTKEY("FA Posting Type","Proposed Depreciation Method","FA Class Code",
        "FA Subclass Code","Depreciation Book Code","FA No.");
      RetroEntry.SETRANGE("FA Posting Type",RetroEntry."FA Posting Type"::Depreciation);
      RetroEntry.SETRANGE("Proposed Depreciation Method","Proposed Depreciation Method");
      IF "FA Subclass Code" <> '' THEN
        RetroEntry.SETRANGE("FA Subclass Code","FA Subclass Code");
      IF "FA Class Code" <> '' THEN
        RetroEntry.SETRANGE("FA Class Code","FA Class Code");
      RetroEntry.SETRANGE("FA Subclass Code","Depreciation Book Code");
      IF "Depreciation Book Code" <> '' THEN
        RetroEntry.SETRANGE("Depreciation Book Code","Depreciation Book Code");
      IF "FA No." <> '' THEN
        RetroEntry.SETFILTER("FA No.","FA No.");
      RetroEntry.DELETEALL;
    END;

    BEGIN
    END.
  }
}

