OBJECT Table 16543 FBT Header
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               FBTHeader.LOCKTABLE;
               IF FBTHeader.FIND('+') THEN
                 "Entry No." := FBTHeader."Entry No." + 1
               ELSE
                 "Entry No." := 1;
               TESTFIELD("Starting Date");
               TESTFIELD("Ending Date");
             END;

    OnDelete=BEGIN
               IF Settlement = TRUE THEN
                 ERROR(Text16501);
               DeleteFBTLines;
               DeleteFBTAdjustments;
             END;

    CaptionML=[ENU=FBT Header;
               ENN=FBT Header];
    LookupPageID=Page16564;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ENN=Entry No.];
                                                   NotBlank=No;
                                                   Editable=No }
    { 2   ;   ;Starting Date       ;Date          ;OnValidate=VAR
                                                                FBTAdjustment@1500000 : Record 16542;
                                                              BEGIN
                                                                FBTHeader.RESET;
                                                                FBTHeader.SETRANGE(Settlement,FALSE);
                                                                FBTHeader.SETFILTER("Entry No.",'<>%1',"Entry No.");
                                                                IF FBTHeader.FINDSET THEN
                                                                  REPEAT
                                                                    IF ("Starting Date" >= FBTHeader."Starting Date") AND ("Starting Date" <= FBTHeader."Ending Date") THEN
                                                                      ERROR(Text16500);
                                                                  UNTIL FBTHeader.NEXT = 0;
                                                                FBTAdjustment.SETRANGE("FBT Entry No.","Entry No.");
                                                                IF FBTAdjustment.FINDSET THEN
                                                                  REPEAT
                                                                    IF "Starting Date" <> xRec."Starting Date" THEN
                                                                      IF NOT ((FBTAdjustment."Adjustment Date" >= FBTHeader."Starting Date") AND
                                                                              (FBTAdjustment."Adjustment Date" <= FBTHeader."Ending Date"))
                                                                      THEN
                                                                        FBTAdjustment."Adjustment Date" := "Ending Date";
                                                                    FBTAdjustment.MODIFY;
                                                                  UNTIL (FBTAdjustment.NEXT = 0)
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ENN=Starting Date];
                                                   NotBlank=Yes }
    { 3   ;   ;Ending Date         ;Date          ;OnValidate=VAR
                                                                FBTAdjustment@1500000 : Record 16542;
                                                              BEGIN
                                                                IF "Starting Date" > "Ending Date" THEN
                                                                  ERROR(Text16502);
                                                                FBTHeader.RESET;
                                                                FBTHeader.SETRANGE(Settlement,FALSE);
                                                                FBTHeader.SETFILTER("Entry No.",'<>%1',"Entry No.");
                                                                IF FBTHeader.FINDSET THEN
                                                                  REPEAT
                                                                    IF (("Ending Date" >= FBTHeader."Starting Date") AND ("Ending Date" <= FBTHeader."Ending Date")) OR
                                                                       ((FBTHeader."Starting Date" >= "Starting Date") AND (FBTHeader."Starting Date" <= "Ending Date")) OR
                                                                       ((FBTHeader."Ending Date" >= "Starting Date") AND (FBTHeader."Ending Date" <= "Ending Date"))
                                                                    THEN
                                                                      ERROR(Text16500);
                                                                  UNTIL FBTHeader.NEXT = 0;
                                                                FBTAdjustment.SETRANGE("FBT Entry No.","Entry No.");
                                                                IF FBTAdjustment.FINDSET THEN
                                                                  REPEAT
                                                                    IF "Ending Date" <> xRec."Ending Date" THEN
                                                                      IF NOT ((FBTAdjustment."Adjustment Date" >= FBTHeader."Starting Date") AND
                                                                              (FBTAdjustment."Adjustment Date" <= FBTHeader."Ending Date"))
                                                                      THEN
                                                                        FBTAdjustment."Adjustment Date" := "Ending Date";
                                                                    FBTAdjustment.MODIFY;
                                                                  UNTIL (FBTAdjustment.NEXT = 0)
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ENN=Ending Date];
                                                   NotBlank=Yes }
    { 4   ;   ;Global Dimension 1 Filter;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                FBTLine.RESET;
                                                                FBTLine.SETRANGE("FBT Entry No.","Entry No.");
                                                                IF FBTLine.FINDFIRST THEN
                                                                  IF "Global Dimension 1 Filter" <> xRec."Global Dimension 1 Filter" THEN
                                                                    MESSAGE(Text16504);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Global Dimension 1 Filter;
                                                              ENN=Global Dimension 1 Filter];
                                                   CaptionClass='1,3,1' }
    { 5   ;   ;Global Dimension 2 Filter;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                FBTLine.RESET;
                                                                FBTLine.SETRANGE("FBT Entry No.","Entry No.");
                                                                IF FBTLine.FINDFIRST THEN
                                                                  IF "Global Dimension 2 Filter" <> xRec."Global Dimension 2 Filter" THEN
                                                                    MESSAGE(Text16504);
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Global Dimension 2 Filter;
                                                              ENN=Global Dimension 2 Filter];
                                                   CaptionClass='1,3,2' }
    { 6   ;   ;Settlement          ;Boolean       ;CaptionML=[ENU=Settlement;
                                                              ENN=Settlement];
                                                   Editable=No }
    { 7   ;   ;Total Fringe Benefit;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FBT Line"."Value of Fringe Benefit" WHERE (FBT Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Total Fringe Benefit;
                                                              ENN=Total Fringe Benefit];
                                                   Editable=No }
    { 8   ;   ;Total Tax Amount    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FBT Line"."Tax Amount" WHERE (FBT Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Total Tax Amount;
                                                              ENN=Total Tax Amount];
                                                   Editable=No }
    { 9   ;   ;Total Surcharge Amount;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FBT Line"."Surcharge Amount" WHERE (FBT Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Total Surcharge Amount;
                                                              ENN=Total Surcharge Amount];
                                                   Editable=No }
    { 10  ;   ;Total eCess Amount  ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FBT Line"."eCess Amount" WHERE (FBT Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Total eCess Amount;
                                                              ENN=Total eCess Amount];
                                                   Editable=No }
    { 12  ;   ;Rounded Total Tax Amount;Decimal   ;CaptionML=[ENU=Rounded Total Tax Amount;
                                                              ENN=Rounded Total Tax Amount];
                                                   Editable=No }
    { 13  ;   ;Rounded Total Surch. Amount;Decimal;CaptionML=[ENU=Rounded Total Surch. Amount;
                                                              ENN=Rounded Total Surch. Amount];
                                                   Editable=No }
    { 14  ;   ;Rounded Total eCess Amount;Decimal ;CaptionML=[ENU=Rounded Total eCess Amount;
                                                              ENN=Rounded Total eCess Amount];
                                                   Editable=No }
    { 15  ;   ;Total FBT           ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FBT Line"."FBT Amount" WHERE (FBT Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Total FBT;
                                                              ENN=Total FBT];
                                                   Editable=No }
    { 16  ;   ;Rounded Total FBT   ;Decimal       ;CaptionML=[ENU=Rounded Total FBT;
                                                              ENN=Rounded Total FBT];
                                                   Editable=No }
    { 17  ;   ;FBT Calculated      ;Boolean       ;CaptionML=[ENU=FBT Calculated;
                                                              ENN=FBT Calculated];
                                                   Editable=No }
    { 18  ;   ;Total SHE Cess Amount;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("FBT Line"."SHE Cess Amount" WHERE (FBT Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Total SHE Cess Amount;
                                                              ENN=Total SHE Cess Amount];
                                                   Editable=No }
    { 19  ;   ;Rounded Total SHE Cess Amount;Decimal;
                                                   CaptionML=[ENU=Rounded Total SHE Cess Amount;
                                                              ENN=Rounded Total SHE Cess Amount];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text16500@1500001 : TextConst 'ENU=Starting/Ending Date already exists in Calculate FBT date range.;ENN=Starting/Ending Date already exists in Calculate FBT date range.';
      Text16501@1500002 : TextConst 'ENU=Settled Entry cannot be deleted.;ENN=Settled Entry cannot be deleted.';
      Text16502@1500006 : TextConst 'ENU=Starting Date cannot be greater than Ending Date.;ENN=Starting Date cannot be greater than Ending Date.';
      Text16503@1500007 : TextConst 'ENU=FBT entries are successfully settled.;ENN=FBT entries are successfully settled.';
      FBTHeader@1500000 : Record 16543;
      FBTLine@1500003 : Record 16544;
      Text16505@1500005 : TextConst 'ENU=Negative FBT cannot be settled.;ENN=Negative FBT cannot be settled.';
      FBTEntry@1500008 : Record 16541;
      Text16504@1500004 : TextConst 'ENU=Click Suggest Lines as Dimension has been changed.;ENN=Click Suggest Lines as Dimension has been changed.';
      FBTGroup@1500009 : Record 16538;

    PROCEDURE RoundFBT@1500014(Amt@1500000 : Decimal) : Decimal;
    VAR
      FBTRoundingDirection@1500001 : Text[1];
      FBTRoundingPrecision@1500002 : Decimal;
      GLSetup@1500003 : Record 98;
    BEGIN
      GLSetup.GET;
      CASE GLSetup."FBT Rounding Type" OF
        GLSetup."FBT Rounding Type"::Nearest:
          FBTRoundingDirection := '=';
        GLSetup."FBT Rounding Type"::Up:
          FBTRoundingDirection := '>';
        GLSetup."FBT Rounding Type"::Down:
          FBTRoundingDirection := '<';
      END;
      IF GLSetup."FBT Rounding Precision" <> 0 THEN
        FBTRoundingPrecision := GLSetup."FBT Rounding Precision"
      ELSE
        FBTRoundingPrecision := 0.01;

      EXIT(ROUND(Amt,FBTRoundingPrecision,FBTRoundingDirection));
    END;

    PROCEDURE DeleteFBTLines@1500000();
    VAR
      FBTLine@1500000 : Record 16544;
    BEGIN
      FBTLine.RESET;
      FBTLine.SETRANGE("FBT Entry No.","Entry No.");
      FBTLine.DELETEALL;
    END;

    PROCEDURE DeleteFBTAdjustments@1500001();
    VAR
      FBTAdjustment@1500000 : Record 16542;
    BEGIN
      FBTAdjustment.RESET;
      FBTAdjustment.SETRANGE("FBT Entry No.","Entry No.");
      FBTAdjustment.DELETEALL;
    END;

    PROCEDURE SuggestLines@1500002();
    VAR
      FBTRate@1500001 : Record 16539;
      FBTAdjustment@1500002 : Record 16542;
    BEGIN
      IF Settlement THEN
        EXIT;
      FBTAdjustment.RESET;
      FBTAdjustment.SETRANGE("FBT Entry No.","Entry No.");
      FBTAdjustment.MODIFYALL("Global Dimension 1 Filter","Global Dimension 1 Filter");
      FBTAdjustment.MODIFYALL("Global Dimension 2 Filter","Global Dimension 2 Filter");

      IF FBTGroup.FINDSET THEN
        REPEAT
          FBTLine.INIT;
          FBTLine."FBT Entry No." := "Entry No.";
          FBTLine."FBT Group Code" := FBTGroup.Code;
          FBTLine."Global Dimension 1 Filter" := "Global Dimension 1 Filter";
          FBTLine."Global Dimension 2 Filter" := "Global Dimension 2 Filter";

          FBTRate.RESET;
          FBTRate.SETRANGE("FBT Group Code",FBTLine."FBT Group Code");
          FBTRate.SETRANGE("Effective Date",0D,"Ending Date");
          IF FBTRate.FINDLAST THEN BEGIN
            FBTLine."Value %" := FBTRate."Value %";
            FBTLine."Tax %" := FBTRate."Tax %";
            FBTLine."Surcharge %" := FBTRate."Surcharge %";
            FBTLine."eCess %" := FBTRate."eCess %";
            FBTLine."SHE Cess %" := FBTRate."SHE Cess %";
          END
          ELSE BEGIN
            FBTLine."Value %" := 0;
            FBTLine."Tax %" := 0;
            FBTLine."Surcharge %" := 0;
            FBTLine."eCess %" := 0;
            FBTLine."SHE Cess %" := 0;
          END;

          FBTEntry.RESET;
          FBTEntry.SETCURRENTKEY("FBT Group Code","Posting Date",Settlement,"Global Dimension 1 Filter","Global Dimension 2 Filter",
            "Type of Expense");
          FBTEntry.SETRANGE("FBT Group Code",FBTLine."FBT Group Code");
          FBTEntry.SETRANGE("Posting Date","Starting Date","Ending Date");
          FBTEntry.SETRANGE(Settlement,FALSE);
          FBTEntry.SETFILTER("Global Dimension 1 Filter","Global Dimension 1 Filter");
          FBTEntry.SETFILTER("Global Dimension 2 Filter","Global Dimension 2 Filter");

          FBTEntry.SETRANGE("Type of Expense",FBTEntry."Type of Expense"::"Accounted in Balance Sheet");
          FBTEntry.CALCSUMS(Amount);
          FBTLine."A/C for in the balance sheet" := -FBTEntry.Amount;

          FBTEntry.SETRANGE("Type of Expense",FBTEntry."Type of Expense"::"Reimbursement from Client");
          FBTEntry.CALCSUMS(Amount);
          FBTLine."Reimbursement from Client Amt" := -FBTEntry.Amount;

          FBTEntry.SETRANGE("Type of Expense",FBTEntry."Type of Expense"::"Recovered from Employee");
          FBTEntry.CALCSUMS(Amount);
          FBTLine."Recovered from Employee Amt" := -FBTEntry.Amount;

          FBTEntry.SETRANGE("Type of Expense",FBTEntry."Type of Expense"::" ");

          FBTEntry.CALCSUMS(Amount);
          FBTLine."Debited to Profit and Loss A/C" := FBTEntry.Amount - FBTLine."Reimbursement from Client Amt" -
            FBTLine."A/C for in the balance sheet";

          FBTLine.CALCFIELDS("Any other head","Other Deduction");

          FBTLine.Amount := FBTLine."Debited to Profit and Loss A/C" + FBTLine."A/C for in the balance sheet" +
            FBTLine."Reimbursement from Client Amt" - FBTLine."Recovered from Employee Amt" +
            FBTLine."Any other head" - FBTLine."Other Deduction";

          FBTLine."Value of Fringe Benefit" := (FBTLine.Amount * FBTLine."Value %") / 100;
          FBTLine."Tax Amount" := FBTLine."Tax %" * FBTLine."Value of Fringe Benefit" / 100;
          FBTLine."Surcharge Amount" := FBTLine."Surcharge %" * FBTLine."Tax Amount" / 100;
          FBTLine."eCess Amount" := FBTLine."eCess %" * (FBTLine."Tax Amount" + FBTLine."Surcharge Amount") / 100;

          FBTLine."SHE Cess Amount" := FBTLine."SHE Cess %" * (FBTLine."Tax Amount" + FBTLine."Surcharge Amount") / 100;
          FBTLine."FBT Amount" := FBTLine."Tax Amount" + FBTLine."Surcharge Amount" + FBTLine."eCess Amount" +
            FBTLine."SHE Cess Amount";

          IF FBTLine.Amount < 0 THEN
            FBTLine.Negative := TRUE;
          IF NOT FBTLine.INSERT THEN
            FBTLine.MODIFY;

          FBTEntry.SETRANGE("Type of Expense");
          FBTEntry.MODIFYALL("FBT Header Entry No.","Entry No.");

          FBTEntry.MODIFYALL("Value %",FBTLine."Value %");
          FBTEntry.MODIFYALL("Tax %",FBTLine."Tax %");
          FBTEntry.MODIFYALL("Surcharge %",FBTLine."Surcharge %");
          FBTEntry.MODIFYALL("eCess %",FBTRate."eCess %");
          FBTEntry.MODIFYALL("SHE Cess %",FBTRate."SHE Cess %");

          FBTAdjustment.SETRANGE("FBT Group Code",FBTLine."FBT Group Code");
          FBTAdjustment.MODIFYALL("Value %",FBTLine."Value %");
          FBTAdjustment.MODIFYALL("Tax %",FBTLine."Tax %");
          FBTAdjustment.MODIFYALL("Surcharge %",FBTLine."Surcharge %");
          FBTAdjustment.MODIFYALL("eCess %",FBTLine."eCess %");
          FBTAdjustment.MODIFYALL("SHE Cess %",FBTLine."SHE Cess %");

          "FBT Calculated" := TRUE;
          CALCFIELDS("Total Fringe Benefit","Total Tax Amount","Total Surcharge Amount","Total eCess Amount",
            "Total SHE Cess Amount","Total FBT");
        UNTIL FBTGroup.NEXT = 0;
    END;

    PROCEDURE Settle@1500003();
    VAR
      FBTGroup@1500000 : Record 16538;
      FBTLine@1500001 : Record 16544;
      FBTAdjustment@1500002 : Record 16542;
    BEGIN
      FBTLine.RESET;
      FBTLine.SETRANGE("FBT Entry No.","Entry No.");
      IF FBTLine.FINDFIRST THEN
        IF (FBTLine."Global Dimension 1 Filter" <> "Global Dimension 1 Filter") OR
           (FBTLine."Global Dimension 2 Filter" <> "Global Dimension 2 Filter")
        THEN
          ERROR(Text16504);

      IF (Settlement = FALSE) AND "FBT Calculated" THEN BEGIN
        IF  "Total FBT" < 0 THEN
          ERROR(Text16505);

        IF FBTGroup.FINDSET THEN
          REPEAT
            FBTEntry.RESET;
            FBTEntry.SETRANGE("FBT Group Code",FBTGroup.Code);
            FBTEntry.SETRANGE("Posting Date","Starting Date","Ending Date");
            FBTEntry.SETFILTER("Global Dimension 1 Filter","Global Dimension 1 Filter");
            FBTEntry.SETFILTER("Global Dimension 2 Filter","Global Dimension 2 Filter");
            FBTEntry.SETRANGE("FBT Header Entry No.","Entry No.");
            FBTEntry.MODIFYALL(Settlement,TRUE);
          UNTIL (FBTGroup.NEXT = 0);

        Settlement := TRUE;
        MODIFY;
        FBTAdjustment.SETRANGE("FBT Entry No.","Entry No.");
        FBTAdjustment.MODIFYALL(Settlement,TRUE);

        MESSAGE(Text16503);
      END;
    END;

    BEGIN
    END.
  }
}

