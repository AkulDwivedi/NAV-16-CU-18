OBJECT Table 16553 Gate Entry Line
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Gate Entry Line;
               ENN=Gate Entry Line];
  }
  FIELDS
  {
    { 1   ;   ;Entry Type          ;Option        ;CaptionML=[ENU=Entry Type;
                                                              ENN=Entry Type];
                                                   OptionCaptionML=[ENU=Inward,Outward;
                                                                    ENN=Inward,Outward];
                                                   OptionString=Inward,Outward }
    { 2   ;   ;Gate Entry No.      ;Code20        ;TableRelation="Gate Entry Header".No. WHERE (Entry Type=FIELD(Entry Type));
                                                   CaptionML=[ENU=Gate Entry No.;
                                                              ENN=Gate Entry No.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENN=Line No.] }
    { 4   ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Source Type" <> xRec."Source Type" THEN BEGIN
                                                                  "Source No." := '';
                                                                  "Source Name" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              ENN=Source Type];
                                                   OptionCaptionML=[ENU=" ,Sales Shipment,Sales Return Order,Purchase Order,Purchase Return Shipment,Transfer Receipt,Transfer Shipment";
                                                                    ENN=" ,Sales Shipment,Sales Return Order,Purchase Order,Purchase Return Shipment,Transfer Receipt,Transfer Shipment"];
                                                   OptionString=[ ,Sales Shipment,Sales Return Order,Purchase Order,Purchase Return Shipment,Transfer Receipt,Transfer Shipment] }
    { 5   ;   ;Source No.          ;Code20        ;OnValidate=BEGIN
                                                                IF "Source Type" = "Source Type"::" " THEN
                                                                  ERROR(Text16500,FIELDCAPTION("Line No."),"Line No.");

                                                                IF "Source No." <> xRec."Source No." THEN
                                                                  "Source Name" := '';
                                                                IF "Source No." = '' THEN BEGIN
                                                                  "Source Name" := '';
                                                                  EXIT;
                                                                END;
                                                                CASE "Source Type" OF
                                                                  "Source Type"::"Sales Shipment":
                                                                    BEGIN
                                                                      SalesShipHeader.GET("Source No.");
                                                                      "Source Name" := SalesShipHeader."Bill-to Name";
                                                                    END;
                                                                  "Source Type"::"Sales Return Order":
                                                                    BEGIN
                                                                      SalesHeader.GET(SalesHeader."Document Type"::"Return Order","Source No.");
                                                                      "Source Name" := SalesHeader."Bill-to Name";
                                                                    END;
                                                                  "Source Type"::"Purchase Order":
                                                                    BEGIN
                                                                      PurchHeader.GET(PurchHeader."Document Type"::Order,"Source No.");
                                                                      "Source Name" := PurchHeader."Pay-to Name";
                                                                    END;
                                                                  "Source Type"::"Purchase Return Shipment":
                                                                    BEGIN
                                                                      ReturnShipHeader.GET("Source No.");
                                                                      "Source Name" := ReturnShipHeader."Pay-to Name";
                                                                    END;
                                                                  "Source Type"::"Transfer Receipt":
                                                                    BEGIN
                                                                      TransHeader.GET("Source No.");
                                                                      "Source Name" := TransHeader."Transfer-from Name";
                                                                    END;
                                                                  "Source Type"::"Transfer Shipment":
                                                                    BEGIN
                                                                      TransShptHeader.GET("Source No.");
                                                                      "Source Name" := TransShptHeader."Transfer-to Name";
                                                                    END
                                                                END;
                                                              END;

                                                   OnLookup=BEGIN
                                                              GateEntryHeader.GET("Entry Type","Gate Entry No.");
                                                              CASE "Source Type" OF
                                                                "Source Type"::"Sales Shipment":
                                                                  BEGIN
                                                                    SalesShipHeader.RESET;
                                                                    SalesShipHeader.FILTERGROUP(2);
                                                                    SalesShipHeader.SETRANGE("Location Code",GateEntryHeader."Location Code");
                                                                    SalesShipHeader.FILTERGROUP(0);
                                                                    IF PAGE.RUNMODAL(0,SalesShipHeader) = ACTION::LookupOK THEN
                                                                      VALIDATE("Source No.",SalesShipHeader."No.");
                                                                  END;
                                                                "Source Type"::"Sales Return Order":
                                                                  BEGIN
                                                                    SalesHeader.RESET;
                                                                    SalesHeader.FILTERGROUP(2);
                                                                    SalesHeader.SETRANGE("Document Type",SalesHeader."Document Type"::"Return Order");
                                                                    SalesHeader.SETRANGE("Location Code",GateEntryHeader."Location Code");
                                                                    SalesHeader.FILTERGROUP(0);
                                                                    IF PAGE.RUNMODAL(0,SalesHeader) = ACTION::LookupOK THEN
                                                                      VALIDATE("Source No.",SalesHeader."No.");
                                                                  END;
                                                                "Source Type"::"Purchase Order":
                                                                  BEGIN
                                                                    PurchHeader.RESET;
                                                                    PurchHeader.FILTERGROUP(2);
                                                                    PurchHeader.SETRANGE("Document Type",PurchHeader."Document Type"::Order);
                                                                    PurchHeader.SETRANGE("Location Code",GateEntryHeader."Location Code");
                                                                    PurchHeader.FILTERGROUP(0);
                                                                    IF PAGE.RUNMODAL(0,PurchHeader) = ACTION::LookupOK THEN
                                                                      VALIDATE("Source No.",PurchHeader."No.");
                                                                  END;
                                                                "Source Type"::"Purchase Return Shipment":
                                                                  BEGIN
                                                                    ReturnShipHeader.RESET;
                                                                    ReturnShipHeader.FILTERGROUP(2);
                                                                    ReturnShipHeader.SETRANGE("Location Code",GateEntryHeader."Location Code");
                                                                    ReturnShipHeader.FILTERGROUP(0);
                                                                    IF PAGE.RUNMODAL(0,ReturnShipHeader) = ACTION::LookupOK THEN
                                                                      VALIDATE("Source No.",ReturnShipHeader."No.");
                                                                  END;
                                                                "Source Type"::"Transfer Receipt":
                                                                  BEGIN
                                                                    TransHeader.RESET;
                                                                    TransHeader.FILTERGROUP(2);
                                                                    TransHeader.SETRANGE("Transfer-to Code",GateEntryHeader."Location Code");
                                                                    TransHeader.FILTERGROUP(0);
                                                                    IF PAGE.RUNMODAL(0,TransHeader) = ACTION::LookupOK THEN
                                                                      VALIDATE("Source No.",TransHeader."No.");
                                                                  END;
                                                                "Source Type"::"Transfer Shipment":
                                                                  BEGIN
                                                                    TransShptHeader.RESET;
                                                                    TransShptHeader.FILTERGROUP(2);
                                                                    TransShptHeader.SETRANGE("Transfer-from Code",GateEntryHeader."Location Code");
                                                                    TransShptHeader.FILTERGROUP(0);
                                                                    IF PAGE.RUNMODAL(0,TransShptHeader) = ACTION::LookupOK THEN
                                                                      VALIDATE("Source No.",TransShptHeader."No.");
                                                                  END
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Source No.;
                                                              ENN=Source No.] }
    { 6   ;   ;Source Name         ;Text30        ;CaptionML=[ENU=Source Name;
                                                              ENN=Source Name];
                                                   Editable=No }
    { 7   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ENN=Status];
                                                   OptionCaptionML=[ENU=Open,Close;
                                                                    ENN=Open,Close];
                                                   OptionString=Open,Close }
    { 8   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ENN=Description] }
    { 9   ;   ;Challan No.         ;Code20        ;CaptionML=[ENU=Challan No.;
                                                              ENN=Challan No.] }
    { 10  ;   ;Challan Date        ;Date          ;CaptionML=[ENU=Challan Date;
                                                              ENN=Challan Date] }
  }
  KEYS
  {
    {    ;Entry Type,Gate Entry No.,Line No.      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      PurchHeader@1500000 : Record 38;
      SalesShipHeader@1500001 : Record 110;
      TransHeader@1500002 : Record 5740;
      SalesHeader@1500003 : Record 36;
      ReturnShipHeader@1500004 : Record 6650;
      TransShptHeader@1500005 : Record 5744;
      GateEntryHeader@1500006 : Record 16552;
      Text16500@1500007 : TextConst 'ENU=Source Type must not be blank in %1 %2.;ENN=Source Type must not be blank in %1 %2.';

    BEGIN
    END.
  }
}

