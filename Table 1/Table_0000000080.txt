OBJECT Table 80 Gen. Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45778,NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Page ID");
             END;

    OnDelete=BEGIN
               GenJnlAlloc.SETRANGE("Journal Template Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name",Name);
               GenJnlLine.DELETEALL(TRUE);
               GenJnlBatch.SETRANGE("Journal Template Name",Name);
               GenJnlBatch.DELETEALL;
             END;

    CaptionML=[ENU=Gen. Journal Template;
               ENN=Gen. Journal Template];
    LookupPageID=Page250;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ENN=Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ENN=Description] }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Test Report ID;
                                                              ENN=Test Report ID] }
    { 6   ;   ;Page ID             ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Page));
                                                   OnValidate=BEGIN
                                                                IF "Page ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[ENU=Page ID;
                                                              ENN=Page ID] }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Posting Report ID;
                                                              ENN=Posting Report ID] }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=[ENU=Force Posting Report;
                                                              ENN=Force Posting Report] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"General Journal - Test";
                                                                "Posting Report ID" := REPORT::"G/L Register";
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::General:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."General Journal";
                                                                      "Page ID" := PAGE::"General Journal";
                                                                    END;
                                                                  Type::Sales:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Sales Journal";
                                                                      "Page ID" := PAGE::"Sales Journal";
                                                                    END;
                                                                  Type::Purchases:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Purchase Journal";
                                                                      "Page ID" := PAGE::"Purchase Journal";
                                                                    END;
                                                                  Type::"Cash Receipts":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Cash Receipt Journal";
                                                                      "Page ID" := PAGE::"Cash Receipt Journal";
                                                                    END;
                                                                  Type::Payments:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Payment Journal";
                                                                      "Page ID" := PAGE::"Payment Journal";
                                                                    END;
                                                                  Type::Assets:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Fixed Asset G/L Journal";
                                                                      "Page ID" := PAGE::"Fixed Asset G/L Journal";
                                                                    END;
                                                                  Type::Intercompany:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."IC General Journal";
                                                                      "Page ID" := PAGE::"IC General Journal";
                                                                    END;
                                                                  Type::Jobs:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Job G/L Journal";
                                                                      "Page ID" := PAGE::"Job G/L Journal";
                                                                    END;
                                                                  Type::LC:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."LC Journal";
                                                                      "Page ID" := PAGE::"LC Journal";
                                                                    END;
                                                                   Type::"TDS Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."TDS Adjustment Journal";
                                                                      "Page ID" := PAGE::"TDS Adjustment Journal";
                                                                    END;
                                                                   Type::"VAT Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."VAT Adjustment Journal";
                                                                      "Page ID" := PAGE::"VAT Adjustment Journal";
                                                                    END;
                                                                   Type::"TCS Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."TCS Adjustment Journal";
                                                                      "Page ID" := PAGE::"TCS Adjustment Journal";
                                                                    END;
                                                                   Type::"Service Tax Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Service Tax Adjustment Journal";
                                                                      "Page ID" := PAGE::"Service Tax Adjustment Journal";
                                                                    END;
                                                                END;

                                                                IF Recurring THEN
                                                                  "Page ID" := PAGE::"Recurring General Journal";
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ENN=Type];
                                                   OptionCaptionML=[ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,,,,,,,,,,,,,,,,TDS Adjustments,LC,VAT Adjustments,TCS Adjustments,Service Tax Adjustments;
                                                                    ENN=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,,,,,,,,,,,,,,,,TDS Adjustments,LC,VAT Adjustments,TCS Adjustments,Service Tax Adjustments];
                                                   OptionString=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,,,,,,,,,,,,,,,,TDS Adjustments,LC,VAT Adjustments,TCS Adjustments,Service Tax Adjustments }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.SETRANGE("Journal Template Name",Name);
                                                                GenJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              ENN=Source Code] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ENN=Reason Code] }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                IF Recurring THEN
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=[ENU=Recurring;
                                                              ENN=Recurring] }
    { 15  ;   ;Test Report Caption ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Test Report ID)));
                                                   CaptionML=[ENU=Test Report Caption;
                                                              ENN=Test Report Caption];
                                                   Editable=No }
    { 16  ;   ;Page Caption        ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Page),
                                                                                                                Object ID=FIELD(Page ID)));
                                                   CaptionML=[ENU=Page Caption;
                                                              ENN=Page Caption];
                                                   Editable=No }
    { 17  ;   ;Posting Report Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Posting Report ID)));
                                                   CaptionML=[ENU=Posting Report Caption;
                                                              ENN=Posting Report Caption];
                                                   Editable=No }
    { 18  ;   ;Force Doc. Balance  ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Force Doc. Balance;
                                                              ENN=Force Doc. Balance] }
    { 19  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              ENN=Bal. Account Type];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                                    ENN=G/L Account,Customer,Vendor,Bank Account,Fixed Asset];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 20  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              ENN=Bal. Account No.] }
    { 21  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ENN=No. Series] }
    { 22  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ENN=Posting No. Series] }
    { 23  ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Copy VAT Setup to Jnl. Lines" <> xRec."Copy VAT Setup to Jnl. Lines" THEN BEGIN
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Copy VAT Setup to Jnl. Lines","Copy VAT Setup to Jnl. Lines");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Copy VAT Setup to Jnl. Lines;
                                                              ENN=Copy VAT Setup to Jnl. Lines] }
    { 24  ;   ;Allow VAT Difference;Boolean       ;OnValidate=BEGIN
                                                                IF "Allow VAT Difference" <> xRec."Allow VAT Difference" THEN BEGIN
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Allow VAT Difference","Allow VAT Difference");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allow VAT Difference;
                                                              ENN=Allow VAT Difference] }
    { 25  ;   ;Cust. Receipt Report ID;Integer    ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   AccessByPermission=TableData 18=R;
                                                   CaptionML=[ENU=Cust. Receipt Report ID;
                                                              ENN=Cust. Receipt Report ID] }
    { 26  ;   ;Cust. Receipt Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Cust. Receipt Report ID)));
                                                   AccessByPermission=TableData 18=R;
                                                   CaptionML=[ENU=Cust. Receipt Report Caption;
                                                              ENN=Cust. Receipt Report Caption];
                                                   Editable=No }
    { 27  ;   ;Vendor Receipt Report ID;Integer   ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   AccessByPermission=TableData 23=R;
                                                   CaptionML=[ENU=Vendor Receipt Report ID;
                                                              ENN=Vendor Receipt Report ID] }
    { 28  ;   ;Vendor Receipt Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Vendor Receipt Report ID)));
                                                   AccessByPermission=TableData 23=R;
                                                   CaptionML=[ENU=Vendor Receipt Report Caption;
                                                              ENN=Vendor Receipt Report Caption];
                                                   Editable=No }
    { 16500;  ;Sub Type            ;Option        ;OnValidate=BEGIN
                                                                SourceCodeSetup.GET;
                                                                CASE "Sub Type" OF
                                                                  "Sub Type"::"Cash Receipt Voucher":
                                                                    BEGIN
                                                                      SourceCodeSetup.TESTFIELD("Cash Receipt Voucher");
                                                                      "Source Code" := SourceCodeSetup."Cash Receipt Voucher";
                                                                      "Page ID" := PAGE::"Cash Receipt Voucher";
                                                                      "Posting Report ID" := REPORT::"Voucher Register";
                                                                    END;
                                                                  "Sub Type"::"Cash Payment Voucher":
                                                                    BEGIN
                                                                      SourceCodeSetup.TESTFIELD("Cash Payment Voucher");
                                                                      "Source Code" := SourceCodeSetup."Cash Payment Voucher";
                                                                      "Page ID" := PAGE::"Cash Payment Voucher";
                                                                      "Posting Report ID" := REPORT::"Voucher Register";
                                                                    END;
                                                                  "Sub Type"::"Bank Receipt Voucher":
                                                                    BEGIN
                                                                      SourceCodeSetup.TESTFIELD("Bank Receipt Voucher");
                                                                      "Source Code" := SourceCodeSetup."Bank Receipt Voucher";
                                                                      "Page ID" := PAGE::"Bank Receipt Voucher";
                                                                      "Posting Report ID" := REPORT::"Voucher Register";
                                                                    END;
                                                                  "Sub Type"::"Bank Payment Voucher":
                                                                    BEGIN
                                                                      SourceCodeSetup.TESTFIELD("Bank Payment Voucher");
                                                                      "Source Code" := SourceCodeSetup."Bank Payment Voucher";
                                                                      "Page ID" := PAGE::"Bank Payment Voucher";
                                                                      "Posting Report ID" := REPORT::"Voucher Register";
                                                                    END;
                                                                  "Sub Type"::"Contra Voucher":
                                                                    BEGIN
                                                                      SourceCodeSetup.TESTFIELD("Contra Voucher");
                                                                      "Source Code" := SourceCodeSetup."Contra Voucher";
                                                                      "Page ID" := PAGE::"Contra Voucher";
                                                                      "Posting Report ID" := REPORT::"Voucher Register";
                                                                    END;
                                                                  "Sub Type"::"Journal Voucher":
                                                                    BEGIN
                                                                      SourceCodeSetup.TESTFIELD("Journal Voucher");
                                                                      "Source Code" := SourceCodeSetup."Journal Voucher";
                                                                      "Page ID" := PAGE::"Journal Voucher";
                                                                      "Posting Report ID" := REPORT::"Voucher Register";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Sub Type;
                                                              ENN=Sub Type];
                                                   OptionCaptionML=[ENU=" ,Cash Receipt Voucher,Cash Payment Voucher,Bank Receipt Voucher,Bank Payment Voucher,Contra Voucher,Journal Voucher";
                                                                    ENN=" ,Cash Receipt Voucher,Cash Payment Voucher,Bank Receipt Voucher,Bank Payment Voucher,Contra Voucher,Journal Voucher"];
                                                   OptionString=[ ,Cash Receipt Voucher,Cash Payment Voucher,Bank Receipt Voucher,Bank Payment Voucher,Contra Voucher,Journal Voucher] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ENN=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1;ENN=must not be %1';
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1004 : Record 81;
      GenJnlAlloc@1005 : Record 221;
      SourceCodeSetup@1006 : Record 242;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

