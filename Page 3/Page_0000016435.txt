OBJECT Page 16435 GST Payment
{
  OBJECT-PROPERTIES
  {
    Date=;
    Time=;
    Version List=NAVIN9.00.00.49326,TFS225680;
  }
  PROPERTIES
  {
    CaptionML=[ENU=GST Payment;
               ENN=GST Payment];
    ActionList=ACTIONS
    {
      { 1500008 ;    ;ActionContainer;
                      Name=Apply Payment;
                      ActionContainerType=NewDocumentItems }
      { 1500009 ;1   ;Action    ;
                      Name=ApplyEntries;
                      ShortCutKey=Shift+F11;
                      CaptionML=[ENU=Apply Entries;
                                 ENN=Apply Entries];
                      Promoted=Yes;
                      Enabled=ApplyBtnEnable;
                      PromotedIsBig=Yes;
                      Image=Payment;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 GSTInputTaxCredit.ApplyGSTSettlement(GSTINNo,PostingDate,AccountType,AccountNo,BankReferenceNo,BankReferenceDate);
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1500000; ;Container ;
                Name=GST Payment;
                CaptionML=[ENU=GST Payment;
                           ENN=GST Payment];
                ContainerType=ContentArea }

    { 1500001;1;Group     ;
                Name=General;
                CaptionML=[ENU=General;
                           ENN=General];
                GroupType=Group }

    { 1500002;2;Field     ;
                CaptionML=[ENU=GST Registration No.;
                           ENN=GST Registration No.];
                SourceExpr=GSTINNo;
                TableRelation="GST Registration Nos." WHERE (Input Service Distributor=CONST(No));
                LookupPageID=GST Registration Nos.;
                OnValidate=VAR
                             GSTRegistrationNos@1500000 : Record 16400;
                           BEGIN
                             GSTRegistrationNos.GET(GSTINNo);
                             IF GSTRegistrationNos."Input Service Distributor" THEN
                               ERROR(ISDGSTRegNoErr);
                             EnableApplyEntries;
                           END;

                ShowMandatory=True }

    { 1500006;2;Field     ;
                CaptionML=[ENU=Posting Date;
                           ENN=Posting Date];
                SourceExpr=PostingDate;
                OnValidate=BEGIN
                             DateFilter := '';
                             IF PostingDate <> 0D THEN BEGIN
                               IsPostingDateAllowed;
                               DateFilter := '..' + FORMAT(GSTInputTaxCredit.GetPeriodEnddate(PostingDate));
                             END;
                             EnableApplyEntries;
                           END;

                ShowMandatory=True }

    { 1500010;2;Field     ;
                CaptionML=[ENU=Account Type;
                           ENN=Account Type];
                SourceExpr=AccountType;
                OnValidate=BEGIN
                             AccountNo := '';
                           END;
                            }

    { 1500011;2;Field     ;
                CaptionML=[ENU=Account No.;
                           ENN=Account No.];
                SourceExpr=AccountNo;
                OnValidate=BEGIN
                             IF AccountType = AccountType::"G/L Account" THEN
                               GSTPaymentBuffer.CheckGLAcc(AccountNo)
                             ELSE
                               GSTPaymentBuffer.CheckBank(AccountNo);
                           END;

                OnLookup=VAR
                           GLAccount@1500000 : Record 15;
                           BankAccount@1500001 : Record 270;
                           GLAccountList@1500002 : Page 18;
                           BankAccountList@1500004 : Page 371;
                         BEGIN
                           IF AccountType = AccountType::"G/L Account" THEN BEGIN
                             GLAccount.SETRANGE("Account Type",GLAccount."Account Type"::Posting);
                             GLAccount.SETRANGE(Blocked,FALSE);
                             GLAccountList.SETTABLEVIEW(GLAccount);
                             GLAccountList.LOOKUPMODE := TRUE;
                             IF GLAccountList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               GLAccountList.GETRECORD(GLAccount);
                               GSTPaymentBuffer.CheckGLAcc(GLAccount."No.");
                               AccountNo := GLAccount."No.";
                             END;
                           END ELSE BEGIN
                             BankAccount.SETRANGE(Blocked,FALSE);
                             BankAccountList.SETTABLEVIEW(BankAccount);
                             BankAccountList.LOOKUPMODE := TRUE;
                             IF BankAccountList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                               BankAccountList.GETRECORD(BankAccount);
                               GSTPaymentBuffer.CheckBank(BankAccount."No.");
                               AccountNo := BankAccount."No.";
                             END;
                           END;
                         END;
                          }

    { 1500012;2;Field     ;
                CaptionML=[ENU=Bank Reference No.;
                           ENN=Bank Reference No.];
                SourceExpr=BankReferenceNo }

    { 1500013;2;Field     ;
                CaptionML=[ENU=Bank Reference Date;
                           ENN=Bank Reference Date];
                SourceExpr=BankReferenceDate }

    { 1500003;2;Field     ;
                Name=DateFilter;
                CaptionML=[ENU=Date Filter;
                           ENN=Date Filter];
                SourceExpr=DateFilter;
                Editable=False }

  }
  CODE
  {
    VAR
      GSTPaymentBuffer@1500010 : Record 16436;
      GSTInputTaxCredit@1500007 : Codeunit 16405;
      GSTINNo@1500000 : Code[15];
      PostingDate@1500003 : Date;
      ApplyBtnEnable@1500006 : Boolean;
      AccountNo@1500008 : Code[20];
      AccountType@1500009 : 'G/L Account,,,Bank Account';
      BankReferenceNo@1500012 : Code[10];
      BankReferenceDate@1500011 : Date;
      DateFilter@1500001 : Text[30];
      PostingDateErr@1500004 : TextConst '@@@="%1 = Date Filter";ENU=Posting date must be after %1. Settlement cannot be posted in already settled period.;ENN=Posting date must be after %1. Settlement cannot be posted in already settled period.';
      ISDGSTRegNoErr@1500044 : TextConst 'ENU=You must select GST Registration No. that has ISD set to False.';

    LOCAL PROCEDURE EnableApplyEntries@1500000();
    BEGIN
      ApplyBtnEnable := (GSTINNo <> '') AND (PostingDate <> 0D);
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE IsPostingDateAllowed@1500002();
    VAR
      PostedSettlementEntries@1500000 : Record 16409;
    BEGIN
      PostedSettlementEntries.SETRANGE("GST Registration No.",GSTINNo);
      IF PostedSettlementEntries.FINDLAST THEN BEGIN
        IF (DATE2DMY(PostingDate,2) < DATE2DMY(PostedSettlementEntries."Posting Date",2)) AND
           (DATE2DMY(PostingDate,3) = DATE2DMY(PostedSettlementEntries."Posting Date",3))
        THEN
          ERROR(PostingDateErr,PostedSettlementEntries."Period End Date");
        IF DATE2DMY(PostingDate,3) < DATE2DMY(PostedSettlementEntries."Posting Date",3) THEN
          ERROR(PostingDateErr,PostedSettlementEntries."Period End Date");
      END;
    END;

    BEGIN
    END.
  }
}

