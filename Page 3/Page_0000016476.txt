OBJECT Page 16476 Service Tax Payment
{
  OBJECT-PROPERTIES
  {
    Date=03-08-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.46621;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Service Tax Payment;
               ENN=Service Tax Payment];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table16474;
    SourceTableView=SORTING(Type,Service Tax Registration No.,Posting Date,Closed)
                    ORDER(Ascending)
                    WHERE(Type=FILTER(Sale|Charge),
                          Closed=CONST(No));
    PageType=Worksheet;
    OnInit=BEGIN
             ApplyBtnEnable := TRUE;
           END;

    OnOpenPage=BEGIN
                 RESET;
                 IF ReverseChargeTrans THEN BEGIN
                   SETRANGE(Type,Type::Purchase);
                   SETRANGE("Reverse Charge",TRUE);
                   SETRANGE("Closed Reverse Charge",FALSE);
                 END ELSE BEGIN
                   SETFILTER(Type,'%1|%2',Type::Sale,Type::Charge);
                   SETRANGE(Closed,FALSE);
                 END;
               END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1280025 ;1   ;Action    ;
                      Name=ApplyBtn;
                      ShortCutKey=Shift+F11;
                      CaptionML=[ENU=&Apply Entries;
                                 ENN=&Apply Entries];
                      Promoted=Yes;
                      Enabled=ApplyBtnEnable;
                      Image=ApplyEntries;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 PayServiceTaxForm@1280001 : Page 16477;
                                 ServTaxPayBuffer@1500001 : Record 16476;
                                 ExciseCenvatClaim@1500002 : Record 16584;
                               BEGIN
                                 IF ServiceTaxRegistrationNo = '' THEN
                                   ERROR(Text001);
                                 IF DateFilter = '' THEN
                                   ERROR(Text002);
                                 IF DocumentNo = '' THEN
                                   ERROR(Text003);
                                 IF PostingDate = 0D THEN
                                   ERROR(Text004);

                                 IF (ECCNo <> '') AND (RGSetOffDate = 0D) THEN
                                   ERROR(Text007);

                                 ServTaxPayBuffer.RESET;
                                 ServTaxPayBuffer.SETCURRENTKEY("Document No.");
                                 ServTaxPayBuffer.SETRANGE("Document No.",DocumentNo);
                                 ServTaxPayBuffer.DELETEALL;

                                 ExciseCenvatClaim.RESET;
                                 ExciseCenvatClaim.SETRANGE("Document No.","Document No.");
                                 ExciseCenvatClaim.DELETEALL;

                                 ServiceTaxEntryDetails.RESET;
                                 ServiceTaxEntryDetails.SETCURRENTKEY("Payment Document No.");
                                 ServiceTaxEntryDetails.SETRANGE("Payment Document No.",DocumentNo);
                                 IF ServiceTaxEntryDetails.FINDFIRST THEN
                                   ERROR(Text005,DocumentNo);

                                 ServiceTaxEntryDetails.RESET;
                                 ServiceTaxEntryDetails.SETCURRENTKEY("Payment Document No. (eCess)");
                                 ServiceTaxEntryDetails.SETRANGE("Payment Document No. (eCess)",DocumentNo);
                                 IF ServiceTaxEntryDetails.FINDFIRST THEN
                                   ERROR(Text005,DocumentNo);

                                 ServiceTaxEntryDetails.RESET;
                                 ServiceTaxEntryDetails.SETCURRENTKEY("Payment Doc. No. (SHECess)");
                                 ServiceTaxEntryDetails.SETRANGE("Payment Doc. No. (SHECess)",DocumentNo);
                                 IF ServiceTaxEntryDetails.FINDFIRST THEN
                                   ERROR(Text005,DocumentNo);

                                 ServiceTaxEntryDetails.RESET;
                                 ServiceTaxEntryDetails.SETCURRENTKEY("Reverse Charge Pmt. Doc. No.");
                                 ServiceTaxEntryDetails.SETRANGE("Reverse Charge Pmt. Doc. No.",DocumentNo);
                                 IF ServiceTaxEntryDetails.FINDFIRST THEN
                                   ERROR(Text005,DocumentNo);

                                 ServiceTaxEntryDetails.RESET;
                                 ServiceTaxEntryDetails.SETCURRENTKEY("Rev.Ch. Pmt. Doc. No. (eCESS)");
                                 ServiceTaxEntryDetails.SETRANGE("Rev.Ch. Pmt. Doc. No. (eCESS)",DocumentNo);
                                 IF ServiceTaxEntryDetails.FINDFIRST THEN
                                   ERROR(Text005,DocumentNo);

                                 ServiceTaxMgt.SetECCNo(ECCNo);
                                 ServiceTaxMgt.SetRGSetOffDate(RGSetOffDate);
                                 ServiceTaxMgt.CreateServiceTaxBuffer(DocumentNo,PostingDate,ServiceTaxRegistrationNo,DateFilter,ReverseChargeTrans);
                                 COMMIT;
                                 PayServiceTaxForm.SetDocument(DocumentNo,PostingDate,ServiceTaxRegistrationNo,DateFilter,ReverseChargeTrans);
                                 PayServiceTaxForm.RUNMODAL;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1280028;1;Group     ;
                CaptionML=[ENU=General;
                           ENN=General] }

    { 1280002;2;Field     ;
                CaptionML=[ENU=Service Tax Registration No.;
                           ENN=Service Tax Registration No.];
                SourceExpr=ServiceTaxRegistrationNo;
                TableRelation="Service Tax Registration Nos.".Code;
                LookupPageID=Service Tax Registration Nos.;
                OnValidate=BEGIN
                             FILTERGROUP(2);
                             SETRANGE("Service Tax Registration No.",ServiceTaxRegistrationNo);
                             FILTERGROUP(0);

                             ApplyBtnEnable := NOT (COUNT = 0);
                             ServiceTaxRegistrationNoOnAfte;
                           END;
                            }

    { 1280004;2;Field     ;
                CaptionML=[ENU=Date Filter;
                           ENN=Date Filter];
                SourceExpr=DateFilter;
                OnValidate=BEGIN
                             IF ApplicationManagement.MakeDateFilter(DateFilter) = 0 THEN;
                             SETFILTER("Posting Date",DateFilter);
                             DateFilter := GETFILTER("Posting Date");

                             ApplyBtnEnable := NOT (COUNT = 0);
                             DateFilterOnAfterValidate;
                           END;
                            }

    { 1500002;2;Field     ;
                CaptionML=[ENU=Reverse Charge;
                           ENN=Reverse Charge];
                SourceExpr=ReverseChargeTrans;
                OnValidate=BEGIN
                             ReverseChargeTransOnPush;
                             CurrPage.UPDATE;
                           END;
                            }

    { 1280006;2;Field     ;
                CaptionML=[ENU=Document No.;
                           ENN=Document No.];
                SourceExpr=DocumentNo }

    { 1280008;2;Field     ;
                CaptionML=[ENU=Posting Date;
                           ENN=Posting Date];
                SourceExpr=PostingDate }

    { 1500004;2;Field     ;
                Name=ECC;
                CaptionML=[ENU=Location E.C.C. No.;
                           ENN=Location E.C.C. No.];
                SourceExpr=ECCNo;
                OnValidate=BEGIN
                             IF ECCNo = '' THEN
                               SETRANGE("Location E.C.C. No.");
                             IF ReverseChargeTrans THEN
                               ERROR(Text16500);
                             ECCNoOnAfterValidate;
                           END;

                OnLookup=VAR
                           ECCNumber@1500001 : Record 13708;
                           Location@1500000 : Record 14;
                           CompanyInfo@1500004 : Record 79;
                         BEGIN
                           IF ReverseChargeTrans THEN
                             ERROR(Text16500);
                           Location.RESET;
                           Location.SETRANGE("Service Tax Registration No.",ServiceTaxRegistrationNo);
                           IF Location.FINDSET THEN
                             REPEAT
                               IF ECCNumber.GET(Location."E.C.C. No.") THEN
                                 ECCNumber.MARK := TRUE;
                             UNTIL Location.NEXT = 0;
                           CompanyInfo.GET;
                           IF ECCNumber.GET(CompanyInfo."E.C.C. No.") THEN
                             ECCNumber.MARK := TRUE;
                           ECCNumber.MARKEDONLY(TRUE);
                           IF PAGE.RUNMODAL(PAGE::"E.C.C. Nos.",ECCNumber) = ACTION::LookupOK THEN
                             ECCNo := ECCNumber.Code;
                           SETRANGE("Location E.C.C. No.",ECCNo);
                           ECCNumber.CLEARMARKS;
                         END;
                          }

    { 1500010;2;Field     ;
                Name=RGDate;
                CaptionML=[ENU=RG Set Off Date;
                           ENN=RG Set Off Date];
                SourceExpr=RGSetOffDate;
                OnValidate=BEGIN
                             IF ECCNo = '' THEN
                               ERROR(Text006);
                           END;
                            }

    { 1280000;1;Group     ;
                GroupType=Repeater }

    { 1280034;2;Field     ;
                SourceExpr=Type }

    { 1500000;2;Field     ;
                SourceExpr="Document Type" }

    { 1280012;2;Field     ;
                SourceExpr="Service Tax Registration No." }

    { 1280032;2;Field     ;
                SourceExpr="Document No." }

    { 1280001;2;Field     ;
                SourceExpr="Posting Date" }

    { 1280030;2;Field     ;
                SourceExpr="Base Amount" }

    { 1280036;2;Field     ;
                SourceExpr="Service Tax Amount" }

    { 1500008;2;Field     ;
                SourceExpr="Service Tax SBC Amount" }

    { 1280026;2;Field     ;
                SourceExpr="Service Tax eCess Amount" }

    { 1500006;2;Field     ;
                SourceExpr="Service Tax SHE Cess Amount" }

    { 1500001;2;Field     ;
                SourceExpr="KK Cess Amount" }

  }
  CODE
  {
    VAR
      ServiceTaxEntryDetails@1500001 : Record 16474;
      ApplicationManagement@1280006 : Codeunit 1;
      ServiceTaxMgt@1280004 : Codeunit 16471;
      DocumentNo@1280000 : Code[20];
      ServiceTaxRegistrationNo@1280002 : Code[20];
      DateFilter@1280003 : Text[30];
      Text001@1280005 : TextConst 'ENU=Service Tax registration No. cannot be Left Blank.;ENN=Service Tax registration No. cannot be Left Blank.';
      PostingDate@1280001 : Date;
      Text002@1280007 : TextConst 'ENU=Date Filter cannot be Left Blank.;ENN=Date Filter cannot be Left Blank.';
      Text003@1280008 : TextConst 'ENU=Document No. cannot be Left Blank.;ENN=Document No. cannot be Left Blank.';
      Text004@1280009 : TextConst 'ENU=Posting date cannot be Left Blank.;ENN=Posting date cannot be Left Blank.';
      ReverseChargeTrans@1500000 : Boolean;
      Text005@1500002 : TextConst 'ENU=Document No. %1 already exists.;ENN=Document No. %1 already exists.';
      ECCNo@1500003 : Code[20];
      Text16500@1500004 : TextConst 'ENU=Location E.C.C. No cannot be selected for Reverse Charge transactions.;ENN=Location E.C.C. No cannot be selected for Reverse Charge transactions.';
      RGSetOffDate@1500005 : Date;
      Text006@1500006 : TextConst 'ENU=Please Enter The Location E.C.C. No.;ENN=Please Enter The Location E.C.C. No.';
      Text007@1500007 : TextConst 'ENU=Please Enter The RG Setoff Date.;ENN=Please Enter The RG Setoff Date.';
      ApplyBtnEnable@19030541 : Boolean INDATASET;

    LOCAL PROCEDURE ServiceTaxRegistrationNoOnAfte@19027087();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE DateFilterOnAfterValidate@19006009();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ReverseChargeTransOnAfterValid@19037431();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ECCNoOnAfterValidate@19007122();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ReverseChargeTransOnPush@19001072();
    BEGIN
      RESET;
      IF ReverseChargeTrans THEN BEGIN
        SETRANGE(Type,Type::Purchase);
        SETRANGE("Reverse Charge",TRUE);
        SETRANGE("Closed Reverse Charge",FALSE);
      END ELSE BEGIN
        SETFILTER(Type,'%1|%2',Type::Sale,Type::Charge);
        SETRANGE(Closed,FALSE);
      END;
    END;

    BEGIN
    END.
  }
}

