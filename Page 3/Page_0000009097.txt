OBJECT Page 9097 Values Lookup
{
  OBJECT-PROPERTIES
  {
    Date=23-02-17;
    Time=[ 6:00:00 AM];
    Modified=Yes;
    Version List=S-02,S-05;
  }
  PROPERTIES
  {
    CaptionML=ENU=Values Lookup;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table370;
    DataCaptionExpr=Formula;
    PageType=ListPlus;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 lRec_Field@1000 : Record 2000000041;
               BEGIN
                 //S-00{
                 gCoU_NAVLib.ServiceInit(gTxt_ServiceID);

                 gInt_MaxLoopsForDistinctValues := 1000;

                 gInt_NbrOfColumns := 6;
                 lRec_Field.SETRANGE(TableNo, gRec_Field.TableNo);
                 IF (lRec_Field.COUNT < gInt_NbrOfColumns) THEN
                   gInt_NbrOfColumns := lRec_Field.COUNT;
                 SetColumns;

                 LoadData;

                 CurrPage.CAPTION := gRec_Field.TableName + ' ' + CurrPage.CAPTION;
                 //S-00}
               END;

    ActionList=ACTIONS
    {
      { 12      ;    ;ActionContainer;
                      Name=ActionContainer;
                      ActionContainerType=ActionItems }
      { 13      ;1   ;Action    ;
                      Name=Load Data;
                      CaptionML=ENU=Load Data;
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ImportDatabase;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 //S-00{
                                 LoadData;
                                 //S-00}
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=ContentArea;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                Name=General;
                CaptionML=ENU=General;
                GroupType=Group }

    { 3   ;2   ;Field     ;
                CaptionML=ENU=Max Loops for Distinct Values;
                SourceExpr=gInt_MaxLoopsForDistinctValues;
                MinValue=1;
                MaxValue=2147483647;
                OnValidate=BEGIN
                             //S-00{
                             CurrPage.UPDATE(FALSE);
                             //S-00}
                           END;
                            }

    { 14  ;2   ;Field     ;
                CaptionML=ENU=Descending Load Data;
                SourceExpr=gBol_DescendingLoadData;
                OnValidate=BEGIN
                             //S-00{
                             CurrPage.UPDATE(FALSE);
                             //S-00}
                           END;
                            }

    { 10  ;2   ;Field     ;
                SourceExpr=gTxt_LookupValue;
                CaptionClass=gTxt_LookupCaption;
                Visible=(gTxt_LookupCaption <> '');
                OnValidate=BEGIN
                             //S-00{
                             CurrPage.UPDATE(FALSE);
                             //S-00}
                           END;
                            }

    { 4   ;1   ;Group     ;
                Name=Repeater;
                GroupType=Repeater }

    { 6   ;2   ;Field     ;
                SourceExpr=Formula;
                CaptionClass=gTxA_Caption[1];
                Visible=gBol_Visible1;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               //S-00{
                               gTxt_LookupValue += Formula;
                               CurrPage.UPDATE(FALSE);
                               //S-00}
                             END;
                              }

    { 7   ;2   ;Field     ;
                SourceExpr=Formula2;
                CaptionClass=gTxA_Caption[2];
                Visible=gBol_Visible2;
                Editable=FALSE;
                OnAssistEdit=BEGIN
                               //S-00{
                               ChangeColumn(2);
                               //S-00}
                             END;
                              }

    { 5   ;2   ;Field     ;
                SourceExpr=Formula3;
                CaptionClass=gTxA_Caption[3];
                Visible=gBol_Visible3;
                OnAssistEdit=BEGIN
                               //S-00{
                               ChangeColumn(3);
                               //S-00}
                             END;
                              }

    { 8   ;2   ;Field     ;
                SourceExpr=Formula4;
                CaptionClass=gTxA_Caption[4];
                Visible=gBol_Visible4;
                OnAssistEdit=BEGIN
                               //S-00{
                               ChangeColumn(4);
                               //S-00}
                             END;
                              }

    { 9   ;2   ;Field     ;
                SourceExpr=Comment;
                CaptionClass=gTxA_Caption[5];
                Visible=gBol_Visible5;
                OnAssistEdit=BEGIN
                               //S-00{
                               ChangeColumn(5);
                               //S-00}
                             END;
                              }

    { 11  ;2   ;Field     ;
                SourceExpr="Cell Value as Text";
                CaptionClass=gTxA_Caption[6];
                Visible=gBol_Visible6;
                OnAssistEdit=BEGIN
                               //S-00{
                               ChangeColumn(6);
                               //S-00}
                             END;
                              }

  }
  CODE
  {
    VAR
      gCoU_NAVLib@1012 : Codeunit 31;
      gRec_Field@1003 : Record 2000000041;
      gInt_MaxLoopsForDistinctValues@1000 : Integer;
      gInt_NbrOfColumns@1001 : Integer;
      gInA_FieldNo@1006 : ARRAY [6] OF Integer;
      gTxA_Caption@1002 : ARRAY [6] OF Text;
      gTxt_ServiceID@1015 : Text;
      gTxt_LookupValue@1014 : Text;
      gTxt_LookupCaption@1005 : Text;
      gBol_DescendingLoadData@1013 : Boolean;
      gBol_Visible1@1007 : Boolean;
      gBol_Visible2@1004 : Boolean;
      gBol_Visible3@1008 : Boolean;
      gBol_Visible4@1009 : Boolean;
      gBol_Visible5@1010 : Boolean;
      gBol_Visible6@1011 : Boolean;

    PROCEDURE SetLookupValue@1(pTxt_ServiceID@1004 : Text;pInt_TableNo@1000 : Integer;pInt_FieldNo@1001 : Integer;pTxt_LookupValue@1002 : Text;pTxt_LookupCaption@1003 : Text);
    BEGIN
      //S-00{
      gTxt_ServiceID := pTxt_ServiceID;
      IF gRec_Field.GET(pInt_TableNo, pInt_FieldNo) THEN;
      gTxt_LookupValue := pTxt_LookupValue;
      gTxt_LookupCaption := pTxt_LookupCaption;
      //S-00}
    END;

    PROCEDURE GetLookupValue_Txt@2() Text : Text;
    BEGIN
      //S-00{
      Text := gTxt_LookupValue;
      //S-00}
    END;

    PROCEDURE SetSelection@5(pTxt_ServiceID@1004 : Text;pInt_TableNo@1000 : Integer;pInt_FieldNo@1001 : Integer);
    BEGIN
      //S-00{
      gTxt_ServiceID := pTxt_ServiceID;
      IF gRec_Field.GET(pInt_TableNo, pInt_FieldNo) THEN;
      //S-00}
    END;

    PROCEDURE GetSelection@7(VAR vReT_ExcelBuffer@1000 : TEMPORARY Record 370);
    BEGIN
      //S-00{
      CurrPage.SETSELECTIONFILTER(Rec);
      IF FINDSET THEN BEGIN
        vReT_ExcelBuffer.RESET;
        vReT_ExcelBuffer.DELETEALL;
        REPEAT
          vReT_ExcelBuffer.INIT;
          vReT_ExcelBuffer.TRANSFERFIELDS(Rec);
          vReT_ExcelBuffer.INSERT;
        UNTIL (NEXT = 0);
      END;
      //S-00}
    END;

    LOCAL PROCEDURE ChangeColumn@6(pInt_ColumnNoFrom1@1000 : Integer);
    VAR
      lRec_Field@1002 : Record 2000000041;
    BEGIN
      //S-00{
      //Column No should between 1 and Total Number of Columns
      IF NOT (pInt_ColumnNoFrom1 IN [1..gInt_NbrOfColumns]) THEN
        EXIT;

      WITH lRec_Field DO BEGIN
        FILTERGROUP(2);
        SETRANGE(TableNo, gRec_Field.TableNo);
        FILTERGROUP(0);
        IF GET(gRec_Field.TableNo, gInA_FieldNo[pInt_ColumnNoFrom1]) THEN;
        IF (PAGE.RUNMODAL(PAGE::"Fields Lookup", lRec_Field, "No.") = ACTION::LookupOK) THEN
          IF (gInA_FieldNo[pInt_ColumnNoFrom1] <> "No.") THEN BEGIN
            SetColumn(pInt_ColumnNoFrom1, "No.");
            CASE pInt_ColumnNoFrom1 OF
              2: Rec.MODIFYALL(Formula2, '');
              3: Rec.MODIFYALL(Formula3, '');
              4: Rec.MODIFYALL(Formula4, '');
              5: Rec.MODIFYALL(Comment, '');
              6: Rec.MODIFYALL("Cell Value as Text", '');
            END;
            CurrPage.UPDATE(FALSE);
          END;
      END;
      //S-00}
    END;

    LOCAL PROCEDURE SetColumn@14(pInt_ColumnNoFrom1@1000 : Integer;pInt_FieldNo@1001 : Integer);
    VAR
      lRec_Field@1002 : Record 2000000041;
    BEGIN
      //S-00{
      //Column No should between 1 and Total Number of Columns
      IF NOT (pInt_ColumnNoFrom1 IN [1..gInt_NbrOfColumns]) THEN
        EXIT;

      //Field no should exist
      IF (pInt_FieldNo = 0) THEN
        EXIT;

      IF lRec_Field.GET(gRec_Field.TableNo, pInt_FieldNo) THEN BEGIN
        gInA_FieldNo[pInt_ColumnNoFrom1] := lRec_Field."No.";
        gTxA_Caption[pInt_ColumnNoFrom1] := lRec_Field."Field Caption";
      END;
      //S-00}
    END;

    LOCAL PROCEDURE SetColumns@4();
    VAR
      lRec_Field@1001 : Record 2000000041;
      i@1000 : Integer;
    BEGIN
      //S-00{
      //Field No. should exist
      IF (gRec_Field."No." = 0) THEN
        EXIT;

      //Clear Captions
      FOR i := 1 TO ARRAYLEN(gTxA_Caption) DO
        gTxA_Caption[i] := ' ';

      //Set the First Column
      gBol_Visible1 := TRUE; //Cannot use Array, for v07.00 compatible
      SetColumn(1, gRec_Field."No.");

      //Set the Rest Columns
      IF (gInt_NbrOfColumns > 1) THEN BEGIN
        lRec_Field.SETRANGE(TableNo, gRec_Field.TableNo);
        IF lRec_Field.FINDFIRST THEN;
        FOR i := 2 TO gInt_NbrOfColumns DO BEGIN
          IF (lRec_Field."No." = gRec_Field."No.") THEN
            lRec_Field.NEXT;
          CASE i OF
            2: gBol_Visible2 := TRUE;
            3: gBol_Visible3 := TRUE;
            4: gBol_Visible4 := TRUE;
            5: gBol_Visible5 := TRUE;
            6: gBol_Visible6 := TRUE;
          END;
          SetColumn(i, lRec_Field."No.");
          lRec_Field.NEXT;
        END;
      END;
      //S-00}
    END;

    LOCAL PROCEDURE LoadData@3();
    BEGIN
      //S-00{
      RESET;
      DELETEALL;
      gCoU_NAVLib.LoadLookupValues(gRec_Field.TableNo, gInA_FieldNo, Rec,
        gInt_NbrOfColumns, gInt_MaxLoopsForDistinctValues, gBol_DescendingLoadData);
      IF FINDFIRST THEN;
      //S-00}
    END;

    BEGIN
    {
      Code  | Version | Author              | Description
      -----------------------------------------------------------------------------------------------------------------------------------
      S-02    140628    www.4BzSoftware.com   Query and Excel Report.
      S-05    150502    www.4BzSoftware.com   Universal Excel Import Export.
    }
    END.
  }
}

