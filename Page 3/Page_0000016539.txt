OBJECT Page 16539 VAT Adjustment Journal
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    CaptionML=[ENU=VAT Adjustment Journal;
               ENN=VAT Adjustment Journal];
    SaveValues=Yes;
    InsertAllowed=No;
    SourceTable=Table16587;
    DelayedInsert=Yes;
    DataCaptionFields=Journal Batch Name;
    PageType=Worksheet;
    AutoSplitKey=Yes;
    OnOpenPage=VAR
                 JnlSelected@1280000 : Boolean;
               BEGIN
                 BalAccName := '';
                 OpenedFromBatch := ("Journal Batch Name" <> '') AND ("Journal Template Name" = '');
                 IF OpenedFromBatch THEN BEGIN
                   CurrentJnlBatchName := "Journal Batch Name";
                   GenJnlManagement.OpenTaxJnl(CurrentJnlBatchName,Rec);
                   EXIT;
                 END;
                 GenJnlManagement.TaxTemplateSelection(PAGE::"VAT Adjustment Journal",2,Rec,JnlSelected);
                 IF NOT JnlSelected THEN
                   ERROR('');
                 GenJnlManagement.OpenTaxJnl(CurrentJnlBatchName,Rec);
               END;

    OnAfterGetRecord=BEGIN
                       ShowShortcutDimCode(ShortcutDimCode);
                       AfterGetCurrentRecord;
                     END;

    OnNewRecord=BEGIN
                  SetUpNewLine(xRec,Balance,FALSE);
                  CLEAR(AccName);
                  AfterGetCurrentRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1500005 ;1   ;ActionGroup;
                      CaptionML=[ENU=&Line;
                                 ENN=&Line];
                      Image=Line }
      { 1500008 ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[ENU=&Dimensions;
                                 ENN=&Dimensions];
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDimensions;
                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 1500013 ;2   ;Action    ;
                      CaptionML=[ENU=&VAT Adjustment Detail;
                                 ENN=&VAT Adjustment Detail];
                      RunObject=Page 16343;
                      RunPageLink=Journal Template Name=FIELD(Journal Template Name),
                                  Journal Batch Name=FIELD(Journal Batch Name),
                                  Line No.=FIELD(Line No.);
                      Image=EditAdjustments }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1500000 ;1   ;ActionGroup;
                      CaptionML=[ENU=P&osting;
                                 ENN=P&osting];
                      Image=Post }
      { 1500007 ;2   ;Action    ;
                      ShortCutKey=F9;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Post;
                                 ENN=Post];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Post;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 // VATManagement.PostAdjustmentJournal(Rec);
                                 TaxJournalMgt.PostTaxJournal(Rec);
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1500014;1;Group     ;
                CaptionML=[ENU=General;
                           ENN=General] }

    { 1500045;2;Field     ;
                Lookup=Yes;
                CaptionML=[ENU=Batch Name;
                           ENN=Batch Name];
                SourceExpr=CurrentJnlBatchName;
                Visible=TRUE;
                OnValidate=BEGIN
                             GenJnlManagement.CheckNameTax(CurrentJnlBatchName,Rec);
                             CurrentJnlBatchNameOnAfterVali;
                           END;

                OnLookup=BEGIN
                           CurrPage.SAVERECORD;
                           GenJnlManagement.LookupNameTax(CurrentJnlBatchName,Rec);
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1500043;2;Field     ;
                CaptionML=[ENU=VAT Entry No.;
                           ENN=VAT Entry No.];
                BlankZero=Yes;
                SourceExpr="Transaction No";
                TableRelation="Detailed Tax Entry"."Entry No." WHERE (Settlement=FILTER(No),
                                                                      Entry Type=FILTER(Initial Entry),
                                                                      Document Type=FILTER(<>Credit Memo),
                                                                      Transaction Type=FILTER(Purchase),
                                                                      Item Charge Entry=CONST(No));
                OnValidate=VAR
                             TaxJnlLine@1280003 : Record 16587;
                             FALedgerEntry@1500013 : Record 5601;
                             DepreciationBook@1500014 : Record 5611;
                             GLEntry@1500015 : Record 17;
                             LineNo@1280002 : Integer;
                             DocumentNo@1280001 : Code[20];
                             Vend@1500001 : Record 23;
                             Cust@1500002 : Record 18;
                             GeneralPostingSetup@1500003 : Record 252;
                             Item@1500004 : Record 27;
                             FixedAsset@1500005 : Record 5600;
                             GLAccount@1500006 : Record 15;
                             ItemLedgerEntry@1500007 : Record 32;
                             DimMgt@1500009 : Codeunit 408;
                             Text16501@1500012 : TextConst 'ENU="VAT Adj. Entry For Doc. No. ";ENN="VAT Adj. Entry For Doc. No. "';
                           BEGIN
                             VATAmountIN := 0;
                             VATBaseAmountIN := 0;
                             InputCreditOutputTaxAmount := 0;
                             AmountLoadedonItem := 0;
                             OriginalQuantity := 0;
                             Counter := 0;
                             TaxJurisdictionCode := '';
                             TaxJnlBatch.GET("Journal Template Name","Journal Batch Name");
                             IF TaxJnlBatch."No. Series" <> '' THEN BEGIN
                               CLEAR(NoSeriesMgt);
                               DocumentNo := NoSeriesMgt.TryGetNextNo(TaxJnlBatch."No. Series","Posting Date");
                             END;

                             TaxJnlLine.LOCKTABLE;

                             TaxJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                             TaxJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                             IF TaxJnlLine.FINDLAST THEN
                               LineNo := TaxJnlLine."Line No." + 10000
                             ELSE
                               LineNo := 10000;
                             DetailedTaxEntry.GET("Transaction No");

                             IF DetailedTaxEntry.Deferment THEN BEGIN
                               MESSAGE(Text16500,DetailedTaxEntry."Main Component Entry No.");
                               "Transaction No" := DetailedTaxEntry."Main Component Entry No.";
                             END ELSE
                               "Transaction No" := DetailedTaxEntry."Entry No.";

                             DetailedTaxEntry2.RESET;
                             DetailedTaxEntry2.SETCURRENTKEY("Document No.","Document Line No.","Main Component Entry No.",
                               "Deferment No.","Tax Jurisdiction Code");
                             IF DetailedTaxEntry.Type IN [DetailedTaxEntry.Type::"G/L Account",DetailedTaxEntry.Type::"Fixed Asset"] THEN
                               DetailedTaxEntry2.SETRANGE("Main Component Entry No.",DetailedTaxEntry."Main Component Entry No.")
                             ELSE
                               DetailedTaxEntry2.SETRANGE("Item Ledger Entry No.",DetailedTaxEntry."Item Ledger Entry No.");
                             DetailedTaxEntry2.SETRANGE("Deferment No.",0);
                             DetailedTaxEntry2.SETRANGE("Entry Type",DetailedTaxEntry2."Entry Type"::"Initial Entry");
                             IF DetailedTaxEntry2.FINDSET THEN
                               REPEAT
                                 DetailedTaxEntry3.RESET;
                                 DetailedTaxEntry3.SETCURRENTKEY("Document No.","Document Line No.","Main Component Entry No.",
                                   "Deferment No.","Tax Jurisdiction Code");
                                 DetailedTaxEntry3.SETRANGE("Document No.",DetailedTaxEntry2."Document No.");
                                 DetailedTaxEntry3.SETRANGE("Document Line No.",DetailedTaxEntry2."Document Line No.");
                                 DetailedTaxEntry3.SETRANGE("Main Component Entry No.",DetailedTaxEntry2."Main Component Entry No.");
                                 DetailedTaxEntry3.SETRANGE("Deferment No.");
                                 DetailedTaxEntry3.SETRANGE("Tax Jurisdiction Code",DetailedTaxEntry2."Tax Jurisdiction Code");
                                 DetailedTaxEntry3.CALCSUMS("Input Credit/Output Tax Amount","Amount Loaded on Item","Remaining Quantity",
                                   "Tax Amount","Tax Base Amount");
                                 InserVATAdjustmentBuffer(DetailedTaxEntry2,LineNo);
                               UNTIL DetailedTaxEntry2.NEXT = 0;

                             TaxJnlLine.INIT;
                             TaxJnlLine."Document No." := DetailedTaxEntry."Document No.";
                             TaxJnlLine."Journal Template Name" := "Journal Template Name";
                             TaxJnlLine."Journal Batch Name" := "Journal Batch Name";
                             TaxJnlLine."Line No." := LineNo;
                             TaxJnlLine.Adjustment := TRUE;
                             TaxJnlLine."Posting Date" := WORKDATE;
                             IF DetailedTaxEntry."Source Type" = DetailedTaxEntry."Source Type"::Vendor THEN BEGIN
                               Vend.GET(DetailedTaxEntry."Source No.");
                               GenBussinessPostingGroup := Vend."Gen. Bus. Posting Group"
                             END;
                             IF DetailedTaxEntry."Source Type" = DetailedTaxEntry."Source Type"::Customer THEN BEGIN
                               Cust.GET(DetailedTaxEntry."Source No.");
                               GenBussinessPostingGroup := Cust."Gen. Bus. Posting Group"
                             END;
                             CASE DetailedTaxEntry.Type OF
                               DetailedTaxEntry.Type::Item:
                                 BEGIN
                                   Item.GET(DetailedTaxEntry."No.");
                                   GenProductPostingGroup := Item."Gen. Prod. Posting Group"
                                 END;
                               DetailedTaxEntry.Type::"Fixed Asset":
                                 BEGIN
                                   FixedAsset.GET(DetailedTaxEntry."No.");
                                   GenProductPostingGroup := FixedAsset."Gen. Prod. Posting Group"
                                 END;
                               DetailedTaxEntry.Type::"G/L Account":
                                 BEGIN
                                   GLAccount.GET(DetailedTaxEntry."No.");
                                   GenProductPostingGroup := GLAccount."Gen. Prod. Posting Group"
                                 END;
                             END;
                             GeneralPostingSetup.GET(GenBussinessPostingGroup,GenProductPostingGroup);
                             TaxJnlLine."Account Type" := TaxJnlLine."Account Type"::"G/L Account";
                             IF DetailedTaxEntry."Transaction Type" = DetailedTaxEntry."Transaction Type"::Purchase THEN BEGIN
                               CASE DetailedTaxEntry.Type OF
                                 DetailedTaxEntry.Type::Item:
                                   TaxJnlLine."Account No." := GeneralPostingSetup."Purch. Account";
                                 DetailedTaxEntry.Type::"Fixed Asset":
                                   BEGIN
                                     FALedgerEntry.RESET;
                                     FALedgerEntry.SETRANGE("FA No.",DetailedTaxEntry."No.");
                                     FALedgerEntry.SETRANGE("Document Type",DetailedTaxEntry."Document Type");
                                     FALedgerEntry.SETRANGE("Document No.",DetailedTaxEntry."Document No.");
                                     DepreciationBook.RESET;
                                     DepreciationBook.SETRANGE("FA Book Type",DepreciationBook."FA Book Type"::" ");
                                     DepreciationBook.SETRANGE("G/L Integration - Acq. Cost",TRUE);
                                     IF DepreciationBook.FINDFIRST THEN
                                       FALedgerEntry.SETRANGE("Depreciation Book Code",DepreciationBook.Code);
                                     IF FALedgerEntry.FINDFIRST THEN BEGIN
                                       GLEntry.GET(FALedgerEntry."G/L Entry No.");
                                       TaxJnlLine."Account No." := GLEntry."G/L Account No.";
                                     END;
                                   END;
                                 DetailedTaxEntry.Type::"G/L Account":
                                   TaxJnlLine."Account No." := DetailedTaxEntry."No.";
                               END;
                             END ELSE
                               TaxJnlLine."Account No." := GeneralPostingSetup."Sales Account";

                             TaxJnlLine."Document Type" := DetailedTaxEntry."Document Type";
                             TaxJnlLine."Bal. Account No." := DetailedTaxEntry."G/L Account No.";
                             TaxJnlLine.Description := Text16501 + DetailedTaxEntry."Document No.";
                             TaxJnlLine."VAT Percentage IN" := DetailedTaxEntry."Tax %";
                             TaxJnlLine."VAT Amount IN" := VATAmountIN;
                             TaxJnlLine."VAT Base Amount IN" := VATBaseAmountIN;
                             TaxJnlLine."Input Credit/Output Tax Amount" := InputCreditOutputTaxAmount;
                             TaxJnlLine."Amount Loaded on Item" := AmountLoadedonItem;
                             IF Counter <> 0 THEN
                               TaxJnlLine."Original Quantity" := OriginalQuantity / Counter;
                             TaxJnlLine."Original Location" := DetailedTaxEntry."Location Code";
                             TaxJnlLine."Original Non ITC Claim Usage %" := DetailedTaxEntry."Non ITC Claimable Usage %";
                             TaxJnlLine."VAT Transaction No." := "Transaction No";
                             TaxJnlLine."System-Created Entry" := TRUE;
                             TaxJnlLine."VAT Adjustment Entry" := TRUE;
                             IF DetailedTaxEntry.Type = DetailedTaxEntry.Type::Item THEN BEGIN
                               ItemLedgerEntry.GET(DetailedTaxEntry."Item Ledger Entry No.");
                               TaxJnlLine."Dimension Set ID" := ItemLedgerEntry."Dimension Set ID";
                               DimMgt.UpdateGlobalDimFromDimSetID(ItemLedgerEntry."Dimension Set ID",TaxJnlLine."Shortcut Dimension 1 Code",
                                 TaxJnlLine."Shortcut Dimension 2 Code");
                             END ELSE
                               IF DetailedTaxEntry.Type = DetailedTaxEntry.Type::"Fixed Asset" THEN
                                 CreateDimensions(DATABASE::"Fixed Asset",FixedAsset."No.",TaxJnlLine)
                               ELSE
                                 IF DetailedTaxEntry.Type = DetailedTaxEntry.Type::"G/L Account" THEN
                                   CreateDimensions(DATABASE::"G/L Account",GLAccount."No.",TaxJnlLine);
                             TaxJnlLine.INSERT;
                             TransactionNoOnAfterValidate;
                           END;
                            }

    { 1500047;1;Group     ;
                GroupType=Repeater }

    { 1500048;2;Field     ;
                SourceExpr="Posting Date" }

    { 1500050;2;Field     ;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 1500052;2;Field     ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 1500054;2;Field     ;
                SourceExpr="Account Type";
                Editable=FALSE }

    { 1500056;2;Field     ;
                SourceExpr="Account No.";
                Editable=FALSE }

    { 1500058;2;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 1500160;2;Field     ;
                SourceExpr="VAT Adjustment Type" }

    { 1500062;2;Field     ;
                SourceExpr="VAT Base Amount IN";
                Editable=FALSE }

    { 1500064;2;Field     ;
                SourceExpr="VAT Amount IN";
                Editable=FALSE }

    { 1500066;2;Field     ;
                SourceExpr="VAT Percentage IN";
                Editable=FALSE }

    { 1500068;2;Field     ;
                SourceExpr="Original Location" }

    { 1500070;2;Field     ;
                SourceExpr="Location Code";
                OnValidate=BEGIN
                             IF ("VAT Adjustment Type" = "VAT Adjustment Type"::"Change in Usage") OR
                                ("VAT Adjustment Type" = "VAT Adjustment Type"::"Lost/Destroy") OR
                                ("VAT Adjustment Type" = "VAT Adjustment Type"::Consumed)
                             THEN
                               TESTFIELD("Location Code",'');
                           END;
                            }

    { 1500072;2;Field     ;
                SourceExpr="Original Quantity" }

    { 1500074;2;Field     ;
                SourceExpr="Quantity to be Adjusted" }

    { 1500076;2;Field     ;
                SourceExpr="Original Non ITC Claim Usage %" }

    { 1500078;2;Field     ;
                SourceExpr="Non ITC Claimable Usage %";
                OnValidate=BEGIN
                             IF ("VAT Adjustment Type" = "VAT Adjustment Type"::"Branch Transfer") OR
                                ("VAT Adjustment Type" = "VAT Adjustment Type"::"Lost/Destroy") OR
                                ("VAT Adjustment Type" = "VAT Adjustment Type"::Consumed)
                             THEN
                               TESTFIELD("Non ITC Claimable Usage %",0);
                           END;
                            }

    { 1500080;2;Field     ;
                SourceExpr="Input Credit/Output Tax Amount" }

    { 1500082;2;Field     ;
                SourceExpr="Amount Loaded on Item" }

    { 1500084;2;Field     ;
                CaptionML=[ENU=Amount Of Adjustment On Item;
                           ENN=Amount Of Adjustment On Item];
                SourceExpr=Amount;
                Editable=FALSE }

    { 1500086;2;Field     ;
                SourceExpr="Bal. Account Type";
                Editable=FALSE }

    { 1500088;2;Field     ;
                SourceExpr="Bal. Account No.";
                Editable=FALSE;
                OnValidate=BEGIN
                             GenJnlManagement.GetAccountsTax(Rec,AccName,BalAccName);
                             ShowShortcutDimCode(ShortcutDimCode);
                           END;
                            }

    { 1500009;2;Field     ;
                SourceExpr="Shortcut Dimension 1 Code" }

    { 1500011;2;Field     ;
                SourceExpr="Shortcut Dimension 2 Code" }

    { 1500006;1;Group      }

    { 1500004;2;Field     ;
                CaptionClass=Text19021552 }

    { 1500003;2;Field     ;
                CaptionML=[ENU=Account Name;
                           ENN=Account Name];
                SourceExpr=AccName;
                Editable=FALSE }

    { 1500001;2;Field     ;
                CaptionML=[ENU=Bal. Account Name;
                           ENN=Bal. Account Name];
                SourceExpr=BalAccName;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      TaxJnlBatch@1500007 : Record 16586;
      DetailedTaxEntry@1500004 : Record 16522;
      GenJnlManagement@1500014 : Codeunit 230;
      NoSeriesMgt@1500006 : Codeunit 396;
      Balance@1500010 : Decimal;
      CurrentJnlBatchName@1500013 : Code[10];
      ShortcutDimCode@1500009 : ARRAY [8] OF Code[20];
      GenBussinessPostingGroup@1500002 : Code[10];
      GenProductPostingGroup@1500000 : Code[10];
      AccName@1500012 : Text[50];
      BalAccName@1500011 : Text[50];
      "Transaction No"@1500008 : Integer;
      Text16500@1500001 : TextConst 'ENU=This is a Deferment Entry and the parent entry no. %1 has been selected automatically.;ENN=This is a Deferment Entry and the parent entry no. %1 has been selected automatically.';
      TaxJournalMgt@1500003 : Codeunit 16508;
      OpenedFromBatch@1500015 : Boolean;
      DetailedTaxEntry2@1500024 : Record 16522;
      DetailedTaxEntry3@1500023 : Record 16522;
      VATAmountIN@1500022 : Decimal;
      VATBaseAmountIN@1500021 : Decimal;
      InputCreditOutputTaxAmount@1500020 : Decimal;
      AmountLoadedonItem@1500019 : Decimal;
      OriginalQuantity@1500018 : Decimal;
      TaxJurisdictionCode@1500017 : Code[20];
      Counter@1500016 : Integer;
      Text19021552@19017106 : TextConst 'ENU=Account Type;ENN=Account Type';

    PROCEDURE CreateDimensions@1500000(Type1@1500000 : Integer;No1@1500001 : Code[10];VAR TaxJournalLine@1500002 : Record 16587);
    VAR
      TableID@1500003 : ARRAY [10] OF Integer;
      No@1500004 : ARRAY [10] OF Code[20];
      DimMgt@1500005 : Codeunit 408;
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" :=
        DimMgt.GetDefaultDimID(
          TableID,No,TaxJournalLine."Source Code",TaxJournalLine."Shortcut Dimension 1 Code",
          TaxJournalLine."Shortcut Dimension 2 Code",0,0);
      DimMgt.UpdateDefaultDim(DATABASE::"Tax Journal Line",No1,
        TaxJournalLine."Shortcut Dimension 1 Code",TaxJournalLine."Shortcut Dimension 2 Code");
    END;

    LOCAL PROCEDURE InserVATAdjustmentBuffer@1500012(DetailedTaxEntry@1500003 : Record 16522;TaxJnlLineNo@1500000 : Integer);
    VAR
      VATAdjustmentBuffer@1500008 : Record 16481;
      TaxJurisdiction@1500006 : Record 320;
    BEGIN
      VATAdjustmentBuffer.RESET;
      VATAdjustmentBuffer.SETRANGE("Entry No.",DetailedTaxEntry."Entry No.");
      IF NOT VATAdjustmentBuffer.FINDFIRST THEN BEGIN
        VATAdjustmentBuffer.INIT;
        VATAdjustmentBuffer."Journal Template Name" := "Journal Template Name";
        VATAdjustmentBuffer."Journal Batch Name" := "Journal Batch Name";
        VATAdjustmentBuffer."Document Type" := DetailedTaxEntry."Document Type";
        VATAdjustmentBuffer."Document No." := DetailedTaxEntry."Document No.";
        VATAdjustmentBuffer."Line No." := TaxJnlLineNo;
        VATAdjustmentBuffer."Entry No." := DetailedTaxEntry."Entry No.";
        VATAdjustmentBuffer."Transaction Type" := DetailedTaxEntry."Transaction Type";
        VATAdjustmentBuffer."Posting Date" := DetailedTaxEntry."Posting Date";
        VATAdjustmentBuffer.Type := DetailedTaxEntry.Type;
        VATAdjustmentBuffer."No." := DetailedTaxEntry."No.";
        VATAdjustmentBuffer."Source Type" := DetailedTaxEntry."Source Type";
        VATAdjustmentBuffer."Source No." := DetailedTaxEntry."Source No.";
        VATAdjustmentBuffer."Non ITC Claimable Usage %" := DetailedTaxEntry."Non ITC Claimable Usage %";
        VATAdjustmentBuffer."Location Code" := DetailedTaxEntry."Location Code";
        VATAdjustmentBuffer.Quantity := DetailedTaxEntry.Quantity;
        VATAdjustmentBuffer."Tax %" := DetailedTaxEntry."Tax %";
        VATAdjustmentBuffer."Purchase Tax %" := DetailedTaxEntry."Purch. Tax %";
        VATAdjustmentBuffer."Tax Jurisdiction Code" := DetailedTaxEntry."Tax Jurisdiction Code";
        VATAdjustmentBuffer."Tax Component" := TaxJurisdiction."Tax Component";
        VATAdjustmentBuffer."Tax Base Amount" := DetailedTaxEntry3."Tax Base Amount";
        VATAdjustmentBuffer."Tax Amount" := DetailedTaxEntry3."Tax Amount";
        VATAdjustmentBuffer."Input Credit/Output Tax Amount" := DetailedTaxEntry3."Input Credit/Output Tax Amount" ;
        VATAdjustmentBuffer."Amount Loaded on Item" := DetailedTaxEntry3."Amount Loaded on Item";
        VATAdjustmentBuffer."Transaction No" := "Transaction No";
        VATAdjustmentBuffer."Tax Component" := DetailedTaxEntry."Tax Component";
        VATAdjustmentBuffer."Item Ledger Entry No." := DetailedTaxEntry."Item Ledger Entry No.";
        VATAdjustmentBuffer.INSERT;
      END ELSE BEGIN
        VATAdjustmentBuffer."Tax Base Amount" := DetailedTaxEntry3."Tax Base Amount";
        VATAdjustmentBuffer."Tax Amount" := DetailedTaxEntry3."Tax Amount";
        VATAdjustmentBuffer."Input Credit/Output Tax Amount" := DetailedTaxEntry3."Input Credit/Output Tax Amount";
        VATAdjustmentBuffer."Amount Loaded on Item" := DetailedTaxEntry3."Amount Loaded on Item";
        VATAdjustmentBuffer.MODIFY;
      END;
      VATAmountIN += DetailedTaxEntry3."Tax Amount";
      VATBaseAmountIN += DetailedTaxEntry3."Tax Base Amount";
      InputCreditOutputTaxAmount += DetailedTaxEntry3."Input Credit/Output Tax Amount";
      AmountLoadedonItem += DetailedTaxEntry3."Amount Loaded on Item";
      OriginalQuantity += DetailedTaxEntry."Remaining Quantity";
      Counter += 1;
    END;

    LOCAL PROCEDURE TransactionNoOnAfterValidate@19023199();
    BEGIN
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE CurrentJnlBatchNameOnAfterVali@19002411();
    BEGIN
      CurrPage.SAVERECORD;
      GenJnlManagement.SetNameTax(CurrentJnlBatchName,Rec);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE AfterGetCurrentRecord@19077479();
    BEGIN
      xRec := Rec;
      GenJnlManagement.GetAccountsTax(Rec,AccName,BalAccName);
    END;

    BEGIN
    END.
  }
}

