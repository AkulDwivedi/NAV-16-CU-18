OBJECT Page 16602 GST Distribution
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.49326;
  }
  PROPERTIES
  {
    CaptionML=[ENU=GST Distribution;
               ENN=GST Distribution];
    SourceTable=Table16444;
    PageType=Document;
    TimerUpdate=50;
    OnOpenPage=BEGIN
                 EnableFillBufferLine;
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1500006 ;1   ;ActionGroup;
                      CaptionML=[ENU=O&rder;
                                 ENN=O&rder];
                      Image=Order }
      { 1500005 ;2   ;Action    ;
                      Name=Dimensions;
                      AccessByPermission=TableData 348=R;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[ENU=Dimensions;
                                 ENN=Dimensions];
                      Promoted=No;
                      PromotedIsBig=No;
                      Image=Dimensions;
                      OnAction=BEGIN
                                 ShowDocDim;
                                 CurrPage.SAVERECORD;
                               END;
                                }
      { 1500030 ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unction;
                                 ENN=F&unction];
                      Image=Action }
      { 1500002 ;2   ;Action    ;
                      ShortCutKey=Shift+F11;
                      CaptionML=[ENU=Apply Entries;
                                 ENN=Apply Entries];
                      Enabled=ApplyBtnEnable;
                      Image=ApplyEntries;
                      OnAction=BEGIN
                                 TempDetailedGSTLedgerEntry.DELETEALL;
                                 DetailedGSTLedgerEntry.RESET;
                                 DetailedGSTLedgerEntry.SETRANGE("Transaction Type",DetailedGSTLedgerEntry."Transaction Type"::Purchase);
                                 DetailedGSTLedgerEntry.SETRANGE("Entry Type",DetailedGSTLedgerEntry."Entry Type"::"Initial Entry");
                                 IF "Dist. Document Type" = "Dist. Document Type"::Invoice THEN
                                   DetailedGSTLedgerEntry.SETRANGE("Document Type",DetailedGSTLedgerEntry."Document Type"::Invoice)
                                 ELSE
                                   DetailedGSTLedgerEntry.SETRANGE("Document Type",DetailedGSTLedgerEntry."Document Type"::"Credit Memo");
                                 DetailedGSTLedgerEntry.SETRANGE("Location  Reg. No.","From GSTIN No.");
                                 DetailedGSTLedgerEntry.SETRANGE(Type,DetailedGSTLedgerEntry.Type::"G/L Account");
                                 IF "Dist. Credit Type" = "Dist. Credit Type"::Availment THEN
                                   DetailedGSTLedgerEntry.SETRANGE("GST Credit",DetailedGSTLedgerEntry."GST Credit"::Availment)
                                 ELSE
                                   DetailedGSTLedgerEntry.SETRANGE("GST Credit",DetailedGSTLedgerEntry."GST Credit"::"Non-Availment");
                                 DetailedGSTLedgerEntry.SETRANGE("Posting Date",0D,"Posting Date");
                                 DetailedGSTLedgerEntry.SETRANGE("Input Service Distribution",TRUE);
                                 DetailedGSTLedgerEntry.SETRANGE("GST Exempted Goods",FALSE);
                                 DetailedGSTLedgerEntry.SETRANGE(Distributed,FALSE);
                                 IF DetailedGSTLedgerEntry.FINDSET THEN
                                   REPEAT
                                     TempDetailedGSTLedgerEntry := DetailedGSTLedgerEntry;
                                     IF DetailedGSTLedgerEntry."Dist. Document No." = "No." THEN
                                       TempDetailedGSTLedgerEntry."Dist. Input GST Credit" := TRUE;
                                     TempDetailedGSTLedgerEntry.INSERT;
                                   UNTIL DetailedGSTLedgerEntry.NEXT = 0;

                                 IF PAGE.RUNMODAL(PAGE::"Dist. Input GST Credit",TempDetailedGSTLedgerEntry) = ACTION::LookupOK THEN BEGIN
                                   TempDetailedGSTLedgerEntry.FINDSET;
                                   REPEAT
                                     DetailedGSTLedgerEntry.GET(TempDetailedGSTLedgerEntry."Entry No.");
                                     IF TempDetailedGSTLedgerEntry."Dist. Input GST Credit" THEN
                                       DetailedGSTLedgerEntry."Dist. Document No." := "No."
                                     ELSE
                                       IF DetailedGSTLedgerEntry."Dist. Document No." = "No." THEN
                                         DetailedGSTLedgerEntry."Dist. Document No." := '';
                                     DetailedGSTLedgerEntry.MODIFY;
                                   UNTIL TempDetailedGSTLedgerEntry.NEXT = 0;
                                 END;

                                 DetailedGSTLedgerEntry.SETRANGE("Dist. Document No.","No.");
                                 IF DetailedGSTLedgerEntry.FINDSET THEN
                                   REPEAT
                                     DetailedGSTLedgerEntry1.SETRANGE("Document Type",DetailedGSTLedgerEntry."Document Type");
                                     DetailedGSTLedgerEntry1.SETRANGE("Document No.",DetailedGSTLedgerEntry."Document No.");
                                     DetailedGSTLedgerEntry1.SETRANGE("Document Line No.",DetailedGSTLedgerEntry."Document Line No.");
                                     DetailedGSTLedgerEntry1.SETRANGE("Dist. Document No.",'');
                                     IF DetailedGSTLedgerEntry1.FINDFIRST THEN
                                       ERROR(
                                         NoSelectErr,
                                         DetailedGSTLedgerEntry.FIELDCAPTION("Dist. Input GST Credit"),
                                         DetailedGSTLedgerEntry."Document Type",
                                         DetailedGSTLedgerEntry."Document No.",
                                         DetailedGSTLedgerEntry."Document Line No.",
                                         DetailedGSTLedgerEntry1."GST Component Code");
                                   UNTIL DetailedGSTLedgerEntry.NEXT = 0;

                                 TempDetailedGSTLedgerEntry.DELETEALL;
                                 DetailedGSTLedgerEntry.SETRANGE("Dist. Document No.","No.");
                                 DetailedGSTLedgerEntry.CALCSUMS("GST Amount");
                                 "Total Amout Applied for Dist." := DetailedGSTLedgerEntry."GST Amount";
                                 MODIFY;

                                 GSTDistributionLine.RESET;
                                 GSTDistributionLine.SETRANGE("Distribution No.","No.");
                                 IF GSTDistributionLine.FINDSET THEN
                                   REPEAT
                                     GSTDistributionLine.VALIDATE("Distribution %");
                                     GSTDistributionLine.MODIFY(TRUE);
                                   UNTIL GSTDistributionLine.NEXT = 0;
                               END;
                                }
      { 1500014 ;2   ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=[ENU=Post;
                                 ENN=Post];
                      Image=ApplyEntries;
                      OnAction=BEGIN
                                 GSTInputTaxCredit.InsertDistComponentAmount("No.",FALSE);
                                 IF GSTInputTaxCredit.PostGSTDistribution("No.",'',FALSE) THEN BEGIN
                                   MESSAGE(GSTDistributionMsg);
                                   CurrPage.CLOSE;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1500000;1;Group     ;
                CaptionML=[ENU=General;
                           ENN=General] }

    { 1500001;2;Field     ;
                SourceExpr="No.";
                OnAssistEdit=BEGIN
                               IF AssistEdit(xRec) THEN
                                 CurrPage.UPDATE;
                             END;
                              }

    { 1500009;2;Field     ;
                SourceExpr="ISD Document Type" }

    { 1500008;2;Field     ;
                SourceExpr="From Location Code";
                OnValidate=BEGIN
                             EnableFillBufferLine;
                           END;
                            }

    { 1500003;2;Field     ;
                SourceExpr="From GSTIN No.";
                OnValidate=BEGIN
                             EnableFillBufferLine;
                           END;
                            }

    { 1500007;2;Field     ;
                SourceExpr="Posting Date";
                OnValidate=BEGIN
                             EnableFillBufferLine;
                           END;
                            }

    { 1500004;2;Field     ;
                SourceExpr="Dist. Document Type";
                OnValidate=BEGIN
                             EnableFillBufferLine;
                           END;
                            }

    { 1500013;2;Field     ;
                SourceExpr="Dist. Credit Type";
                OnValidate=BEGIN
                             EnableFillBufferLine;
                           END;
                            }

    { 1500015;2;Field     ;
                SourceExpr="Total Amout Applied for Dist.";
                Editable=FALSE }

    { 1500016;2;Field     ;
                SourceExpr="Distribution Basis" }

    { 1500010;2;Field     ;
                SourceExpr="Shortcut Dimension 1 Code" }

    { 1500012;2;Field     ;
                SourceExpr="Shortcut Dimension 2 Code" }

    { 1500011;1;Part      ;
                Name=Subform;
                SubPageLink=Distribution No.=FIELD(No.);
                PagePartID=Page16603 }

  }
  CODE
  {
    VAR
      DetailedGSTLedgerEntry@1500002 : Record 16419;
      DetailedGSTLedgerEntry1@1500005 : Record 16419;
      TempDetailedGSTLedgerEntry@1500003 : TEMPORARY Record 16419;
      GSTDistributionLine@1500006 : Record 16445;
      GSTInputTaxCredit@1500001 : Codeunit 16405;
      ApplyBtnEnable@1500000 : Boolean;
      NoSelectErr@1500004 : TextConst '@@@="%1 = Field Name, %2 = Document Type, %3 = Document No., %4 = Document Line No., %5 = GST Component Code";ENU=All components of same Document Line No. must be selected for distribution. You must select %1 for Document Type: %2 Document No.: %3 Document Line No.: %4 GST Component Code %5.;ENN=All components of same Document Line No. must be selected for distribution. You must select %1 for Document Type: %2 Document No.: %3 Document Line No.: %4 GST Component Code %5.';
      GSTDistributionMsg@1500007 : TextConst 'ENU=Distribution Lines Posted successfully.;ENN=Distribution Lines Posted successfully.';

    LOCAL PROCEDURE EnableFillBufferLine@1500000();
    BEGIN
      ApplyBtnEnable :=
        ("From Location Code" <> '') AND ("Posting Date" <> 0D) AND
        ("Dist. Document Type" <> "Dist. Document Type"::" ") AND
        ("Dist. Credit Type" <> "Dist. Credit Type"::" ");
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

