OBJECT Report 16523 Correct 27EQ Entries
{
  OBJECT-PROPERTIES
  {
    Date=29-09-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.47042;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Correct 27EQ Entries;
               ENN=Correct 27EQ Entries];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (Quarter = 0) OR (FinancialYear = '') OR (CorrectionType = CorrectionType::" ") THEN
                    ERROR(Text16501);
                  IF CorrectionType = CorrectionType::Party THEN
                    Form27EQEntry
                  ELSE
                    BatchHeaderCorrection;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1500007;2;Field     ;
                  CaptionML=[ENU=Quarter;
                             ENN=Quarter];
                  SourceExpr=Quarter }

      { 1500005;2;Field     ;
                  CaptionML=[ENU=Financial Year;
                             ENN=Financial Year];
                  BlankNumbers=BlankZero;
                  SourceExpr=FinancialYear;
                  OnLookup=VAR
                             TDSTCSSetup@1500001 : Record 16501;
                             IncomeTaxAccountingPeriod@1500000 : Record 13724;
                           BEGIN
                             IF Quarter > 0 THEN BEGIN
                               TDSTCSSetup.RESET;
                               TDSTCSSetup.SETRANGE(Quarter,Quarter);
                               IF TDSTCSSetup.FINDFIRST THEN BEGIN
                                 IncomeTaxAccountingPeriod.GET(TDSTCSSetup."No.");
                                 IF PAGE.RUNMODAL(13721,IncomeTaxAccountingPeriod) = ACTION::LookupOK THEN
                                   FinancialYear := IncomeTaxAccountingPeriod."Financial Year for eTDS/TCS";
                               END;
                             END;
                           END;
                            }

      { 1500002;2;Field     ;
                  CaptionML=[ENU=Correction Type;
                             ENN=Correction Type];
                  SourceExpr=CorrectionType }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Quarter@1500000 : ' ,Q1,Q2,Q3,Q4';
      FinancialYear@1500001 : Code[6];
      Text16500@1500003 : TextConst '@@@="%1 = Text";ENU=No records were found for the given %1.;ENN=No records were found for the given %1.';
      CorrectionType@1500004 : ' ,Collector,Party';
      Text16501@1500005 : TextConst 'ENU=All options are mandatory.;ENN=All options are mandatory.';
      Text16502@1500002 : TextConst 'ENU=Quarter;ENN=Quarter';
      Text16503@1500006 : TextConst 'ENU=Financial Year;ENN=Financial Year';

    PROCEDURE BatchHeaderCorrection@1500001();
    VAR
      TCSBatchHeader@1500001 : Record 16515;
      TempTCSBatchHeader@1500000 : TEMPORARY Record 16515;
    BEGIN
      TCSBatchHeader.RESET;
      TCSBatchHeader.SETRANGE("Financial Year",FinancialYear);
      IF NOT TCSBatchHeader.FIND('-') THEN
        ERROR(Text16500,Text16503);
      TCSBatchHeader.SETRANGE(Quarter,Quarter);
      IF NOT TCSBatchHeader.FIND('-') THEN
        ERROR(Text16500,Text16502);
      IF TCSBatchHeader.FIND('-') THEN BEGIN
        TempTCSBatchHeader.DELETEALL;
        TempTCSBatchHeader.RESET;
        REPEAT
          TempTCSBatchHeader.TRANSFERFIELDS(TCSBatchHeader);
          TempTCSBatchHeader.Revised := FALSE;
          TempTCSBatchHeader.INSERT;
        UNTIL TCSBatchHeader.NEXT = 0;
        IF CorrectionType = CorrectionType::Collector THEN
          PAGE.RUNMODAL(PAGE::"Correct Collector Record",TempTCSBatchHeader)
        ELSE
          PAGE.RUNMODAL(PAGE::"Correct Collector T.C.A.N.",TempTCSBatchHeader);
      END;
    END;

    PROCEDURE Form27EQEntry@1500003();
    VAR
      Form27EQEntry@1500001 : Record 16517;
      TempForm27EQEntry@1500000 : TEMPORARY Record 16517;
    BEGIN
      Form27EQEntry.RESET;
      Form27EQEntry.SETRANGE("Financial Year",FinancialYear);
      IF NOT Form27EQEntry.FIND('-') THEN
        ERROR(Text16500,Text16503);
      Form27EQEntry.SETRANGE(Quarter,Quarter);
      IF NOT Form27EQEntry.FIND('-') THEN
        ERROR(Text16500,Text16502);
      IF Form27EQEntry.FIND('-') THEN BEGIN
        TempForm27EQEntry.DELETEALL;
        TempForm27EQEntry.RESET;
        REPEAT
          IF (Form27EQEntry."Filing Date" <> 0D) OR Form27EQEntry."Correction-C9" THEN BEGIN
            TempForm27EQEntry.TRANSFERFIELDS(Form27EQEntry);
            TempForm27EQEntry.Revised := FALSE;
            TempForm27EQEntry.INSERT;
          END;
        UNTIL Form27EQEntry.NEXT = 0;
        PAGE.RUNMODAL(PAGE::"Correct Party Record",TempForm27EQEntry);
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

