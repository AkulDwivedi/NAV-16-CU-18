OBJECT Report 16568 Avail Service Tax Credit
{
  OBJECT-PROPERTIES
  {
    Date=03-08-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.46621;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Avail Service Tax Credit;
               ENN=Avail Service Tax Credit];
    ShowPrintStatus=No;
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5444;    ;DataItem;                    ;
               DataItemTable=Table2000000026;
               DataItemTableView=SORTING(Number)
                                 ORDER(Ascending)
                                 WHERE(Number=CONST(1));
               OnPreDataItem=BEGIN
                               IF ServTaxRegNo = '' THEN
                                 ERROR(Text16500);
                               IF ECCNo = '' THEN
                                 ERROR(Text16501);
                             END;

               OnAfterGetRecord=VAR
                                  ServTaxEntryDetail@1500000 : Record 16474;
                                  TempServTaxEntryDetail@1500001 : TEMPORARY Record 16474;
                                BEGIN
                                  ServTaxEntryDetail.RESET;
                                  ServTaxEntryDetail.SETRANGE("Service Tax Registration No.",ServTaxRegNo);
                                  ServTaxEntryDetail.SETRANGE("Location E.C.C. No.",ECCNo);
                                  ServTaxEntryDetail.SETRANGE("Input Service Distribution",FALSE);
                                  IF ServTaxEntryDetail.FINDSET THEN
                                    REPEAT
                                      IF (ServTaxEntryDetail.FullRemainingCredit <> 0) OR (ServTaxEntryDetail.FullRemainingSBCCredit <> 0) OR
                                         (ServTaxEntryDetail.KKCessRemainingCredit <> 0) THEN BEGIN
                                        TempServTaxEntryDetail := ServTaxEntryDetail;
                                        TempServTaxEntryDetail.INSERT;
                                      END;
                                    UNTIL ServTaxEntryDetail.NEXT = 0;
                                  PAGE.RUNMODAL(PAGE::"Avail Service Tax Credit",TempServTaxEntryDetail);
                                END;
                                 }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1500000;2;Field     ;
                  CaptionML=[ENU=Service Tax Registration No.;
                             ENN=Service Tax Registration No.];
                  SourceExpr=ServTaxRegNo;
                  TableRelation="Service Tax Registration Nos." }

      { 1500002;2;Field     ;
                  CaptionML=[ENU=Location E.C.C. No.;
                             ENN=Location E.C.C. No.];
                  SourceExpr=ECCNo;
                  OnLookup=VAR
                             ECCNumber@1500001 : Record 13708;
                             Location@1500000 : Record 14;
                             CompanyInfo@1500004 : Record 79;
                           BEGIN
                             IF ServTaxRegNo = '' THEN
                               ERROR(Text16500);
                             Location.RESET;
                             Location.SETRANGE("Service Tax Registration No.",ServTaxRegNo);
                             IF Location.FINDSET THEN
                               REPEAT
                                 IF ECCNumber.GET(Location."E.C.C. No.") THEN
                                   ECCNumber.MARK := TRUE;
                               UNTIL Location.NEXT = 0;
                             IF NOT Location .FINDSET THEN BEGIN
                               CompanyInfo.GET;
                               IF ECCNumber.GET(CompanyInfo."E.C.C. No.") THEN
                                 ECCNumber.MARK := TRUE;
                             END;
                             ECCNumber.MARKEDONLY(TRUE);
                             IF PAGE.RUNMODAL(PAGE::"E.C.C. Nos.",ECCNumber) = ACTION::LookupOK THEN
                               ECCNo := ECCNumber.Code;
                             ECCNumber.CLEARMARKS;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      ServTaxRegNo@1500000 : Code[20];
      ECCNo@1500001 : Code[20];
      Text16500@1500002 : TextConst 'ENU=Please enter the service tax registration number.;ENN=Please enter the service tax registration number.';
      Text16501@1500003 : TextConst 'ENU=Please enter the Location E.C.C. No.;ENN=Please enter the Location E.C.C. No.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

