OBJECT Report 16520 Update TCS Challan Details
{
  OBJECT-PROPERTIES
  {
    Date=29-09-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.47042;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Update TCS Challan Details;
               ENN=Update TCS Challan Details];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  CompanyInformation@1500000 : Record 79;
                BEGIN
                  CompanyInformation.GET;

                  IF PayTCSDocNo = '' THEN
                    ERROR(Text16503Err);
                  IF ChallanNo = '' THEN
                    ERROR(Text16504Err);
                  IF STRLEN(ChallanNo) > 5 THEN
                    ERROR(ChallanNoLengthValidationErr);
                  IF ChallanDate = 0D THEN
                    ERROR(Text16505Err);
                  IF BankName = '' THEN
                    ERROR(Text16508Err);
                  IF BSRCode = '' THEN
                    IF CompanyInformation."Company Status" <> CompanyInformation."Company Status"::Government THEN
                      ERROR(Text16501Err);
                  IF STRLEN(BSRCode) < 7 THEN
                    IF CompanyInformation."Company Status" <> CompanyInformation."Company Status"::Government THEN
                      ERROR(Text16502Err);
                  IF MinorHeadCode = MinorHeadCode::" " THEN
                    ERROR(Text16510Err);

                  TCSEntry.RESET;
                  TCSEntry.SETRANGE("Pay TCS Document No.",PayTCSDocNo);
                  IF TCSEntry.FIND('-') THEN BEGIN
                    REPEAT
                      IF FilingStatus(TCSEntry."Entry No.") THEN BEGIN
                        TCSEntry."Challan No." := ChallanNo;
                        TCSEntry."Challan Date" := ChallanDate;
                        TCSEntry."Bank Name" := BankName;
                        TCSEntry."BSR Code" := BSRCode;
                        TCSEntry."Check/DD No." := CheckNo;
                        TCSEntry."Check Date" := CheckDate;
                        TCSEntry."Minor Head Code" := MinorHeadCode;
                        UpdateForm27QEntry;
                        TCSEntry.MODIFY;
                      END;
                    UNTIL TCSEntry.NEXT = 0;
                    UpdateTCSChallanRegister;
                  END ELSE
                    ERROR(Text16507Err);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   MinorHeadCode := MinorHeadCode::"200";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1500001;2;Field     ;
                  CaptionML=[ENU=Pay TCS Document No.;
                             ENN=Pay TCS Document No.];
                  SourceExpr=PayTCSDocNo }

      { 1500002;2;Field     ;
                  CaptionML=[ENU=Challan No.;
                             ENN=Challan No.];
                  SourceExpr=ChallanNo }

      { 1500004;2;Field     ;
                  CaptionML=[ENU=Challan Date;
                             ENN=Challan Date];
                  SourceExpr=ChallanDate }

      { 1500007;2;Field     ;
                  CaptionML=[ENU=Bank Name;
                             ENN=Bank Name];
                  SourceExpr=BankName }

      { 1500009;2;Field     ;
                  CaptionML=[ENU=BSR Code;
                             ENN=BSR Code];
                  SourceExpr=BSRCode }

      { 1500010;2;Field     ;
                  CaptionML=[ENU=Cheque No.;
                             ENN=Cheque No.];
                  SourceExpr=CheckNo }

      { 1500012;2;Field     ;
                  CaptionML=[ENU=Cheque Date;
                             ENN=Cheque Date];
                  SourceExpr=CheckDate }

      { 1500013;2;Field     ;
                  CaptionML=[ENU=Minor Head Code;
                             ENN=Minor Head Code];
                  SourceExpr=MinorHeadCode }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TCSEntry@1500007 : Record 16514;
      FormManagement@1500016 : Codeunit 16400;
      ChallanNo@1500006 : Code[5];
      PayTCSDocNo@1500003 : Code[20];
      CheckNo@1500001 : Code[20];
      ChallanDate@1500005 : Date;
      CheckDate@1500000 : Date;
      BankName@1500004 : Text[100];
      BSRCode@1500002 : Text[7];
      Text16501Err@1500015 : TextConst 'ENU=You must enter a BSR code.;ENN=You must enter a BSR code.';
      Text16502Err@1500014 : TextConst 'ENU=The BSR code must have at least 7 digits.;ENN=The BSR code must have at least 7 digits.';
      Text16503Err@1500013 : TextConst 'ENU=You must enter a pay TCS document number.;ENN=You must enter a pay TCS document number.';
      Text16504Err@1500012 : TextConst 'ENU=You must enter a Challan number.;ENN=You must enter a Challan number.';
      Text16505Err@1500011 : TextConst 'ENU=You must enter a Challan date.;ENN=You must enter a Challan date.';
      Text16506Err@1500008 : TextConst 'ENU=The Challan number is already filed.;ENN=The Challan number is already filed.';
      Text16507Err@1500010 : TextConst 'ENU=There are no records with this document number.;ENN=There are no records with this document number.';
      Text16508Err@1500009 : TextConst 'ENU=You must enter a bank name.;ENN=You must enter a bank name.';
      Text16510Err@1500017 : TextConst 'ENU=The Minor Head Code field must not be empty.;ENN=The Minor Head Code field must not be empty.';
      ChallanNoLengthValidationErr@1500018 : TextConst 'ENU=The Challan number must not be longer than 5 characters.;ENN=The Challan number must not be longer than 5 characters.';
      MinorHeadCode@1500019 : ' ,200,400';

    PROCEDURE SetDocumentNo@1280000(DocumentNo@1280000 : Code[20]);
    BEGIN
      PayTCSDocNo := DocumentNo;
    END;

    PROCEDURE UpdateTCSChallanRegister@1500000();
    VAR
      TCSChallanRegister@1500005 : Record 16516;
      TDSTCSAccountingSetup@1500004 : Record 13724;
      GenJnlPosLine@1500003 : Codeunit 12;
      Form27EQEntry@1500002 : Record 16517;
      TCSEntry1@1500001 : Record 16514;
      CompanyInformation@1500000 : Record 79;
    BEGIN
      TCSChallanRegister.RESET;
      TCSChallanRegister.SETRANGE("Challan No.",ChallanNo);
      TCSChallanRegister.SETRANGE("Challan Date",ChallanDate);
      IF TCSChallanRegister.FINDFIRST THEN BEGIN
        IF TCSChallanRegister.Filed OR TCSChallanRegister.Revised THEN
          ERROR(Text16506Err);
        Form27EQEntry.SETRANGE("Challan No.",TCSChallanRegister."Challan No.");
        Form27EQEntry.SETRANGE("Challan Date",TCSChallanRegister."Challan Date");
        Form27EQEntry.SETRANGE("Pay TCS Document No.",PayTCSDocNo);
        IF Form27EQEntry.FINDFIRST THEN BEGIN
          Form27EQEntry.MODIFYALL("Challan-Detail Record No.",TCSChallanRegister."Challan-Detail Record No.");
          Form27EQEntry.MODIFYALL("Challan Register Entry No.",TCSChallanRegister."Entry No.");
        END;
        EXIT;
      END;

      CompanyInformation.GET;
      TCSChallanRegister.RESET;
      TCSChallanRegister.SETRANGE("Pay TCS Document No.",PayTCSDocNo);
      TCSEntry.SETCURRENTKEY("Pay TCS Document No.","Posting Date");
      TCSEntry.FIND('+');
      TCSChallanRegister.SETRANGE("TCS Payment Date",TCSEntry."TCS Payment Date");
      IF TCSChallanRegister.FINDFIRST THEN BEGIN
        TCSChallanRegister."Last Bank Challan No." := TCSChallanRegister."Challan No.";
        TCSChallanRegister."Last Bank-Branch Code" := TCSChallanRegister."BSR Code";
        TCSChallanRegister."Last Date of Challan No." := TCSChallanRegister."Last Date of Challan No.";
        TCSChallanRegister."Challan No." := ChallanNo;
        TCSChallanRegister."BSR Code" := BSRCode;
        TCSChallanRegister."Challan Date" := ChallanDate;
        TCSChallanRegister."Bank Name" := BankName;
        TCSChallanRegister."Minor Head Code" := MinorHeadCode;
        IF CompanyInformation."Company Status" = CompanyInformation."Company Status"::Government THEN BEGIN
          TCSChallanRegister."Last Transfer Voucher No." := TCSChallanRegister."Transfer Voucher No.";
          TCSChallanRegister."Transfer Voucher No." := ChallanNo;
        END;
        // Check if Revised or Filed
        IF TCSChallanRegister.Filed OR (TCSChallanRegister.Revised AND (NOT TCSChallanRegister."Correction-C3"))
        THEN BEGIN
          IF TCSChallanRegister.Filed THEN
            TCSChallanRegister."No. of Revision" := TCSChallanRegister."No. of Revision" + 1;
          TCSChallanRegister."Correction-C2" := TRUE;
          TCSChallanRegister.Filed := FALSE;
          TCSChallanRegister.Revised := TRUE;
        END;
        TCSChallanRegister.MODIFY;
      END ELSE BEGIN
        TCSChallanRegister.INIT;
        TCSChallanRegister."Entry No." := TDSTCSAccountingSetup.GetTCSLastEntryNo(DATABASE::"TCS Challan Register") + 1;
        TCSChallanRegister."Challan No." := ChallanNo;
        TCSChallanRegister."Last Bank Challan No." := ChallanNo;
        TCSChallanRegister."BSR Code" := COPYSTR(BSRCode,1,7);
        TCSChallanRegister."Last Bank-Branch Code" := COPYSTR(BSRCode,1,7);
        TCSChallanRegister."Challan Date" := ChallanDate;
        TCSChallanRegister."Last Date of Challan No." := ChallanDate;
        TCSChallanRegister."Bank Name" := BankName;
        TCSChallanRegister."Pay TCS Document No." := PayTCSDocNo;
        TCSChallanRegister."Minor Head Code" := MinorHeadCode;
        TCSChallanRegister."TCS Payment Date" := ChallanDate;
        TCSChallanRegister."T.C.A.N. No." := TCSEntry."T.C.A.N. No.";
        TCSChallanRegister."Check / DD No." := TCSEntry."Check/DD No.";
        TCSChallanRegister."Check / DD Date" := TCSEntry."Check Date";
        TCSChallanRegister."TCS Type" := TCSEntry."TCS Type";
        // TCSChallanRegister."Challan-Detail Record No." := GenJnlPosLine.GetTCSChallanDetailRecNo
        // (GenJnlPosLine.GetQuarter(TCSEntry."Posting Date"),
        // GenJnlPosLine.GetFinancialYear(TCSEntry."Posting Date"));
        TCSChallanRegister."Challan-Detail Record No." :=
          GenJnlPosLine.GetTCSChallanDetailRecNo(
            FormManagement.GetQuarter(TCSEntry."Posting Date"),FormManagement.GetFinancialYear(TCSEntry."Posting Date"),
            TCSEntry."T.C.A.N. No.");
        // Check for Filed entries
        IF CheckTCSFiledEntries(
             FormManagement.GetQuarter(TCSEntry."Posting Date"),FormManagement.GetFinancialYear(TCSEntry."Posting Date"))
        THEN BEGIN
          TCSChallanRegister."Correction-C9" := TRUE;
          TCSChallanRegister.Revised := TRUE;
          TCSChallanRegister."No. of Revision" += 1;
        END;
        TCSChallanRegister."Financial Year" := FormManagement.GetFinancialYear(TCSEntry."Posting Date");
        TCSChallanRegister.Quarter := FormManagement.GetQuarter(TCSEntry."Posting Date");
        TCSChallanRegister."Assessment Year" := FormManagement.GetAssessmentYear(TCSEntry."Posting Date");
        IF CompanyInformation."Company Status" = CompanyInformation."Company Status"::Government THEN
          TCSChallanRegister."Transfer Voucher No." := ChallanNo;
        TCSChallanRegister."Last Tot Deposit Amt/ Challan" := TCSChallanRegister."Total TCS Including SHE Cess" +
          TCSChallanRegister."Oltas Interest";
        TCSChallanRegister."User ID" := USERID;
        TCSChallanRegister.INSERT;

        Form27EQEntry.SETRANGE("Challan No.",TCSChallanRegister."Challan No.");
        Form27EQEntry.SETRANGE("Challan Date",TCSChallanRegister."Challan Date");
        Form27EQEntry.SETRANGE("Pay TCS Document No.",PayTCSDocNo);
        IF Form27EQEntry.FINDFIRST THEN BEGIN
          Form27EQEntry.MODIFYALL("Challan-Detail Record No.",TCSChallanRegister."Challan-Detail Record No.");
          Form27EQEntry.MODIFYALL("Challan Register Entry No.",TCSChallanRegister."Entry No.");
        END;
        TCSEntry1.RESET;
        TCSEntry1.SETRANGE("Challan No.",TCSChallanRegister."Challan No.");
        TCSEntry1.MODIFYALL("Challan Register Entry No.",TCSChallanRegister."Entry No.");
      END;
    END;

    PROCEDURE CheckTCSFiledEntries@1500001(Quarter@1500000 : Integer;FinancialYear@1500001 : Code[6]) : Boolean;
    VAR
      TCSChallanRegister@1500003 : Record 16516;
    BEGIN
      TCSChallanRegister.SETRANGE(Quarter,Quarter);
      TCSChallanRegister.SETRANGE("Financial Year",FinancialYear);
      TCSChallanRegister.SETRANGE(Filed,TRUE);
      IF NOT TCSChallanRegister.FINDFIRST THEN BEGIN
        TCSChallanRegister.SETRANGE(Filed);
        TCSChallanRegister.SETRANGE(Revised,TRUE);
        EXIT(TCSChallanRegister.FINDFIRST);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE UpdateForm27QEntry@1500002();
    VAR
      Form27EQEntry@1500000 : Record 16517;
    BEGIN
      Form27EQEntry.RESET;
      Form27EQEntry.SETRANGE("TCS Entry No.",TCSEntry."Entry No.");
      IF Form27EQEntry.FINDFIRST THEN BEGIN
        Form27EQEntry."Challan No." := TCSEntry."Challan No.";
        Form27EQEntry."BSR Code" := TCSEntry."BSR Code";
        Form27EQEntry."Challan Date" := TCSEntry."Challan Date";
        Form27EQEntry."Bank Name" := TCSEntry."Bank Name";
        Form27EQEntry."Check / DD No." := TCSEntry."Check/DD No.";
        Form27EQEntry."Check / DD Date" := TCSEntry."Check Date";
        Form27EQEntry."TCS Payment Date" := ChallanDate;
        Form27EQEntry.MODIFY(TRUE);
      END;
    END;

    PROCEDURE FilingStatus@1500003(EntryNo@1500002 : Integer) : Boolean;
    VAR
      Form27EQEntry@1500001 : Record 16517;
    BEGIN
      Form27EQEntry.RESET;
      Form27EQEntry.SETRANGE("TCS Entry No.",EntryNo);
      Form27EQEntry.SETRANGE("Correction-C9",TRUE);
      IF Form27EQEntry.FINDFIRST THEN
        EXIT(TRUE);
      Form27EQEntry.SETRANGE("Correction-C9");
      Form27EQEntry.SETRANGE(Filed,FALSE);
      Form27EQEntry.SETRANGE(Revised,FALSE);
      EXIT(Form27EQEntry.FINDFIRST);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

