OBJECT Page 403 Purchase Order Statistics
{
  OBJECT-PROPERTIES
  {
    Date=01-08-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.48992,NAVIN9.00.00.48992;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Purchase Order Statistics;
               ENN=Purchase Order Statistics];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table38;
    PageType=Card;
    OnOpenPage=BEGIN
                 PurchSetup.GET;
                 AllowInvDisc :=
                   NOT (PurchSetup."Calc. Inv. Discount" AND VendInvDiscRecExists("Invoice Disc. Code"));
                 AllowVATDifference :=
                   PurchSetup."Allow VAT Difference" AND
                   NOT ("Document Type" IN ["Document Type"::Quote,"Document Type"::"Blanket Order"]);
                 VATLinesFormIsEditable := AllowVATDifference OR AllowInvDisc;
                 CurrPage.EDITABLE := VATLinesFormIsEditable;
               END;

    OnAfterGetRecord=VAR
                       StructureOrderDetails@1500003 : Record 13794;
                       GSTManagement@1500000 : Codeunit 16401;
                       Transactiontype@1500001 : 'Purchase,Sale,Transfer,Service';
                       PurchLineQty@1500002 : Decimal;
                     BEGIN
                       RefreshOnAfterGetRecord;
                       ClearGSTStatisticsAmount;
                       IF GSTManagement.IsGSTApplicable(Structure) THEN BEGIN
                         PurchaseLine.SETRANGE("Document Type","Document Type");
                         PurchaseLine.SETRANGE("Document No.","No.");
                         PurchaseLine.SETFILTER(Type,'<>%1',PurchaseLine.Type::" ");
                         PurchaseLine.SETFILTER(Quantity,'<>%1',0);
                         IF PurchaseLine.FINDSET THEN BEGIN
                           REPEAT
                             TotalGST += GSTManagement.RoundTotalGSTAmountQtyFactor(Transactiontype::Purchase,PurchaseLine."Document Type",
                               PurchaseLine."Document No.",PurchaseLine."Line No.",1,"Currency Code");
                             IF PurchaseLine."GST Assessable Value" <> 0 THEN
                               TotalGSTInvoiced += GSTManagement.RoundTotalGSTAmountforGSTAssessable(Transactiontype::Purchase,PurchaseLine."Document Type",
                                 PurchaseLine."Document No.",PurchaseLine."Line No.","Currency Code")
                             ELSE
                             TotalGSTInvoiced += GSTManagement.RoundTotalGSTAmountQtyFactor(Transactiontype::Purchase,PurchaseLine."Document Type",
                               PurchaseLine."Document No.",PurchaseLine."Line No.",PurchaseLine."Qty. to Invoice" / PurchaseLine.Quantity,"Currency Code");
                             IF "Document Type" IN ["Document Type"::"Return Order","Document Type"::"Credit Memo"] THEN
                               PurchLineQty := PurchaseLine."Return Qty. to Ship"
                             ELSE
                               PurchLineQty := PurchaseLine."Qty. to Receive";
                             IF PurchaseLine."GST Assessable Value" <> 0 THEN
                               TotalGSTShipped += GSTManagement.RoundTotalGSTAmountforGSTAssessable(Transactiontype::Purchase,PurchaseLine."Document Type",
                                 PurchaseLine."Document No.",PurchaseLine."Line No.","Currency Code")
                             ELSE
                             TotalGSTShipped += GSTManagement.RoundTotalGSTAmountQtyFactor(Transactiontype::Purchase,PurchaseLine."Document Type",
                               PurchaseLine."Document No.",PurchaseLine."Line No.",PurchLineQty / PurchaseLine.Quantity,"Currency Code");
                           UNTIL PurchaseLine.NEXT = 0;
                           IF PurchaseLine."GST Reverse Charge" THEN BEGIN
                             ReverseChargeGST := TotalGST;
                             ReverseChargeGSTInvoiced := TotalGSTInvoiced;
                             ReverseChargeGSTShipped := TotalGSTShipped;
                           END;
                         END;
                         IF ("Applies-to Doc. No." <> '') OR ("Applies-to ID" <> '')THEN BEGIN
                           TotalAdvAmount += GSTApplicationAmount;
                           TotalAmountApplied := GetApplicableAmount(TotalAdvAmount,TotalGST);
                           TotalAdvGSTInvoiced := GetApplicableAmount(TotalAmountApplied,TotalGSTInvoiced);
                           TotalAdvGSTShipped := GetApplicableAmount(TotalAmountApplied,TotalGSTShipped);
                         END;
                       END;
                     END;

    OnQueryClosePage=BEGIN
                       GetVATSpecification(PrevTab);
                       IF TempVATAmountLine1.GetAnyLineModified OR TempVATAmountLine2.GetAnyLineModified THEN
                         UpdateVATOnPurchLines;
                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENN=General] }

    { 35  ;2   ;Field     ;
                Name=LineAmountGeneral;
                CaptionML=[ENU=Amount;
                           ENN=Amount];
                SourceExpr=TotalPurchLine[1]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                Name=InvDiscountAmount_General;
                CaptionML=[ENU=Inv. Discount Amount;
                           ENN=Inv. Discount Amount];
                SourceExpr=TotalPurchLine[1]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::General;
                             UpdateInvDiscAmount(1);
                           END;
                            }

    { 26  ;2   ;Field     ;
                Name=Total_General;
                CaptionML=[ENU=Total;
                           ENN=Total];
                SourceExpr=TotalAmount1[1];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::General;
                             UpdateTotalAmount(1);
                           END;
                            }

    { 1500011;2;Field     ;
                CaptionML=[ENU=Excise Amount;
                           ENN=Excise Amount];
                SourceExpr=TotalPurchLine[1]."Excise Amount";
                Editable=FALSE }

    { 1500010;2;Field     ;
                CaptionML=[ENU=Tax Amount;
                           ENN=Tax Amount];
                SourceExpr=TotalPurchLine[1]."Tax Amount";
                Editable=FALSE }

    { 1500000;2;Field     ;
                CaptionML=[ENU=GST Amount;
                           ENN=GST Amount];
                SourceExpr=ABS(TotalGST - TotalAmountApplied);
                Editable=FALSE }

    { 1500014;2;Field     ;
                CaptionML=[ENU=Service Tax Amount;
                           ENN=Service Tax Amount];
                SourceExpr=TotalPurchLine[1]."Service Tax Amount" + TotalPurchLine[1]."Service Tax eCess Amount" + TotalPurchLine[1]."Service Tax SHE Cess Amount" + TotalPurchLine[1]."Service Tax SBC Amount" + TotalPurchLine[1]."KK Cess Amount";
                Editable=FALSE }

    { 1500009;2;Field     ;
                CaptionML=[ENU=Charges Amount;
                           ENN=Charges Amount];
                SourceExpr=TotalPurchLine[1]."Charges To Vendor";
                Editable=FALSE }

    { 1500012;2;Field     ;
                CaptionML=[ENU=TDS Amount;
                           ENN=TDS Amount];
                SourceExpr=TotalPurchLine[1]."Bal. TDS Including SHE CESS";
                Editable=FALSE }

    { 1500013;2;Field     ;
                CaptionML=[ENU=Work Tax Amount;
                           ENN=Work Tax Amount];
                SourceExpr=TotalPurchLine[1]."Work Tax Amount";
                Editable=FALSE }

    { 1500018;2;Field     ;
                CaptionML=[ENU=Net Total;
                           ENN=Net Total];
                SourceExpr=TotalPurchLine[1]."Amount To Vendor" - ReverseChargeGST;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 37  ;2   ;Field     ;
                Name=Quantity_General;
                CaptionML=[ENU=Quantity;
                           ENN=Quantity];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[1].Quantity;
                Editable=FALSE }

    { 30  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ENN=Parcels];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[1]."Units per Parcel";
                Editable=FALSE }

    { 28  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ENN=Net Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[1]."Net Weight";
                Editable=FALSE }

    { 23  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ENN=Gross Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[1]."Gross Weight";
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ENN=Volume];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[1]."Unit Volume";
                Editable=FALSE }

    { 40  ;2   ;Field     ;
                Name=NoOfVATLines_General;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ENN=No. of VAT Lines];
                SourceExpr=TempVATAmountLine1.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempVATAmountLine1,FALSE);
                              UpdateHeaderInfo(1,TempVATAmountLine1);
                            END;
                             }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Purchase (LCY);
                           ENN=Purchase (LCY)];
                SourceExpr=TotalPurchLineLCY[1].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 1905885101;1;Group  ;
                CaptionML=[ENU=Invoicing;
                           ENN=Invoicing] }

    { 59  ;2   ;Field     ;
                CaptionML=[ENU=Amount;
                           ENN=Amount];
                SourceExpr=TotalPurchLine[2]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 57  ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ENN=Inv. Discount Amount];
                SourceExpr=TotalPurchLine[2]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Invoicing;
                             UpdateInvDiscAmount(2);
                           END;
                            }

    { 51  ;2   ;Field     ;
                Name=Total_Invoicing;
                CaptionML=[ENU=Total;
                           ENN=Total];
                SourceExpr=TotalAmount1[2];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Invoicing;
                             UpdateTotalAmount(2);
                           END;
                            }

    { 1500026;2;Field     ;
                Name=InvoicingExciseAmount;
                CaptionML=[ENU=Excise Amount;
                           ENN=Excise Amount];
                SourceExpr=TotalPurchLine[2]."Excise Amount";
                Editable=FALSE }

    { 1500025;2;Field     ;
                Name=InvoicingTaxAmount;
                CaptionML=[ENU=Tax Amount;
                           ENN=Tax Amount];
                SourceExpr=TotalPurchLine[2]."Tax Amount";
                Editable=FALSE }

    { 1500001;2;Field     ;
                CaptionML=[ENU=GST Amount;
                           ENN=GST Amount];
                SourceExpr=ABS (TotalGSTInvoiced) -TotalAdvGSTInvoiced;
                Editable=false }

    { 1500030;2;Field     ;
                CaptionML=[ENU=Service Tax Amount;
                           ENN=Service Tax Amount];
                SourceExpr=TotalPurchLine[2]."Service Tax Amount" + TotalPurchLine[2]."Service Tax eCess Amount" + TotalPurchLine[2]."Service Tax SHE Cess Amount" + TotalPurchLine[2]."Service Tax SBC Amount"  + TotalPurchLine[2]."KK Cess Amount";
                Editable=FALSE }

    { 1500027;2;Field     ;
                CaptionML=[ENU=Charges Amount;
                           ENN=Charges Amount];
                SourceExpr=TotalPurchLine[2]."Charges To Vendor";
                Editable=FALSE }

    { 1500028;2;Field     ;
                CaptionML=[ENU=TDS Amount;
                           ENN=TDS Amount];
                SourceExpr=TotalPurchLine[2]."Bal. TDS Including SHE CESS";
                Editable=FALSE }

    { 1500029;2;Field     ;
                CaptionML=[ENU=Work Tax Amount;
                           ENN=Work Tax Amount];
                SourceExpr=TotalPurchLine[2]."Work Tax Amount";
                Editable=FALSE }

    { 1500036;2;Field     ;
                CaptionML=[ENU=Net Total;
                           ENN=Net Total];
                SourceExpr=TotalPurchLine[2]."Amount To Vendor" - TotalPurchLine[2]."Total GST Amount" + TotalGSTInvoiced - ReverseChargeGSTInvoiced;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 61  ;2   ;Field     ;
                Name=Quantity_Invoicing;
                CaptionML=[ENU=Quantity;
                           ENN=Quantity];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[2].Quantity;
                Editable=FALSE }

    { 55  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ENN=Parcels];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[2]."Units per Parcel";
                Editable=FALSE }

    { 53  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ENN=Net Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[2]."Net Weight";
                Editable=FALSE }

    { 48  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ENN=Gross Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[2]."Gross Weight";
                Editable=FALSE }

    { 43  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ENN=Volume];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[2]."Unit Volume";
                Editable=FALSE }

    { 64  ;2   ;Field     ;
                Name=NoOfVATLines_Invoicing;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ENN=No. of VAT Lines];
                SourceExpr=TempVATAmountLine2.COUNT;
                OnDrillDown=BEGIN
                              ActiveTab := ActiveTab::Invoicing;
                              VATLinesDrillDown(TempVATAmountLine2,TRUE);
                              UpdateHeaderInfo(2,TempVATAmountLine2);

                              IF TempVATAmountLine2.GetAnyLineModified THEN BEGIN
                                UpdateVATOnPurchLines;
                                RefreshOnAfterGetRecord;
                              END;
                            END;
                             }

    { 41  ;2   ;Field     ;
                CaptionML=[ENU=Purchase (LCY);
                           ENN=Purchase (LCY)];
                SourceExpr=TotalPurchLineLCY[2].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 1906801201;1;Group  ;
                CaptionML=[ENU=Shipping;
                           ENN=Shipping] }

    { 83  ;2   ;Field     ;
                CaptionML=[ENU=Amount;
                           ENN=Amount];
                SourceExpr=TotalPurchLine[3]."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 81  ;2   ;Field     ;
                CaptionML=[ENU=Inv. Discount Amount;
                           ENN=Inv. Discount Amount];
                SourceExpr=TotalPurchLine[3]."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 75  ;2   ;Field     ;
                CaptionML=[ENU=Total;
                           ENN=Total];
                SourceExpr=TotalAmount1[3];
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                Editable=FALSE }

    { 1500046;2;Field     ;
                CaptionML=[ENU=Excise Amount;
                           ENN=Excise Amount];
                SourceExpr=TotalPurchLine[3]."Excise Amount";
                Editable=FALSE }

    { 1500045;2;Field     ;
                CaptionML=[ENU=Tax Amount;
                           ENN=Tax Amount];
                SourceExpr=TotalPurchLine[3]."Tax Amount";
                Editable=FALSE }

    { 1500002;2;Field     ;
                CaptionML=[ENU=GST Amount;
                           ENN=GST Amount];
                SourceExpr=ABS (TotalGSTShipped) - TotalAdvGSTShipped;
                Editable=false }

    { 1500049;2;Field     ;
                CaptionML=[ENU=Service Tax Amount;
                           ENN=Service Tax Amount];
                SourceExpr=TotalPurchLine[3]."Service Tax Amount" + TotalPurchLine[3]."Service Tax eCess Amount" + TotalPurchLine[3]."Service Tax SHE Cess Amount" + TotalPurchLine[3]."Service Tax SBC Amount" + TotalPurchLine[3]."KK Cess Amount";
                Editable=FALSE }

    { 1500044;2;Field     ;
                CaptionML=[ENU=Charges Amount;
                           ENN=Charges Amount];
                SourceExpr=TotalPurchLine[3]."Charges To Vendor";
                Editable=FALSE }

    { 1500047;2;Field     ;
                CaptionML=[ENU=TDS Amount;
                           ENN=TDS Amount];
                SourceExpr=TotalPurchLine[3]."Bal. TDS Including SHE CESS";
                Editable=FALSE }

    { 1500048;2;Field     ;
                CaptionML=[ENU=Work Tax Amount;
                           ENN=Work Tax Amount];
                SourceExpr=TotalPurchLine[3]."Work Tax Amount";
                Editable=FALSE }

    { 1500054;2;Field     ;
                CaptionML=[ENU=Net Total;
                           ENN=Net Total];
                SourceExpr=TotalPurchLine[3]."Amount To Vendor" - TotalPurchLine[3]."Total GST Amount" + TotalGSTShipped - ReverseChargeGSTShipped;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 85  ;2   ;Field     ;
                Name=Quantity_Shipping;
                CaptionML=[ENU=Quantity;
                           ENN=Quantity];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[3].Quantity;
                Editable=FALSE }

    { 79  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ENN=Parcels];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[3]."Units per Parcel";
                Editable=FALSE }

    { 77  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ENN=Net Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[3]."Net Weight";
                Editable=FALSE }

    { 72  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ENN=Gross Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[3]."Gross Weight";
                Editable=FALSE }

    { 67  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ENN=Volume];
                DecimalPlaces=0:5;
                SourceExpr=TotalPurchLine[3]."Unit Volume";
                Editable=FALSE }

    { 88  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ENN=No. of VAT Lines];
                SourceExpr=TempVATAmountLine3.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempVATAmountLine3,FALSE);
                            END;
                             }

    { 65  ;2   ;Field     ;
                CaptionML=[ENU=Purchase (LCY);
                           ENN=Purchase (LCY)];
                SourceExpr=TotalPurchLineLCY[3].Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 1900201301;1;Group  ;
                CaptionML=[ENU=Prepayment;
                           ENN=Prepayment] }

    { 15  ;2   ;Field     ;
                Name=PrepmtTotalAmount;
                SourceExpr=PrepmtTotalAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text006,FALSE);
                OnValidate=BEGIN
                             ActiveTab := ActiveTab::Prepayment;
                             UpdatePrepmtAmount;
                           END;
                            }

    { 13  ;2   ;Field     ;
                CaptionML=[ENU=Prepayment Amount Invoiced;
                           ENN=Prepayment Amount Invoiced];
                SourceExpr=PrepmtVATAmount;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=FORMAT(PrepmtVATAmountText);
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Prepmt. Amount Invoiced;
                           ENN=Prepmt. Amount Invoiced];
                SourceExpr=PrepmtTotalAmount2;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text006,TRUE);
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr=TotalPurchLine[1]."Prepmt. Amt. Inv.";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text007,FALSE);
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Invoiced % of Prepayment Amt.;
                           ENN=Invoiced % of Prepayment Amt.];
                ToolTipML=[ENU=Invoiced % of Prepayment Amt.;
                           ENN=Invoiced % of Prepayment Amt.];
                SourceExpr=PrepmtInvPct }

    { 5   ;2   ;Field     ;
                SourceExpr=TotalPurchLine[1]."Prepmt Amt Deducted";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text008,FALSE);
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Deducted % of Prepayment Amt. to Deduct;
                           ENN=Deducted % of Prepayment Amt. to Deduct];
                ToolTipML=[ENU=Deducted % of Prepayment Amt. to Deduct;
                           ENN=Deducted % of Prepayment Amt. to Deduct];
                SourceExpr=PrepmtDeductedPct }

    { 4   ;2   ;Field     ;
                SourceExpr=TotalPurchLine[1]."Prepmt Amt to Deduct";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text009,FALSE);
                Editable=FALSE }

    { 90  ;2   ;Field     ;
                DrillDown=Yes;
                CaptionML=[ENU=No. of VAT Lines;
                           ENN=No. of VAT Lines];
                SourceExpr=TempVATAmountLine4.COUNT;
                OnDrillDown=BEGIN
                              VATLinesDrillDown(TempVATAmountLine4,TRUE);
                            END;
                             }

    { 1901061301;1;Group  ;
                CaptionML=[ENU=Vendor;
                           ENN=Vendor] }

    { 131 ;2   ;Field     ;
                CaptionML=[ENU=Balance (LCY);
                           ENN=Balance (LCY)];
                SourceExpr=Vend."Balance (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Purchase %1 Statistics;ENN=Purchase %1 Statistics';
      Text001@1001 : TextConst 'ENU=Total;ENN=Total';
      Text002@1002 : TextConst 'ENU=Amount;ENN=Amount';
      Text003@1003 : TextConst 'ENU=%1 must not be 0.;ENN=%1 must not be 0.';
      Text004@1004 : TextConst 'ENU=%1 must not be greater than %2.;ENN=%1 must not be greater than %2.';
      Text005@1005 : TextConst 'ENU=You cannot change the invoice discount because there is a %1 record for %2 %3.;ENN=You cannot change the invoice discount because there is a %1 record for %2 %3.';
      TotalPurchLine@1006 : ARRAY [3] OF Record 39;
      TotalPurchLineLCY@1007 : ARRAY [3] OF Record 39;
      PurchaseLine@1500000 : Record 39;
      Vend@1008 : Record 23;
      TempVATAmountLine1@1009 : TEMPORARY Record 290;
      TempVATAmountLine2@1010 : TEMPORARY Record 290;
      TempVATAmountLine3@1011 : TEMPORARY Record 290;
      TempVATAmountLine4@1030 : TEMPORARY Record 290;
      PurchSetup@1012 : Record 312;
      PurchPost@1013 : Codeunit 90;
      GSTManagement@1500006 : Codeunit 16401;
      TotalAmount1@1014 : ARRAY [3] OF Decimal;
      TotalAmount2@1015 : ARRAY [3] OF Decimal;
      VATAmount@1016 : ARRAY [3] OF Decimal;
      PrepmtTotalAmount@1028 : Decimal;
      PrepmtVATAmount@1027 : Decimal;
      PrepmtTotalAmount2@1026 : Decimal;
      VATAmountText@1017 : ARRAY [3] OF Text[30];
      PrepmtVATAmountText@1029 : Text[30];
      PrepmtInvPct@1032 : Decimal;
      PrepmtDeductedPct@1031 : Decimal;
      i@1018 : Integer;
      PrevNo@1019 : Code[20];
      ActiveTab@1020 : 'General,Invoicing,Shipping,Prepayment';
      PrevTab@1021 : 'General,Invoicing,Shipping,Prepayment';
      VATLinesFormIsEditable@1023 : Boolean;
      AllowInvDisc@1024 : Boolean;
      AllowVATDifference@1025 : Boolean;
      Text006@1033 : TextConst 'ENU=Prepmt. Amount;ENN=Prepmt. Amount';
      Text007@1034 : TextConst 'ENU=Prepmt. Amt. Invoiced;ENN=Prepmt. Amt. Invoiced';
      Text008@1035 : TextConst 'ENU=Prepmt. Amt. Deducted;ENN=Prepmt. Amt. Deducted';
      Text009@1036 : TextConst 'ENU=Prepmt. Amt. to Deduct;ENN=Prepmt. Amt. to Deduct';
      VATLinesForm@1037 : Page 9401;
      UpdateInvDiscountQst@1022 : TextConst 'ENU=One or more lines have been invoiced. The discount distributed to invoiced lines will not be taken into account.\\Do you want to update the invoice discount?;ENN=One or more lines have been invoiced. The discount distributed to invoiced lines will not be taken into account.\\Do you want to update the invoice discount?';
      TotalGSTInvoiced@1500001 : Decimal;
      TotalGSTShipped@1500002 : Decimal;
      TotalAdvGSTInvoiced@1500003 : Decimal;
      TotalAdvGSTShipped@1500004 : Decimal;
      TotalGSTAmt@1500005 : Decimal;
      TotalGST@1500007 : Decimal;
      TotalAmountApplied@1500008 : Decimal;
      TotalAdvAmount@1500009 : Decimal;
      ReverseChargeGST@1500012 : Decimal;
      ReverseChargeGSTInvoiced@1500011 : Decimal;
      ReverseChargeGSTShipped@1500010 : Decimal;

    LOCAL PROCEDURE RefreshOnAfterGetRecord@11();
    VAR
      PurchLine@1003 : Record 39;
      TempPurchLine@1002 : TEMPORARY Record 39;
      PurchPostPrepayments@1001 : Codeunit 444;
    BEGIN
      CurrPage.CAPTION(STRSUBSTNO(Text000,"Document Type"));

      IF PrevNo = "No." THEN
        EXIT;
      PrevNo := "No.";
      FILTERGROUP(2);
      SETRANGE("No.",PrevNo);
      FILTERGROUP(0);

      CLEAR(PurchLine);
      CLEAR(TotalPurchLine);
      CLEAR(TotalPurchLineLCY);

      FOR i := 1 TO 3 DO BEGIN
        TempPurchLine.DELETEALL;
        CLEAR(TempPurchLine);
        CLEAR(PurchPost);
        PurchPost.GetPurchLines(Rec,TempPurchLine,i - 1);
        CLEAR(PurchPost);
        CASE i OF
          1:
            PurchLine.CalcVATAmountLines(0,Rec,TempPurchLine,TempVATAmountLine1);
          2:
            PurchLine.CalcVATAmountLines(0,Rec,TempPurchLine,TempVATAmountLine2);
          3:
            PurchLine.CalcVATAmountLines(0,Rec,TempPurchLine,TempVATAmountLine3);
        END;

        PurchPost.SumPurchLinesTemp(
          Rec,TempPurchLine,i - 1,TotalPurchLine[i],TotalPurchLineLCY[i],
          VATAmount[i],VATAmountText[i]);
        IF "Prices Including VAT" THEN BEGIN
          TotalAmount2[i] := TotalPurchLine[i].Amount;
          TotalAmount1[i] := TotalAmount2[i] + VATAmount[i];
          TotalPurchLine[i]."Line Amount" := TotalAmount1[i] + TotalPurchLine[i]."Inv. Discount Amount";
        END ELSE BEGIN
          TotalAmount1[i] := TotalPurchLine[i].Amount;
          TotalAmount2[i] := TotalPurchLine[i]."Amount Including VAT";
        END;
      END;
      TempPurchLine.DELETEALL;
      CLEAR(TempPurchLine);
      PurchPostPrepayments.GetPurchLines(Rec,0,TempPurchLine);
      PurchPostPrepayments.SumPrepmt(
        Rec,TempPurchLine,TempVATAmountLine4,PrepmtTotalAmount,PrepmtVATAmount,PrepmtVATAmountText);
      PrepmtInvPct :=
        Pct(TotalPurchLine[1]."Prepmt. Amt. Inv.",PrepmtTotalAmount);
      PrepmtDeductedPct :=
        Pct(TotalPurchLine[1]."Prepmt Amt Deducted",TotalPurchLine[1]."Prepmt. Amt. Inv.");
      IF "Prices Including VAT" THEN BEGIN
        PrepmtTotalAmount2 := PrepmtTotalAmount;
        PrepmtTotalAmount := PrepmtTotalAmount + PrepmtVATAmount;
      END ELSE
        PrepmtTotalAmount2 := PrepmtTotalAmount + PrepmtVATAmount;

      IF Vend.GET("Pay-to Vendor No.") THEN
        Vend.CALCFIELDS("Balance (LCY)")
      ELSE
        CLEAR(Vend);

      TempVATAmountLine1.MODIFYALL(Modified,FALSE);
      TempVATAmountLine2.MODIFYALL(Modified,FALSE);
      TempVATAmountLine3.MODIFYALL(Modified,FALSE);
      TempVATAmountLine4.MODIFYALL(Modified,FALSE);

      PrevTab := -1;
      UpdateHeaderInfo(2,TempVATAmountLine2);
    END;

    LOCAL PROCEDURE UpdateHeaderInfo@5(IndexNo@1000 : Integer;VAR VATAmountLine@1001 : Record 290);
    VAR
      CurrExchRate@1002 : Record 330;
      UseDate@1003 : Date;
    BEGIN
      TotalPurchLine[IndexNo]."Inv. Discount Amount" := VATAmountLine.GetTotalInvDiscAmount;
      TotalAmount1[IndexNo] :=
        TotalPurchLine[IndexNo]."Line Amount" - TotalPurchLine[IndexNo]."Inv. Discount Amount";
      VATAmount[IndexNo] := VATAmountLine.GetTotalVATAmount;
      IF "Prices Including VAT" THEN BEGIN
        TotalAmount1[IndexNo] := VATAmountLine.GetTotalAmountInclVAT;
        TotalAmount2[IndexNo] := TotalAmount1[IndexNo] - VATAmount[IndexNo];
        TotalPurchLine[IndexNo]."Line Amount" :=
          TotalAmount1[IndexNo] + TotalPurchLine[IndexNo]."Inv. Discount Amount";
      END ELSE
        TotalAmount2[IndexNo] := TotalAmount1[IndexNo] + VATAmount[IndexNo];

      IF "Prices Including VAT" THEN
        TotalPurchLineLCY[IndexNo].Amount := TotalAmount2[IndexNo]
      ELSE
        TotalPurchLineLCY[IndexNo].Amount := TotalAmount1[IndexNo];
      IF "Currency Code" <> '' THEN BEGIN
        IF "Posting Date" = 0D THEN
          UseDate := WORKDATE
        ELSE
          UseDate := "Posting Date";

        TotalPurchLineLCY[IndexNo].Amount :=
          CurrExchRate.ExchangeAmtFCYToLCY(
            UseDate,"Currency Code",TotalPurchLineLCY[IndexNo].Amount,"Currency Factor");
      END;
    END;

    LOCAL PROCEDURE GetVATSpecification@21(QtyType@1000 : 'General,Invoicing,Shipping');
    BEGIN
      CASE QtyType OF
        QtyType::General:
          BEGIN
            VATLinesForm.GetTempVATAmountLine(TempVATAmountLine1);
            IF TempVATAmountLine1.GetAnyLineModified THEN
              UpdateHeaderInfo(1,TempVATAmountLine1);
          END;
        QtyType::Invoicing:
          BEGIN
            VATLinesForm.GetTempVATAmountLine(TempVATAmountLine2);
            IF TempVATAmountLine2.GetAnyLineModified THEN
              UpdateHeaderInfo(2,TempVATAmountLine2);
          END;
        QtyType::Shipping:
          VATLinesForm.GetTempVATAmountLine(TempVATAmountLine3);
      END;
    END;

    LOCAL PROCEDURE UpdateTotalAmount@16(IndexNo@1000 : Integer);
    VAR
      SaveTotalAmount@1001 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      IF "Prices Including VAT" THEN BEGIN
        SaveTotalAmount := TotalAmount1[IndexNo];
        UpdateInvDiscAmount(IndexNo);
        TotalAmount1[IndexNo] := SaveTotalAmount;
      END;
      WITH TotalPurchLine[IndexNo] DO
        "Inv. Discount Amount" := "Line Amount" - TotalAmount1[IndexNo];
      UpdateInvDiscAmount(IndexNo);
    END;

    LOCAL PROCEDURE UpdateInvDiscAmount@3(ModifiedIndexNo@1000 : Integer);
    VAR
      PartialInvoicing@1001 : Boolean;
      MaxIndexNo@1002 : Integer;
      IndexNo@1003 : ARRAY [2] OF Integer;
      i@1004 : Integer;
      InvDiscBaseAmount@1005 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      IF NOT (ModifiedIndexNo IN [1,2]) THEN
        EXIT;

      IF InvoicedLineExists THEN
        IF NOT CONFIRM(UpdateInvDiscountQst,FALSE) THEN
          ERROR('');

      IF ModifiedIndexNo = 1 THEN
        InvDiscBaseAmount := TempVATAmountLine1.GetTotalInvDiscBaseAmount(FALSE,"Currency Code")
      ELSE
        InvDiscBaseAmount := TempVATAmountLine2.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");

      IF InvDiscBaseAmount = 0 THEN
        ERROR(Text003,TempVATAmountLine2.FIELDCAPTION("Inv. Disc. Base Amount"));

      IF TotalPurchLine[ModifiedIndexNo]."Inv. Discount Amount" / InvDiscBaseAmount > 1 THEN
        ERROR(
          Text004,
          TotalPurchLine[ModifiedIndexNo].FIELDCAPTION("Inv. Discount Amount"),
          TempVATAmountLine2.FIELDCAPTION("Inv. Disc. Base Amount"));

      PartialInvoicing := (TotalPurchLine[1]."Line Amount" <> TotalPurchLine[2]."Line Amount");

      IndexNo[1] := ModifiedIndexNo;
      IndexNo[2] := 3 - ModifiedIndexNo;
      IF (ModifiedIndexNo = 2) AND PartialInvoicing THEN
        MaxIndexNo := 1
      ELSE
        MaxIndexNo := 2;

      IF NOT PartialInvoicing THEN
        IF ModifiedIndexNo = 1 THEN
          TotalPurchLine[2]."Inv. Discount Amount" := TotalPurchLine[1]."Inv. Discount Amount"
        ELSE
          TotalPurchLine[1]."Inv. Discount Amount" := TotalPurchLine[2]."Inv. Discount Amount";

      FOR i := 1 TO MaxIndexNo DO
        WITH TotalPurchLine[IndexNo[i]] DO BEGIN
          IF (i = 1) OR NOT PartialInvoicing THEN
            IF IndexNo[i] = 1 THEN BEGIN
              TempVATAmountLine1.SetInvoiceDiscountAmount(
                "Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
            END ELSE
              TempVATAmountLine2.SetInvoiceDiscountAmount(
                "Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");

          IF (i = 2) AND PartialInvoicing THEN
            IF IndexNo[i] = 1 THEN BEGIN
              InvDiscBaseAmount := TempVATAmountLine2.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");
              IF InvDiscBaseAmount = 0 THEN
                TempVATAmountLine1.SetInvoiceDiscountPercent(
                  0,"Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %")
              ELSE
                TempVATAmountLine1.SetInvoiceDiscountPercent(
                  100 * TempVATAmountLine2.GetTotalInvDiscAmount / InvDiscBaseAmount,
                  "Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %");
            END ELSE BEGIN
              InvDiscBaseAmount := TempVATAmountLine1.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");
              IF InvDiscBaseAmount = 0 THEN
                TempVATAmountLine2.SetInvoiceDiscountPercent(
                  0,"Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %")
              ELSE
                TempVATAmountLine2.SetInvoiceDiscountPercent(
                  100 * TempVATAmountLine1.GetTotalInvDiscAmount / InvDiscBaseAmount,
                  "Currency Code","Prices Including VAT",FALSE,"VAT Base Discount %");
            END;
        END;

      UpdateHeaderInfo(1,TempVATAmountLine1);
      UpdateHeaderInfo(2,TempVATAmountLine2);

      IF ModifiedIndexNo = 1 THEN
        VATLinesForm.SetTempVATAmountLine(TempVATAmountLine1)
      ELSE
        VATLinesForm.SetTempVATAmountLine(TempVATAmountLine2);

      "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
      "Invoice Discount Value" := TotalPurchLine[1]."Inv. Discount Amount";
      MODIFY;
      UpdateVATOnPurchLines;
    END;

    LOCAL PROCEDURE UpdatePrepmtAmount@9();
    VAR
      TempPurchLine@1001 : TEMPORARY Record 39;
      PurchPostPrepmt@1000 : Codeunit 444;
    BEGIN
      PurchPostPrepmt.UpdatePrepmtAmountOnPurchLines(Rec,PrepmtTotalAmount);
      PurchPostPrepmt.GetPurchLines(Rec,0,TempPurchLine);
      PurchPostPrepmt.SumPrepmt(
        Rec,TempPurchLine,TempVATAmountLine4,PrepmtTotalAmount,PrepmtVATAmount,PrepmtVATAmountText);
      PrepmtInvPct :=
        Pct(TotalPurchLine[1]."Prepmt. Amt. Inv.",PrepmtTotalAmount);
      PrepmtDeductedPct :=
        Pct(TotalPurchLine[1]."Prepmt Amt Deducted",TotalPurchLine[1]."Prepmt. Amt. Inv.");
      IF "Prices Including VAT" THEN BEGIN
        PrepmtTotalAmount2 := PrepmtTotalAmount;
        PrepmtTotalAmount := PrepmtTotalAmount + PrepmtVATAmount;
      END ELSE
        PrepmtTotalAmount2 := PrepmtTotalAmount + PrepmtVATAmount;
      MODIFY;
    END;

    LOCAL PROCEDURE GetCaptionClass@2(FieldCaption@1000 : Text[100];ReverseCaption@1001 : Boolean) : Text[80];
    BEGIN
      IF "Prices Including VAT" XOR ReverseCaption THEN
        EXIT('2,1,' + FieldCaption);

      EXIT('2,0,' + FieldCaption);
    END;

    LOCAL PROCEDURE UpdateVATOnPurchLines@1();
    VAR
      PurchLine@1000 : Record 39;
    BEGIN
      GetVATSpecification(ActiveTab);
      IF TempVATAmountLine1.GetAnyLineModified THEN
        PurchLine.UpdateVATOnLines(0,Rec,PurchLine,TempVATAmountLine1);
      IF TempVATAmountLine2.GetAnyLineModified THEN
        PurchLine.UpdateVATOnLines(1,Rec,PurchLine,TempVATAmountLine2);
      PrevNo := '';
    END;

    LOCAL PROCEDURE VendInvDiscRecExists@4(InvDiscCode@1000 : Code[20]) : Boolean;
    VAR
      VendInvDisc@1001 : Record 24;
    BEGIN
      VendInvDisc.SETRANGE(Code,InvDiscCode);
      EXIT(VendInvDisc.FINDFIRST);
    END;

    LOCAL PROCEDURE CheckAllowInvDisc@8();
    VAR
      VendInvDisc@1000 : Record 24;
    BEGIN
      IF NOT AllowInvDisc THEN
        ERROR(
          Text005,
          VendInvDisc.TABLECAPTION,FIELDCAPTION("Invoice Disc. Code"),"Invoice Disc. Code");
    END;

    LOCAL PROCEDURE Pct@6(Numerator@1001 : Decimal;Denominator@1000 : Decimal) : Decimal;
    BEGIN
      IF Denominator = 0 THEN
        EXIT(0);
      EXIT(ROUND(Numerator / Denominator * 10000,1));
    END;

    LOCAL PROCEDURE VATLinesDrillDown@7(VAR VATLinesToDrillDown@1000 : Record 290;ThisTabAllowsVATEditing@1001 : Boolean);
    BEGIN
      CLEAR(VATLinesForm);
      VATLinesForm.SetTempVATAmountLine(VATLinesToDrillDown);
      VATLinesForm.InitGlobals(
        "Currency Code",AllowVATDifference,AllowVATDifference AND ThisTabAllowsVATEditing,
        "Prices Including VAT",AllowInvDisc,"VAT Base Discount %");
      VATLinesForm.RUNMODAL;
      VATLinesForm.GetTempVATAmountLine(VATLinesToDrillDown);
    END;

    PROCEDURE GSTApplicationAmount@1500002() : Decimal;
    VAR
      VendorLedgerEntry@1500000 : Record 25;
      DetailedGSTLedgerEntry@1500001 : Record 16419;
      GSTApplicationBuffer@1500004 : Record 16423;
      TotalAdvAmount@1500002 : Decimal;
    BEGIN
      VendorLedgerEntry.SETCURRENTKEY("Document No.","Document Type","Buy-from Vendor No.");
      IF "Applies-to Doc. No." <> '' THEN
        VendorLedgerEntry.SETRANGE("Document No.","Applies-to Doc. No.");
      IF "Applies-to ID" <> '' THEN
        VendorLedgerEntry.SETRANGE("Applies-to ID","Applies-to ID");
      VendorLedgerEntry.SETRANGE("Document Type","Applies-to Doc. Type");
      VendorLedgerEntry.SETRANGE("Buy-from Vendor No.","Buy-from Vendor No.");
      VendorLedgerEntry.SETRANGE("GST on Advance Payment",TRUE);
      VendorLedgerEntry.SETRANGE(Open,TRUE);
      VendorLedgerEntry.SETRANGE("On Hold",'');
      VendorLedgerEntry.SETFILTER("Amount to Apply",'<>%1',0);
      IF VendorLedgerEntry.FINDSET THEN REPEAT
        GSTApplicationBuffer.SETRANGE("Transaction Type",GSTApplicationBuffer."Transaction Type"::Purchase);
        GSTApplicationBuffer.SETRANGE("Original Document Type",GSTApplicationBuffer."Original Document Type"::Payment);
        GSTApplicationBuffer.SETRANGE("Original Document No.",VendorLedgerEntry."Document No.");
        GSTApplicationBuffer.SETRANGE("GST Group Code",VendorLedgerEntry."GST Group Code");
        GSTApplicationBuffer.SETRANGE("CLE/VLE Entry No.",VendorLedgerEntry."Entry No.");
        IF GSTApplicationBuffer.FINDSET THEN REPEAT
          TotalAdvAmount += ABS(GSTApplicationBuffer."Applied Amount") ;//* QtyFactor;
        UNTIL GSTApplicationBuffer.NEXT = 0;
      UNTIL VendorLedgerEntry.NEXT = 0;
      EXIT(TotalAdvAmount);
    END;

    LOCAL PROCEDURE GetApplicableAmount@1500000(TotalAdvAmount@1500002 : Decimal;TotalGSTAmountInvShip@1500001 : Decimal) : Decimal;
    VAR
      TotalGSTAmountApplied@1500000 : Decimal;
    BEGIN
      IF TotalGSTAmountInvShip > TotalAdvAmount THEN
        TotalGSTAmountApplied := TotalAdvAmount
      ELSE
        TotalGSTAmountApplied := TotalGSTAmountInvShip;
      EXIT(TotalGSTAmountApplied);
    END;

    LOCAL PROCEDURE ClearGSTStatisticsAmount@1500200();
    BEGIN
      TotalGST := 0;
      TotalGSTInvoiced := 0;
      TotalGSTShipped := 0;
      TotalAdvAmount := 0;
      TotalAmountApplied := 0;
      TotalAdvGSTInvoiced := 0;
      TotalAdvGSTShipped := 0;
      ReverseChargeGST := 0;
      ReverseChargeGSTInvoiced := 0;
      ReverseChargeGSTShipped := 0;
    END;

    BEGIN
    END.
  }
}

