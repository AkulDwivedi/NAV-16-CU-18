OBJECT Page 160 Sales Statistics
{
  OBJECT-PROPERTIES
  {
    Date=29-06-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.48822,NAVIN9.00.00.48822;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Sales Statistics;
               ENN=Sales Statistics];
    InsertAllowed=No;
    DeleteAllowed=No;
    LinksAllowed=No;
    SourceTable=Table36;
    PageType=ListPlus;
    OnOpenPage=BEGIN
                 SalesSetup.GET;
                 CALCFIELDS("Price Inclusive of Taxes");
                 IF NOT "Price Inclusive of Taxes" THEN
                   AllowInvDisc := NOT (SalesSetup."Calc. Inv. Discount" AND CustInvDiscRecExists("Invoice Disc. Code"))
                 ELSE
                   AllowInvDisc := FALSE;
                 AllowVATDifference :=
                   SalesSetup."Allow VAT Difference" AND
                   NOT ("Document Type" IN ["Document Type"::Quote,"Document Type"::"Blanket Order"]);
                 CurrPage.EDITABLE :=
                   AllowVATDifference OR AllowInvDisc;
                 SetVATSpecification;
               END;

    OnAfterGetRecord=VAR
                       SalesLine@1000 : Record 37;
                       TempSalesLine@1001 : TEMPORARY Record 37;
                       GSTManagement@1500001 : Codeunit 16401;
                       Transactiontype@1500000 : 'Purchase,Sale';
                     BEGIN
                       CurrPage.CAPTION(STRSUBSTNO(Text000,"Document Type"));
                       IF PrevNo = "No." THEN BEGIN
                         GetVATSpecification;
                         EXIT;
                       END;
                       PrevNo := "No.";
                       FILTERGROUP(2);
                       SETRANGE("No.",PrevNo);
                       FILTERGROUP(0);
                       CLEAR(SalesLine);
                       CLEAR(TotalSalesLine);
                       CLEAR(TotalSalesLineLCY);
                       CLEAR(SalesPost);
                       CLEAR(TotalGSTInvoiced);
                       CLEAR(TotalAdvGSTInvoiced);

                       SalesLine.CheckHeaderAndLineLocation(Rec);
                       SalesPost.GetSalesLines(Rec,TempSalesLine,0);
                       CLEAR(SalesPost);
                       SalesPost.SumSalesLinesTemp(
                         Rec,TempSalesLine,0,TotalSalesLine,TotalSalesLineLCY,
                         VATAmount,VATAmountText,ProfitLCY,ProfitPct,TotalAdjCostLCY);

                       AdjProfitLCY := TotalSalesLineLCY.Amount - TotalAdjCostLCY;
                       IF TotalSalesLineLCY.Amount <> 0 THEN
                         AdjProfitPct := ROUND(AdjProfitLCY / TotalSalesLineLCY.Amount * 100,0.1);

                       IF (TotalSalesLine."Service Tax Amount" = 0) AND
                         (TotalSalesLine."Service Tax eCess Amount" + TotalSalesLine."Service Tax SHE Cess Amount" <> 0) THEN BEGIN
                         TotalSalesLine."Service Tax eCess Amount" := 0;
                         TotalSalesLine."Service Tax SHE Cess Amount" := 0;
                       END;

                       IF "Prices Including VAT" THEN BEGIN
                         TotalAmount2 := TotalSalesLine.Amount;
                         TotalAmount1 := TotalAmount2 + VATAmount;
                         TotalSalesLine."Line Amount" := TotalAmount1 + TotalSalesLine."Inv. Discount Amount";
                       END ELSE BEGIN
                         TotalAmount1 := TotalSalesLine.Amount;
                         // TotalAmount2 := TotalSalesLine."Amount Including VAT";
                         TotalAmount2 := TotalAmount1;
                       END;

                       ClearGSTStatisticsAmount;
                       SalesLine.SETRANGE("Document Type","Document Type");
                       SalesLine.SETRANGE("Document No.","No.");
                       SalesLine.SETFILTER(Quantity,'<>%1',0);
                       IF SalesLine.FINDSET THEN
                         REPEAT
                           TotalGSTInvoiced += GSTManagement.RoundTotalGSTAmountQtyFactor(Transactiontype::Sale,SalesLine."Document Type",
                             SalesLine."Document No.",SalesLine."Line No.",1,"Currency Code");
                         UNTIL SalesLine.NEXT = 0;
                       IF ("Applies-to Doc. No." <> '') OR ("Applies-to ID" <> '')THEN BEGIN
                         TotalAdvAmount += GSTApplicationAmount;
                         TotalAdvGSTInvoiced := GetApplicableAmount(TotalAdvAmount,TotalGSTInvoiced);
                       END;

                       IF Cust.GET("Bill-to Customer No.") THEN
                         Cust.CALCFIELDS("Balance (LCY)")
                       ELSE
                         CLEAR(Cust);
                       IF Cust."Credit Limit (LCY)" = 0 THEN
                         CreditLimitLCYExpendedPct := 0
                       ELSE
                         IF Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" < 0 THEN
                           CreditLimitLCYExpendedPct := 0
                         ELSE
                           IF Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" > 1 THEN
                             CreditLimitLCYExpendedPct := 10000
                           ELSE
                             CreditLimitLCYExpendedPct := ROUND(Cust."Balance (LCY)" / Cust."Credit Limit (LCY)" * 10000,1);

                       SalesLine.CalcVATAmountLines(0,Rec,TempSalesLine,TempVATAmountLine);
                       TempVATAmountLine.MODIFYALL(Modified,FALSE);
                       SetVATSpecification;

                       VATAmount := 0;
                     END;

    OnQueryClosePage=BEGIN
                       GetVATSpecification;
                       IF TempVATAmountLine.GetAnyLineModified THEN
                         UpdateVATOnSalesLines;
                       EXIT(TRUE);
                     END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ENN=General] }

    { 129 ;2   ;Field     ;
                Name=Amount;
                CaptionML=[ENU=Amount;
                           ENN=Amount];
                SourceExpr=TotalSalesLine."Line Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text002,FALSE);
                Editable=FALSE }

    { 93  ;2   ;Field     ;
                Name=InvDiscountAmount;
                CaptionML=[ENU=Inv. Discount Amount;
                           ENN=Inv. Discount Amount];
                SourceExpr=TotalSalesLine."Inv. Discount Amount";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                OnValidate=BEGIN
                             UpdateInvDiscAmount;
                           END;
                            }

    { 80  ;2   ;Field     ;
                CaptionML=[ENU=Total;
                           ENN=Total];
                SourceExpr=TotalAmount1;
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                CaptionClass=GetCaptionClass(Text001,FALSE);
                OnValidate=BEGIN
                             UpdateTotalAmount;
                           END;
                            }

    { 1280001;2;Field     ;
                CaptionML=[ENU=Excise Amount;
                           ENN=Excise Amount];
                SourceExpr=TotalSalesLine."Excise Amount";
                Editable=FALSE }

    { 1280000;2;Field     ;
                CaptionML=[ENU=Tax Amount;
                           ENN=Tax Amount];
                SourceExpr=TotalSalesLine."Tax Amount";
                Editable=FALSE }

    { 1500000;2;Field     ;
                CaptionML=[ENU=GST Amount;
                           ENN=GST Amount];
                SourceExpr=ABS(TotalGSTInvoiced) - TotalAdvGSTInvoiced;
                Editable=FALSE }

    { 1280004;2;Field     ;
                CaptionML=[ENU=Service Tax;
                           ENN=Service Tax];
                SourceExpr=TotalSalesLine."Service Tax Amount" + TotalSalesLine."Service Tax eCess Amount" + TotalSalesLine."Service Tax SHE Cess Amount" + TotalSalesLine."Service Tax SBC Amount" + TotalSalesLine."KK Cess Amount";
                Editable=FALSE }

    { 1280003;2;Field     ;
                CaptionML=[ENU=Charges;
                           ENN=Charges];
                SourceExpr=TotalSalesLine."Charges To Customer";
                Editable=FALSE }

    { 1500004;2;Field     ;
                CaptionML=[ENU=TCS Amount;
                           ENN=TCS Amount];
                SourceExpr=TotalSalesLine."Total TDS/TCS Incl. SHE CESS";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 1280005;2;Field     ;
                CaptionML=[ENU=Net Total;
                           ENN=Net Total];
                SourceExpr=TotalSalesLine."Amount To Customer";
                AutoFormatType=1;
                AutoFormatExpr="Currency Code";
                Editable=FALSE }

    { 95  ;2   ;Field     ;
                CaptionML=[ENU=Quantity;
                           ENN=Quantity];
                DecimalPlaces=0:5;
                SourceExpr=TotalSalesLine.Quantity;
                Editable=FALSE }

    { 73  ;2   ;Field     ;
                CaptionML=[ENU=Parcels;
                           ENN=Parcels];
                DecimalPlaces=0:5;
                SourceExpr=TotalSalesLine."Units per Parcel";
                Editable=FALSE }

    { 91  ;2   ;Field     ;
                CaptionML=[ENU=Net Weight;
                           ENN=Net Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalSalesLine."Net Weight";
                Editable=FALSE }

    { 82  ;2   ;Field     ;
                CaptionML=[ENU=Gross Weight;
                           ENN=Gross Weight];
                DecimalPlaces=0:5;
                SourceExpr=TotalSalesLine."Gross Weight";
                Editable=FALSE }

    { 71  ;2   ;Field     ;
                CaptionML=[ENU=Volume;
                           ENN=Volume];
                DecimalPlaces=0:5;
                SourceExpr=TotalSalesLine."Unit Volume";
                Editable=FALSE }

    { 78  ;2   ;Field     ;
                CaptionML=[ENU=Original Cost (LCY);
                           ENN=Original Cost (LCY)];
                SourceExpr=TotalSalesLineLCY."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Cost (LCY);
                           ENN=Adjusted Cost (LCY)];
                SourceExpr=TotalAdjCostLCY;
                AutoFormatType=1;
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                CaptionML=[ENU=Cost Adjmt. Amount (LCY);
                           ENN=Cost Adjmt. Amount (LCY)];
                SourceExpr=TotalAdjCostLCY - TotalSalesLineLCY."Unit Cost (LCY)";
                AutoFormatType=1;
                Editable=FALSE;
                OnLookup=BEGIN
                           LookupAdjmtValueEntries(0);
                         END;
                          }

    { 2   ;2   ;Field     ;
                CaptionML=[ENU=Sales (LCY);
                           ENN=Sales (LCY)];
                SourceExpr=TotalSalesLineLCY.Amount;
                AutoFormatType=1;
                Editable=FALSE }

    { 79  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit (LCY);
                           ENN=Original Profit (LCY)];
                SourceExpr=ProfitLCY;
                AutoFormatType=1;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit (LCY);
                           ENN=Adjusted Profit (LCY)];
                SourceExpr=AdjProfitLCY;
                AutoFormatType=1;
                Editable=FALSE }

    { 81  ;2   ;Field     ;
                CaptionML=[ENU=Original Profit %;
                           ENN=Original Profit %];
                DecimalPlaces=1:1;
                SourceExpr=ProfitPct;
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                CaptionML=[ENU=Adjusted Profit %;
                           ENN=Adjusted Profit %];
                DecimalPlaces=1:1;
                SourceExpr=AdjProfitPct;
                Editable=FALSE }

    { 1903289601;1;Group  ;
                CaptionML=[ENU=Customer;
                           ENN=Customer] }

    { 67  ;2   ;Field     ;
                CaptionML=[ENU=Balance (LCY);
                           ENN=Balance (LCY)];
                SourceExpr=Cust."Balance (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 68  ;2   ;Field     ;
                CaptionML=[ENU=Credit Limit (LCY);
                           ENN=Credit Limit (LCY)];
                SourceExpr=Cust."Credit Limit (LCY)";
                AutoFormatType=1;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                ExtendedDatatype=Ratio;
                CaptionML=[ENU=Expended % of Credit Limit (LCY);
                           ENN=Expended % of Credit Limit (LCY)];
                ToolTipML=[ENU=Expended % of Credit Limit (LCY);
                           ENN=Expended % of Credit Limit (LCY)];
                SourceExpr=CreditLimitLCYExpendedPct }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Sales %1 Statistics;ENN=Sales %1 Statistics';
      Text001@1001 : TextConst 'ENU=Total;ENN=Total';
      Text002@1002 : TextConst 'ENU=Amount;ENN=Amount';
      Text003@1003 : TextConst 'ENU=%1 must not be 0.;ENN=%1 must not be 0.';
      Text004@1004 : TextConst 'ENU=%1 must not be greater than %2.;ENN=%1 must not be greater than %2.';
      Text005@1005 : TextConst '@@@=You cannot change the invoice discount because there is a Cust. Invoice Disc. record for Invoice Disc. Code 30000.;ENU=You cannot change the invoice discount because there is a %1 record for %2 %3.;ENN=You cannot change the invoice discount because there is a %1 record for %2 %3.';
      TotalSalesLine@1006 : Record 37;
      TotalSalesLineLCY@1007 : Record 37;
      Cust@1008 : Record 18;
      TempVATAmountLine@1009 : TEMPORARY Record 290;
      SalesSetup@1010 : Record 311;
      SalesPost@1011 : Codeunit 80;
      TotalAmount1@1012 : Decimal;
      TotalAmount2@1013 : Decimal;
      VATAmount@1014 : Decimal;
      VATAmountText@1015 : Text[30];
      ProfitLCY@1016 : Decimal;
      ProfitPct@1017 : Decimal;
      AdjProfitLCY@1024 : Decimal;
      AdjProfitPct@1023 : Decimal;
      TotalAdjCostLCY@1022 : Decimal;
      CreditLimitLCYExpendedPct@1018 : Decimal;
      PrevNo@1019 : Code[20];
      AllowInvDisc@1020 : Boolean;
      AllowVATDifference@1021 : Boolean;
      TotalGSTInvoiced@1500000 : Decimal;
      TotalAdvGSTInvoiced@1500001 : Decimal;
      TotalAdvAmount@1500002 : Decimal;

    LOCAL PROCEDURE UpdateHeaderInfo@5();
    VAR
      CurrExchRate@1000 : Record 330;
      UseDate@1001 : Date;
    BEGIN
      TotalSalesLine."Inv. Discount Amount" := TempVATAmountLine.GetTotalInvDiscAmount;
      TotalAmount1 :=
        TotalSalesLine."Line Amount" - TotalSalesLine."Inv. Discount Amount";
      VATAmount := TempVATAmountLine.GetTotalVATAmount;
      IF "Prices Including VAT" THEN BEGIN
        TotalAmount1 := TempVATAmountLine.GetTotalAmountInclVAT;
        TotalAmount2 := TotalAmount1 - VATAmount;
        TotalSalesLine."Line Amount" := TotalAmount1 + TotalSalesLine."Inv. Discount Amount";
      END ELSE
        TotalAmount2 := TotalAmount1 + VATAmount;

      IF "Prices Including VAT" THEN
        TotalSalesLineLCY.Amount := TotalAmount2
      ELSE
        TotalSalesLineLCY.Amount := TotalAmount1;
      IF "Currency Code" <> '' THEN BEGIN
        IF ("Document Type" IN ["Document Type"::"Blanket Order","Document Type"::Quote]) AND
           ("Posting Date" = 0D)
        THEN
          UseDate := WORKDATE
        ELSE
          UseDate := "Posting Date";

        TotalSalesLineLCY.Amount :=
          CurrExchRate.ExchangeAmtFCYToLCY(
            UseDate,"Currency Code",TotalSalesLineLCY.Amount,"Currency Factor");
      END;
      ProfitLCY := TotalSalesLineLCY.Amount - TotalSalesLineLCY."Unit Cost (LCY)";
      IF TotalSalesLineLCY.Amount = 0 THEN
        ProfitPct := 0
      ELSE
        ProfitPct := ROUND(100 * ProfitLCY / TotalSalesLineLCY.Amount,0.01);

      AdjProfitLCY := TotalSalesLineLCY.Amount - TotalAdjCostLCY;
      IF TotalSalesLineLCY.Amount = 0 THEN
        AdjProfitPct := 0
      ELSE
        AdjProfitPct := ROUND(100 * AdjProfitLCY / TotalSalesLineLCY.Amount,0.01);
    END;

    LOCAL PROCEDURE GetVATSpecification@21();
    BEGIN
      // CurrForm.SubForm.PAGE.GetTempVATAmountLine(TempVATAmountLine);
      IF TempVATAmountLine.GetAnyLineModified THEN
        UpdateHeaderInfo;
    END;

    LOCAL PROCEDURE SetVATSpecification@11();
    BEGIN
      // CurrForm.SubForm.PAGE.SetTempVATAmountLine(TempVATAmountLine);
      // CurrForm.SubForm.PAGE.InitGlobals(
      // "Currency Code",AllowVATDifference,AllowVATDifference,
      // "Prices Including VAT",AllowInvDisc,"VAT Base Discount %");
    END;

    LOCAL PROCEDURE UpdateTotalAmount@16();
    VAR
      SaveTotalAmount@1000 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      IF "Prices Including VAT" THEN BEGIN
        SaveTotalAmount := TotalAmount1;
        UpdateInvDiscAmount;
        TotalAmount1 := SaveTotalAmount;
      END;
      WITH TotalSalesLine DO
        "Inv. Discount Amount" := "Line Amount" - TotalAmount1;
      UpdateInvDiscAmount;
    END;

    LOCAL PROCEDURE UpdateInvDiscAmount@3();
    VAR
      InvDiscBaseAmount@1000 : Decimal;
    BEGIN
      CheckAllowInvDisc;
      InvDiscBaseAmount := TempVATAmountLine.GetTotalInvDiscBaseAmount(FALSE,"Currency Code");
      IF InvDiscBaseAmount = 0 THEN
        ERROR(Text003,TempVATAmountLine.FIELDCAPTION("Inv. Disc. Base Amount"));

      IF TotalSalesLine."Inv. Discount Amount" / InvDiscBaseAmount > 1 THEN
        ERROR(
          Text004,
          TotalSalesLine.FIELDCAPTION("Inv. Discount Amount"),
          TempVATAmountLine.FIELDCAPTION("Inv. Disc. Base Amount"));

      TempVATAmountLine.SetInvoiceDiscountAmount(
        TotalSalesLine."Inv. Discount Amount","Currency Code","Prices Including VAT","VAT Base Discount %");
      // CurrForm.SubForm.PAGE.SetTempVATAmountLine(TempVATAmountLine);
      UpdateHeaderInfo;

      "Invoice Discount Calculation" := "Invoice Discount Calculation"::Amount;
      "Invoice Discount Value" := TotalSalesLine."Inv. Discount Amount";
      MODIFY;
      UpdateVATOnSalesLines;
    END;

    LOCAL PROCEDURE GetCaptionClass@2(FieldCaption@1000 : Text[100];ReverseCaption@1001 : Boolean) : Text[80];
    BEGIN
      IF "Prices Including VAT" XOR ReverseCaption THEN
        EXIT('2,1,' + FieldCaption);

      EXIT('2,0,' + FieldCaption);
    END;

    LOCAL PROCEDURE UpdateVATOnSalesLines@1();
    VAR
      SalesLine@1000 : Record 37;
    BEGIN
      GetVATSpecification;
      IF TempVATAmountLine.GetAnyLineModified THEN BEGIN
        SalesLine.UpdateVATOnLines(0,Rec,SalesLine,TempVATAmountLine);
        SalesLine.UpdateVATOnLines(1,Rec,SalesLine,TempVATAmountLine);
      END;
      PrevNo := '';
    END;

    LOCAL PROCEDURE CustInvDiscRecExists@4(InvDiscCode@1000 : Code[20]) : Boolean;
    VAR
      CustInvDisc@1001 : Record 19;
    BEGIN
      CustInvDisc.SETRANGE(Code,InvDiscCode);
      EXIT(CustInvDisc.FINDFIRST);
    END;

    LOCAL PROCEDURE CheckAllowInvDisc@8();
    VAR
      CustInvDisc@1000 : Record 19;
    BEGIN
      IF NOT AllowInvDisc THEN
        ERROR(
          Text005,
          CustInvDisc.TABLECAPTION,FIELDCAPTION("Invoice Disc. Code"),"Invoice Disc. Code");
    END;

    LOCAL PROCEDURE GSTApplicationAmount@1500001() : Decimal;
    VAR
      CustLedgerEntry@1500000 : Record 21;
      DetailedGSTLedgerEntry@1500001 : Record 16419;
      GSTApplicationBuffer@1500004 : Record 16423;
      TotalAdvAmount@1500002 : Decimal;
      TotalInvAmount@1500006 : Decimal;
      TotalGSTAmount@1500007 : Decimal;
      ApplicationFactor@1500009 : Decimal;
    BEGIN
      CustLedgerEntry.SETCURRENTKEY("Document No.","Document Type","Sell-to Customer No.");
      IF "Applies-to Doc. No." <> '' THEN
        CustLedgerEntry.SETRANGE("Document No.","Applies-to Doc. No.");
      IF "Applies-to ID" <> '' THEN
        CustLedgerEntry.SETRANGE("Applies-to ID","Applies-to ID");
      CustLedgerEntry.SETRANGE("Document Type","Applies-to Doc. Type");
      CustLedgerEntry.SETRANGE("Sell-to Customer No.","Sell-to Customer No.");
      CustLedgerEntry.SETRANGE("GST on Advance Payment",TRUE);
      CustLedgerEntry.SETRANGE(Open,TRUE);
      CustLedgerEntry.SETRANGE("On Hold",'');
      CustLedgerEntry.SETFILTER("Amount to Apply",'<>%1',0);
      IF CustLedgerEntry.FINDSET THEN REPEAT
        GSTApplicationBuffer.SETRANGE("Transaction Type",GSTApplicationBuffer."Transaction Type"::Sale);
        GSTApplicationBuffer.SETRANGE("Original Document Type",GSTApplicationBuffer."Original Document Type"::Payment);
        GSTApplicationBuffer.SETRANGE("Original Document No.",CustLedgerEntry."Document No.");
        GSTApplicationBuffer.SETRANGE("GST Group Code",CustLedgerEntry."GST Group Code");
        GSTApplicationBuffer.SETRANGE("CLE/VLE Entry No.",CustLedgerEntry."Entry No.");
        IF GSTApplicationBuffer.FINDSET THEN REPEAT
          TotalAdvAmount += ABS(GSTApplicationBuffer."Applied Amount") ;
        UNTIL GSTApplicationBuffer.NEXT = 0;
      UNTIL CustLedgerEntry.NEXT = 0;
      EXIT(TotalAdvAmount);
    END;

    LOCAL PROCEDURE GetApplicableAmount@1500000(TotalAdvAmount@1500002 : Decimal;TotalGSTAmountInvShip@1500001 : Decimal) : Decimal;
    VAR
      TotalGSTAmountApplied@1500000 : Decimal;
    BEGIN
      IF ABS(TotalGSTAmountInvShip) > ABS(TotalAdvAmount) THEN
        TotalGSTAmountApplied := ABS(TotalAdvAmount)
      ELSE
        TotalGSTAmountApplied := ABS(TotalGSTAmountInvShip);
      EXIT(TotalGSTAmountApplied);
    END;

    LOCAL PROCEDURE ClearGSTStatisticsAmount@1500200();
    BEGIN
      TotalGSTInvoiced := 0;
      TotalAdvAmount := 0;
      TotalAdvGSTInvoiced := 0;
    END;

    BEGIN
    END.
  }
}

