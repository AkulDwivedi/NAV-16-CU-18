OBJECT Codeunit 5610 Calculate Depreciation
{
  OBJECT-PROPERTIES
  {
    Date=03-08-16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.46621,NAVIN9.00.00.46621;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DeprBook@1000 : Record 5611;
      FADeprBook@1001 : Record 5612;
      CalculateNormalDepr@1002 : Codeunit 5611;
      CalculateCustom1Depr@1003 : Codeunit 5612;
      FAShiftLineNo@1500000 : Integer;
      FARetroBook@1500001 : Record 16528;

    PROCEDURE Calculate@1(VAR DeprAmount@1000 : Decimal;VAR Custom1Amount@1001 : Decimal;VAR NumberOfDays@1002 : Integer;VAR Custom1NumberOfDays@1003 : Integer;FANo@1004 : Code[20];DeprBookCode@1005 : Code[10];UntilDate@1006 : Date;EntryAmounts@1007 : ARRAY [4] OF Decimal;DateFromProjection@1008 : Date;DaysInPeriod@1009 : Integer);
    VAR
      FA@1500000 : Record 5600;
    BEGIN
      DeprAmount := 0;
      Custom1Amount := 0;
      NumberOfDays := 0;
      Custom1NumberOfDays := 0;
      IF NOT DeprBook.GET(DeprBookCode) THEN
        EXIT;
      IF NOT FADeprBook.GET(FANo,DeprBookCode) THEN
        EXIT;

      CheckDeprDaysInFiscalYear(DateFromProjection = 0D,UntilDate);

      IF NOT (DeprBook."Use Custom 1 Depreciation" AND
              (FADeprBook."Depr. Ending Date (Custom 1)" > 0D))
      THEN
        CalculateNormalDepr.SetFAShiftLineNo(FAShiftLineNo);

      IF DeprBook."Use Custom 1 Depreciation" AND
         (FADeprBook."Depr. Ending Date (Custom 1)" > 0D)
      THEN
        CalculateCustom1Depr.Calculate(
          DeprAmount,Custom1Amount,NumberOfDays,
          Custom1NumberOfDays,FANo,DeprBookCode,UntilDate,
          EntryAmounts,DateFromProjection,DaysInPeriod)
      ELSE
        CalculateNormalDepr.Calculate(
          DeprAmount,NumberOfDays,FANo,DeprBookCode,UntilDate,
          EntryAmounts,DateFromProjection,DaysInPeriod);
      IF FA.GET(FANo) THEN
        IF FA."Add. Depr. Applicable" THEN
          CalcAddDeprSecondYear(DeprAmount,FANo,DeprBookCode,DeprBook."Depr. Threshold Days");
    END;

    LOCAL PROCEDURE CheckDeprDaysInFiscalYear@2(CheckDeprDays@1001 : Boolean;UntilDate@1000 : Date);
    VAR
      DepreciationCalc@1004 : Codeunit 5616;
      FADateCalc@1002 : Codeunit 5617;
      FiscalYearBegin@1003 : Date;
      NoOfDeprDays@1005 : Integer;
    BEGIN
      IF DeprBook."Allow more than 360/365 Days" OR NOT CheckDeprDays THEN
        EXIT;
      IF  (FADeprBook."Depreciation Method" = FADeprBook."Depreciation Method"::"Declining-Balance 1") OR
         (FADeprBook."Depreciation Method" = FADeprBook."Depreciation Method"::"DB1/SL")
      THEN
        FiscalYearBegin := FADateCalc.GetFiscalYear(DeprBook.Code,UntilDate);
      IF DeprBook."Fiscal Year 365 Days" THEN
        NoOfDeprDays := DepreciationCalc.DeprDays(FiscalYearBegin,UntilDate,DeprBook."Fiscal Year 365 Days")
      ELSE
        NoOfDeprDays := 360;
      IF (DepreciationCalc.DeprDays(
            FiscalYearBegin,UntilDate,DeprBook."Fiscal Year 365 Days") > NoOfDeprDays) AND
         (DeprBook."FA Book Type" <> DeprBook."FA Book Type"::"Income Tax")
      THEN
        DeprBook.TESTFIELD("Allow more than 360/365 Days");
    END;

    PROCEDURE SetFAShiftLineNo@1500001(LineNo@1500000 : Integer);
    BEGIN
      FAShiftLineNo := LineNo;
    END;

    PROCEDURE CalculateRetro@1500000(VAR DeprAmount@1000 : Decimal;VAR NumberOfDays@1002 : Integer;UntilDate@1006 : Date;EntryAmounts@1007 : ARRAY [4] OF Decimal;DateFromProjection@1008 : Date;DaysInPeriod@1009 : Integer;FARetrospectiveBook@1500001 : Record 16528;FALedgerEntryRetro@1500000 : Record 5601;SLPercent@1500002 : Decimal;DBPercent@1500003 : Decimal);
    BEGIN
      DeprAmount := 0;
      NumberOfDays := 0;

      FARetroBook := FARetrospectiveBook;
      IF NOT DeprBook.GET(FARetroBook."Depreciation Book Code") THEN
        EXIT;

      CheckDeprDaysInFiscalYearRetro(DateFromProjection = 0D,UntilDate);
      CalculateNormalDepr.CalculateRetro(DeprAmount,NumberOfDays,UntilDate,EntryAmounts,DateFromProjection,
        DaysInPeriod,FARetrospectiveBook,FALedgerEntryRetro,SLPercent,DBPercent);
    END;

    LOCAL PROCEDURE CheckDeprDaysInFiscalYearRetro@1500002(CheckDeprDays@1001 : Boolean;UntilDate@1000 : Date);
    VAR
      DepreciationCalc@1004 : Codeunit 5616;
      FADateCalc@1002 : Codeunit 5617;
      FiscalYearBegin@1003 : Date;
      NoOfDeprDays@1005 : Integer;
    BEGIN
      IF DeprBook."Allow more than 360/365 Days" OR NOT CheckDeprDays THEN
        EXIT;
      IF  (FARetroBook."Proposed Depreciation Method" = FARetroBook."Proposed Depreciation Method"::"Declining-Balance 1") OR
         (FARetroBook."Proposed Depreciation Method" = FARetroBook."Proposed Depreciation Method"::"DB1/SL")
      THEN
        FiscalYearBegin := FADateCalc.GetFiscalYear(DeprBook.Code,UntilDate);
      IF DeprBook."Fiscal Year 365 Days" THEN
        NoOfDeprDays := DepreciationCalc.DeprDays(FiscalYearBegin,UntilDate,DeprBook."Fiscal Year 365 Days")
      ELSE
        NoOfDeprDays := 360;
      IF (DepreciationCalc.DeprDays(
            FiscalYearBegin,UntilDate,DeprBook."Fiscal Year 365 Days") > NoOfDeprDays) AND
         (DeprBook."FA Book Type" <> DeprBook."FA Book Type"::"Income Tax")
      THEN
        DeprBook.TESTFIELD("Allow more than 360/365 Days");
    END;

    PROCEDURE CalcAddDeprSecondYear@1500003(VAR AddDeprAmount@1500003 : Decimal;FANumber@1500002 : Code[20];DeprBookCode2@1500001 : Code[10];DeprBookThresholdDays@1500004 : Integer);
    VAR
      FALedgerEntry@1500005 : Record 5601;
      FADateCalculation@1500000 : Codeunit 5617;
      Count@1500007 : Integer;
      AcqisitionFiscalEndDate@1500006 : Date;
    BEGIN
      FALedgerEntry.SETRANGE("FA No.",FANumber);
      FALedgerEntry.SETRANGE("Depreciation Book Code",DeprBookCode2);
      FALedgerEntry.SETRANGE("FA Posting Type",FALedgerEntry."FA Posting Type"::"Acquisition Cost");
      IF FALedgerEntry.FINDFIRST THEN
        AcqisitionFiscalEndDate := FADateCalculation.GetFiscalYearEndDateInc(FALedgerEntry."Posting Date");

      FALedgerEntry.SETRANGE("FA Posting Type",FALedgerEntry."FA Posting Type"::Depreciation);
      IF NOT FALedgerEntry.FINDFIRST THEN
        EXIT;
      Count := FALedgerEntry.COUNT;

      IF FALedgerEntry."Posting Date" <= AcqisitionFiscalEndDate THEN
        IF (Count = 1) AND (FALedgerEntry."Add. Depreciation Amount" <> 0) AND
           (FALedgerEntry."No. of Depreciation Days" <= DeprBookThresholdDays)
        THEN
          AddDeprAmount += FALedgerEntry."Add. Depreciation Amount";
    END;

    BEGIN
    END.
  }
}

