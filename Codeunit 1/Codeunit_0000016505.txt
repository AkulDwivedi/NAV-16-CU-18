OBJECT Codeunit 16505 Gate Entry- Post
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    TableNo=16552;
    OnRun=BEGIN
            GateEntryHeader := Rec;
            WITH GateEntryHeader DO BEGIN
              TESTFIELD("Posting Date");
              TESTFIELD("Document Date");

              GateEntryLine.RESET;
              GateEntryLine.SETRANGE("Entry Type","Entry Type");
              GateEntryLine.SETRANGE("Gate Entry No.","No.");
              IF NOT GateEntryLine.FIND('-') THEN
                ERROR(Text16500);

              IF GateEntryLine.FINDSET THEN
                REPEAT
                  IF GateEntryLine."Source Type" <> GateEntryLine."Source Type"::" " THEN
                    GateEntryLine.TESTFIELD("Source No.");
                  IF GateEntryLine."Source Type" = GateEntryLine."Source Type"::" " THEN
                    GateEntryLine.TESTFIELD(Description);
                UNTIL GateEntryLine.NEXT = 0;

              IF GUIALLOWED THEN
                Window.OPEN(
                  '#1###########################\\' +
                  Text16501);
              IF GUIALLOWED THEN
                Window.UPDATE(1,STRSUBSTNO('%1 %2',Text16502,"No."));

              IF "Posting No. Series" = '' THEN BEGIN
                GateEntryLocSetup.GET("Entry Type","Location Code");
                "Posting No. Series" := GateEntryLocSetup."Posting No. Series";
                MODIFY;
                COMMIT;
              END;
              IF "Posting No." = '' THEN BEGIN
                "Posting No." := NoSeriesMgt.GetNextNo("Posting No. Series","Posting Date",TRUE);
                ModifyHeader := TRUE;
              END;
              IF ModifyHeader THEN BEGIN
                MODIFY;
                COMMIT;
              END;

              GateEntryLine.LOCKTABLE;

              PostedGateEntryHeader.INIT;
              PostedGateEntryHeader.TRANSFERFIELDS(GateEntryHeader);
              PostedGateEntryHeader."No." := "Posting No.";
              PostedGateEntryHeader."No. Series" := "Posting No. Series";
              PostedGateEntryHeader."Gate Entry No." := "No.";

              IF GUIALLOWED THEN
                Window.UPDATE(1,STRSUBSTNO(Text16503,"No.",PostedGateEntryHeader."No."));
              PostedGateEntryHeader.INSERT;

              // Posting Comments to posted tables.
              CopyCommentLines("Entry Type","Entry Type","No.",PostedGateEntryHeader."No.");

              GateEntryLine.RESET;
              GateEntryLine.SETRANGE("Entry Type","Entry Type");
              GateEntryLine.SETRANGE("Gate Entry No.","No.");
              LineCount := 0;
              IF GateEntryLine.FINDSET THEN
                REPEAT
                  LineCount += 1;
                  IF GUIALLOWED THEN
                    Window.UPDATE(2,LineCount);
                  PostedGateEntryLine.INIT;
                  PostedGateEntryLine.TRANSFERFIELDS(GateEntryLine);
                  PostedGateEntryLine."Entry Type" := PostedGateEntryHeader."Entry Type";
                  PostedGateEntryLine."Gate Entry No." := PostedGateEntryHeader."No.";
                  PostedGateEntryLine.INSERT;
                UNTIL GateEntryLine.NEXT = 0;

              DELETE;
              GateEntryLine.DELETEALL;
            END;
            IF GUIALLOWED THEN
              Window.CLOSE;
            Rec := GateEntryHeader;
          END;

  }
  CODE
  {
    VAR
      GateEntryHeader@1500000 : Record 16552;
      GateEntryLine@1500001 : Record 16553;
      PostedGateEntryHeader@1500010 : Record 16555;
      PostedGateEntryLine@1500011 : Record 16556;
      Text16500@1500003 : TextConst 'ENU=There is nothing to post.;ENN=There is nothing to post.';
      Text16501@1500004 : TextConst 'ENU=Posting Lines #2######\;ENN=Posting Lines #2######\';
      Text16502@1500006 : TextConst 'ENU=Gate Entry.;ENN=Gate Entry.';
      Text16503@1500007 : TextConst 'ENU=Gate Entry %1 -> Posted Gate Entry %2.;ENN=Gate Entry %1 -> Posted Gate Entry %2.';
      GateEntryLocSetup@1500013 : Record 16554;
      GateEntryCommentLine@1500014 : Record 16559;
      GateEntryCommentLine2@1500015 : Record 16559;
      NoSeriesMgt@1500008 : Codeunit 396;
      Window@1500002 : Dialog;
      ModifyHeader@1500009 : Boolean;
      LineCount@1500012 : Integer;

    PROCEDURE CopyCommentLines@1500000(FromEntryType@1500003 : Integer;ToEntryType@1500002 : Integer;FromNumber@1500001 : Code[20];ToNumber@1500000 : Code[20]);
    BEGIN
      GateEntryCommentLine.SETRANGE("Gate Entry Type",FromEntryType);
      GateEntryCommentLine.SETRANGE("No.",FromNumber);
      IF GateEntryCommentLine.FINDSET THEN
        REPEAT
          GateEntryCommentLine2 := GateEntryCommentLine;
          GateEntryCommentLine2."Gate Entry Type" := ToEntryType;
          GateEntryCommentLine2."No." := ToNumber;
          GateEntryCommentLine2.INSERT;
        UNTIL GateEntryCommentLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

