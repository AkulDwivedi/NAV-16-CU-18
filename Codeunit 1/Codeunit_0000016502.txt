OBJECT Codeunit 16502 FA Fiscal Year-Close
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    TableNo=16519;
    OnRun=BEGIN
            AccountingPeriod.COPY(Rec);
            Code;
            Rec := AccountingPeriod;
          END;

  }
  CODE
  {
    VAR
      AccountingPeriod@1500004 : Record 16519;
      AccountingPeriod2@1500003 : Record 16519;
      AccountingPeriod3@1500002 : Record 16519;
      FiscalYearStartDate@1500001 : Date;
      FiscalYearEndDate@1500000 : Date;
      Text16500@1500008 : TextConst 'ENU=You must create a new fiscal year before you can close the old year.;ENN=You must create a new fiscal year before you can close the old year.';
      Text16501@1500007 : TextConst 'ENU="This function closes the fiscal year from %1 to %2. ";ENN="This function closes the fiscal year from %1 to %2. "';
      Text16502@1500006 : TextConst 'ENU=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\;ENN=Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed.\\';
      Text16503@1500005 : TextConst 'ENU=Do you want to close the fiscal year?;ENN=Do you want to close the fiscal year?';

    PROCEDURE Code@1500003();
    BEGIN
      WITH AccountingPeriod DO BEGIN
        AccountingPeriod2.SETRANGE(Closed,FALSE);
        AccountingPeriod2.FIND('-');

        FiscalYearStartDate := AccountingPeriod2."Starting Date";
        AccountingPeriod := AccountingPeriod2;
        TESTFIELD("New Fiscal Year",TRUE);

        AccountingPeriod2.SETRANGE("New Fiscal Year",TRUE);
        IF AccountingPeriod2.FIND('>') THEN BEGIN
          FiscalYearEndDate := CALCDATE('<-1D>',AccountingPeriod2."Starting Date");

          AccountingPeriod3 := AccountingPeriod2;
          AccountingPeriod2.SETRANGE("New Fiscal Year");
          AccountingPeriod2.FIND('<');
        END ELSE
          ERROR(Text16500);

        IF NOT
           CONFIRM(
             Text16501 +
             Text16502 +
             Text16503,FALSE,
             FiscalYearStartDate,FiscalYearEndDate)
        THEN
          EXIT;

        RESET;

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod2."Starting Date");
        MODIFYALL(Closed,TRUE);

        SETRANGE("Starting Date",FiscalYearStartDate,AccountingPeriod3."Starting Date");
        MODIFYALL("Date Locked",TRUE);

        RESET;
      END;
    END;

    BEGIN
    END.
  }
}

