OBJECT Report 13717 Create TDS/TCS Fiscal Year
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create TDS/TCS Fiscal Year;
               ENN=Create TDS/TCS Fiscal Year];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF StartDate > EndDate THEN
                    ERROR(Text005);
                  TDSAccountingPeriod."Starting Date" := StartDate;
                  TDSAccountingPeriod.TESTFIELD("Starting Date");
                  IF TDSAccountingPeriod.FIND('-') THEN BEGIN
                    FirstPeriodStartDate := TDSAccountingPeriod."Starting Date";
                    IF TDSAccountingPeriod.Closed THEN
                      FirstPeriodLocked := TRUE
                    ELSE
                      FirstPeriodLocked := FALSE;
                    IF (StartDate < FirstPeriodStartDate) AND FirstPeriodLocked THEN
                      IF NOT
                         CONFIRM(
                           Text000 +
                           Text001)
                      THEN
                        EXIT;
                    IF TDSAccountingPeriod.FIND('+') THEN BEGIN
                      LastPeriodStartDate := TDSAccountingPeriod."Ending Date";
                      LastEntryNo := TDSAccountingPeriod."No." + 1;
                    END;
                  END ELSE
                    IF NOT
                       CONFIRM(
                         Text002 +
                         Text003)
                    THEN
                      EXIT;

                  IF FirstPeriodStartDate <> 0D THEN
                    IF (StartDate >= FirstPeriodStartDate) AND (StartDate < LastPeriodStartDate) THEN
                      ERROR(Text004);
                  TDSAccountingPeriod.INIT;
                  TDSAccountingPeriod."Starting Date" := StartDate;
                  TDSAccountingPeriod.VALIDATE("Starting Date");
                  TDSAccountingPeriod."Ending Date" := EndDate;
                  TDSAccountingPeriod."Financial Year for eTDS/TCS" := FORMAT(DATE2DMY(StartDate,3)) + FORMAT(EndDate,2,'<year,2>');
                  TDSAccountingPeriod."Assessment Year for eTDS/TCS" := FORMAT(DATE2DMY(EndDate,3)) + FORMAT(CALCDATE('1Y',EndDate),2,'<year,2>');
                  IF LastEntryNo = 0 THEN
                    LastEntryNo := 1;
                  TDSAccountingPeriod."No." := LastEntryNo;

                  IF NOT TDSAccountingPeriod.FIND('=') THEN
                    TDSAccountingPeriod.INSERT;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1280000;2;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ENN=Starting Date];
                  SourceExpr=StartDate }

      { 1280002;2;Field     ;
                  Name=EndingDate;
                  CaptionML=[ENU=Ending Date;
                             ENN=Ending Date];
                  SourceExpr=EndDate }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TDSAccountingPeriod@1280002 : Record 13724;
      StartDate@1280000 : Date;
      EndDate@1280001 : Date;
      FirstPeriodStartDate@1280006 : Date;
      LastPeriodStartDate@1280005 : Date;
      FirstPeriodLocked@1280004 : Boolean;
      Text000@1280014 : TextConst 'ENU=The new TDS fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\;ENN=The new TDS fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\';
      Text001@1280013 : TextConst 'ENU=Do you want to create and close the fiscal year?;ENN=Do you want to create and close the fiscal year?';
      Text002@1280012 : TextConst 'ENU=Once you create the new fiscal year you cannot change its starting date.\\;ENN=Once you create the new fiscal year you cannot change its starting date.\\';
      Text003@1280011 : TextConst 'ENU=Do you want to create the fiscal year?;ENN=Do you want to create the fiscal year?';
      Text004@1280010 : TextConst 'ENU=It is only possible to create new fiscal years before or after the existing ones.;ENN=It is only possible to create new fiscal years before or after the existing ones.';
      LastEntryNo@1280007 : Integer;
      Text005@1500000 : TextConst 'ENU=Please check the dates, starting date cannot be greater than ending date.;ENN=Please check the dates, starting date cannot be greater than ending date.';

    PROCEDURE InitializeRequest@1500000(NewStartDate@1500000 : Date;NewEndDate@1500001 : Date);
    BEGIN
      StartDate := NewStartDate;
      EndDate := NewEndDate;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

