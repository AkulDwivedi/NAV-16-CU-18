OBJECT Codeunit 5981 Service-Post (Yes/No)
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.49326,NAVIN9.00.00.49326;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume;ENN=&Ship,&Invoice,Ship &and Invoice,Ship and &Consume';
      Text001@1001 : TextConst 'ENU=Do you want to post the %1?;ENN=Do you want to post the %1?';
      ServiceHeader@1003 : Record 5900;
      ServicePost@1004 : Codeunit 5980;
      Selection@1005 : Integer;
      PreviewMode@1002 : Boolean;
      CancelErr@1006 : TextConst 'ENU=The preview has been canceled.;ENN=The preview has been canceled.';
      Text16500@1500001 : TextConst 'ENU=You have to attach transit document for this invoice.;ENN=You have to attach transit document for this invoice.';
      Text16501@1500000 : TextConst 'ENU=Transit document(s) are not required  for this invoice.;ENN=Transit document(s) are not required  for this invoice.';
      LinewisePosting@1500002 : Boolean;
      NothingToPostErr@1007 : TextConst 'ENU=There is nothing to post.;ENN=There is nothing to post.';

    [TryFunction]
    LOCAL PROCEDURE Code@1(VAR PassedServLine@1003 : Record 5902);
    VAR
      FormLine@1500001 : Record 13757;
      TransDocDetails@1500000 : Record 13768;
      Ship@1000 : Boolean;
      Consume@1001 : Boolean;
      Invoice@1002 : Boolean;
    BEGIN
      IF NOT ServiceHeader.FIND THEN
        ERROR(NothingToPostErr);

      WITH ServiceHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Order:
            BEGIN
              Selection := STRMENU(Text000,3);
              IF Selection = 0 THEN BEGIN
                IF PreviewMode THEN
                  ERROR(CancelErr);
                EXIT;
              END;
              Ship := Selection IN [1,3,4];
              Consume := Selection IN [4];
              Invoice := Selection IN [2,3];
            END
          ELSE
            IF NOT PreviewMode THEN
              IF NOT CONFIRM(Text001,FALSE,"Document Type") THEN
                EXIT;
        END;
        IF "Form Code" <> '' THEN BEGIN
          FormLine.SETRANGE("Form Code","Form Code");
          FormLine.SETRANGE("Form No.","Form No.");
          FormLine.MODIFYALL(Issued,TRUE);

          TransDocDetails.SETRANGE(Type,TransDocDetails.Type::Sale);
          TransDocDetails.SETRANGE("PO / SO No.","No.");
          IF "Transit Document" AND (NOT TransDocDetails.FINDFIRST) THEN
            ERROR(Text16500)
          ELSE
            IF TransDocDetails.FINDFIRST THEN
              ERROR(Text16501);
        END;
        IF LinewisePosting THEN
          ServicePost.SetLinewisePosting;
        ServicePost.SetPreviewMode(PreviewMode);
        ServicePost.PostWithLines(ServiceHeader,PassedServLine,Ship,Consume,Invoice);
        IF NOT PreviewMode THEN
          COMMIT;
      END;
    END;

    PROCEDURE PostDocument@2(VAR Rec@1000 : Record 5900);
    VAR
      DummyServLine@1001 : TEMPORARY Record 5902;
    BEGIN
      ServiceHeader.COPY(Rec);
      Code(DummyServLine);
      Rec := ServiceHeader;
    END;

    PROCEDURE PostDocumentWithLines@3(VAR Rec@1000 : Record 5900;VAR PassedServLine@1001 : Record 5902);
    BEGIN
      LinewisePosting := TRUE;
      ServiceHeader.COPY(Rec);
      Code(PassedServLine);
      Rec := ServiceHeader;
    END;

    PROCEDURE PreviewDocument@4(VAR ServHeader@1000 : Record 5900);
    VAR
      TempServLine@1001 : TEMPORARY Record 5902;
      GenJnlPostPreview@1003 : Codeunit 19;
    BEGIN
      GenJnlPostPreview.Start;
      PreviewMode := TRUE;
      ServiceHeader.COPY(ServHeader);
      IF NOT Code(TempServLine) THEN BEGIN
      //  IF GETLASTERRORTEXT = CancelErr THEN
      //    EXIT;
        GenJnlPostPreview.Finish;
        //IF GETLASTERRORTEXT = GenJnlPostPreview.GetPreviewModeErrMessage THEN BEGIN
        IF GETLASTERRORTEXT <> GenJnlPostPreview.GetPreviewModeErrMessage THEN
          ERROR(GETLASTERRORTEXT);
          GenJnlPostPreview.ShowAllEntries;
          ERROR('');
        END;

      //GenJnlPostPreview.ShowAllEntries;
    END;

    PROCEDURE PreviewDocumentWithLines@5(VAR ServHeader@1000 : Record 5900;VAR PassedServLine@1004 : Record 5902);
    VAR
      GenJnlPostPreview@1003 : Codeunit 19;
    BEGIN
      GenJnlPostPreview.Start;
      PreviewMode := TRUE;
      ServiceHeader.COPY(ServHeader);
      IF NOT Code(PassedServLine) THEN BEGIN
      //  IF GETLASTERRORTEXT = CancelErr THEN
      //    EXIT;
        GenJnlPostPreview.Finish;
        IF GETLASTERRORTEXT <> GenJnlPostPreview.GetPreviewModeErrMessage THEN
          ERROR(GETLASTERRORTEXT);
        GenJnlPostPreview.ShowAllEntries;
        ERROR('');
      END;

      //GenJnlPostPreview.ShowAllEntries;
    END;

    BEGIN
    END.
  }
}

