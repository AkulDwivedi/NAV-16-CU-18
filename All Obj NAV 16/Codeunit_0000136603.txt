OBJECT Codeunit 136603 ERM RS Package Operations
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
            isInitialized := FALSE;
          END;

  }
  CODE
  {
    VAR
      LibraryRapidStart@1002 : Codeunit 131903;
      LibraryUtility@1003 : Codeunit 131000;
      LibraryERM@1023 : Codeunit 131300;
      LibraryWarehouse@1007 : Codeunit 132204;
      LibrarySales@1106 : Codeunit 130509;
      LibraryPurchase@1006 : Codeunit 130512;
      LibraryRandom@1001 : Codeunit 130440;
      LibraryVariableStorage@1031 : Codeunit 131004;
      LibraryReportValidation@1032 : Codeunit 131002;
      ConfigValidateMgt@1025 : Codeunit 8617;
      ConfigPackageMgt@1016 : Codeunit 8611;
      ConfigXMLExchange@1015 : Codeunit 8614;
      ConfigPckgCompressionMgt@1024 : Codeunit 8619;
      FileMgt@1017 : Codeunit 419;
      Assert@1004 : Codeunit 130000;
      isInitialized@1000 : Boolean;
      PackageDataError@1014 : TextConst 'ENU=There are errors in Package Data Error.';
      ApplyError@1018 : TextConst 'ENU=There are errors in Apply Migration Data.';
      Text101@1030 : TextConst 'ENU=Package Card page shows incorrect data.';
      Text103@1028 : TextConst 'ENU=Gen. Journal Line is not posted.';
      NoDataAfterImportErr@1008 : TextConst 'ENU=No Data In Package.';
      ErrorInApplyingWithoutValidationFlag@1009 : TextConst 'ENU=Data is not applied in case validation flag for field is set to false.';
      FlowFieldAppearedInData@1010 : TextConst 'ENU=Only normal fields should be available after import.';
      ErrorOnEvaluating@1005 : TextConst 'ENU=Error on evaluating %1 datatype.';
      NoErrorOnEvaluating@1011 : TextConst 'ENU=Must be error on evaluating %1 datatype.';
      FieldValueIsIncorrect@1012 : TextConst 'ENU=%1 is incorrect.';
      ExportImportInterfereError@1112 : TextConst 'ENU=XML Package Data Export/Import change existing package data.';
      ExportImportWrongPackageError@1113 : TextConst 'ENU=XML Package Data Export/Import process wrong package.';
      TableNotValidated@1019 : TextConst 'ENU=Table ID was validated incorrectly.';
      XMLGeneratedIncorrectly@1013 : TextConst 'ENU=Localization information in the XML document was generated incorrectly.';
      FileTextMsg@1020 : TextConst 'ENU=FileText';
      NotGZIPFormatErr@1021 : TextConst 'ENU=Generated file is not in GZIP format.';
      FileContentMismatchErr@1022 : TextConst 'ENU=File content mismatch after GZIP compression.';
      DecompressWrongResultErr@1026 : TextConst 'ENU=Decompress returns true for non GZip file.';
      ValueIsIncorrectErr@1027 : TextConst 'ENU=%1 value is incorrect.';
      PackageErr@1029 : TextConst 'ENU=There are errors in Package %1.';
      PackageImportErr@1034 : TextConst '@@@=An error occurred while importing the 452 table. The table does not exist in the database.;ENU=An error occurred while importing the %1 table. The table does not exist in the database.';

    [Test]
    PROCEDURE CreatePackage@17();
    VAR
      ConfigPackage@1003 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      ConfigPackageField@1002 : Record 8616;
      Customer@1001 : Record 18;
    BEGIN
      // Test if a new Package can be created and applied for Master Records.
      Initialize;

      // 2. Exercise: Create new package table record for Customer.
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);
      ConfigPackageField.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigPackageField.SETRANGE("Table ID",DATABASE::Customer);
      ConfigPackageMgt.SelectAllPackageFields(ConfigPackageField,TRUE);

      // 3. Verify: Check that the information in package table and package fields
      ConfigPackageTable.CALCFIELDS("Table Name");
      Assert.AreEqual(ConfigPackageTable.GetNoOfDatabaseRecords,Customer.COUNT,'Wrong number of Database Records in the package');
      ConfigPackageTable.TESTFIELD("Table Name",Customer.TABLENAME);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportPackageToXML@20();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      FilePath@1002 : Text;
    BEGIN
      // Test if the new Package can be exported to XML.
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);
      ExportToXML(ConfigPackage.Code,ConfigPackageTable,FilePath);

      LibraryUtility.CheckFileNotEmpty(FilePath);

      ERASE(FilePath);
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ImportPackageFromXML@25();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
    BEGIN
      // Test if the new Package can be imported from XML.
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      ExportImportXML(ConfigPackage.Code);

      Assert.IsTrue(ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer),NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportLocalizedPackageToXML@29();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      PackageXML@1002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNode@1003 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XPathNodeList@1005 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XPathNodeList";
    BEGIN
      // Test if localization attributes are exported to XML
      Initialize;

      // Generate localized package
      LibraryRapidStart.CreatePackage(ConfigPackage);
      ConfigPackage.VALIDATE("Language ID",FindFirstLanguage);
      ConfigPackage.MODIFY(TRUE);

      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,FindRandomTableID(100));
      SetLocalizeFields(ConfigPackage.Code,ConfigPackageTable."Table ID");

      PackageXML := PackageXML.XmlDocument;
      ConfigPackageTable.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigXMLExchange.ExportPackageXMLDocument(PackageXML,ConfigPackageTable,ConfigPackage,TRUE);

      // Verify that _locDefinition node and _loc attributes have been generated in XML document
      XMLNode := PackageXML.SelectSingleNode('//_locDefinition');
      XPathNodeList := PackageXML.SelectNodes('//@_loc');

      Assert.IsTrue((XMLNode.InnerXml <> '') AND (XPathNodeList.Count <> 0),XMLGeneratedIncorrectly);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithPageID_DataImported@11();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      RandomPageId@1002 : Integer;
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      RandomPageId := LibraryRandom.RandInt(1000);
      ConfigPackageTable."Page ID" := RandomPageId;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(RandomPageId,ConfigPackageTable."Page ID",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithPackageProcessingOrder_DataImported@14();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      RandomId@1002 : Integer;
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      RandomId := LibraryRandom.RandInt(1000);
      ConfigPackageTable."Package Processing Order" := RandomId;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(RandomId,ConfigPackageTable."Package Processing Order",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithProcessingOrder_DataImported@16();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      RandomId@1002 : Integer;
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      RandomId := LibraryRandom.RandInt(1000);
      ConfigPackageTable."Processing Order" := RandomId;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(RandomId,ConfigPackageTable."Processing Order",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithDataTemplate_DataImported@19();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      RandomCode@1002 : Code[10];
    BEGIN
      // Test if the new Package can be imported from XML.
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      RandomCode := LibraryUtility.GenerateRandomCode(ConfigPackageTable.FIELDNO("Data Template"),DATABASE::"Config. Package Table");
      ConfigPackageTable."Data Template" := RandomCode;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(RandomCode,ConfigPackageTable."Data Template",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithComments_DataImported@21();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      RandomCode@1002 : Code[10];
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      RandomCode := LibraryUtility.GenerateRandomCode(ConfigPackageTable.FIELDNO(Comments),DATABASE::"Config. Package Table");
      ConfigPackageTable.Comments := RandomCode;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(RandomCode,ConfigPackageTable.Comments,NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithCreatedByUser_DataImported@23();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      RandomCode@1002 : Code[10];
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      RandomCode :=
        LibraryUtility.GenerateRandomCode(ConfigPackageTable.FIELDNO("Created by User ID"),DATABASE::"Config. Package Table");
      ConfigPackageTable."Created by User ID" := RandomCode;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(RandomCode,ConfigPackageTable."Created by User ID",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithSkipTableTriggers_DataImported@24();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      ConfigPackageTable."Skip Table Triggers" := TRUE;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(TRUE,ConfigPackageTable."Skip Table Triggers",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackage_ExportPackageTableWithDimensionsAsColumns_DataImported@26();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
    BEGIN
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      ConfigPackageTable."Dimensions as Columns" := TRUE;
      ConfigPackageTable.MODIFY;

      ExportImportXML(ConfigPackage.Code);

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(TRUE,ConfigPackageTable."Dimensions as Columns",NoDataAfterImportErr);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackages_ExportOneOfTwoPackage_ImpordDoNotSpoiledExistingData@106();
    VAR
      ConfigPackage1@1000 : Record 8623;
      ConfigPackage2@1001 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      FilePath@1003 : Text;
    BEGIN
      Initialize;
      CreateSimplePackage(ConfigPackage1);
      CreateSimplePackage(ConfigPackage2);

      ExportToXML(ConfigPackage1.Code,ConfigPackageTable,FilePath);
      ConfigXMLExchange.ImportPackageXML(FilePath);
      ERASE(FilePath);

      Assert.IsTrue(IsPackageDataExists(ConfigPackage2.Code,FALSE),ExportImportWrongPackageError);
    END;

    [Test]
    PROCEDURE ExportImportPackages_ImportOneOfTwoPackage_RequiredPackageProcessed@108();
    VAR
      ConfigPackage1@1003 : Record 8623;
      ConfigPackage2@1002 : Record 8623;
    BEGIN
      Initialize;
      CreateSimplePackage(ConfigPackage1);
      CreateSimplePackage(ConfigPackage2);

      ExportImportXML(ConfigPackage1.Code);
      Cleanup(ConfigPackage2.Code);

      Assert.IsTrue(
        IsPackageDataExists(ConfigPackage1.Code,FALSE) AND NOT IsPackageDataExists(ConfigPackage2.Code,TRUE),
        ExportImportInterfereError);
    END;

    [Test]
    PROCEDURE GZIP_CompressTextFile_CompressedFileInGZIPFormat@37();
    VAR
      CompressedServerFileName@1002 : Text;
    BEGIN
      CompressedServerFileName := CreateCompressedFile;

      VerifyGZIPHeader(CompressedServerFileName);
    END;

    [Test]
    PROCEDURE GZIP_CompressDecompressTextFile_FileContentMatch@105();
    VAR
      CompressedServerFileName@1002 : Text;
      DecompressedServerFileName@1003 : Text;
      Text@1004 : Text;
    BEGIN
      CompressedServerFileName := CreateCompressedFile;

      DecompressedServerFileName := FileMgt.ServerTempFileName('');
      ConfigPckgCompressionMgt.ServersideDecompress(CompressedServerFileName,DecompressedServerFileName);

      Text := ReadTextFromFile(DecompressedServerFileName);

      Assert.IsTrue(Text = FileTextMsg,FileContentMismatchErr);
    END;

    [Test]
    PROCEDURE GZIP_DecompressNonGZipFile_FunctionReturnFalse@8();
    VAR
      ServerFileName@1002 : Text;
      DecompressedServerFileName@1003 : Text;
    BEGIN
      CreateTextFile(ServerFileName);

      DecompressedServerFileName := FileMgt.ServerTempFileName('');

      Assert.IsFalse(
        ConfigPckgCompressionMgt.ServersideDecompress(ServerFileName,DecompressedServerFileName),
        DecompressWrongResultErr);
    END;

    [Test]
    PROCEDURE ImportPackageFromXMLMultiple@62();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      FilePath@1000 : Text;
      PackageCode@1170000000 : Code[20];
    BEGIN
      // Test if the new Package can be imported from XML for related tables.
      Initialize;

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"VAT Product Posting Group");
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"VAT Business Posting Group");
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"VAT Posting Setup");
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Business Posting Group");
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Product Posting Group");
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"General Posting Setup");
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"G/L Account");

      FilePath := FileMgt.ServerTempFileName('xml');
      ConfigPackageTable.SETRANGE("Package Code",ConfigPackage.Code);
      IF ConfigPackageTable.FINDSET THEN
        REPEAT
          ConfigXMLExchange.ExportPackageXML(ConfigPackageTable,FilePath);
        UNTIL ConfigPackageTable.NEXT = 0;

      PackageCode := ConfigPackage.Code;
      Cleanup(PackageCode);

      // 2. Exercise: Import package from XML.
      ConfigXMLExchange.ImportPackageXML(FilePath);

      ConfigPackage.GET(PackageCode);
      Assert.IsTrue(
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"VAT Product Posting Group") AND
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"VAT Business Posting Group") AND
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"VAT Posting Setup") AND
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"Gen. Business Posting Group") AND
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"Gen. Product Posting Group") AND
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"General Posting Setup") AND
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"G/L Account"),
        PackageDataError);

      ERASE(FilePath);
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ImportPackageFromXMLValidate@49();
    VAR
      ConfigPackage@1000 : Record 8623;
      ConfigPackageTable@1009 : Record 8613;
      ConfigPackageError@1008 : Record 8617;
      Customer@1007 : Record 18;
      ConfigPackageField@1003 : Record 8616;
      XMLDOMManagement@1005 : Codeunit 6224;
      RecRef@1002 : RecordRef;
      XMLDocument@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      // Test if the new Package can be validated with function Validate Table Relation.
      Initialize;
      XMLDOMManagement.LoadXMLDocumentFromText(
        '<?xml version="1.0" encoding="UTF-16" standalone="yes"?><DataList></DataList>',XMLDocument);

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);
      ConfigPackageMgt.SelectAllPackageFields(ConfigPackageField,FALSE);
      ConfigPackageTable.CALCFIELDS("Table Name");

      LibrarySales.CreateCustomer(Customer);
      RecRef.GETTABLE(Customer);
      AddConfigPackageTableToXML(XMLDocument,XMLDocument.DocumentElement,ConfigPackageTable,RecRef);

      ConfigXMLExchange.ImportPackageXMLDocument(XMLDocument);

      LibraryRapidStart.ValidatePackage(ConfigPackage,FALSE);
      // 2. Validate table relation
      Assert.IsTrue(ConfigPackageError.ISEMPTY,PackageDataError);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ImportPackageFromXMLApply@79();
    VAR
      ConfigPackage@1002 : Record 8623;
      Country@1000 : Record 9;
      Location@1001 : Record 14;
      CountryCode@1004 : Code[10];
      LocationCode@1003 : Code[10];
    BEGIN
      Initialize;
      ImportFromXML(ConfigPackage,CountryCode,LocationCode);

      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);

      Cleanup(ConfigPackage.Code);

      Assert.IsFalse(Country.GET(CountryCode) AND Location.GET(LocationCode),ApplyError);
    END;

    [Test]
    PROCEDURE ImportAndApplyRSPackage_DataImported@41();
    VAR
      ConfigPackage@1002 : Record 8623;
      ConfigPackageTable@1001 : Record 8613;
      PaymentMethod@1006 : Record 289;
      ConfigPackageImport@1003 : Codeunit 8620;
      ConfigPckgCompressionMgt@1000 : Codeunit 8619;
      PaymentMethodCode@1007 : Code[10];
      FilePath@1004 : Text;
      CompressedServerFileName@1005 : Text;
    BEGIN
      Initialize;

      CreatePaymentMethod(PaymentMethod);
      PaymentMethodCode := PaymentMethod.Code;

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"Payment Method");
      ExportToXML(ConfigPackage.Code,ConfigPackageTable,FilePath);
      LibraryUtility.CheckFileNotEmpty(FilePath);

      CompressedServerFileName := FileMgt.ServerTempFileName('');
      ConfigPckgCompressionMgt.ServersideCompress(FilePath,CompressedServerFileName);

      PaymentMethod.DELETE;
      ERASE(FilePath);
      Cleanup(ConfigPackage.Code);

      ConfigPackageImport.ImportAndApplyRapidStartPackage(CompressedServerFileName);

      Assert.IsTrue(
        ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::"Payment Method") AND
        PaymentMethod.GET(PaymentMethodCode),
        NoDataAfterImportErr);

      PaymentMethod.DELETE;
      ERASE(CompressedServerFileName);
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ImportPackageFromXMLKeyApply@73();
    VAR
      ConfigPackage@1000 : Record 8623;
      GenBusPostingGroup@1001 : Record 250;
      GenProductPostingGroup@1002 : Record 251;
      GeneralPostingSetup@1003 : Record 252;
      GenProductPostingGroupCode@1006 : Code[10];
      GenBusPostingGroupCode@1005 : Code[10];
    BEGIN
      Initialize;
      ImportFromXMLKey(ConfigPackage,GenProductPostingGroupCode,GenBusPostingGroupCode);

      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);

      Cleanup(ConfigPackage.Code);

      Assert.IsFalse(
        GenBusPostingGroup.GET(GenBusPostingGroupCode) AND
        GenProductPostingGroup.GET(GenProductPostingGroupCode) AND
        GeneralPostingSetup.GET(GenBusPostingGroupCode,GenProductPostingGroupCode),
        ApplyError);
    END;

    [Test]
    PROCEDURE ImportDataFromXMLValidatedYes@58();
    VAR
      ConfigPackage@1006 : Record 8623;
      ConfigPackageTable@1001 : Record 8613;
      Location@1002 : Record 14;
      PostCode@1004 : Record 225;
      LocationCode@1000 : Code[20];
      PostCode2@1003 : Code[20];
      City2@1005 : Text[30];
      FilePath@1007 : Text;
    BEGIN
      Initialize;

      // Create new location
      LibraryWarehouse.CreateLocation(Location);
      LibraryERM.CreatePostCode(PostCode);
      Location.City := PostCode.City;
      Location."Post Code" := PostCode.Code;
      Location.MODIFY;
      LocationCode := Location.Code;
      PostCode2 := Location."Post Code";
      City2 := Location.City;

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Location);
      LibraryRapidStart.SetIncludeOneField(ConfigPackage.Code,DATABASE::Location,Location.FIELDNO(Name),TRUE);
      LibraryRapidStart.SetIncludeOneField(ConfigPackage.Code,DATABASE::Location,Location.FIELDNO("Name 2"),TRUE);
      LibraryRapidStart.SetIncludeOneField(ConfigPackage.Code,DATABASE::Location,Location.FIELDNO(City),TRUE);
      LibraryRapidStart.SetIncludeOneField(ConfigPackage.Code,DATABASE::Location,Location.FIELDNO("Post Code"),TRUE);

      ExportToXML(ConfigPackage.Code,ConfigPackageTable,FilePath);

      Cleanup(ConfigPackage.Code);
      PostCode.DELETE;
      Location.DELETE;

      ConfigXMLExchange.ImportPackageXML(FilePath);
      LibraryRapidStart.SetValidateOneField(ConfigPackage.Code,DATABASE::Location,Location.FIELDNO(City),FALSE);
      LibraryRapidStart.SetValidateOneField(ConfigPackage.Code,DATABASE::Location,Location.FIELDNO("Post Code"),FALSE);
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);

      Location.GET(LocationCode);
      Assert.IsTrue(
        (City2 = Location.City) AND
        (PostCode2 = Location."Post Code"),
        ErrorInApplyingWithoutValidationFlag);

      ERASE(FilePath);
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ImportNormalFieldsOnly@69();
    VAR
      ConfigPackage@1002 : Record 8623;
      ConfigPackageTable@1003 : Record 8613;
      ConfigPackageField@1000 : Record 8616;
      AvailableFields@1001 : Integer;
    BEGIN
      // Verify that no FlowFields or FlowFilters are created after import
      Initialize;

      // Package Table Fields are automatically initialized here and contain only normal field class fields and no blobs
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);
      // Export all package table fields
      ConfigPackageField.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigPackageField.SETRANGE("Table ID",DATABASE::Customer);
      ConfigPackageMgt.SelectAllPackageFields(ConfigPackageField,TRUE);
      AvailableFields := GetNoOfAvailableFields(ConfigPackageTable);

      ExportImportXML(ConfigPackage.Code);

      // Check number of available fields after import
      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Customer);
      Assert.AreEqual(AvailableFields,GetNoOfAvailableFields(ConfigPackageTable),FlowFieldAppearedInData);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ImportGenJnlLineAndPost@51();
    VAR
      GenJnlLine@1002 : Record 81;
      ConfigPackage@1004 : Record 8623;
      GLAccount@1013 : Record 15;
      PaymentMethod@1019 : Record 289;
      GenBusinessPostingGroup@1018 : Record 250;
      GenProductPostingGroup@1017 : Record 251;
      GeneralPostingSetup@1016 : Record 252;
      VATBusinessPostingGroup@1015 : Record 323;
      VATProductPostingGroup@1014 : Record 324;
      VendorPostingGroup@1009 : Record 93;
      PaymentTerms@1007 : Record 3;
      DocumentNo@1008 : Code[20];
      Amount@1010 : Decimal;
      FilePath@1000 : Text;
    BEGIN
      Initialize;

      // Init general setup
      CreateGenJnlLineSetup(
        GLAccount,
        PaymentMethod,
        GenBusinessPostingGroup,
        GenProductPostingGroup,
        GeneralPostingSetup,
        VATBusinessPostingGroup,
        VATProductPostingGroup,
        VendorPostingGroup,
        PaymentTerms);

      CreateGenJnlLines(GenJnlLine,Amount,GLAccount."No.",DocumentNo);
      // Export data to XML
      CreateAndExportPackageDataForGenJnlLine(ConfigPackage,FilePath);

      // Delete the created line before import
      GenJnlLine.DELETE(TRUE);
      Cleanup(ConfigPackage.Code);
      // Import created file
      ConfigXMLExchange.ImportPackageXML(FilePath);
      LibraryRapidStart.ApplyPackage(ConfigPackage,TRUE);

      PostGenJnlLines(GLAccount."No.",DocumentNo);

      // Verification
      VerifyGenJnlLineAmount(GenJnlLine."Document Type"::Invoice,DocumentNo,GLAccount."No.",Amount);

      ERASE(FilePath);
      Cleanup(ConfigPackage.Code);
      CleanupGenJnlSetupData(
        PaymentMethod,
        GenBusinessPostingGroup,
        GenProductPostingGroup,
        GeneralPostingSetup,
        VATBusinessPostingGroup,
        VATProductPostingGroup,
        VendorPostingGroup,
        PaymentTerms);
    END;

    [Test]
    PROCEDURE CheckNoofFields@72();
    VAR
      ConfigPackage@1003 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      ConfigPackageField@1001 : Record 8616;
      NoOfFields@1002 : Integer;
    BEGIN
      // Check that in Package Table the field "No of Fields Included" represents the actual data
      Initialize;

      // Add table Vendor to Package
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Vendor);

      // Select some package fields
      LibraryRapidStart.SetIncludeFields(ConfigPackage.Code,DATABASE::Vendor,2,22,TRUE);
      LibraryRapidStart.SetIncludeFields(ConfigPackage.Code,DATABASE::Vendor,54,85,TRUE);
      LibraryRapidStart.SetIncludeFields(ConfigPackage.Code,DATABASE::Vendor,92,103,TRUE);

      ConfigPackageField.RESET;
      ConfigPackageField.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigPackageField.SETRANGE("Table ID",DATABASE::Vendor);
      ConfigPackageField.SETRANGE("Include Field",TRUE);
      NoOfFields := ConfigPackageField.COUNT;

      ConfigPackageTable.GET(ConfigPackage.Code,DATABASE::Vendor);
      ConfigPackageTable.CALCFIELDS("No. of Fields Included");

      // Validate field show correct data
      Assert.AreEqual(NoOfFields,ConfigPackageTable."No. of Fields Included",Text101);

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE PackageOverviewPage_DatabaseDataAction@48();
    VAR
      ConfigPackageCard@1002 : TestPage 8614;
      VendorList@1001 : TestPage 27;
    BEGIN
      // Preparation
      Initialize;
      InitializePackageCard(ConfigPackageCard,FALSE);

      // Check page Vendor List is opened by Database Records
      VendorList.TRAP;
      ConfigPackageCard."Config. Package Subform".DatabaseRecords.INVOKE;
      VendorList.CLOSE;

      Cleanup(ConfigPackageCard.Code.VALUE);
    END;

    [Test]
    [HandlerFunctions(PackageRecordsPageHandler)]
    PROCEDURE PackageOverviewPage_PackageDataAction@12();
    VAR
      ConfigPackageCard@1000 : TestPage 8614;
    BEGIN
      Initialize;
      InitializePackageCard(ConfigPackageCard,TRUE);

      // Package Records page open
      ConfigPackageCard."Config. Package Subform".PackageRecords.INVOKE;

      Cleanup(ConfigPackageCard.Code.VALUE);
    END;

    [Test]
    [HandlerFunctions(PackageFieldsPageHandler)]
    PROCEDURE PackageOverviewPage_PackageFieldsAction@36();
    VAR
      ConfigPackageCard@1000 : TestPage 8614;
    BEGIN
      Initialize;
      InitializePackageCard(ConfigPackageCard,TRUE);

      // Package Fields page open
      ConfigPackageCard."Config. Package Subform".PackageFields.INVOKE;

      Cleanup(ConfigPackageCard.Code.VALUE);
    END;

    [Test]
    [HandlerFunctions(ConfirmYesHandler,MessageHandler)]
    PROCEDURE PackageOverviewPage_ValidateRelationsAction@38();
    VAR
      ConfigPackageCard@1000 : TestPage 8614;
    BEGIN
      Initialize;
      InitializePackageCard(ConfigPackageCard,TRUE);

      // Validate relations
      ConfigPackageCard."Config. Package Subform".ValidateRelations.INVOKE;

      Cleanup(ConfigPackageCard.Code.VALUE);
    END;

    [Test]
    PROCEDURE EvaluateValue_Option@42();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Local Address Format"),
          FORMAT(GLSetup."Local Address Format"::"City+Post Code")) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Option'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Local Address Format"),
          FORMAT(GLSetup."Inv. Rounding Type (LCY)"::Nearest)) = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Option'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Integer@43();
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"G/L Entry",GLEntry.FIELDNO("Entry No."),FORMAT(LibraryRandom.RandInt(1000))) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Integer'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"G/L Entry",GLEntry.FIELDNO("Entry No."),FORMAT(LibraryRandom.RandDec(1000,5))) = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Integer'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Decimal@44();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Inv. Rounding Precision (LCY)"),
          FORMAT(LibraryRandom.RandDec(1000,5))) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Decimal'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Inv. Rounding Precision (LCY)"),'WrongDecimal') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Decimal'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Date@45();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Allow Posting From"),FORMAT(WORKDATE)) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Date'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Allow Posting From"),'WrongDate') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Date'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Time@46();
    VAR
      ChangeLogEntry@1000 : Record 405;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"Change Log Entry",ChangeLogEntry.FIELDNO(Time),FORMAT(TIME)) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Time'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"Change Log Entry",ChangeLogEntry.FIELDNO(Time),'WrongTime') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Time'));
    END;

    [Test]
    PROCEDURE EvaluateValue_DateTime@50();
    VAR
      ChangeLogEntry@1000 : Record 405;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"Change Log Entry",ChangeLogEntry.FIELDNO("Date and Time"),FORMAT(CURRENTDATETIME)) = '',
        STRSUBSTNO(ErrorOnEvaluating,'DateTime'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"Change Log Entry",ChangeLogEntry.FIELDNO("Date and Time"),'WrongDateTime') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'DateTime'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Boolean@52();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Register Time"),FORMAT(FALSE)) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Boolean'));
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Register Time"),FORMAT(TRUE)) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Boolean'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Register Time"),'Maybe') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Boolean'));
    END;

    [Test]
    PROCEDURE EvaluateValue_BigInteger@53();
    VAR
      ChangeLogEntry@1000 : Record 405;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"Change Log Entry",ChangeLogEntry.FIELDNO("Entry No."),
          FORMAT(LibraryRandom.RandInt(1000))) = '',
        STRSUBSTNO(ErrorOnEvaluating,'BigInteger'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"Change Log Entry",ChangeLogEntry.FIELDNO("Entry No."),'WrongBigInteger') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'BigInteger'));
    END;

    [Test]
    PROCEDURE EvaluateValue_GUID@54();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"Job Queue Entry",JobQueueEntry.FIELDNO(ID),FORMAT(CREATEGUID)) = '',
        STRSUBSTNO(ErrorOnEvaluating,'GUID'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"Job Queue Entry",JobQueueEntry.FIELDNO(ID),'WrongGuid') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'GUID'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Code@55();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Bank Account Nos."),GLSetup."Bank Account Nos.") = '',
        STRSUBSTNO(ErrorOnEvaluating,'Code'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Bank Account Nos."),'TooLongCode0123456789') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Code'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Text@57();
    VAR
      GLEntry@1000 : Record 17;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"G/L Entry",GLEntry.FIELDNO(Description),'Test text') = '',
        STRSUBSTNO(ErrorOnEvaluating,'Text'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"G/L Entry",GLEntry.FIELDNO(Description),
          'TooLongText12345678901234567890123456789012345678901234567890') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Text'));
    END;

    [Test]
    PROCEDURE EvaluateValue_DateFormula@59();
    VAR
      GLSetup@1000 : Record 98;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Payment Discount Grace Period"),'+1Y') = '',
        STRSUBSTNO(ErrorOnEvaluating,'DateFormula'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"General Ledger Setup",GLSetup.FIELDNO("Payment Discount Grace Period"),
          'Long Long Time Ago In a galaxy far away') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'DateFormula'));
    END;

    [Test]
    PROCEDURE EvaluateValue_Duration@61();
    VAR
      ToDo@1000 : Record 5080;
    BEGIN
      Assert.IsTrue(
        EvaluateValue(DATABASE::"To-do",ToDo.FIELDNO(Duration),FORMAT(CURRENTDATETIME - CREATEDATETIME(WORKDATE - 1,TIME))) = '',
        STRSUBSTNO(ErrorOnEvaluating,'Duration'));
      Assert.IsFalse(
        EvaluateValue(DATABASE::"To-do",ToDo.FIELDNO(Duration),'WrongDuration') = '',
        STRSUBSTNO(NoErrorOnEvaluating,'Duration'));
    END;

    [Test]
    PROCEDURE PackageCardDatabaseRecords@35();
    VAR
      PaymentMethod@1000 : Record 289;
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      ConfigPackageCard@1003 : TestPage 8614;
      I@1004 : Integer;
    BEGIN
      Initialize;

      // Create several database records
      FOR I := 1 TO 3 DO
        CreatePaymentMethod(PaymentMethod);

      COMMIT;

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"Payment Method");
      ConfigPackageCard.OPENVIEW;
      ConfigPackageCard.GOTORECORD(ConfigPackage);
      ConfigPackageCard."Config. Package Subform".FIRST;

      Assert.AreEqual(
        FORMAT(PaymentMethod.COUNT),
        ConfigPackageCard."Config. Package Subform".NoOfDatabaseRecords.VALUE,
        STRSUBSTNO(FieldValueIsIncorrect,ConfigPackageCard."Config. Package Subform".NoOfDatabaseRecords.CAPTION));

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE PackageCardPackageCreation@34();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageCard@1000 : TestPage 8614;
      TableID@1002 : Integer;
    BEGIN
      // Verify that a package can be created from the package card page
      ConfigPackageCard.OPENNEW;
      ConfigPackageCard.Code.SETVALUE(LibraryUtility.GenerateRandomCode(ConfigPackage.FIELDNO(Code),DATABASE::"Config. Package"));
      ConfigPackageCard."Package Name".SETVALUE(ConfigPackageCard.Code);
      ConfigPackageCard."Language ID".SETVALUE(FindFirstLanguage);

      TableID := FindRandomTableID(100);
      ConfigPackageCard."Config. Package Subform".NEW;
      ConfigPackageCard."Config. Package Subform"."Table ID".SETVALUE(TableID);
      // Verify that the table ID is validated
      Assert.AreEqual(GetTableName(TableID),ConfigPackageCard."Config. Package Subform"."Table Name".VALUE,TableNotValidated);
    END;

    [Test]
    [HandlerFunctions(ConfirmYesHandler)]
    PROCEDURE ExportImportStructuredPackageWithDimAsCol@10();
    VAR
      ConfigPackage@1000 : Record 8623;
      ConfigLine@1001 : Record 8622;
      PackageCode@1003 : Code[20];
    BEGIN
      // Export/Import package with structured config. lines with Dim As Columns in additional area
      HideDialog;

      ConfigLine.DELETEALL;

      LibraryRapidStart.CreatePackage(ConfigPackage);
      PackageCode := ConfigPackage.Code;

      FillInWorksheet(PackageCode);

      // EXECUTE
      ExportImportXML(PackageCode);

      // VERIFY that last config line has correct Table ID value
      ConfigLine.SETRANGE("Package Code",PackageCode);
      ConfigLine.FINDLAST;
      Assert.AreEqual(DATABASE::Customer,ConfigLine."Table ID",STRSUBSTNO(ValueIsIncorrectErr,ConfigLine.FIELDNAME("Table ID")));

      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE ExportImportPackageWithMultilineConfigTemplate@71();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      ConfigTemplateHeaderCode@1003 : Code[10];
    BEGIN
      // Verify that TestHierarchy function of Config Template works successfully when importing multiple lines from package.

      // Setup: Create package with multiline configuration template.
      Initialize;
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"Config. Template Line");
      ConfigTemplateHeaderCode := CreateConfigTemplateWithMultipleLines;

      // Exercise: Export package, cleanup all packages and templates and import packageç
      ExportImportXMLWithPackageAndTemplateCleanup(ConfigPackage.Code,ConfigTemplateHeaderCode);

      // Verify: Check that there are no errors of import.
      ConfigPackage.CALCFIELDS("No. of Errors");
      Assert.AreEqual(0,ConfigPackage."No. of Errors",STRSUBSTNO(PackageErr,ConfigPackage.Code));
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    [HandlerFunctions(CheckProcessingOrderPackageFieldsPageHandler)]
    PROCEDURE ProcessingOrderAfterAddTableField@75();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      ConfigPackageField@1004 : Record 8616;
      ConfigPackageCard@1000 : TestPage 8614;
    BEGIN
      // [SCENARIO 371875] Config. Package Fields Processing Order after adding a Field
      Initialize;

      // [GIVEN] Config Package with Table "X" and "Z" fields
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"Payment Method");
      ConfigPackageTable.CALCFIELDS("No. of Fields Included");
      LibraryVariableStorage.Enqueue(ConfigPackageTable."No. of Fields Included");

      // [GIVEN] New field added to the Table "X"
      ConfigPackageField.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigPackageField.SETRANGE("Table ID",DATABASE::"Payment Method");
      ConfigPackageField.FINDLAST;
      ConfigPackageField.DELETE;

      // [WHEN] Package Fields page is opened
      ConfigPackageCard.OPENVIEW;
      ConfigPackageCard.GOTORECORD(ConfigPackage);
      ConfigPackageCard."Config. Package Subform".FIRST;
      ConfigPackageCard."Config. Package Subform".PackageFields.INVOKE;

      // [THEN] Processing Order for all fields begins from 1 and ends with "Z" + 1
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    PROCEDURE EvaluateValue_DateTimeInOADateFormat@88();
    VAR
      PositivePayEntry@1000 : Record 1231;
      DateTime@1001 : DateTime;
      OADate@1002 : Integer;
      OATime@1003 : Decimal;
      NowTime@1004 : Time;
      NowDate@1005 : Date;
    BEGIN
      // [SCENARIO 376593] RapidStart correctly processes Date and Time values in OADate format

      // [GIVEN] Current Date = 30/10/15 and Time = 15:32:57 in OADate format (42307 and 0,6478819444)
      DateTime := CREATEDATETIME(TODAY,TIME);
      DateTime := ROUNDDATETIME(DateTime,1000,'<'); // rounding to avoid milliseconds
      NowDate := DT2DATE(DateTime);
      NowTime := DT2TIME(DateTime);
      GetOADateTime(DateTime,OADate,OATime);

      // [WHEN] RapidStart evaluates Date and Time in OADate format
      EvaluateDateTimeValue(
        DATABASE::"Positive Pay Entry",PositivePayEntry.FIELDNO("Last Upload Date"),
        PositivePayEntry.FIELDNO("Last Upload Time"),FORMAT(OADate),FORMAT(OATime),NowDate,NowTime);
      // [THEN] Fields values of Date = 30/10/15, Time = 15:32:57
    END;

    [Test]
    PROCEDURE ExportToExcelCustomProcessingOrderedPackageField@78();
    VAR
      ConfigPackage@1003 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      ConfigPackageField@1001 : Record 8616;
      Currency@1000 : Record 4;
    BEGIN
      // [SCENARIO] [Excel] Configuration package table takes into account settings of "Processing Order" value of related configuration package fields on exporting to excel.
      Initialize;
      Currency.FINDFIRST;

      // [GIVEN] Config package "A" for table "Currency"
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Currency);

      // [GIVEN] "Config. Package Field"."Processing Order" for "Description" field changed from 12 (default) to 1
      // [GIVEN] "Config. Package Field"."Processing Order" for "Code" field changed from 1 (default) to 12
      ConfigPackageField.SETRANGE("Package Code",ConfigPackage.Code);
      ConfigPackageField.SETRANGE("Table ID",DATABASE::Currency);
      SetConfigPackageFieldProcessingFolder(
        ConfigPackageField,Currency.FIELDNO(Code),12);
      SetConfigPackageFieldProcessingFolder(
        ConfigPackageField,Currency.FIELDNO(Description),1);

      // [WHEN] Export "A" to Excel
      COMMIT;
      ExportToExcel(ConfigPackageTable);

      // [THEN] Field "Description" exported into first ('A') column
      // [THEN] Field "Code" exported into 12th ('L') column
      LibraryReportValidation.OpenExcelFile;
      LibraryReportValidation.VerifyCellValueByRef('A',3,1,Currency.FIELDCAPTION(Description));
      LibraryReportValidation.VerifyCellValueByRef('L',3,1,Currency.FIELDCAPTION(Code));
      LibraryReportValidation.VerifyCellValueByRef('A',4,1,Currency.Description);
      LibraryReportValidation.VerifyCellValueByRef('L',4,1,Currency.Code);

      // Clean up
      Cleanup(ConfigPackage.Code);
    END;

    [Test]
    [TransactionModel(AutoRollback)]
    PROCEDURE ImportPackageWithNonExistingTable@82();
    VAR
      ConfigPackageError@1005 : Record 8617;
      XMLDOMManagement@1001 : Codeunit 6224;
      XMLDocument@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      TableID@1000 : Integer;
      TextXMLPackage@1002 : Text;
      PackageCode@1006 : Code[20];
    BEGIN
      // [SCENARIO 380200] Importing of Package with non existing table does not break import process, but creates Package Error
      // [FEATURE] [UT]

      // [GIVEN] Package "PACK01" with non existing table "-452"
      PackageCode := 'PACK01';
      TableID := -452;
      TextXMLPackage :=
        STRSUBSTNO(
          '<?xml version="1.0" encoding="UTF-16" standalone="yes"?>' +
          '<DataList LanguageID="1033" ProductVersion="NAV 7.1" PackageName="Tables" Code="%1">' +
          '<ApprovalSetupList><TableID>%2</TableID></ApprovalSetupList></DataList>',
          PackageCode,
          TableID);
      XMLDOMManagement.LoadXMLDocumentFromText(TextXMLPackage,XMLDocument);

      // [WHEN] Importing "PACK01"
      // [THEN] Package imported
      ConfigXMLExchange.ImportPackageXMLDocument(XMLDocument);

      // [THEN] A config. package error is created for "PACK01" informing that table "-452" does not exists
      ConfigPackageError.SETRANGE("Package Code",PackageCode);
      ConfigPackageError.FINDFIRST;
      ConfigPackageError.TESTFIELD("Table ID",TableID);
      ConfigPackageError.TESTFIELD("Error Text",STRSUBSTNO(PackageImportErr,TableID));
    END;

    [Test]
    [TransactionModel(AutoRollback)]
    PROCEDURE GetNoOfDatabaseRecordsWhenTableIDIsZero@81();
    VAR
      DummyConfigPackage@1002 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      ProcedureResult@1001 : Integer;
    BEGIN
      // [SCENARIO 380200] GetNoOfDatabaseRecords returns 0 if "Table ID" is 0
      // [FEATURE] [UT]

      ConfigPackageTable.INIT;
      ConfigPackageTable."Package Code" := DummyConfigPackage.Code;
      ConfigPackageTable."Table ID" := 0;
      ProcedureResult := ConfigPackageTable.GetNoOfDatabaseRecords;
      Assert.AreEqual(0,ProcedureResult,'GetNoOfDatabaseRecords result must be 0');
    END;

    [Test]
    [TransactionModel(AutoRollback)]
    PROCEDURE GetNoOfDatabaseRecordsWhenTableIDExists@85();
    VAR
      Area@1004 : Record 284;
      DummyConfigPackage@1003 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      ProcedureResult@1000 : Integer;
    BEGIN
      // [SCENARIO 380200] GetNoOfDatabaseRecords returns count of records for an existing table ID
      // [FEATURE] [UT]

      Area.DELETEALL;
      Area.INIT;
      Area.INSERT;

      CreatePackageWithTable(DummyConfigPackage,ConfigPackageTable,DATABASE::Area);
      ProcedureResult := ConfigPackageTable.GetNoOfDatabaseRecords;
      Assert.AreEqual(1,ProcedureResult,STRSUBSTNO('GetNoOfDatabaseRecords result must be %1',1));
    END;

    [Test]
    [TransactionModel(AutoRollback)]
    PROCEDURE GetNoOfDatabaseRecordsWhenTableIDNotExists@90();
    VAR
      DummyConfigPackage@1002 : Record 8623;
      ConfigPackageTable@1001 : Record 8613;
      ProcedureResult@1000 : Integer;
    BEGIN
      // [SCENARIO 380200] GetNoOfDatabaseRecords returns 0 for a non-existing table ID
      // [FEATURE] [UT]

      ConfigPackageTable.INIT;
      ConfigPackageTable."Package Code" := DummyConfigPackage.Code;
      ConfigPackageTable."Table ID" := -452;
      ProcedureResult := ConfigPackageTable.GetNoOfDatabaseRecords;
      Assert.AreEqual(0,ProcedureResult,'GetNoOfDatabaseRecords result must be 0');
    END;

    [Test]
    PROCEDURE ImportPackage_ImportCreateMissingCodesValueInFieldPackage@92();
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1000 : Record 8613;
      ConfigPackageField@1002 : Record 8616;
      Customer@1003 : Record 18;
      FieldNo1@1004 : Integer;
      FieldNo2@1005 : Integer;
    BEGIN
      // [FEATURE] [UT]
      // [SCENARIO 382272] Import Config Package should save the "Create Missing Codes" package field value
      Initialize;

      // [GIVEN] Config Package for Customer
      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::Customer);

      // [GIVEN] Config Package Field setup for Customer.City and Customer."Territory Code"
      FieldNo1 := Customer.FIELDNO(City);
      FieldNo2 := Customer.FIELDNO("Territory Code");
      LibraryRapidStart.SetIncludeOneField(ConfigPackage.Code,DATABASE::Customer,FieldNo1,TRUE);
      LibraryRapidStart.SetIncludeOneField(ConfigPackage.Code,DATABASE::Customer,FieldNo2,TRUE);

      // [GIVEN] Customer.City field setup "Create Missing Codes" set to TRUE
      LibraryRapidStart.SetCreateMissingCodesForField(ConfigPackage.Code,DATABASE::Customer,FieldNo1,TRUE);
      // [GIVEN] Customer."Territory Code" field setup "Create Missing Codes" set to FALSE
      LibraryRapidStart.SetCreateMissingCodesForField(ConfigPackage.Code,DATABASE::Customer,FieldNo2,FALSE);

      // [WHEN] Import Config Package
      ExportImportXML(ConfigPackage.Code);

      // [THEN] Customer.City field setup "Create Missing Codes" is TRUE
      ConfigPackageField.GET(ConfigPackage.Code,DATABASE::Customer,FieldNo1);
      ConfigPackageField.TESTFIELD("Create Missing Codes",TRUE);

      // [THEN] Customer."Territory Code" field setup "Create Missing Codes" is FALSE
      ConfigPackageField.GET(ConfigPackage.Code,DATABASE::Customer,FieldNo2);
      ConfigPackageField.TESTFIELD("Create Missing Codes",FALSE);

      Cleanup(ConfigPackage.Code);
    END;

    LOCAL PROCEDURE Initialize@4();
    VAR
      LibraryERMCountryData@1001 : Codeunit 131305;
    BEGIN
      LibraryVariableStorage.Clear;
      IF isInitialized THEN
        EXIT;

      HideDialog;
      LibraryERMCountryData.UpdateGeneralPostingSetup;

      isInitialized := TRUE;
      COMMIT;
    END;

    LOCAL PROCEDURE Cleanup@60(PackageCode@1010 : Code[20]);
    VAR
      ConfigPackageTable@1000 : Record 8613;
      ConfigPackageData@1002 : Record 8615;
      ConfigPackageError@1001 : Record 8617;
      ConfigPackageRecord@1003 : Record 8614;
      ConfigPackage@1004 : Record 8623;
    BEGIN
      ConfigPackage.SETRANGE(Code,PackageCode);
      ConfigPackage.DELETEALL(TRUE);
      ConfigPackageTable.SETRANGE("Package Code",PackageCode);
      ConfigPackageTable.DELETEALL(TRUE);
      ConfigPackageData.SETRANGE("Package Code",PackageCode);
      ConfigPackageData.DELETEALL(TRUE);
      ConfigPackageError.SETRANGE("Package Code",PackageCode);
      ConfigPackageError.DELETEALL(TRUE);
      ConfigPackageRecord.SETRANGE("Package Code",PackageCode);
      ConfigPackageRecord.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE ImportFromXML@64(VAR ConfigPackage@1003 : Record 8623;VAR CountryCode@1006 : Code[10];VAR LocationCode@1007 : Code[10]);
    VAR
      ConfigPackageTable@1009 : Record 8613;
      ConfigPackageField@1012 : Record 8616;
      Field@1011 : Record 2000000041;
      Country@1001 : Record 9;
      Location@1002 : Record 14;
      XMLDOMManagement@1008 : Codeunit 6224;
      RecRef@1005 : RecordRef;
      XMLDocument@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      XMLDOMManagement.LoadXMLDocumentFromText(
        '<?xml version="1.0" encoding="UTF-16" standalone="yes"?><DataList></DataList>',XMLDocument);

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"Country/Region");
      ConfigPackageTable.CALCFIELDS("Table Name");

      LibraryERM.CreateCountryRegion(Country);
      RecRef.GETTABLE(Country);
      AddConfigPackageTableToXML(XMLDocument,XMLDocument.DocumentElement,ConfigPackageTable,RecRef);
      CountryCode := Country.Code;

      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::Location);
      ConfigPackageTable.CALCFIELDS("Table Name");
      LibraryWarehouse.CreateLocation(Location);
      Location."Country/Region Code" := CountryCode;
      LocationCode := Location.Code;

      Field.SETRANGE(TableNo,ConfigPackageTable."Table ID");
      Field.SETRANGE(FieldName,'Country/Region Code');
      IF Field.FINDFIRST THEN
        IF ConfigPackageField.GET(ConfigPackageTable."Package Code",ConfigPackageTable."Table ID",Field."No.") THEN BEGIN
          ConfigPackageField."Include Field" := TRUE;
          ConfigPackageField.MODIFY;
        END;
      RecRef.GETTABLE(Location);
      AddConfigPackageTableToXML(XMLDocument,XMLDocument.DocumentElement,ConfigPackageTable,RecRef);

      Country.DELETE;
      Location.DELETE;

      ConfigXMLExchange.ImportPackageXMLDocument(XMLDocument);
    END;

    LOCAL PROCEDURE ImportFromXMLKey@47(VAR ConfigPackage@1003 : Record 8623;VAR GenProductPostingGroupCode@1005 : Code[10];VAR GenBusPostingGroupCode@1007 : Code[10]);
    VAR
      ConfigPackageTable@1009 : Record 8613;
      GeneralPostingSetup@1000 : Record 252;
      GenBusPostingGroup@1001 : Record 250;
      GenProductPostingGroup@1002 : Record 251;
      XMLDOMManagement@1010 : Codeunit 6224;
      RecRef@1006 : RecordRef;
      XMLDocument@1004 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
    BEGIN
      XMLDOMManagement.LoadXMLDocumentFromText(
        '<?xml version="1.0" encoding="UTF-16" standalone="yes"?><DataList></DataList>',XMLDocument);

      LibraryERM.CreateGenProdPostingGroup(GenProductPostingGroup);
      LibraryERM.CreateGenBusPostingGroup(GenBusPostingGroup);
      LibraryERM.CreateGeneralPostingSetup(GeneralPostingSetup,GenBusPostingGroup.Code,GenProductPostingGroup.Code);

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,DATABASE::"General Posting Setup");
      ConfigPackageTable.CALCFIELDS("Table Name");
      RecRef.GETTABLE(GeneralPostingSetup);
      AddConfigPackageTableToXML(XMLDocument,XMLDocument.DocumentElement,ConfigPackageTable,RecRef);

      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Business Posting Group");
      ConfigPackageTable.CALCFIELDS("Table Name");
      RecRef.GETTABLE(GenBusPostingGroup);
      AddConfigPackageTableToXML(XMLDocument,XMLDocument.DocumentElement,ConfigPackageTable,RecRef);

      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Product Posting Group");
      ConfigPackageTable.CALCFIELDS("Table Name");
      RecRef.GETTABLE(GenProductPostingGroup);
      AddConfigPackageTableToXML(XMLDocument,XMLDocument.DocumentElement,ConfigPackageTable,RecRef);

      ConfigXMLExchange.ImportPackageXMLDocument(XMLDocument);

      GenProductPostingGroupCode := GenProductPostingGroup.Code;
      GenBusPostingGroupCode := GenBusPostingGroup.Code;

      GenProductPostingGroup.DELETE;
      GenBusPostingGroup.DELETE;
      GeneralPostingSetup.DELETE;
    END;

    LOCAL PROCEDURE AddXMLNode@65(VAR XMLDocument@1001 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";ParentNode@1000 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";Node@1004 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";FieldName@1002 : Text[250];FieldText@1003 : Text[250]);
    BEGIN
      Node := XMLDocument.CreateElement(ConfigXMLExchange.GetElementName(FieldName));
      Node.InnerText := FORMAT(FieldText);
      ParentNode.AppendChild(Node);
    END;

    LOCAL PROCEDURE AddConfigPackageTableToXML@86(VAR XMLDocument@1017 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";DocumentElement@1008 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";ConfigPackageTable@1006 : Record 8613;RecRef@1004 : RecordRef);
    VAR
      ConfigPackageField@1012 : Record 8616;
      FieldRef@1005 : FieldRef;
      TableNode@1015 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      TableIDNode@1002 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FormIDNode@1001 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      RecordNode@1010 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      FieldNode@1007 : DotNet "'System.Xml, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
    BEGIN
      TableNode := XMLDocument.CreateElement(ConfigXMLExchange.GetElementName(ConfigPackageTable."Table Name" + 'List'));
      DocumentElement.AppendChild(TableNode);

      AddXMLNode(XMLDocument,TableNode,TableIDNode,ConfigPackageTable.FIELDNAME("Table ID"),FORMAT(ConfigPackageTable."Table ID"));
      AddXMLNode(XMLDocument,TableNode,FormIDNode,ConfigPackageTable.FIELDNAME("Page ID"),FORMAT(ConfigPackageTable."Page ID"));
      AddXMLNode(
        XMLDocument,TableNode,FormIDNode,ConfigPackageTable.FIELDNAME("Processing Order"),
        FORMAT(ConfigPackageTable."Processing Order"));
      AddXMLNode(XMLDocument,TableNode,RecordNode,ConfigPackageTable."Table Name",'');

      ConfigPackageField.RESET;
      ConfigPackageField.SETRANGE("Table ID",ConfigPackageTable."Table ID");
      ConfigPackageField.SETRANGE("Include Field",TRUE);
      IF ConfigPackageField.FINDSET THEN BEGIN
        REPEAT
          FieldRef := RecRef.FIELD(ConfigPackageField."Field ID");
          AddXMLNode(XMLDocument,RecordNode,FieldNode,FieldRef.NAME,FORMAT(FieldRef.VALUE));
        UNTIL ConfigPackageField.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE VerifyGenJnlLineAmount@66(DocumentType@1000 : Integer;DocumentNo@1001 : Code[20];BalAccountNo@1002 : Code[20];Amount@1003 : Decimal);
    VAR
      GLEntry@1004 : Record 17;
    BEGIN
      GLEntry.RESET;
      GLEntry.SETRANGE("Document Type",DocumentType);
      GLEntry.SETRANGE("Document No.",DocumentNo);
      GLEntry.SETRANGE("Bal. Account No.",BalAccountNo);
      GLEntry.FINDFIRST;

      Assert.AreEqual(-Amount,GLEntry.Amount,Text103);
    END;

    LOCAL PROCEDURE GetNoOfAvailableFields@68(VAR ConfigPackageTable@1000 : Record 8613) : Integer;
    BEGIN
      WITH ConfigPackageTable DO BEGIN
        CALCFIELDS("No. of Fields Available");
        EXIT("No. of Fields Available");
      END;
    END;

    LOCAL PROCEDURE EvaluateValue@15(TableNo@1000 : Integer;FieldNo@1001 : Integer;Value@1002 : Text[250]) ErrorText : Text;
    VAR
      RecRef@1003 : RecordRef;
      FieldRef@1004 : FieldRef;
    BEGIN
      RecRef.OPEN(TableNo);
      FieldRef := RecRef.FIELD(FieldNo);
      ErrorText := ConfigValidateMgt.EvaluateValue(FieldRef,Value,FALSE);
      IF ErrorText <> '' THEN
        EXIT(ErrorText);

      ConfigValidateMgt.EvaluateTextToFieldRef(Value,FieldRef,TRUE);
      ConfigValidateMgt.EvaluateTextToFieldRef(Value,FieldRef,FALSE);
    END;

    LOCAL PROCEDURE EvaluateDateTimeValue@89(TableNo@1000 : Integer;FieldNoDate@1001 : Integer;FieldNoTime@1006 : Integer;ValueDate@1002 : Text[250];ValueTime@1007 : Text[250];Date@1005 : Date;Time@1008 : Time);
    VAR
      RecRef@1003 : RecordRef;
      FieldRef@1004 : FieldRef;
    BEGIN
      RecRef.OPEN(TableNo);

      FieldRef := RecRef.FIELD(FieldNoDate);
      ConfigValidateMgt.EvaluateValue(FieldRef,ValueDate,FALSE);
      FieldRef.TESTFIELD(Date);

      FieldRef := RecRef.FIELD(FieldNoTime);
      ConfigValidateMgt.EvaluateValue(FieldRef,ValueTime,FALSE);
      FieldRef.TESTFIELD(Time);
    END;

    LOCAL PROCEDURE HideDialog@56();
    BEGIN
      ConfigPackageMgt.SetHideDialog(TRUE);
      ConfigXMLExchange.SetCalledFromCode(TRUE);
      ConfigXMLExchange.SetHideDialog(TRUE);
    END;

    LOCAL PROCEDURE CreatePackageWithTable@76(VAR ConfigPackage@1000 : Record 8623;VAR ConfigPackageTable@1001 : Record 8613;TableNo@1002 : Integer);
    BEGIN
      LibraryRapidStart.CreatePackage(ConfigPackage);
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,TableNo);
    END;

    LOCAL PROCEDURE CreatePaymentMethod@1(VAR PaymentMethod@1000 : Record 289);
    BEGIN
      PaymentMethod.INIT;
      PaymentMethod.VALIDATE(Code,LibraryUtility.GenerateRandomCode(PaymentMethod.FIELDNO(Code),DATABASE::"Payment Method"));
      PaymentMethod.INSERT(TRUE);
    END;

    LOCAL PROCEDURE CreatePurchSetup@2();
    VAR
      PurchSetup@1000 : Record 312;
    BEGIN
      IF NOT PurchSetup.GET THEN BEGIN
        PurchSetup.INIT;
        PurchSetup.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE CreateVendorPostingGroup@3(VAR VendorPostingGroup@1000 : Record 93);
    BEGIN
      VendorPostingGroup.INIT;
      VendorPostingGroup.VALIDATE(
        Code,
        COPYSTR(LibraryUtility.GenerateRandomCode(VendorPostingGroup.FIELDNO(Code),DATABASE::"Vendor Posting Group"),
          1,LibraryUtility.GetFieldLength(DATABASE::"Vendor Posting Group",VendorPostingGroup.FIELDNO(Code))));

      VendorPostingGroup.INSERT(TRUE);
    END;

    LOCAL PROCEDURE CreateAccountingPeriod@5();
    VAR
      AccountingPeriod@1000 : Record 50;
      Date@1001 : Record 2000000007;
    BEGIN
      AccountingPeriod.SETRANGE(Closed,FALSE);
      IF NOT AccountingPeriod.FINDFIRST THEN BEGIN
        Date.SETRANGE("Period Type",Date."Period Type"::Month);
        Date.SETRANGE("Period Start",0D,WORKDATE);
        Date.FINDLAST;

        AccountingPeriod.INIT;
        AccountingPeriod.VALIDATE("Starting Date",Date."Period Start");
        AccountingPeriod.INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE CreateGenJnlLineSetup@13(VAR GLAccount@1008 : Record 15;VAR PaymentMethod@1007 : Record 289;VAR GenBusinessPostingGroup@1006 : Record 250;VAR GenProductPostingGroup@1005 : Record 251;VAR GeneralPostingSetup@1004 : Record 252;VAR VATBusinessPostingGroup@1003 : Record 323;VAR VATProductPostingGroup@1002 : Record 324;VAR VendorPostingGroup@1001 : Record 93;VAR PaymentTerms@1000 : Record 3);
    BEGIN
      LibraryERM.CreateGenBusPostingGroup(GenBusinessPostingGroup);
      LibraryERM.CreateGenProdPostingGroup(GenProductPostingGroup);
      LibraryERM.CreateGeneralPostingSetup(GeneralPostingSetup,GenBusinessPostingGroup.Code,GenProductPostingGroup.Code);
      LibraryERM.CreateVATBusinessPostingGroup(VATBusinessPostingGroup);
      LibraryERM.CreateVATProductPostingGroup(VATProductPostingGroup);
      LibraryERM.CreateGLAccount(GLAccount);

      LibraryERM.CreatePaymentTerms(PaymentTerms);
      EVALUATE(PaymentTerms."Due Date Calculation",'<0D>');
      PaymentTerms.MODIFY(TRUE);

      CreateVendorPostingGroup(VendorPostingGroup);
      VendorPostingGroup."Payables Account" := GLAccount."No.";
      VendorPostingGroup.MODIFY(TRUE);
      CreatePurchSetup;
      CreatePaymentMethod(PaymentMethod);

      CreateAccountingPeriod;
    END;

    LOCAL PROCEDURE CreateGenJnlLines@18(VAR GenJnlLine@1012 : Record 81;VAR Amount@1007 : Decimal;GLAccountNo@1000 : Code[20];VAR DocumentNo@1001 : Code[20]);
    VAR
      Vendor@1005 : Record 23;
      GenJnlTemplate@1004 : Record 80;
      GenJnlTemplate2@1003 : Record 80;
      GenJnlBatch@1002 : Record 232;
      VendorNo@1006 : Code[20];
    BEGIN
      // Create Gen. Journal Template and Batch
      LibraryERM.CreateGenJournalTemplate(GenJnlTemplate);
      GenJnlTemplate.VALIDATE(Type,GenJnlTemplate.Type::Payments);
      GenJnlTemplate.MODIFY(TRUE);
      // There must be at least 1 gen. journal template of type "General", otherwise CreateGeneralJnlLine fails
      LibraryERM.CreateGenJournalTemplate(GenJnlTemplate2);
      LibraryERM.CreateGenJournalBatch(GenJnlBatch,GenJnlTemplate.Name);

      // Creating Gen. Journal Line
      LibraryPurchase.CreateVendor(Vendor);
      VendorNo := Vendor."No.";
      Amount := LibraryRandom.RandDec(1000,2);
      LibraryERM.CreateGeneralJnlLine(
        GenJnlLine,GenJnlTemplate.Name,GenJnlBatch.Name,GenJnlLine."Document Type"::Invoice,
        GenJnlLine."Account Type"::Vendor,VendorNo,-Amount);
      GenJnlLine.VALIDATE("Bal. Account Type",GenJnlLine."Bal. Account Type"::"G/L Account");
      GenJnlLine.VALIDATE("Bal. Account No.",GLAccountNo);
      GenJnlLine.MODIFY(TRUE);
      DocumentNo := GenJnlLine."Document No.";
    END;

    LOCAL PROCEDURE CreateConfigTemplateWithMultipleLines@84() : Code[10];
    VAR
      Item@1002 : Record 27;
      ConfigTemplateHeader@1001 : Record 8618;
    BEGIN
      WITH ConfigTemplateHeader DO BEGIN
        LibraryRapidStart.CreateConfigTemplateHeader(ConfigTemplateHeader);
        VALIDATE("Table ID",DATABASE::Item);
        MODIFY(TRUE);

        CreateConfigTemplateLine(
          Code,Item.FIELDNO("Inventory Posting Group"),Item.FIELDNAME("Inventory Posting Group"));
        CreateConfigTemplateLine(
          Code,Item.FIELDNO("Gen. Prod. Posting Group"),Item.FIELDNAME("Gen. Prod. Posting Group"));
        CreateConfigTemplateLine(
          Code,Item.FIELDNO("Allow Invoice Disc."),Item.FIELDNAME("Allow Invoice Disc."));
        EXIT(Code);
      END;
    END;

    LOCAL PROCEDURE CreateConfigTemplateLine@87(ConfigTemplateHeaderCode@1001 : Code[10];FieldID@1002 : Integer;FieldNameValue@1003 : Text[30]);
    VAR
      ConfigTemplateLine@1000 : Record 8619;
    BEGIN
      WITH ConfigTemplateLine DO BEGIN
        LibraryRapidStart.CreateConfigTemplateLine(ConfigTemplateLine,ConfigTemplateHeaderCode);
        VALIDATE(Type,Type::Field);
        VALIDATE("Field ID",FieldID);
        VALIDATE("Field Name",FieldNameValue);
        MODIFY(TRUE);
      END;
    END;

    LOCAL PROCEDURE CleanupGenJnlSetupData@22(VAR PaymentMethod@1007 : Record 289;VAR GenBusinessPostingGroup@1006 : Record 250;VAR GenProductPostingGroup@1005 : Record 251;VAR GeneralPostingSetup@1004 : Record 252;VAR VATBusinessPostingGroup@1003 : Record 323;VAR VATProductPostingGroup@1002 : Record 324;VAR VendorPostingGroup@1001 : Record 93;VAR PaymentTerms@1000 : Record 3);
    BEGIN
      IF PaymentMethod.GET(PaymentMethod.Code) THEN
        PaymentMethod.DELETE(TRUE);

      IF GenBusinessPostingGroup.GET(GenBusinessPostingGroup.Code) THEN
        GenBusinessPostingGroup.DELETE(TRUE);

      IF GenProductPostingGroup.GET(GenProductPostingGroup.Code) THEN
        GenProductPostingGroup.DELETE(TRUE);

      IF GeneralPostingSetup.GET(GenBusinessPostingGroup.Code,GenProductPostingGroup.Code) THEN
        GeneralPostingSetup.DELETE(TRUE);

      IF VATBusinessPostingGroup.GET(VATBusinessPostingGroup.Code) THEN
        VATBusinessPostingGroup.DELETE(TRUE);

      IF VATProductPostingGroup.GET(VATProductPostingGroup.Code) THEN
        VATProductPostingGroup.DELETE(TRUE);

      IF VendorPostingGroup.GET(VendorPostingGroup.Code) THEN
        VendorPostingGroup.DELETE(TRUE);

      IF PaymentTerms.GET(PaymentTerms.Code) THEN
        PaymentTerms.DELETE(TRUE);
    END;

    LOCAL PROCEDURE CleanupPackageAndTemplate@104(PackageCode@1002 : Code[20];ConfigTemplateHeaderCode@1003 : Code[10]);
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigTemplateHeader@1000 : Record 8618;
    BEGIN
      ConfigPackage.SETRANGE(Code,PackageCode);
      ConfigPackage.DELETEALL(TRUE);
      ConfigTemplateHeader.SETRANGE(Code,ConfigTemplateHeaderCode);
      ConfigTemplateHeader.DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE CreateAndExportPackageDataForGenJnlLine@28(VAR ConfigPackage@1001 : Record 8623;VAR FilePath@1002 : Text);
    VAR
      ConfigPackageTable@1000 : Record 8613;
    BEGIN
      // Creating Package with table Gen. Journal Line
      LibraryRapidStart.CreatePackage(ConfigPackage);
      LibraryRapidStart.CreatePackageTable(ConfigPackageTable,ConfigPackage.Code,DATABASE::"Gen. Journal Line");

      // Select package table fields for export
      LibraryRapidStart.SetIncludeFields(ConfigPackage.Code,DATABASE::"Gen. Journal Line",2,77,TRUE);

      // Export data to XML
      ExportToXML(ConfigPackage.Code,ConfigPackageTable,FilePath);
    END;

    LOCAL PROCEDURE PostGenJnlLines@31(GLAccountNo@1000 : Code[20];DocumentNo@1001 : Code[20]);
    VAR
      GenJnlLine@1002 : Record 81;
    BEGIN
      GenJnlLine.RESET;
      GenJnlLine.SETRANGE("Bal. Account No.",GLAccountNo);
      IF GenJnlLine.FINDFIRST THEN BEGIN
        IF GenJnlLine."Document No." = DocumentNo THEN
          LibraryERM.PostGeneralJnlLine(GenJnlLine);
      END;
    END;

    LOCAL PROCEDURE ExportImportXML@27(PackageCode@1000 : Code[20]);
    VAR
      ConfigPackageTable@1002 : Record 8613;
      FilePath@1001 : Text;
    BEGIN
      ExportToXML(PackageCode,ConfigPackageTable,FilePath);
      Cleanup(PackageCode);
      ConfigXMLExchange.ImportPackageXML(FilePath);
      ERASE(FilePath);
    END;

    LOCAL PROCEDURE ExportImportXMLWithPackageAndTemplateCleanup@80(PackageCode@1002 : Code[20];ConfigTemplateHeaderCode@1003 : Code[10]);
    VAR
      ConfigPackageTable@1001 : Record 8613;
      FilePath@1000 : Text;
    BEGIN
      ExportToXML(PackageCode,ConfigPackageTable,FilePath);
      CleanupPackageAndTemplate(PackageCode,ConfigTemplateHeaderCode);
      ConfigXMLExchange.ImportPackageXML(FilePath);
      ERASE(FilePath);
    END;

    LOCAL PROCEDURE ExportToXML@9(PackageCode@1000 : Code[20];VAR ConfigPackageTable@1001 : Record 8613;VAR FilePath@1002 : Text);
    BEGIN
      FilePath := FileMgt.ServerTempFileName('xml');
      ConfigPackageTable.SETRANGE("Package Code",PackageCode);
      ConfigXMLExchange.ExportPackageXML(ConfigPackageTable,FilePath);
    END;

    LOCAL PROCEDURE ExportToExcel@93(VAR ConfigPackageTable@1001 : Record 8613);
    VAR
      ConfigExcelExchange@1000 : Codeunit 8618;
      FileManagement@1003 : Codeunit 419;
      FileName@1002 : Text;
    BEGIN
      ConfigPackageTable.SETRECFILTER;
      ConfigExcelExchange.SetHideDialog(TRUE);
      ConfigExcelExchange.ExportExcel(FileName,ConfigPackageTable,FALSE);
      FileName := FileManagement.UploadFileSilent(FileName);
      LibraryReportValidation.SetFullFileName(FileName);
    END;

    LOCAL PROCEDURE FindRandomTableID@33(MaxID@1000 : Integer) : Integer;
    VAR
      Object@1002 : Record 2000000001;
    BEGIN
      Object.SETRANGE(Type,Object.Type::Table);
      Object.FINDSET;
      IF MaxID <= Object.ID THEN
        EXIT(Object.ID);

      Object.NEXT(LibraryRandom.RandInt(MaxID - Object.ID));
      EXIT(Object.ID);
    END;

    LOCAL PROCEDURE GetTableName@83(TableID@1000 : Integer) : Text;
    VAR
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.OPEN(TableID);
      EXIT(RecRef.NAME);
    END;

    LOCAL PROCEDURE FindFirstLanguage@32() : Integer;
    VAR
      WinLanguage@1000 : Record 2000000045;
    BEGIN
      WinLanguage.FINDFIRST;
      EXIT(WinLanguage."Language ID");
    END;

    LOCAL PROCEDURE SetLocalizeFields@30(ConfigPackageCode@1000 : Code[20];TableID@1001 : Integer);
    VAR
      ConfigPackageField@1002 : Record 8616;
    BEGIN
      ConfigPackageField.SETRANGE("Package Code",ConfigPackageCode);
      ConfigPackageField.SETRANGE("Table ID",TableID);
      ConfigPackageField.MODIFYALL("Localize Field",TRUE);
    END;

    LOCAL PROCEDURE CreateSimplePackage@130(VAR ConfigPackage@1000 : Record 8623);
    VAR
      GenJnlTemplateConfigPackageTable@1010 : Record 8613;
      GenJournalTemplate@1008 : Record 80;
      GenJournalTemplateName@1001 : Code[10];
    BEGIN
      LibraryERM.CreateGenJournalTemplate(GenJournalTemplate);
      GenJournalTemplateName := GenJournalTemplate.Name;
      GenJournalTemplate.DELETE;
      LibraryRapidStart.CreatePackageDataForField(
        ConfigPackage,
        GenJnlTemplateConfigPackageTable,
        DATABASE::"Gen. Journal Template",
        GenJournalTemplate.FIELDNO(Name),
        GenJournalTemplateName,
        1);
    END;

    LOCAL PROCEDURE IsPackageDataExists@133(ConfigPackageCode@1000 : Code[20];AnyData@1004 : Boolean) : Boolean;
    VAR
      ConfigPackage@1001 : Record 8623;
      ConfigPackageTable@1002 : Record 8613;
      ConfigPackageField@1003 : Record 8616;
    BEGIN
      ConfigPackageTable.SETRANGE("Package Code",ConfigPackageCode);
      ConfigPackageField.SETRANGE("Package Code",ConfigPackageCode);

      IF AnyData THEN
        EXIT(ConfigPackage.GET(ConfigPackageCode) OR NOT ConfigPackageTable.ISEMPTY OR NOT ConfigPackageField.ISEMPTY);
      EXIT(ConfigPackage.GET(ConfigPackageCode) AND NOT ConfigPackageTable.ISEMPTY AND NOT ConfigPackageField.ISEMPTY);
    END;

    LOCAL PROCEDURE CreateTextFile@101(VAR FileName@1001 : Text);
    VAR
      File@1000 : File;
    BEGIN
      FileName := FileMgt.ServerTempFileName('');
      File.WRITEMODE := TRUE;
      File.TEXTMODE := TRUE;
      File.CREATE(FileName);
      File.WRITE(FileTextMsg);
      File.CLOSE;
    END;

    LOCAL PROCEDURE ReadTextFromFile@119(FileName@1001 : Text) Text : Text[1024];
    VAR
      File@1000 : File;
    BEGIN
      File.WRITEMODE := FALSE;
      File.TEXTMODE := TRUE;
      File.OPEN(FileName);
      File.READ(Text);
    END;

    LOCAL PROCEDURE VerifyGZIPHeader@144(CompressedServerFileName@1000 : Text);
    BEGIN
      Assert.IsTrue(FileMgt.IsGZip(CompressedServerFileName),NotGZIPFormatErr);
    END;

    LOCAL PROCEDURE CreateCompressedFile@149() CompressedServerFileName : Text;
    VAR
      ServerFileName@1000 : Text;
    BEGIN
      CreateTextFile(ServerFileName);

      CompressedServerFileName := FileMgt.ServerTempFileName('');
      ConfigPckgCompressionMgt.ServersideCompress(ServerFileName,CompressedServerFileName);
    END;

    LOCAL PROCEDURE InitializePackageCard@39(VAR ConfigPackageCard@1000 : TestPage 8614;FillPackageData@1001 : Boolean);
    VAR
      ConfigPackage@1002 : Record 8623;
      ConfigPackageTable@1004 : Record 8613;
      TableID@1003 : Integer;
    BEGIN
      // Create package
      TableID := DATABASE::Vendor;

      CreatePackageWithTable(ConfigPackage,ConfigPackageTable,TableID);
      // Export and import data to fill package tables
      IF FillPackageData THEN
        ExportImportXML(ConfigPackage.Code);

      // Open Package Card page and add table
      ConfigPackageCard.OPENEDIT;
      ConfigPackageCard.GOTORECORD(ConfigPackage);
    END;

    LOCAL PROCEDURE FindTableID@70() : Integer;
    VAR
      Object@1000 : Record 2000000001;
    BEGIN
      Object.SETRANGE(Type,Object.Type::Table);
      Object.FINDFIRST;

      EXIT(Object.ID);
    END;

    LOCAL PROCEDURE FindNextTableID@67(TableID@1001 : Integer) : Integer;
    VAR
      Object@1000 : Record 2000000001;
    BEGIN
      Object.SETRANGE(Type,Object.Type::Table);
      Object.SETFILTER(ID,'>%1',TableID);
      Object.FINDFIRST;

      EXIT(Object.ID);
    END;

    LOCAL PROCEDURE FillInWorksheet@40(PackageCode@1000 : Code[20]);
    VAR
      ConfigLine@1001 : Record 8622;
      TableID@1002 : Integer;
    BEGIN
      TableID := FindTableID;
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Area,0,'main area',PackageCode,FALSE);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Group,0,'',PackageCode,FALSE);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Table,TableID,'',PackageCode,FALSE);
      TableID := FindNextTableID(TableID);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Group,0,'',PackageCode,FALSE);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Table,TableID,'',PackageCode,FALSE);
      TableID := FindNextTableID(TableID);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Group,0,'',PackageCode,FALSE);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Table,TableID,'',PackageCode,FALSE);

      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Area,0,'additional area',PackageCode,FALSE);
      LibraryRapidStart.CreateConfigLine(ConfigLine,ConfigLine."Line Type"::Table,DATABASE::Customer,'',PackageCode,TRUE);
    END;

    LOCAL PROCEDURE GetOADateTime@77(DateTime@1000 : DateTime;VAR OADate@1007 : Integer;VAR OATime@1008 : Decimal);
    VAR
      DotNetDateTime@1001 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.DateTime";
      Date@1002 : Date;
      Time@1003 : Time;
      Hours@1004 : Integer;
      Minutes@1005 : Integer;
      Seconds@1006 : Integer;
      OADateTime@1009 : Decimal;
    BEGIN
      Date := DT2DATE(DateTime);
      Time := DT2TIME(DateTime);
      EVALUATE(Hours,FORMAT(Time,2,'<Hours24,2>'));
      EVALUATE(Minutes,FORMAT(Time,2,'<Minutes,2>'));
      EVALUATE(Seconds,FORMAT(Time,2,'<Seconds,2>'));
      DotNetDateTime :=
        DotNetDateTime.DateTime(
          DATE2DMY(Date,3),DATE2DMY(Date,2),DATE2DMY(Date,1),
          Hours,Minutes,Seconds);

      OADateTime := DotNetDateTime.ToOADate;
      OADate := OADateTime DIV 1;
      OATime := OADateTime - OADate;
    END;

    LOCAL PROCEDURE SetConfigPackageFieldProcessingFolder@91(VAR ConfigPackageField@1000 : Record 8616;FieldId@1001 : Integer;ProcessingOrder@1002 : Integer);
    BEGIN
      ConfigPackageField.SETRANGE("Field ID",FieldId);
      ConfigPackageField.FINDFIRST;
      ConfigPackageField."Processing Order" := ProcessingOrder;
      ConfigPackageField.MODIFY;
    END;

    [ModalPageHandler]
    PROCEDURE PackageRecordsPageHandler@7(VAR ConfigPackageRecords@1000 : TestPage 8626);
    BEGIN
      ConfigPackageRecords.OK.INVOKE;
    END;

    [ModalPageHandler]
    PROCEDURE PackageFieldsPageHandler@6(VAR ConfigPackageFields@1000 : TestPage 8624);
    BEGIN
      ConfigPackageFields.OK.INVOKE;
    END;

    [ModalPageHandler]
    PROCEDURE CheckProcessingOrderPackageFieldsPageHandler@95(VAR ConfigPackageFields@1000 : TestPage 8624);
    VAR
      NoOfFields@1001 : Integer;
      I@1002 : Integer;
    BEGIN
      NoOfFields := LibraryVariableStorage.DequeueInteger;
      ConfigPackageFields.FIRST;
      FOR I := 1 TO NoOfFields DO BEGIN
        ConfigPackageFields."Processing Order".ASSERTEQUALS(I);
        ConfigPackageFields.NEXT;
      END;
      ConfigPackageFields.OK.INVOKE;
    END;

    [ConfirmHandler]
    PROCEDURE ConfirmYesHandler@63(Question@1000 : Text[1024];VAR Reply@1001 : Boolean);
    BEGIN
      Reply := TRUE;
    END;

    [MessageHandler]
    PROCEDURE MessageHandler@74(Message@1000 : Text[1024]);
    BEGIN
    END;

    BEGIN
    {
      // [FEATURE] [Rapid Start]
      Unsupported version tags:
      SE: Unable to Execute

      1.  Test if a Master Record can be created using Standard Master Data Templates.
      2.  Test if a new Master Data Template can be created and applied for Master Records.
      3.  Test if a new Package can be created and applied for Master Records.
      4.  Test if the new Package can be exported to Excel.
      5.  Test if the new Package can be exported to XML.
      6.  Test if the new Package can be imported from Excel.
      7.  Test if the new Package can be imported from XML.
      8.  Test if new Pages are created for Industry Specific Setup Data.
      9.  Test if new Pages are created for Master Data Templates.
      10. Test Creation and Maintenance of templates - many to one.
      11. Test Creation and Maintenance of templates - one to one.
      12. Test Creation and Maintenance of templates - insert same Template on Template Line.
      13. Test Creation and Maintenance of templates - insert Template that contains fields that are already on Template Line.
      14. Test Creation and Maintenance of templates - insert a field that is already on Template Line.
      15. Test Creation and Maintenance of templates - Insert invalid Default value on Data Template Line.

      TFS_TS_ID = 234784
      Covers Test cases:
      ------------------------------------------------------------------------------------
      Test Function Name                                                            TFS ID
      ------------------------------------------------------------------------------------
      CreateVendorFromTemplate, CreateItemFromTemplate, CreateContactFromTemplate   122572
      CreateApplyMasterConfigTemplate                                               122573
      CreatePackage                                                                 122575
      ExportPackageToExcel                                                          122576
      ExportPackageToXML                                                            122577
      ImportPackageFromExcel                                                        122578
      ImportPackageFromXML                                                          122579

      TFS_TS_ID = 237440
      Covers Test cases:
      ------------------------------------------------------------------------------------
      Test Function Name                                                            TFS ID
      ------------------------------------------------------------------------------------
      CreateMaintainTemplateManyOne, CreateMaintainTemplateOneToOne                 128460
      TemplateApplyToItselfError, TemplateFieldExistOnLineError                     128460
      DuplicateFieldOnLineError                                                     128460

      TFS_TS_ID = 241192
      Covers Test cases:
      ------------------------------------------------------------------------------------
      Test Function Name                                                            TFS ID
      ------------------------------------------------------------------------------------
      InvalidDefaultValueOnLineError                                                128460

      -- Verify bug 333276 fix
      ExportImportStructuredPackageWithDimAsCol

      ExportImportPackageWithMultilineConfigTemplate                                101422
    }
    END.
  }
}

