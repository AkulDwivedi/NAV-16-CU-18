OBJECT Report 16510 Update Revised Filing Status
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Update Revised Filing Status;
               ENN=Update Revised Filing Status];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6267;    ;DataItem;                    ;
               DataItemTable=Table16505;
               DataItemTableView=SORTING(Entry No.)
                                 ORDER(Ascending)
                                 WHERE(Revised eTDS Generated=FILTER(Yes));
               OnPreDataItem=VAR
                               Form26Q27QEntry@1500000 : Record 16505;
                             BEGIN
                               IF Quar = 0 THEN
                                 ERROR(Text16500,'Quarter');
                               IF FinYear = '' THEN
                                 ERROR(Text16500,'Financial Year');
                               IF FilingDate = 0D THEN
                                 ERROR(Text16500,'Filling Date');
                               IF "RRRNo." = '' THEN
                                 ERROR(Text16500,'Provisional Receipt No.');

                               SETRANGE("Financial Year",FinYear);
                               SETRANGE("Reporting Quarter",Quar);
                               SETRANGE("Revised eTDS Generated",TRUE);
                               SETRANGE("T.A.N. No.",TANLocation);

                               Form26Q27QEntry.RESET;
                               Form26Q27QEntry.SETRANGE("Financial Year",FinYear);
                               IF NOT Form26Q27QEntry.FINDFIRST THEN
                                 ERROR(Text16501,'Financial Year');
                               Form26Q27QEntry.SETRANGE("Reporting Quarter",Quar);
                               IF Form26Q27QEntry.ISEMPTY THEN
                                 ERROR(Text16501,'Quarter');
                               IF STRPOS(GETFILTERS,'26Q') <> 0 THEN
                                 Form26Q27QEntry.SETRANGE("Form No.",Form26Q27QEntry."Form No."::"26Q");
                               IF Form26Q27QEntry.ISEMPTY THEN
                                 ERROR(Text16501,'26Q');
                               IF STRPOS(GETFILTERS,'27Q') <> 0 THEN
                                 Form26Q27QEntry.SETRANGE("Form No.",Form26Q27QEntry."Form No."::"27Q");
                               IF Form26Q27QEntry.ISEMPTY THEN
                                 ERROR(Text16501,'27Q');
                               Form26Q27QEntry.SETRANGE("Revised eTDS Generated",TRUE);
                               IF Form26Q27QEntry.ISEMPTY THEN
                                 ERROR(Text16502);
                               InsertIntoProvisionalReceipt;
                             END;

               OnAfterGetRecord=BEGIN
                                  Filed := TRUE;
                                  IF NOT Revised THEN
                                    "Filing Date" := FilingDate
                                  ELSE BEGIN
                                    "Filing Date of Revised eTDS" := FilingDate;
                                    Revised := FALSE;
                                    "Correction-C3" := FALSE;
                                    "Correction-C5" := FALSE;
                                    "Correction-C9" := FALSE;
                                  END;
                                  IF ChallanReg.GET("Reporting Challan Entry No.") THEN BEGIN
                                    ChallanReg.Filed := TRUE;
                                    ChallanReg.Revised := FALSE;
                                    ChallanReg."Filing Date" := FilingDate;
                                    ChallanReg."Correction-C2" := FALSE;
                                    ChallanReg."Correction-C3" := FALSE;
                                    ChallanReg."Correction-C5" := FALSE;
                                    ChallanReg."Correction-C9" := FALSE;
                                    ChallanReg."Challan Updation Indicator" := 0;
                                  END;
                                  ChallanReg.MODIFY;
                                  MODIFY;
                                END;

               OnPostDataItem=BEGIN
                                BatchHeader.SETRANGE("Financial Year",FinYear);
                                BatchHeader.SETRANGE(Quarter,Quar);
                                BatchHeader.SETRANGE("Revised eTDS Generated",TRUE);
                                IF GETFILTER("Non Resident Payments") = 'Yes' THEN
                                  BatchHeader.SETRANGE("Form No.",BatchHeader."Form No."::"27Q")
                                ELSE
                                  BatchHeader.SETRANGE("Form No.",BatchHeader."Form No."::"26Q");
                                IF BatchHeader.FINDFIRST THEN BEGIN
                                  BatchHeader."Batch Updation Indicator" := 0;
                                  BatchHeader.Filed := TRUE;
                                  BatchHeader."Revised eTDS Filing Date" := FilingDate;
                                  BatchHeader."Previous RRR No." := "RRRNo.";
                                  BatchHeader."Correction-C1" := FALSE;
                                  BatchHeader."Correction-C2" := FALSE;
                                  BatchHeader."Correction-C3" := FALSE;
                                  BatchHeader."Correction-Y" := FALSE;
                                END;
                                BatchHeader.MODIFY;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1500008;2;Field     ;
                  CaptionML=[ENU=TAN Location;
                             ENN=TAN Location];
                  SourceExpr=TANLocation;
                  TableRelation="T.A.N. Nos." }

      { 1500000;2;Field     ;
                  CaptionML=[ENU=Quarter;
                             ENN=Quarter];
                  SourceExpr=Quar }

      { 1500002;2;Field     ;
                  CaptionML=[ENU=Financial Year;
                             ENN=Financial Year];
                  SourceExpr=FinYear;
                  OnLookup=VAR
                             TDSSetup@1500001 : Record 16501;
                             TDSAccountingPeriod@1500000 : Record 13724;
                           BEGIN
                             IF Quar > 0 THEN BEGIN
                               TDSSetup.RESET;
                               TDSSetup.SETRANGE(Quarter,Quar);
                               IF TDSSetup.FINDFIRST THEN BEGIN
                                 TDSAccountingPeriod.GET(TDSSetup."No.");
                                 IF PAGE.RUNMODAL(13721,TDSAccountingPeriod) = ACTION::LookupOK THEN
                                   FinYear := TDSAccountingPeriod."Financial Year for eTDS/TCS";
                               END;
                             END;
                           END;
                            }

      { 1500004;2;Field     ;
                  CaptionML=[ENU=Filing Date;
                             ENN=Filing Date];
                  SourceExpr=FilingDate }

      { 1500006;2;Field     ;
                  CaptionML=[ENU=Provision Receipt No.;
                             ENN=Provision Receipt No.];
                  SourceExpr="RRRNo.";
                  OnLookup=BEGIN
                             ProvisionalRcptNo.RESET;
                             // ProvisionalRcptNo.SETRANGE(Quarter,Quar);  //Bugfix
                             // ProvisionalRcptNo.SETRANGE("Financial Year",FinYear);  //Bugfix
                             IF PAGE.RUNMODAL(0,ProvisionalRcptNo) = ACTION::LookupOK THEN
                               "RRRNo." := ProvisionalRcptNo."Provisional Receipt No.";
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TANLocation@1500010 : Code[20];
      Quar@1500000 : ' ,Q1,Q2,Q3,Q4';
      FinYear@1500001 : Code[6];
      FilingDate@1500002 : Date;
      "RRRNo."@1500003 : Code[20];
      ChallanReg@1500005 : Record 16504;
      BatchHeader@1500006 : Record 16503;
      Text16500@1500004 : TextConst 'ENU=Please enter %1.;ENN=Please enter %1.';
      Text16501@1500007 : TextConst 'ENU=No records found for the %1.;ENN=No records found for the %1.';
      Text16502@1500009 : TextConst 'ENU=Revised records cannot be updated.;ENN=Revised records cannot be updated.';
      ProvisionalRcptNo@1500008 : Record 16506;

    PROCEDURE InsertIntoProvisionalReceipt@1500000();
    BEGIN
      IF "RRRNo." <> '' THEN BEGIN
        IF NOT ProvisionalRcptNo.GET("RRRNo.") THEN BEGIN
          ProvisionalRcptNo.INIT;
          ProvisionalRcptNo."Provisional Receipt No." := "RRRNo.";
          ProvisionalRcptNo.Quarter := Quar;
          ProvisionalRcptNo."Financial Year" := FinYear;
          ProvisionalRcptNo.INSERT;
        END;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

