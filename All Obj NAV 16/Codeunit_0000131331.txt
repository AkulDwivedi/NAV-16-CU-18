OBJECT Codeunit 131331 Library - Cash Flow
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LibraryUtility@1000 : Codeunit 131000;

    PROCEDURE FindCashFlowCard@1(VAR CashFlowForecast@1000 : Record 840);
    BEGIN
      CashFlowForecast.FINDFIRST;
    END;

    PROCEDURE FindCashFlowAccount@2(VAR CashFlowAccount@1000 : Record 841);
    BEGIN
      CashFlowAccount.SETRANGE("Account Type",CashFlowAccount."Account Type"::Entry);
      CashFlowAccount.FINDFIRST;
    END;

    PROCEDURE FindCashFlowAnalysisView@14(VAR AnalysisView@1000 : Record 363);
    BEGIN
      WITH AnalysisView DO BEGIN
        RESET;
        SETRANGE("Account Source","Account Source"::"Cash Flow Account");
        FINDFIRST;
      END
    END;

    PROCEDURE CreateJournalLine@3(VAR CFWorksheetLine@1002 : Record 846;CFNo@1004 : Code[20];CFAccountNo@1005 : Code[20]);
    VAR
      LibraryUtility@1001 : Codeunit 131000;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(CFWorksheetLine);
      CFWorksheetLine.VALIDATE("Line No.",LibraryUtility.GetNewLineNo(RecRef,CFWorksheetLine.FIELDNO("Line No.")));
      CFWorksheetLine.INSERT(TRUE);
      CFWorksheetLine.VALIDATE("Cash Flow Forecast No.",CFNo);
      CFWorksheetLine.VALIDATE("Cash Flow Account No.",CFAccountNo);
      CFWorksheetLine.VALIDATE("Cash Flow Date",WORKDATE);  // Defaults to work date.
      CFWorksheetLine.MODIFY(TRUE);
    END;

    PROCEDURE DeleteJournalLine@4();
    BEGIN
    END;

    PROCEDURE FillJournal@7(ConsiderSource@1002 : ARRAY [11] OF Boolean;CFNo@1013 : Code[20];GroupByDocumentType@1003 : Boolean);
    VAR
      CFWorksheetLine@1001 : Record 846;
      SuggestWorksheetLines@1000 : Report 840;
    BEGIN
      CFWorksheetLine.INIT;
      SuggestWorksheetLines.InitializeRequest(ConsiderSource,CFNo,'',GroupByDocumentType);
      SuggestWorksheetLines.USEREQUESTPAGE := FALSE;
      SuggestWorksheetLines.RUN;
    END;

    PROCEDURE FillBudgetJournal@16(CFFunds@1002 : Boolean;CFNo@1013 : Code[20];GLBudgetName@1003 : Code[10]);
    VAR
      CFWorksheetLine@1001 : Record 846;
      SuggestWorksheetLines@1000 : Report 840;
      ConsiderSource@1004 : ARRAY [11] OF Boolean;
      SourceType@1005 : ',Customer,Vendor,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Budgeted Fixed Asset,Sale of Fixed Asset,Service Order,G/L Budget';
    BEGIN
      CFWorksheetLine.INIT;
      ConsiderSource[SourceType::"Liquid Funds"] := CFFunds;
      ConsiderSource[SourceType::"G/L Budget"] := TRUE;
      SuggestWorksheetLines.InitializeRequest(ConsiderSource,CFNo,GLBudgetName,FALSE);
      SuggestWorksheetLines.USEREQUESTPAGE := FALSE;
      SuggestWorksheetLines.RUN;
    END;

    PROCEDURE ClearJournal@9();
    VAR
      CFWorksheetLine@1000 : Record 846;
    BEGIN
      CFWorksheetLine.DELETEALL(TRUE);
    END;

    PROCEDURE PostJournal@10();
    VAR
      CFWorksheetLine@1000 : Record 846;
    BEGIN
      CODEUNIT.RUN(CODEUNIT::"Cash Flow Wksh.-Register Batch",CFWorksheetLine);
    END;

    PROCEDURE PostJournalLines@15(VAR CFWorksheetLine@1003 : Record 846);
    BEGIN
      CFWorksheetLine.FINDSET;
      CODEUNIT.RUN(CODEUNIT::"Cash Flow Wksh.-Register Batch",CFWorksheetLine);
    END;

    PROCEDURE CreateManualLinePayment@11(VAR CFManualExpense@1000 : Record 850;CFAccountNo@1002 : Code[20]);
    BEGIN
      CFManualExpense.INIT;
      CFManualExpense.VALIDATE("Cash Flow Account No.",CFAccountNo);
      CFManualExpense.VALIDATE(Code,
        LibraryUtility.GenerateRandomCode(CFManualExpense.FIELDNO(Code),DATABASE::"Cash Flow Manual Expense"));
      CFManualExpense.VALIDATE("Starting Date",WORKDATE);  // Required field to post
      CFManualExpense.INSERT(TRUE);
    END;

    PROCEDURE CreateManualLineRevenue@20(VAR CFManualRevenue@1000 : Record 849;CFAccountNo@1002 : Code[20]);
    BEGIN
      CFManualRevenue.INIT;
      CFManualRevenue.VALIDATE("Cash Flow Account No.",CFAccountNo);
      CFManualRevenue.VALIDATE(Code,
        LibraryUtility.GenerateRandomCode(CFManualRevenue.FIELDNO(Code),DATABASE::"Cash Flow Manual Revenue"));
      CFManualRevenue.VALIDATE("Starting Date",WORKDATE);  // Required field to post
      CFManualRevenue.INSERT(TRUE);
    END;

    PROCEDURE DeleteManualLine@12();
    BEGIN
    END;

    PROCEDURE CreateCashFlowCard@13(VAR CashFlowForecast@1000 : Record 840);
    BEGIN
      CashFlowForecast.INIT;
      CashFlowForecast.VALIDATE(
        "No.",LibraryUtility.GenerateRandomCode(CashFlowForecast.FIELDNO("No."),DATABASE::"Cash Flow Forecast"));
      CashFlowForecast.INSERT(TRUE);
    END;

    PROCEDURE CreateCashFlowAccount@19(VAR CashFlowAccount@1000 : Record 841;AccountType@1001 : Option);
    BEGIN
      CashFlowAccount.INIT;
      CashFlowAccount.VALIDATE("No.",
        LibraryUtility.GenerateRandomCode(CashFlowAccount.FIELDNO("No."),DATABASE::"Cash Flow Account"));
      CashFlowAccount.VALIDATE("Account Type",AccountType);
      CashFlowAccount.VALIDATE(Name,
        LibraryUtility.GenerateRandomCode(CashFlowAccount.FIELDNO(Name),DATABASE::"Cash Flow Account"));
      CashFlowAccount.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

