OBJECT Codeunit 358 DateFilter-Calc
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.45778,NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Fiscal Year %1;ENN=Fiscal Year %1';
      AccountingPeriod@1001 : Record 50;
      StartDate@1002 : Date;

    PROCEDURE CreateFiscalYearFilter@3(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];Date@1002 : Date;NextStep@1003 : Integer);
    BEGIN
      CreateAccountingDateFilter(Filter,Name,TRUE,Date,NextStep);
    END;

    PROCEDURE CreateAccountingPeriodFilter@2(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];Date@1002 : Date;NextStep@1003 : Integer);
    BEGIN
      CreateAccountingDateFilter(Filter,Name,FALSE,Date,NextStep);
    END;

    LOCAL PROCEDURE CreateAccountingDateFilter@1(VAR Filter@1000 : Text[30];VAR Name@1001 : Text[30];FiscalYear@1002 : Boolean;Date@1003 : Date;NextStep@1004 : Integer);
    BEGIN
      AccountingPeriod.RESET;
      IF FiscalYear THEN
        AccountingPeriod.SETRANGE("New Fiscal Year",TRUE);
      AccountingPeriod."Starting Date" := Date;
      AccountingPeriod.FIND('=<>');
      IF AccountingPeriod."Starting Date" > Date THEN
        NextStep := NextStep - 1;
      IF NextStep <> 0 THEN
        IF AccountingPeriod.NEXT(NextStep) <> NextStep THEN BEGIN
          IF NextStep < 0 THEN
            Filter := '..' + FORMAT(AccountingPeriod."Starting Date" - 1)
          ELSE
            Filter := FORMAT(AccountingPeriod."Starting Date") + '..' + FORMAT(31129999D);
          Name := '...';
          EXIT;
        END;
      StartDate := AccountingPeriod."Starting Date";
      IF FiscalYear THEN
        Name := STRSUBSTNO(Text000,FORMAT(DATE2DMY(StartDate,3)))
      ELSE
        Name := AccountingPeriod.Name;
      IF AccountingPeriod.NEXT <> 0 THEN
        Filter := FORMAT(StartDate) + '..' + FORMAT(AccountingPeriod."Starting Date" - 1)
      ELSE BEGIN
        Filter := FORMAT(StartDate) + '..' + FORMAT(31129999D);
        Name := Name + '...';
      END;
    END;

    PROCEDURE CreateTDSAccountingDateFilter@1280000(VAR Filter@1000 : Text[30];FiscalYear@1002 : Boolean;Date@1003 : Date;NextStep@1004 : Integer);
    VAR
      TDSAccountingPeriod@1280000 : Record 13724;
    BEGIN
      TDSAccountingPeriod.RESET;
      TDSAccountingPeriod.SETFILTER("Ending Date",'>=%1',Date);
      TDSAccountingPeriod.SETFILTER("Starting Date",'<=%1',Date);
      TDSAccountingPeriod.FIND('+');

      IF TDSAccountingPeriod."Starting Date" > Date THEN
        NextStep := NextStep - 1;
      IF NextStep <> 0 THEN
        IF TDSAccountingPeriod.NEXT(NextStep) <> NextStep THEN BEGIN
          IF NextStep < 0 THEN
            Filter := '..' + FORMAT(TDSAccountingPeriod."Ending Date")
          ELSE
            Filter := FORMAT(TDSAccountingPeriod."Starting Date") + '..' + FORMAT(31129999D);
          EXIT;
        END;
      StartDate := TDSAccountingPeriod."Starting Date";
      Filter := FORMAT(StartDate) + '..' + FORMAT(TDSAccountingPeriod."Ending Date")
    END;

    PROCEDURE CreateTCSAccountingDateFilter@1500000(VAR Filter@1000 : Text[30];FiscalYear@1002 : Boolean;Date@1003 : Date;NextStep@1004 : Integer);
    VAR
      IncomeTaxAccountingPeriod@1280000 : Record 13724;
    BEGIN
      IncomeTaxAccountingPeriod.RESET;
      IncomeTaxAccountingPeriod.SETFILTER("Starting Date",'<=%1',Date);
      IncomeTaxAccountingPeriod.SETFILTER("Ending Date",'>=%1',Date);
      IncomeTaxAccountingPeriod.FIND('+');
      IF IncomeTaxAccountingPeriod."Starting Date" > Date THEN
        NextStep := NextStep - 1;
      IF NextStep <> 0 THEN
        IF IncomeTaxAccountingPeriod.NEXT(NextStep) <> NextStep THEN BEGIN
          IF NextStep < 0 THEN
            Filter := '..' + FORMAT(IncomeTaxAccountingPeriod."Ending Date")
          ELSE
            Filter := FORMAT(IncomeTaxAccountingPeriod."Starting Date") + '..' + FORMAT(31129999D);
          EXIT;
        END;
      StartDate := IncomeTaxAccountingPeriod."Starting Date";
      Filter := FORMAT(StartDate) + '..' + FORMAT(IncomeTaxAccountingPeriod."Ending Date")
    END;

    BEGIN
    END.
  }
}

