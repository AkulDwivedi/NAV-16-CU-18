OBJECT Codeunit 16402 GST Fiscal Year-Close
{
  OBJECT-PROPERTIES
  {
    Date=23-02-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.48067;
  }
  PROPERTIES
  {
    TableNo=16402;
    OnRun=BEGIN
            GSTAccountingSubPeriod.COPY(Rec);
            Code;
            Rec := GSTAccountingSubPeriod;
          END;

  }
  CODE
  {
    VAR
      GSTAccountingSubPeriod@1500004 : Record 16402;
      GSTAccountingSubPeriod2@1500003 : Record 16402;
      GSTAccountingSubPeriod3@1500002 : Record 16402;
      GSTFiscalYearStartDate@1500001 : Date;
      GSTFiscalYearEndDate@1500000 : Date;
      NewYrOldCloseErr@1500008 : TextConst 'ENU=You must create a new fiscal year before you can close the old year.;ENN=You must create a new fiscal year before you can close the old year.';
      YrCloseQst@1500007 : TextConst '@@@="%1 = Startedate, %2 = EndDate";ENU=This function closes the fiscal year from %1 to %2. Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed. Do you want to close the fiscal year?;ENN=This function closes the fiscal year from %1 to %2. Once the fiscal year is closed it cannot be opened again, and the periods in the fiscal year cannot be changed. Do you want to close the fiscal year?';

    LOCAL PROCEDURE Code@1500000();
    VAR
      GSTAccountingPeriod@1500000 : Record 16401;
    BEGIN
      WITH GSTAccountingSubPeriod DO BEGIN
        GSTAccountingSubPeriod2.SETRANGE(Closed,FALSE);
        GSTAccountingSubPeriod2.FINDFIRST;
        GSTFiscalYearStartDate := GSTAccountingSubPeriod2."Starting Date";
        GSTAccountingSubPeriod := GSTAccountingSubPeriod2;
        TESTFIELD("New Financial Year",TRUE);
        GSTAccountingSubPeriod2.SETRANGE("New Financial Year",TRUE);
        GSTAccountingSubPeriod2.SETFILTER("Starting Date",'>%1',GSTAccountingSubPeriod2."Starting Date");
        IF GSTAccountingSubPeriod2.FINDFIRST THEN BEGIN
          GSTFiscalYearEndDate := CALCDATE('<-1D>',GSTAccountingSubPeriod2."Starting Date");
          GSTAccountingSubPeriod3 := GSTAccountingSubPeriod2;
          GSTAccountingSubPeriod2.SETRANGE("New Financial Year");
          GSTAccountingSubPeriod2.SETFILTER("Starting Date",'<%1',GSTFiscalYearEndDate);
          GSTAccountingSubPeriod2.FINDLAST;
        END ELSE
          ERROR(NewYrOldCloseErr);
        IF NOT
           CONFIRM(STRSUBSTNO(YrCloseQst,GSTFiscalYearStartDate,GSTFiscalYearEndDate),FALSE)
        THEN
          EXIT;
        RESET;
        SETRANGE("Starting Date",GSTFiscalYearStartDate,GSTAccountingSubPeriod2."Starting Date");
        MODIFYALL(Closed,TRUE);
        SETRANGE("Starting Date",GSTFiscalYearStartDate,GSTAccountingSubPeriod3."Starting Date");
        MODIFYALL("Date Locked",TRUE);
        RESET;
        GSTAccountingPeriod.SETRANGE("Starting Date",GSTAccountingSubPeriod2."Accounting Period Start Date");
        GSTAccountingPeriod.MODIFYALL("Year Closed",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

