OBJECT Table 50 Accounting Period
{
  OBJECT-PROPERTIES
  {
    Date=23-11-17;
    Time=[ 4:10:31 PM];
    Version List=NAVW19.00.00.45778;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               AccountingPeriod2 := Rec;
               IF AccountingPeriod2.FIND('>') THEN
                 AccountingPeriod2.TESTFIELD("Date Locked",FALSE);
               UpdateAvgItems(1);
             END;

    OnModify=BEGIN
               UpdateAvgItems(2);
             END;

    OnDelete=BEGIN
               TESTFIELD("Date Locked",FALSE);
               UpdateAvgItems(3);
             END;

    OnRename=BEGIN
               TESTFIELD("Date Locked",FALSE);
               AccountingPeriod2 := Rec;
               IF AccountingPeriod2.FIND('>') THEN
                 AccountingPeriod2.TESTFIELD("Date Locked",FALSE);
               UpdateAvgItems(4);
             END;

    CaptionML=[ENU=Accounting Period;
               ENN=Accounting Period];
    LookupPageID=Page100;
  }
  FIELDS
  {
    { 1   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                Name := FORMAT("Starting Date",0,Text000);
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ENN=Starting Date];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text10        ;CaptionML=[ENU=Name;
                                                              ENN=Name] }
    { 3   ;   ;New Fiscal Year     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Date Locked",FALSE);
                                                                IF "New Fiscal Year" THEN BEGIN
                                                                  IF NOT InvtSetup.GET THEN
                                                                    EXIT;
                                                                  "Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                                                                  "Average Cost Period" := InvtSetup."Average Cost Period";
                                                                END ELSE BEGIN
                                                                  "Average Cost Calc. Type" := "Average Cost Calc. Type"::" ";
                                                                  "Average Cost Period" := "Average Cost Period"::" ";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=New Fiscal Year;
                                                              ENN=New Fiscal Year] }
    { 4   ;   ;Closed              ;Boolean       ;CaptionML=[ENU=Closed;
                                                              ENN=Closed];
                                                   Editable=No }
    { 5   ;   ;Date Locked         ;Boolean       ;CaptionML=[ENU=Date Locked;
                                                              ENN=Date Locked];
                                                   Editable=No }
    { 5804;   ;Average Cost Calc. Type;Option     ;AccessByPermission=TableData 27=R;
                                                   CaptionML=[ENU=Average Cost Calc. Type;
                                                              ENN=Average Cost Calc. Type];
                                                   OptionCaptionML=[ENU=" ,Item,Item & Location & Variant";
                                                                    ENN=" ,Item,Item & Location & Variant"];
                                                   OptionString=[ ,Item,Item & Location & Variant];
                                                   Editable=No }
    { 5805;   ;Average Cost Period ;Option        ;AccessByPermission=TableData 27=R;
                                                   CaptionML=[ENU=Average Cost Period;
                                                              ENN=Average Cost Period];
                                                   OptionCaptionML=[ENU=" ,Day,Week,Month,Quarter,Year,Accounting Period";
                                                                    ENN=" ,Day,Week,Month,Quarter,Year,Accounting Period"];
                                                   OptionString=[ ,Day,Week,Month,Quarter,Year,Accounting Period];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Starting Date                           ;Clustered=Yes }
    {    ;New Fiscal Year,Date Locked              }
    {    ;Closed                                   }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Starting Date,Name,New Fiscal Year,Closed }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=<Month Text>;ENN=<Month Text>';
      AccountingPeriod2@1001 : Record 50;
      InvtSetup@1002 : Record 313;

    PROCEDURE UpdateAvgItems@1(UpdateType@1005 : Option);
    VAR
      ChangeAvgCostSetting@1002 : Codeunit 5810;
    BEGIN
      ChangeAvgCostSetting.UpdateAvgCostFromAccPeriodChg(Rec,xRec,UpdateType);
    END;

    PROCEDURE GetFiscalYearEndDate@9(ReferenceDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record 50;
    BEGIN
      WITH AccountingPeriod DO BEGIN
        SETRANGE("New Fiscal Year",TRUE);
        SETRANGE("Starting Date",0D,ReferenceDate);
        IF FINDLAST THEN
          SETRANGE("Starting Date");
        IF FIND('>') THEN
          EXIT("Starting Date" - 1);
      END;
    END;

    PROCEDURE GetFiscalYearStartDate@10(ReferenceDate@1000 : Date) : Date;
    VAR
      AccountingPeriod@1001 : Record 50;
    BEGIN
      WITH AccountingPeriod DO BEGIN
        SETRANGE("New Fiscal Year",TRUE);
        SETRANGE("Starting Date",0D,ReferenceDate);
        IF FINDLAST THEN
          EXIT("Starting Date")
      END;
    END;

    BEGIN
    END.
  }
}

