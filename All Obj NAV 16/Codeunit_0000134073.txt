OBJECT Codeunit 134073 Check Document No. Unit Test
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1004 : Codeunit 130000;
      LibraryERM@1002 : Codeunit 131300;
      LibraryPurchase@1003 : Codeunit 130512;
      LibraryUtility@1005 : Codeunit 131000;
      LibraryRandom@1001 : Codeunit 130440;
      DocumentNoErr@1006 : TextConst 'ENU=Document No. must be equal to ''%1''  in %2';

    [Test]
    PROCEDURE MultipleLinesDifferentVendorDifferentDocNo@4();
    VAR
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1002 : Record 81;
      Vendor1@1001 : Record 23;
      Vendor2@1000 : Record 23;
    BEGIN
      // Pre-Setup
      CreateGenJnlBatch(GenJnlBatch);
      LibraryPurchase.CreateVendor(Vendor1);
      LibraryPurchase.CreateVendor(Vendor2);

      // Setup
      CreateDiffAccNoDiffDocNoLines(GenJnlBatch,GenJnlLine."Account Type"::Vendor,Vendor1."No.",Vendor2."No.");

      // Pre-Exercise
      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);

      // Exercise
      GenJnlLine.CheckDocNoOnLines;

      // Verify
      BatchPostJournalLines(GenJnlBatch);
    END;

    [Test]
    PROCEDURE MultipleLinesDifferentVendorSameDocNo@3();
    VAR
      GenJnlBatch@1002 : Record 232;
      GenJnlLine@1001 : Record 81;
      Vendor1@1000 : Record 23;
      Vendor2@1003 : Record 23;
    BEGIN
      // Pre-Setup
      CreateGenJnlBatch(GenJnlBatch);
      LibraryPurchase.CreateVendor(Vendor1);
      LibraryPurchase.CreateVendor(Vendor2);

      // Setup
      CreateDiffAccNoSameDocNoLines(GenJnlBatch,GenJnlLine."Account Type"::Vendor,Vendor1."No.",Vendor2."No.");

      // Pre-Exercise
      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);

      // Exercise
      GenJnlLine.CheckDocNoOnLines;

      // Verify
      BatchPostJournalLines(GenJnlBatch);
    END;

    [Test]
    PROCEDURE MulitpleLinesSameVendorSameDocNo@2();
    VAR
      GenJnlBatch@1000 : Record 232;
      GenJnlLine@1002 : Record 81;
      Vendor@1001 : Record 23;
    BEGIN
      // Pre-Setup
      CreateGenJnlBatch(GenJnlBatch);
      LibraryPurchase.CreateVendor(Vendor);

      // Setup
      CreateSameAccNoSameDocNoLines(GenJnlBatch,GenJnlLine."Account Type"::Vendor,Vendor."No.");

      // Pre-Exercise
      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);

      // Exercise
      GenJnlLine.CheckDocNoOnLines;

      // Verify
      BatchPostJournalLines(GenJnlBatch);
    END;

    [Test]
    PROCEDURE MultipleLinesSameVendorWithDocNoGap@20();
    VAR
      GenJnlBatch@1000 : Record 232;
      GenJnlLine@1002 : Record 81;
      Vendor@1001 : Record 23;
      CorrectDocumentNo@1003 : Code[20];
    BEGIN
      // Pre-Setup
      CreateGenJnlBatch(GenJnlBatch);
      LibraryPurchase.CreateVendor(Vendor);

      // Setup
      CorrectDocumentNo := CreateSameAccLinesWithDocNoGap(GenJnlBatch,GenJnlLine."Account Type"::Vendor,Vendor."No.");

      // Pre-Exercise
      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);

      // Exercise
      ASSERTERROR GenJnlLine.CheckDocNoOnLines;

      // Verify
      Assert.ExpectedError(STRSUBSTNO(DocumentNoErr,CorrectDocumentNo,GenJnlLine.TABLECAPTION));
    END;

    LOCAL PROCEDURE CreateGenJnlBatch@9(VAR GenJnlBatch@1000 : Record 232);
    VAR
      BankAcc@1001 : Record 270;
      NoSeries@1002 : Record 308;
      NoSeriesLine@1003 : Record 309;
    BEGIN
      LibraryERM.CreateGenJournalBatch(GenJnlBatch,LibraryERM.SelectGenJnlTemplate);

      LibraryERM.CreateBankAccount(BankAcc);
      GenJnlBatch.VALIDATE("Bal. Account Type",GenJnlBatch."Bal. Account Type"::"Bank Account");
      GenJnlBatch.VALIDATE("Bal. Account No.",BankAcc."No.");
      GenJnlBatch.MODIFY(TRUE);

      LibraryUtility.CreateNoSeries(NoSeries,TRUE,FALSE,FALSE);
      LibraryUtility.CreateNoSeriesLine(NoSeriesLine,NoSeries.Code,'','');
      GenJnlBatch.VALIDATE("No. Series",NoSeries.Code);
      GenJnlBatch.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE CreateDiffAccNoDiffDocNoLines@29(GenJnlBatch@1000 : Record 232;AccountType@1001 : Option;FirstAccountNo@1002 : Code[20];SecondAccountNo@1005 : Code[20]);
    VAR
      GenJnlLine1@1003 : Record 81;
      GenJnlLine2@1004 : Record 81;
    BEGIN
      LibraryERM.CreateGeneralJnlLine(GenJnlLine1,GenJnlBatch."Journal Template Name",GenJnlBatch.Name,
        GenJnlLine1."Document Type"::Payment,AccountType,FirstAccountNo,LibraryRandom.RandDec(1000,2));

      LibraryERM.CreateGeneralJnlLine(GenJnlLine2,GenJnlBatch."Journal Template Name",GenJnlBatch.Name,
        GenJnlLine2."Document Type"::Payment,AccountType,SecondAccountNo,LibraryRandom.RandDec(1000,2));
      GenJnlLine2.VALIDATE("Document No.",INCSTR(GenJnlLine1."Document No."));
      GenJnlLine2.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE BatchPostJournalLines@39(GenJnlBatch@1000 : Record 232);
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
      GenJnlLine.FINDFIRST;
      LibraryERM.PostGeneralJnlLine(GenJnlLine);
    END;

    LOCAL PROCEDURE CreateDiffAccNoSameDocNoLines@27(GenJnlBatch@1000 : Record 232;AccountType@1001 : Option;FirstAccountNo@1002 : Code[20];SecondAccountNo@1005 : Code[20]);
    VAR
      GenJnlLine@1003 : Record 81;
      DocumentNo@1004 : Code[20];
    BEGIN
      CreateDiffAccNoDiffDocNoLines(GenJnlBatch,AccountType,FirstAccountNo,SecondAccountNo);

      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
      GenJnlLine.FINDFIRST;
      DocumentNo := GenJnlLine."Document No.";

      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
      GenJnlLine.MODIFYALL("Document No.",DocumentNo,TRUE);
    END;

    LOCAL PROCEDURE CreateSameAccNoSameDocNoLines@22(GenJnlBatch@1000 : Record 232;AccountType@1001 : Option;AccountNo@1002 : Code[20]);
    BEGIN
      CreateDiffAccNoSameDocNoLines(GenJnlBatch,AccountType,AccountNo,AccountNo);
    END;

    LOCAL PROCEDURE CreateSameAccLinesWithDocNoGap@28(GenJnlBatch@1002 : Record 232;AccountType@1001 : Option;AccountNo@1000 : Code[20]) CorrectDocumentNo : Code[20];
    VAR
      GenJnlLine@1003 : Record 81;
    BEGIN
      CreateDiffAccNoDiffDocNoLines(GenJnlBatch,AccountType,AccountNo,AccountNo);

      GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
      GenJnlLine.FINDLAST;
      CorrectDocumentNo := GenJnlLine."Document No.";

      GenJnlLine.VALIDATE("Document No.",INCSTR(GenJnlLine."Document No."));
      GenJnlLine.MODIFY(TRUE);
    END;

    BEGIN
    {
      // [FEATURE] [Document No.] [General Journal] [UT]
    }
    END.
  }
}

