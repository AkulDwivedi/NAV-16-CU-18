OBJECT Table 16302 LC Terms
{
  OBJECT-PROPERTIES
  {
    Date=23-09-13;
    Time=12:00:00 PM;
    Version List=NAVIN7.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=LC Terms;
               ENN=LC Terms];
  }
  FIELDS
  {
    { 1   ;   ;LC No.              ;Code20        ;CaptionML=[ENU=LC No.;
                                                              ENN=LC No.] }
    { 2   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              ENN=Description] }
    { 3   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              ENN=Attachment No.] }
    { 4   ;   ;Line No.            ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Line No.;
                                                              ENN=Line No.] }
    { 5   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ENN=Date] }
    { 6   ;   ;Released            ;Boolean       ;CaptionML=[ENU=Released;
                                                              ENN=Released] }
  }
  KEYS
  {
    {    ;LC No.,Line No.                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1280002 : TextConst 'ENU=You have canceled the create process.;ENN=You have canceled the create process.';
      Text001@1280001 : TextConst 'ENU=Replace existing attachment?;ENN=Replace existing attachment?';
      Text002@1280000 : TextConst 'ENU=You have canceled the import process.;ENN=You have canceled the import process.';

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      TempAttachment@1001 : TEMPORARY Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      CLEAR(TempAttachment);
      IF TempAttachment.ImportAttachmentFromClientFile('',TRUE,TRUE) THEN BEGIN
        IF "Attachment No." = 0 THEN
          Attachment.GET(AttachmentManagement.InsertAttachment(0))
        ELSE
          Attachment.GET("Attachment No.");
        TempAttachment."No." := Attachment."No.";
        TempAttachment."Storage Pointer" := Attachment."Storage Pointer";
        TempAttachment.WizSaveAttachment;
        Attachment."Storage Type" := TempAttachment."Storage Type";
        Attachment."Storage Pointer" := TempAttachment."Storage Pointer";
        Attachment.Attachment := TempAttachment.Attachment;
        Attachment."File Extension" := TempAttachment."File Extension";
        Attachment.MODIFY;
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.OpenAttachment("LC No." + ' ' + Description,FALSE,'');
    END;

    PROCEDURE CreateAttachment@1();
    VAR
      Attachment@1000 : Record 5062;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF WordManagement.IsWordDocumentExtension('DOC') THEN;

      NewAttachNo := WordManagement.CreateWordAttachment("LC No." + ' ' + Description,'');
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        MODIFY;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      Attachment@1000 : Record 5062;
      FileName@1500000 : Text[1024];
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.ExportAttachmentToClientFile(FileName);
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean);
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
    END;

    BEGIN
    END.
  }
}

