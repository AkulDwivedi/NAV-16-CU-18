OBJECT Codeunit 130101 Library - Payment Format
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      LibraryUtility@1000 : Codeunit 131000;

    PROCEDURE CreateDataExchDef@1(VAR DataExchDef@1000 : Record 1222;DataHandlingCodeunit@1005 : Integer;ValidationCodeunit@1004 : Integer;ReadingWritingCodeunit@1003 : Integer;ReadingWritingXMLport@1001 : Integer;ExternalDataHandlingCodeunit@1002 : Integer;UserFeedbackCodeunit@1006 : Integer);
    BEGIN
      DataExchDef.InsertRecForExport(
        LibraryUtility.GenerateGUID,LibraryUtility.GenerateGUID,DataExchDef.Type::"Payment Export",
        ReadingWritingXMLport,DataExchDef."File Type"::"Variable Text");
      DataExchDef.VALIDATE("Ext. Data Handling Codeunit",ExternalDataHandlingCodeunit);
      DataExchDef.VALIDATE("Reading/Writing Codeunit",ReadingWritingCodeunit);
      DataExchDef.VALIDATE("Validation Codeunit",ValidationCodeunit);
      DataExchDef.VALIDATE("Data Handling Codeunit",DataHandlingCodeunit);
      DataExchDef.VALIDATE("User Feedback Codeunit",UserFeedbackCodeunit);
      DataExchDef.MODIFY(TRUE);
    END;

    PROCEDURE CreateDataExchColumnDef@10(VAR DataExchColumnDef@1000 : Record 1223;DataExchDefCode@1001 : Code[20];DataExchLineDefCode@1002 : Code[20]);
    VAR
      GenJnlLine@1003 : Record 81;
    BEGIN
      DataExchColumnDef.InsertRec(DataExchDefCode,DataExchLineDefCode,1,GenJnlLine.FIELDCAPTION(Description),
        TRUE,DataExchColumnDef."Data Type"::Text,'','',GenJnlLine.FIELDNAME(Description));
      DataExchColumnDef.InsertRec(DataExchDefCode,DataExchLineDefCode,2,GenJnlLine.FIELDCAPTION("Posting Date"),
        TRUE,DataExchColumnDef."Data Type"::Date,'<Day,2><Month,2><Year4>','',GenJnlLine.FIELDNAME("Posting Date"));
      DataExchColumnDef.InsertRec(DataExchDefCode,DataExchLineDefCode,3,GenJnlLine.FIELDCAPTION(Amount),
        TRUE,DataExchColumnDef."Data Type"::Decimal,'<Precision,2><sign><Integer><Decimals><Comma,.>','',
        GenJnlLine.FIELDNAME(Amount));
      DataExchColumnDef.MODIFY(TRUE);
    END;

    PROCEDURE CreateDataExchMapping@14(VAR DataExchMapping@1000 : Record 1224;DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1001 : Code[20];PreMappingCodeunit@1003 : Integer;MappingCodeunit@1005 : Integer;PostMappingCodeunit@1004 : Integer);
    BEGIN
      DataExchMapping.InsertRecForExport(DataExchDefCode,DataExchLineDefCode,
        DATABASE::"Payment Export Data",LibraryUtility.GenerateGUID,MappingCodeunit);
      DataExchMapping.VALIDATE("Pre-Mapping Codeunit",PreMappingCodeunit);
      DataExchMapping.VALIDATE("Post-Mapping Codeunit",PostMappingCodeunit);
      DataExchMapping.MODIFY(TRUE);
    END;

    PROCEDURE CreateDataExchFieldMapping@18(VAR DataExchFieldMapping@1000 : Record 1225;DataExchDefCode@1002 : Code[20];DataExchLineDefCode@1001 : Code[20]);
    VAR
      PaymentExportData@1003 : Record 1226;
    BEGIN
      DataExchFieldMapping.InsertRec(DataExchDefCode,DataExchLineDefCode,
        DATABASE::"Payment Export Data",1,PaymentExportData.FIELDNO("Document No."),FALSE,0);
      DataExchFieldMapping.InsertRec(DataExchDefCode,DataExchLineDefCode,
        DATABASE::"Payment Export Data",2,PaymentExportData.FIELDNO("Transfer Date"),FALSE,0);
      DataExchFieldMapping.InsertRec(DataExchDefCode,DataExchLineDefCode,
        DATABASE::"Payment Export Data",3,PaymentExportData.FIELDNO(Amount),FALSE,1);
    END;

    PROCEDURE CreateBankExportImportSetup@22(VAR BankExportImportSetup@1000 : Record 1200;DataExchDef@1001 : Record 1222);
    BEGIN
      BankExportImportSetup.VALIDATE(Code,DataExchDef.Code);
      BankExportImportSetup.VALIDATE(Name,DataExchDef.Name);
      BankExportImportSetup.VALIDATE(Direction,BankExportImportSetup.Direction::Export);
      BankExportImportSetup.VALIDATE("Data Exch. Def. Code",DataExchDef.Code);
      BankExportImportSetup.INSERT(TRUE);
    END;

    BEGIN
    END.
  }
}

