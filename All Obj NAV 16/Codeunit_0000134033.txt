OBJECT Codeunit 134033 ERM Vendor Date Compression
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
            IsInitialized := FALSE;
          END;

  }
  CODE
  {
    VAR
      LibraryERM@1002 : Codeunit 131300;
      LibraryFiscalYear@1006 : Codeunit 131302;
      LibraryRandom@1001 : Codeunit 130440;
      Assert@1005 : Codeunit 130000;
      IsInitialized@1000 : Boolean;
      AmountError@1004 : TextConst 'ENU="%1 must be %2 in \\%3, %4=%5."';
      CountError@1003 : TextConst 'ENU=No. of Entries must be %1 in %2.';

    [Test]
    [HandlerFunctions(ConfirmHandler)]
    PROCEDURE DateCompressionByWeek@4();
    VAR
      DateComprRegister@1001 : Record 87;
      VendorLedgerEntry@1000 : Record 25;
      VendorNo@1002 : Code[20];
      FirstPostingDate@1004 : Date;
      LastPostingDate@1005 : Date;
      NoOfEntries@1003 : Integer;
    BEGIN
      // Check No. of Vendor Ledger Entries, Amount after posting Payment and Invoices, and Running Date Compression by Week.

      // Create Vendor and Create multiple Invoice and Payment entries on General Journal Line and Post them. Date Compress by Week and
      // verify the Amount and No. of Entries in Vendor Ledger Entries. Take 1 Week Interval between posting dates to test date
      // compression on lower and upper bounds randomly.
      Initialize;
      VendorNo := CreateVendor;
      FirstPostingDate := LibraryFiscalYear.GetFirstPostingDate(TRUE);
      LastPostingDate := VendorDateCompression(VendorNo,DateComprRegister."Period Length"::Week,FirstPostingDate,'<1W>');
      NoOfEntries := ComputeNoOfWeek(FirstPostingDate,LastPostingDate);

      // Verify: Verify No. of Entries after Date Compression by Week.
      Assert.AreEqual(NoOfEntries,GetVendorLedgerEntries(VendorNo),STRSUBSTNO(CountError,NoOfEntries,VendorLedgerEntry.TABLECAPTION));
    END;

    [Test]
    [HandlerFunctions(ConfirmHandler)]
    PROCEDURE DateCompressionByMonth@5();
    VAR
      DateComprRegister@1002 : Record 87;
    BEGIN
      // Check No. of Vendor Ledger Entries, Amount after posting Payment and Invoices, and Running Date Compression by Month.

      // Date Compress by Month and verify No. of Entries and Amount in Vendor Ledger Entries. Pass 2 to fetch Entries for Month.
      // Take 10 Days interval between entries to test Date Compression on Upper and Lower boundaries randomly.
      Initialize;
      DateCompressionMonthYear(DateComprRegister."Period Length"::Month,'<10D>',2)
    END;

    [Test]
    [HandlerFunctions(ConfirmHandler)]
    PROCEDURE DateCompressionByYear@6();
    VAR
      DateComprRegister@1002 : Record 87;
    BEGIN
      // Check No. of Vendor Ledger Entries, Amount after posting Payment and Invoices, and Running Date Compression by Year.

      // Date Compress by Year and verify No. of Entries and Amount in Vendor Ledger Entries. Pass 3 to fetch Entries for Year.
      // Take 3 Months interval between entries to test Date Compression on Upper and Lower boundaries randomly.
      Initialize;
      DateCompressionMonthYear(DateComprRegister."Period Length"::Year,'<3M>',3)
    END;

    LOCAL PROCEDURE DateCompressionMonthYear@20(PeriodLength@1004 : Option;PeriodDifference@1005 : Text[10];PeriodOption@1006 : Integer);
    VAR
      VendorLedgerEntry@1002 : Record 25;
      VendorNo@1001 : Code[20];
      FirstPostingDate@1007 : Date;
      LastPostingDate@1000 : Date;
      NoOfEntries@1003 : Integer;
    BEGIN
      // Create Vendor. Create multiple Invoice and Payment entries on General Journal Line and Post them. Date Compress as per the
      // Period Length selected. Verify the Amount and No. of Entries in Vendor Ledger Entries.
      VendorNo := CreateVendor;
      FirstPostingDate := LibraryFiscalYear.GetFirstPostingDate(TRUE);
      LastPostingDate := VendorDateCompression(VendorNo,PeriodLength,FirstPostingDate,PeriodDifference);

      // Compute No. of Entries after Date Compression by Month. Pass 2 For Month, 3 for Year as option for computing entries.
      NoOfEntries := 1 + (DATE2DMY(LastPostingDate,PeriodOption) - DATE2DMY(FirstPostingDate,PeriodOption));

      // Verify: Verify No. of Entries after Date Compression for Month/Year selected.
      Assert.AreEqual(NoOfEntries,GetVendorLedgerEntries(VendorNo),STRSUBSTNO(CountError,NoOfEntries,VendorLedgerEntry.TABLECAPTION));
    END;

    LOCAL PROCEDURE Initialize@8();
    VAR
      LibraryERMCountryData@1000 : Codeunit 131305;
    BEGIN
      IF IsInitialized THEN
        EXIT;
      LibraryERMCountryData.CreateVATData;
      LibraryERMCountryData.UpdateGeneralPostingSetup;
      LibraryERMCountryData.UpdateLocalData;
      IsInitialized := TRUE;
      COMMIT;
    END;

    LOCAL PROCEDURE VendorDateCompression@10(VendorNo@1003 : Code[20];PeriodLength@1011 : Option;FirstPostingDate@1004 : Date;Period@1013 : Text[10]) : Date;
    VAR
      GenJournalLine@1000 : Record 81;
      NoOfLines@1001 : Integer;
      JnlLineAmount@1002 : Decimal;
    BEGIN
      // Setup: Close the Open Fiscal Year. Make Payment and Invoice entries for a Vendor from General Journal Line with same random
      // amount and Post them.
      LibraryFiscalYear.CloseFiscalYear;
      NoOfLines := LibraryRandom.RandInt(5);
      CreateDocumentLine(GenJournalLine,VendorNo,NoOfLines,FirstPostingDate,Period);
      JnlLineAmount := NoOfLines * GenJournalLine.Amount;
      LibraryERM.PostGeneralJnlLine(GenJournalLine);

      // Exercise: Run the Date Compress Vendor Ledger Batch Report.
      DateCompressForVendor(GenJournalLine,FirstPostingDate,PeriodLength);

      // Verify: Verify the Amount in Vendor Ledger Entry.
      VerifyVendorLedgerEntry(GenJournalLine."Account No.",JnlLineAmount);
      EXIT(GenJournalLine."Posting Date");
    END;

    LOCAL PROCEDURE CreateVendor@41() : Code[20];
    VAR
      Vendor@1000 : Record 23;
      LibraryPurchase@1002 : Codeunit 130512;
    BEGIN
      // Create Vendor with Application Method: Apply to Oldest.
      LibraryPurchase.CreateVendor(Vendor);
      Vendor.VALIDATE("Application Method",Vendor."Application Method"::"Apply to Oldest");
      Vendor.MODIFY(TRUE);
      EXIT(Vendor."No.");
    END;

    LOCAL PROCEDURE CreateDocumentLine@9(VAR GenJournalLine@1004 : Record 81;VendorNo@1000 : Code[20];NoOfLines@1003 : Integer;PostingDate@1009 : Date;Period@1007 : Text[10]);
    VAR
      GenJournalBatch@1005 : Record 232;
      Amount@1008 : Decimal;
      Counter@1002 : Integer;
    BEGIN
      // Create multiple Journal Lines for Payment and Invoice with similar Random Amount. Take different Posting Dates.
      LibraryERM.SelectGenJnlBatch(GenJournalBatch);
      LibraryERM.ClearGenJournalLines(GenJournalBatch);
      Amount := LibraryRandom.RandDec(5,2);
      FOR Counter := 1 TO NoOfLines DO BEGIN
        LibraryFiscalYear.CheckPostingDate(PostingDate);  // Check that no Posting Dates are outside Accounting Periods.
        CreateGeneralJournalLine(
          GenJournalLine,GenJournalBatch,VendorNo,GenJournalLine."Document Type"::Invoice,INCSTR(GenJournalLine."Document No."),
          -Amount,PostingDate);
        CreateGeneralJournalLine(
          GenJournalLine,GenJournalBatch,VendorNo,GenJournalLine."Document Type"::Payment,INCSTR(GenJournalLine."Document No."),
          Amount,PostingDate);
        PostingDate := CALCDATE('<' + Period + '>',PostingDate);
      END;
    END;

    LOCAL PROCEDURE CreateGeneralJournalLine@40(VAR GenJournalLine@1005 : Record 81;GenJournalBatch@1000 : Record 232;VendorNo@1002 : Code[20];DocumentType@1003 : Option;DocumentNo@1001 : Code[20];Amount@1004 : Decimal;PostingDate@1007 : Date);
    BEGIN
      LibraryERM.CreateGeneralJnlLine(
        GenJournalLine,GenJournalBatch."Journal Template Name",GenJournalBatch.Name,DocumentType,GenJournalLine."Account Type"::Vendor,
        VendorNo,Amount);
      GenJournalLine.VALIDATE("Posting Date",PostingDate);
      IF DocumentNo <> '' THEN BEGIN
        GenJournalLine.VALIDATE("Document No.",DocumentNo);
        GenJournalLine.VALIDATE("External Document No.",GenJournalLine."Document No.");
      END;
      GenJournalLine.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE DateCompressForVendor@12(GenJournalLine@1005 : Record 81;StartingDate@1003 : Date;PeriodLength@1002 : Option);
    VAR
      VendorLedgerEntry@1000 : Record 25;
      DateCompressVendorLedger@1001 : Report 398;
    BEGIN
      // Run the Date Compress Vendor Ledger Report with a closed Accounting Period.
      VendorLedgerEntry.SETRANGE("Vendor No.",GenJournalLine."Account No.");
      DateCompressVendorLedger.SETTABLEVIEW(VendorLedgerEntry);
      DateCompressVendorLedger.InitializeRequest(StartingDate,GenJournalLine."Posting Date",PeriodLength,'',FALSE,FALSE,FALSE,'');
      DateCompressVendorLedger.USEREQUESTPAGE(FALSE);
      DateCompressVendorLedger.RUN;
    END;

    LOCAL PROCEDURE GetVendorLedgerEntries@21(VendorNo@1001 : Code[20]) : Integer;
    VAR
      VendorLedgerEntry@1000 : Record 25;
    BEGIN
      VendorLedgerEntry.SETRANGE("Vendor No.",VendorNo);
      VendorLedgerEntry.SETRANGE("Document Type",VendorLedgerEntry."Document Type"::Payment);
      EXIT(VendorLedgerEntry.COUNT);
    END;

    LOCAL PROCEDURE ComputeNoOfWeek@14(FirstPostingDate@1001 : Date;LastPostingDate@1000 : Date) : Integer;
    VAR
      Date@1004 : Record 2000000007;
    BEGIN
      Date.SETRANGE("Period Type",Date."Period Type"::Week);
      Date.SETRANGE("Period Start",FindFirstDayOfPeriod(FirstPostingDate),FindFirstDayOfPeriod(LastPostingDate));
      IF (DATE2DMY(LastPostingDate,1) = 1) AND (FirstPostingDate <> LastPostingDate) THEN
        EXIT(Date.COUNT + 1);
      EXIT(Date.COUNT);
    END;

    LOCAL PROCEDURE FindFirstDayOfPeriod@11(PeriodStart@1000 : Date) StartingDate : Date;
    VAR
      Date@1001 : Record 2000000007;
    BEGIN
      Date.SETRANGE("Period Type",Date."Period Type"::Date);
      Date.SETRANGE("Period Start",PeriodStart);
      Date.FINDFIRST;
      StartingDate := CALCDATE('<-' + FORMAT(Date."Period No." - 1) + 'D>',Date."Period Start");
    END;

    LOCAL PROCEDURE VerifyVendorLedgerEntry@7(VendorNo@1001 : Code[20];JnlLinePaymentAmt@1004 : Decimal);
    VAR
      VendorLedgerEntry@1000 : Record 25;
      VendorLedgerEntryAmt@1002 : Decimal;
    BEGIN
      VendorLedgerEntry.SETRANGE("Vendor No.",VendorNo);
      VendorLedgerEntry.SETRANGE("Document Type",VendorLedgerEntry."Document Type"::Payment);
      VendorLedgerEntry.FINDSET;
      REPEAT
        VendorLedgerEntry.CALCFIELDS(Amount);
        VendorLedgerEntryAmt += VendorLedgerEntry.Amount;
      UNTIL VendorLedgerEntry.NEXT = 0;
      Assert.AreEqual(
        JnlLinePaymentAmt,VendorLedgerEntryAmt,STRSUBSTNO(AmountError,VendorLedgerEntry.FIELDCAPTION(Amount),
          JnlLinePaymentAmt,VendorLedgerEntry.TABLECAPTION,VendorLedgerEntry.FIELDCAPTION("Entry No."),VendorLedgerEntry."Entry No."));
    END;

    [ConfirmHandler]
    PROCEDURE ConfirmHandler@15(Question@1000 : Text[1024];VAR Reply@1001 : Boolean);
    BEGIN
      // Confirm Handler for Date Compression confirmation message.
      Reply := TRUE;
    END;

    BEGIN
    {
      // [FEATURE] [Date Compression] [Purchase]
      Test Date Compression functionality for Vendor with different Period Lengths.
        1. Test Date Compression for Period Length: Week after Posting Payment and Invoice entries for Vendor.
        2. Test Date Compression for Period Length: Month after Posting Payment and Invoice entries for Vendor.
        3. Test Date Compression for Period Length: Year after Posting Payment and Invoice entries for Vendor.

      Covers test cases:
            -------------------------------------------------------
            Test Function Name               Test TFS ID
            -------------------------------------------------------
            DateCompressionByWeek            125038, 127767
            DateCompressionByMonth           125038, 121597
            DateCompressionByYear            125038, 125039, 125040
    }
    END.
  }
}

