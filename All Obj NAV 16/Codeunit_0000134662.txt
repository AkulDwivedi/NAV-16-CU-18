OBJECT Codeunit 134662 Export Framework Scenario Test
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1000 : Codeunit 130000;
      LibraryPurchase@1006 : Codeunit 130512;
      LibrarySales@1002 : Codeunit 130509;
      LibraryPaymentExport@1001 : Codeunit 130100;
      ValueNotFoundErr@1003 : TextConst 'ENU=The value %1 is not found in the exported line %2.';
      LibraryTextFileValidation@1005 : Codeunit 131003;

    [Test]
    PROCEDURE ExportMultipleCustomersToFile@2();
    VAR
      Customer@1000 : Record 18;
      DataExch@1006 : Record 1220;
      DataExchMapping@1001 : Record 1224;
      ExportLauncher@1002 : Codeunit 1268;
      Source@1003 : Variant;
      FirstCustomerNo@1004 : Code[20];
      SecondCustomerNo@1005 : Code[20];
    BEGIN
      // Pre-Setup
      DefineExportFormat(DataExchMapping,DATABASE::Customer,Customer.FIELDNO("No."));

      // Setup
      FirstCustomerNo := LibrarySales.CreateCustomerNo;
      SecondCustomerNo := LibrarySales.CreateCustomerNo;
      Customer.SETFILTER("No.",'%1|%2',FirstCustomerNo,SecondCustomerNo);

      // Exercise
      Source := Customer;
      ExportLauncher.SetSourceRecord(Source);
      ExportLauncher.RUN(DataExchMapping);

      // Pre-Verify
      DataExch.SETRANGE("Data Exch. Def Code",DataExchMapping."Data Exch. Def Code");
      DataExch.SETRANGE("Data Exch. Line Def Code",DataExchMapping."Data Exch. Line Def Code");
      DataExch.FINDLAST;

      // Verify
      VerifyFileContent(DataExch,FirstCustomerNo,SecondCustomerNo);
    END;

    [Test]
    PROCEDURE ExportMultipleVendorsToFile@4();
    VAR
      DataExch@1006 : Record 1220;
      DataExchMapping@1001 : Record 1224;
      Vendor@1000 : Record 23;
      ExportLauncher@1002 : Codeunit 1268;
      Source@1003 : Variant;
      FirstVendorNo@1004 : Code[20];
      SecondVendorNo@1005 : Code[20];
    BEGIN
      // Pre-Setup
      DefineExportFormat(DataExchMapping,DATABASE::Vendor,Vendor.FIELDNO("No."));

      // Setup
      FirstVendorNo := LibraryPurchase.CreateVendorNo;
      SecondVendorNo := LibraryPurchase.CreateVendorNo;
      Vendor.SETFILTER("No.",'%1|%2',FirstVendorNo,SecondVendorNo);

      // Exercise
      Source := Vendor;
      ExportLauncher.SetSourceRecord(Source);
      ExportLauncher.RUN(DataExchMapping);

      // Pre-Verify
      DataExch.SETRANGE("Data Exch. Def Code",DataExchMapping."Data Exch. Def Code");
      DataExch.SETRANGE("Data Exch. Line Def Code",DataExchMapping."Data Exch. Line Def Code");
      DataExch.FINDLAST;

      // Verify
      VerifyFileContent(DataExch,FirstVendorNo,SecondVendorNo);
    END;

    LOCAL PROCEDURE DefineExportFormat@15(VAR DataExchMapping@1002 : Record 1224;TableID@1003 : Integer;FieldID@1004 : Integer);
    VAR
      DataExchDef@1000 : Record 1222;
      DataExchFieldMapping@1001 : Record 1225;
    BEGIN
      LibraryPaymentExport.CreateSimpleDataExchDefWithMapping2(
        DataExchDef,DataExchMapping,DataExchFieldMapping,TableID,FieldID);

      DataExchDef."Reading/Writing Codeunit" := CODEUNIT::"Exp. Writing Gen. Jnl.";
      DataExchDef."Reading/Writing XMLport" := XMLPORT::"Export Generic CSV";
      DataExchDef."Ext. Data Handling Codeunit" := CODEUNIT::"Save Data Exch. Blob Sample";
      DataExchDef.MODIFY;

      DataExchMapping."Mapping Codeunit" := CODEUNIT::"Export Mapping";
      DataExchMapping.MODIFY;
    END;

    LOCAL PROCEDURE VerifyFileContent@22(DataExch@1000 : Record 1220;FirstValue@1003 : Text;SecondValue@1004 : Text);
    VAR
      Line@1002 : Text;
    BEGIN
      Line := LibraryTextFileValidation.ReadLine(DataExch."File Name",1);
      Assert.AreNotEqual(0,STRPOS(Line,FirstValue),STRSUBSTNO(ValueNotFoundErr,FirstValue,Line));

      Line := LibraryTextFileValidation.ReadLine(DataExch."File Name",2);
      Assert.AreNotEqual(0,STRPOS(Line,SecondValue),STRSUBSTNO(ValueNotFoundErr,SecondValue,Line));
    END;

    BEGIN
    {
      // [FEATURE] [Data Exchange] [Export Launcher]
    }
    END.
  }
}

