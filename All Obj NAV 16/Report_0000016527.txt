OBJECT Report 16527 Update Revised TCS Filing
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Update Revised TCS Filing;
               ENN=Update Revised TCS Filing];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5365;    ;DataItem;                    ;
               DataItemTable=Table16517;
               DataItemTableView=SORTING(Entry No.)
                                 ORDER(Ascending)
                                 WHERE(Revised eTCS Generated=FILTER(Yes));
               OnPreDataItem=VAR
                               Form27EQEntry@1500000 : Record 16517;
                             BEGIN
                               IF Quarter2 = 0 THEN
                                 ERROR(Text16500,'Quarter');
                               IF FinYear = '' THEN
                                 ERROR(Text16500,'Financial Year');
                               IF FilingDate = 0D THEN
                                 ERROR(Text16500,'Filling Date');
                               IF "RRRNo." = '' THEN
                                 ERROR(Text16500,'Provisional Receipt No.');

                               SETRANGE("T.C.A.N. No.",TCANLoc);
                               SETRANGE("Financial Year",FinYear);
                               SETRANGE(Quarter,Quarter2);
                               SETRANGE("Revised eTCS Generated",TRUE);

                               Form27EQEntry.RESET;
                               Form27EQEntry.SETRANGE("Financial Year",FinYear);
                               IF NOT Form27EQEntry.FINDFIRST THEN
                                 ERROR(Text16501,'Financial Year');
                               Form27EQEntry.SETRANGE(Quarter,Quarter2);
                               IF NOT Form27EQEntry.FINDFIRST THEN
                                 ERROR(Text16501,'Quarter');
                               Form27EQEntry.SETRANGE("Revised eTCS Generated",TRUE);
                               IF NOT Form27EQEntry.FINDFIRST THEN
                                 ERROR(Text16502);
                               InsertIntoProvisionalReceipt;
                             END;

               OnAfterGetRecord=BEGIN
                                  Filed := TRUE;
                                  IF NOT Revised THEN
                                    "Filing Date" := FilingDate
                                  ELSE BEGIN
                                    "Filing Date of Revised eTCS" := FilingDate;
                                    Revised := FALSE;
                                    "Correction-C3" := FALSE;
                                    "Correction-C5" := FALSE;
                                    "Correction-C9" := FALSE;
                                  END;
                                  IF TCSChallanRegister.GET("Challan Register Entry No.") THEN BEGIN
                                    TCSChallanRegister.Filed := TRUE;
                                    TCSChallanRegister.Revised := FALSE;
                                    TCSChallanRegister."Filing Date" := FilingDate;
                                    TCSChallanRegister."Correction-C2" := FALSE;
                                    TCSChallanRegister."Correction-C3" := FALSE;
                                    TCSChallanRegister."Correction-C5" := FALSE;
                                    TCSChallanRegister."Correction-C9" := FALSE;
                                    TCSChallanRegister."Challan Updation Indicator" := 0;
                                  END;
                                  TCSChallanRegister.MODIFY;
                                  MODIFY;
                                END;

               OnPostDataItem=BEGIN
                                TCSBatchHeader.SETRANGE("Financial Year",FinYear);
                                TCSBatchHeader.SETRANGE(Quarter,Quarter2);
                                TCSBatchHeader.SETRANGE("Revised eTCS Generated",TRUE);
                                IF TCSBatchHeader.FINDFIRST THEN BEGIN
                                  TCSBatchHeader."Batch Updation Indicator" := 0;
                                  TCSBatchHeader.Filed := TRUE;
                                  TCSBatchHeader."Revised eTCS Filing Date" := FilingDate;
                                  TCSBatchHeader."Previous RRR No." := "RRRNo.";
                                  TCSBatchHeader."Correction-C1" := FALSE;
                                  TCSBatchHeader."Correction-C2" := FALSE;
                                  TCSBatchHeader."Correction-C3" := FALSE;
                                  TCSBatchHeader."Correction-Y" := FALSE;
                                  TCSBatchHeader.MODIFY;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1500009;2;Field     ;
                  CaptionML=[ENU=T.C.A.N. Location;
                             ENN=T.C.A.N. Location];
                  SourceExpr=TCANLoc;
                  TableRelation="T.C.A.N. No." }

      { 1500000;2;Field     ;
                  CaptionML=[ENU=Quarter;
                             ENN=Quarter];
                  SourceExpr=Quarter2 }

      { 1500007;2;Field     ;
                  CaptionML=[ENU=Financial Year;
                             ENN=Financial Year];
                  SourceExpr=FinYear;
                  OnLookup=VAR
                             TDSSetup@1500001 : Record 16501;
                             TDSAccountingPeriod@1500000 : Record 13724;
                           BEGIN
                             IF Quarter2 > 0 THEN BEGIN
                               TDSSetup.RESET;
                               TDSSetup.SETRANGE(Quarter,Quarter2);
                               IF TDSSetup.FINDFIRST THEN BEGIN
                                 TDSAccountingPeriod.GET(TDSSetup."No.");
                                 IF PAGE.RUNMODAL(13721,TDSAccountingPeriod) = ACTION::LookupOK THEN
                                   FinYear := TDSAccountingPeriod."Financial Year for eTDS/TCS";
                               END;
                             END;
                           END;
                            }

      { 1500003;2;Field     ;
                  CaptionML=[ENU=Filing Date;
                             ENN=Filing Date];
                  SourceExpr=FilingDate }

      { 1500005;2;Field     ;
                  CaptionML=[ENU=Provision Receipt No.;
                             ENN=Provision Receipt No.];
                  SourceExpr="RRRNo.";
                  OnLookup=BEGIN
                             TCSProvisionalRcptNo.RESET;
                             // TCSProvisionalRcptNo.SETRANGE(Quarter,Quarter2);  //Bugfix
                             // TCSProvisionalRcptNo.SETRANGE("Financial Year",FinYear);  //Bugfix
                             IF PAGE.RUNMODAL(0,TCSProvisionalRcptNo) = ACTION::LookupOK THEN
                               "RRRNo." := TCSProvisionalRcptNo."Provisional Receipt No.";
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TCSChallanRegister@1500011 : Record 16516;
      TCSBatchHeader@1500002 : Record 16515;
      TCSProvisionalRcptNo@1500001 : Record 16513;
      Quarter2@1500006 : ' ,Q1,Q2,Q3,Q4';
      FinYear@1500005 : Code[6];
      FilingDate@1500004 : Date;
      "RRRNo."@1500003 : Code[20];
      Text16500@1500009 : TextConst 'ENU=Please enter %1.;ENN=Please enter %1.';
      Text16501@1500008 : TextConst 'ENU=No records found for the %1.;ENN=No records found for the %1.';
      Text16502@1500007 : TextConst 'ENU=Revised records cannot be updated.;ENN=Revised records cannot be updated.';
      TCANLoc@1500010 : Code[10];

    PROCEDURE InsertIntoProvisionalReceipt@1500000();
    BEGIN
      IF "RRRNo." <> '' THEN BEGIN
        IF NOT TCSProvisionalRcptNo.GET("RRRNo.") THEN BEGIN
          TCSProvisionalRcptNo.INIT;
          TCSProvisionalRcptNo."Provisional Receipt No." := "RRRNo.";
          TCSProvisionalRcptNo.Quarter := Quarter2;
          TCSProvisionalRcptNo."Financial Year" := FinYear;
          TCSProvisionalRcptNo.INSERT;
        END;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

