OBJECT Report 16400 Create GST Year
{
  OBJECT-PROPERTIES
  {
    Date=23-02-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.48067;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create GST Year;
               ENN=Create GST Year];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF GSTAccountingPeriod1.GET(GSTFiscalYearStartDate) THEN BEGIN
                    GSTAccountingSubPeriod."Starting Date" := GSTFiscalYearStartDate;
                    GSTAccountingSubPeriod.TESTFIELD("Starting Date");
                    GSTAccountingPeriod := GSTFiscalYearStartDate;

                    IF GSTAccountingSubPeriod.FINDFIRST THEN BEGIN
                      GSTAccPeriodStartDate := GSTAccountingSubPeriod."Starting Date";
                      GSTAccPeriodDateLocked := GSTAccountingSubPeriod."Date Locked";
                      IF (GSTFiscalYearStartDate < GSTAccPeriodStartDate) AND GSTAccPeriodDateLocked THEN
                        IF NOT CONFIRM(CloseYrQst) THEN
                          EXIT;
                      IF GSTAccountingSubPeriod.FINDLAST THEN
                        GSTAccPeriodEndDate := GSTAccountingSubPeriod."Starting Date";
                    END ELSE
                      IF NOT CONFIRM(CreateFiscalQst) THEN
                        EXIT;
                    FOR i := 1 TO NoOfPeriods + 1 DO BEGIN
                      IF (GSTFiscalYearStartDate <= GSTAccPeriodStartDate) AND (i = NoOfPeriods + 1) THEN
                        EXIT;
                      IF GSTAccPeriodStartDate <> 0D THEN
                        IF (GSTFiscalYearStartDate >= GSTAccPeriodStartDate) AND (GSTFiscalYearStartDate < GSTAccPeriodEndDate) THEN
                          ERROR(ExistingFiscalErr);
                      GSTAccountingSubPeriod.INIT;
                      GSTAccountingSubPeriod."Accounting Period Start Date" := GSTAccountingPeriod;
                      GSTAccountingSubPeriod.VALIDATE("Starting Date",GSTFiscalYearStartDate);
                      IF (i = 1) OR (i = NoOfPeriods + 1) THEN
                        GSTAccountingSubPeriod."New Financial Year" := TRUE;
                      IF (GSTAccPeriodStartDate = 0D) AND (i = 1) THEN
                        GSTAccountingSubPeriod."Date Locked" := TRUE;
                      IF (GSTAccountingSubPeriod."Starting Date" < GSTAccPeriodStartDate) AND GSTAccPeriodDateLocked THEN BEGIN
                        GSTAccountingSubPeriod.Closed := TRUE;
                        GSTAccountingSubPeriod."Date Locked" := TRUE;
                        GSTAccountingPeriod1."Year Closed" := TRUE;
                        GSTAccountingPeriod1.MODIFY;
                      END;
                      IF NOT GSTAccountingSubPeriod.FIND THEN
                        GSTAccountingSubPeriod.INSERT;
                      GSTFiscalYearStartDate := CALCDATE(PeriodLength,GSTFiscalYearStartDate);
                    END;
                  END ELSE
                    ERROR(GSTStartDateErr);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF NoOfPeriods = 0 THEN BEGIN
                     NoOfPeriods := 12;
                     EVALUATE(PeriodLength,'<1M>');
                   END;
                 END;

    }
    CONTROLS
    {
      { 1500000;0;Container ;
                  ContainerType=ContentArea }

      { 1500001;1;Group     ;
                  CaptionML=[ENU=Group;
                             ENN=Group] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ENN=Starting Date];
                  SourceExpr=GSTFiscalYearStartDate }

      { 2   ;2   ;Field     ;
                  Name=NoOfPeriods;
                  CaptionML=[ENU=No. of Periods;
                             ENN=No. of Periods];
                  SourceExpr=NoOfPeriods }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[ENU=Period Length;
                             ENN=Period Length];
                  SourceExpr=PeriodLength }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GSTAccountingSubPeriod@1500008 : Record 16402;
      GSTAccountingPeriod1@1500015 : Record 16401;
      NoOfPeriods@1500007 : Integer;
      PeriodLength@1500006 : DateFormula;
      GSTFiscalYearStartDate@1500005 : Date;
      GSTAccountingPeriod@1500014 : Date;
      GSTAccPeriodStartDate@1500003 : Date;
      GSTAccPeriodEndDate@1500002 : Date;
      GSTAccPeriodDateLocked@1500001 : Boolean;
      i@1500000 : Integer;
      CloseYrQst@1500013 : TextConst 'ENU=The new GST fiscal year begins before an existing GST fiscal year, so the new year will be closed automatically. Do you want to create and close the GST fiscal year?;ENN=The new GST fiscal year begins before an existing GST fiscal year, so the new year will be closed automatically. Do you want to create and close the GST fiscal year?';
      CreateFiscalQst@1500011 : TextConst 'ENU=After you create the new GST fiscal year, you cannot change its starting date. Do you want to create the GST fiscal year?;ENN=After you create the new GST fiscal year, you cannot change its starting date. Do you want to create the GST fiscal year?';
      ExistingFiscalErr@1500009 : TextConst 'ENU=It is only possible to create new GST fiscal years before or after the existing ones.;ENN=It is only possible to create new GST fiscal years before or after the existing ones.';
      GSTStartDateErr@1500016 : TextConst 'ENU=You must create start date first for GST.;ENN=You must create start date first for GST.';

    PROCEDURE SetFiscalStartDate@1500000(StartDate@1500000 : Date);
    BEGIN
      GSTFiscalYearStartDate := StartDate;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

