OBJECT Table 16596 Capitalization
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               CapitalizeCWIPHeader@1500000 : Record 16597;
             BEGIN
               CapitalizeCWIPHeader.GET("Document No.");
               "Posting Date" := CapitalizeCWIPHeader."Posting Date";
             END;

    OnDelete=BEGIN
               DeleteCWIPFADetail(TRUE);
             END;

    CaptionML=[ENU=Capitalization;
               ENN=Capitalization];
    DrillDownPageID=Page16452;
  }
  FIELDS
  {
    { 1   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ENN=Document No.] }
    { 2   ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Job No.;
                                                              ENN=Job No.] }
    { 3   ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   CaptionML=[ENU=Job Task No.;
                                                              ENN=Job Task No.] }
    { 4   ;   ;FA Code             ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF ("FA Code" <> xRec."FA Code") AND (xRec."FA Code" <> '') THEN
                                                                  DeleteCWIPFADetail(FALSE);
                                                              END;

                                                   OnLookup=VAR
                                                              CapitalizeCWIPHeader@1500000 : Record 16597;
                                                              FixedAsset@1500001 : Record 5600;
                                                              FADepreciationBook@1500002 : Record 5612;
                                                            BEGIN
                                                              CapitalizeCWIPHeader.GET("Document No.");
                                                              CapitalizeCWIPHeader.TESTFIELD("Depreciation Book");
                                                              FADepreciationBook.RESET;
                                                              FADepreciationBook.SETRANGE("Depreciation Book Code",CapitalizeCWIPHeader."Depreciation Book");
                                                              IF FADepreciationBook.FINDSET THEN
                                                                REPEAT
                                                                  IF FixedAsset.GET(FADepreciationBook."FA No.") THEN
                                                                    FixedAsset.MARK(TRUE);
                                                                UNTIL FADepreciationBook.NEXT = 0;
                                                              FixedAsset.MARKEDONLY(TRUE);
                                                              FixedAsset.FINDFIRST;
                                                              IF PAGE.RUNMODAL(PAGE::"Fixed Asset List",FixedAsset) = ACTION::LookupOK THEN
                                                                "FA Code" := FixedAsset."No.";
                                                              VALIDATE("FA Code");
                                                            END;

                                                   CaptionML=[ENU=FA Code;
                                                              ENN=FA Code];
                                                   NotBlank=Yes }
    { 5   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENN=Line No.];
                                                   Editable=No }
    { 6   ;   ;Allocation % (Labor);Decimal       ;OnValidate=BEGIN
                                                                GetCapitalizeCWIPLine;
                                                                "Allocation Amt (Labor)" := GetAmount("Allocation % (Labor)",CapitalizeCWIPLine."Amt to Capitalize (Labor)");
                                                              END;

                                                   CaptionML=[ENU=Allocation % (Labor);
                                                              ENN=Allocation % (Labor)];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 7   ;   ;Allocation Amt (Labor);Decimal     ;OnValidate=BEGIN
                                                                GetCapitalizeCWIPLine;
                                                                "Allocation % (Labor)" := GetPercent("Allocation Amt (Labor)",CapitalizeCWIPLine."Amt to Capitalize (Labor)");
                                                              END;

                                                   CaptionML=[ENU=Allocation Amt (Labor);
                                                              ENN=Allocation Amt (Labor)];
                                                   AutoFormatType=1 }
    { 8   ;   ;Allocation % (Material);Decimal    ;OnValidate=BEGIN
                                                                GetCapitalizeCWIPLine;
                                                                "Allocation Amt (Material)" := GetAmount("Allocation % (Material)",CapitalizeCWIPLine."Amt to Capitalize (Material)");
                                                              END;

                                                   CaptionML=[ENU=Allocation % (Material);
                                                              ENN=Allocation % (Material)];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 9   ;   ;Allocation Amt (Material);Decimal  ;OnValidate=BEGIN
                                                                GetCapitalizeCWIPLine;
                                                                "Allocation % (Material)" := GetPercent("Allocation Amt (Material)",CapitalizeCWIPLine."Amt to Capitalize (Material)");
                                                              END;

                                                   CaptionML=[ENU=Allocation Amt (Material);
                                                              ENN=Allocation Amt (Material)];
                                                   AutoFormatType=1 }
    { 10  ;   ;Allocation % (Overheads);Decimal   ;OnValidate=BEGIN
                                                                GetCapitalizeCWIPLine;
                                                                "Allocation Amt (Overheads)" := GetAmount("Allocation % (Overheads)",CapitalizeCWIPLine."Amt to Capitalize (Overheads)");
                                                              END;

                                                   CaptionML=[ENU=Allocation % (Overheads);
                                                              ENN=Allocation % (Overheads)];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 11  ;   ;Allocation Amt (Overheads);Decimal ;OnValidate=BEGIN
                                                                GetCapitalizeCWIPLine;
                                                                "Allocation % (Overheads)" := GetPercent("Allocation Amt (Overheads)",CapitalizeCWIPLine."Amt to Capitalize (Overheads)");
                                                              END;

                                                   CaptionML=[ENU=Allocation Amt (Overheads);
                                                              ENN=Allocation Amt (Overheads)];
                                                   AutoFormatType=1 }
    { 12  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ENN=Posting Date];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Document No.,Job No.,Job Task No.,Line No.,FA Code;
                                                   SumIndexFields=Allocation Amt (Labor),Allocation Amt (Material),Allocation Amt (Overheads);
                                                   Clustered=Yes }
    {    ;Document No.,Job No.,FA Code            ;SumIndexFields=Allocation Amt (Labor),Allocation Amt (Material),Allocation Amt (Overheads) }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CapitalizeCWIPLine@1500000 : Record 16598;
      Text001@1500001 : TextConst 'ENU="Allocation Amt=%1 cannot be greater than Amt to Capitalize=%2.";ENN="Allocation Amt=%1 cannot be greater than Amt to Capitalize=%2."';
      Capitalization1@1500002 : Record 16596;
      CWIPFADetails@1500005 : Record 13703;

    PROCEDURE GetCapitalizeCWIPLine@1500000();
    BEGIN
      CapitalizeCWIPLine.GET("Document No.","Job No.","Job Task No.","Line No.");
    END;

    PROCEDURE GetPercent@1500001(AllocationAmount@1500000 : Decimal;AmtToCapitalize@1500001 : Decimal) "Allocation%" : Decimal;
    BEGIN
      IF AllocationAmount > AmtToCapitalize THEN
        ERROR(Text001,AllocationAmount,AmtToCapitalize);
      EXIT((AllocationAmount / AmtToCapitalize) * 100);
    END;

    PROCEDURE GetAmount@1500011(AllocationPercentage@1500000 : Decimal;AmtToCapitalize@1500001 : Decimal) AllocationAmt : Decimal;
    BEGIN
      EXIT(ROUND((AllocationPercentage * AmtToCapitalize) / 100));
    END;

    PROCEDURE TotalAllocationAmt@1500002(AllFixedAssets@1500000 : Boolean;Type@1500001 : 'Labor,Material,Overheads') : Decimal;
    BEGIN
      Capitalization1.RESET;
      Capitalization1.SETRANGE("Document No.","Document No.");
      Capitalization1.SETRANGE("Job No.","Job No.");
      Capitalization1.SETRANGE("Job Task No.","Job Task No.");
      Capitalization1.SETRANGE("Line No.","Line No.");
      IF NOT AllFixedAssets THEN
        Capitalization1.SETRANGE("FA Code","FA Code");
      IF Type = Type::Labor THEN BEGIN
        Capitalization1.CALCSUMS("Allocation Amt (Labor)");
        EXIT(Capitalization1."Allocation Amt (Labor)");
      END;
      IF Type = Type::Material THEN BEGIN
        Capitalization1.CALCSUMS("Allocation Amt (Material)");
        EXIT(Capitalization1."Allocation Amt (Material)");
      END;
      Capitalization1.CALCSUMS("Allocation Amt (Overheads)");
      EXIT(Capitalization1."Allocation Amt (Overheads)");
    END;

    PROCEDURE DeleteCWIPFADetail@1500007(CurrRec@1500000 : Boolean);
    BEGIN
      CWIPFADetails.RESET;
      CWIPFADetails.SETRANGE("Document No.","Document No.");
      CWIPFADetails.SETRANGE("Job No.","Job No.");
      IF CurrRec THEN
        CWIPFADetails.SETRANGE("FA Code","FA Code")
      ELSE
        CWIPFADetails.SETRANGE("FA Code",xRec."FA Code");
      IF CWIPFADetails.COUNT <= 1 THEN
        CWIPFADetails.DELETEALL;
    END;

    BEGIN
    END.
  }
}

