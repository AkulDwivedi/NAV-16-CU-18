OBJECT Codeunit 134913 ERM Purch Inv Disc With FA
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
            IsInitialized := FALSE;
          END;

  }
  CODE
  {
    VAR
      LibraryPurchase@1003 : Codeunit 130512;
      LibraryRandom@1001 : Codeunit 130440;
      LibraryERM@1002 : Codeunit 131300;
      LibraryFixedAsset@1005 : Codeunit 131330;
      IsInitialized@1000 : Boolean;
      AmountError@1004 : TextConst 'ENU=Amount must be %1 in %2.';

    [Test]
    PROCEDURE PurchaseInvoiceDiscountFA@11();
    VAR
      PurchaseHeader@1000 : Record 38;
      PurchaseLine@1004 : Record 39;
      PurchFADiscAccount@1002 : Code[20];
      InvoiceAmount@1003 : Decimal;
      OldSubtractDiscinPurchInv@1005 : Boolean;
    BEGIN
      // Create and Post Purchase Invoice and Update General Posting setup for Purchase Invoice Discount Amount.

      // Setup: Create Fixed Asset, Purchase Invoice and Update General Posting Setup.
      Initialize;
      UpdateDepreciationBook(OldSubtractDiscinPurchInv,TRUE);
      InvoiceAmount := CreatePurchaseInvoice(PurchaseHeader,PurchaseLine);
      PurchFADiscAccount := UpdateGeneralPostingSetup(PurchaseLine);

      // Exercise: Post Purchase Invoice.
      LibraryPurchase.PostPurchaseDocument(PurchaseHeader,TRUE,TRUE);

      // Verify: Verify GL Entry for Purchase Invoice Discount Amount.
      VerifyGLEntry(PurchFADiscAccount,InvoiceAmount,PurchaseLine."Document No.");

      // Tear Down: Reset the initial value of Subtract Discount in Depreciation Book.
      UpdateDepreciationBook(OldSubtractDiscinPurchInv,OldSubtractDiscinPurchInv);
      LibraryFixedAsset.VerifyLastFARegisterGLRegisterOneToOneRelation; // TFS 376879
    END;

    LOCAL PROCEDURE Initialize@8();
    VAR
      LibraryERMCountryData@1001 : Codeunit 131305;
    BEGIN
      IF IsInitialized THEN
        EXIT;
      LibraryERMCountryData.CreateVATData;
      LibraryERMCountryData.CreateGeneralPostingSetupData;
      LibraryERMCountryData.UpdatePurchasesPayablesSetup;
      LibraryERMCountryData.UpdateGeneralLedgerSetup;
      LibraryERMCountryData.UpdateGeneralPostingSetup;
      IsInitialized := TRUE;
      COMMIT;
    END;

    LOCAL PROCEDURE CreateFixedAsset@9() : Code[20];
    VAR
      FixedAsset@1002 : Record 5600;
      FADepreciationBook@1001 : Record 5612;
      FAPostingGroup@1000 : Record 5606;
    BEGIN
      FAPostingGroup.FINDFIRST;
      LibraryFixedAsset.CreateFixedAsset(FixedAsset);
      LibraryFixedAsset.CreateFADepreciationBook(FADepreciationBook,FixedAsset."No.",LibraryFixedAsset.GetDefaultDeprBook);
      FADepreciationBook.VALIDATE("FA Posting Group",FAPostingGroup.Code);
      FADepreciationBook.MODIFY(TRUE);
      EXIT(FixedAsset."No.");
    END;

    LOCAL PROCEDURE CreatePurchaseInvoice@10(VAR PurchaseHeader@1000 : Record 38;VAR PurchaseLine@1004 : Record 39) : Decimal;
    BEGIN
      LibraryPurchase.CreatePurchHeader(PurchaseHeader,PurchaseHeader."Document Type"::Invoice,'');

      // Using Random values for calculation and value is not important for Test Case.
      LibraryPurchase.CreatePurchaseLine(
        PurchaseLine,PurchaseHeader,PurchaseLine.Type::"Fixed Asset",CreateFixedAsset,LibraryRandom.RandInt(5));
      PurchaseLine.VALIDATE("Direct Unit Cost",300 * LibraryRandom.RandDec(10,2));
      PurchaseLine.VALIDATE("Line Discount %",LibraryRandom.RandInt(15));
      PurchaseLine.MODIFY(TRUE);
      EXIT(PurchaseLine.Quantity * PurchaseLine."Direct Unit Cost" * PurchaseLine."Line Discount %" / 100);
    END;

    LOCAL PROCEDURE UpdateGeneralPostingSetup@4(PurchaseLine@1000 : Record 39) : Code[20];
    VAR
      GeneralPostingSetup@1004 : Record 252;
    BEGIN
      GeneralPostingSetup.GET(PurchaseLine."Gen. Bus. Posting Group",PurchaseLine."Gen. Prod. Posting Group");
      GeneralPostingSetup.VALIDATE("Purch. FA Disc. Account",LibraryERM.CreateGLAccountNo);
      GeneralPostingSetup.MODIFY(TRUE);
      EXIT(GeneralPostingSetup."Purch. FA Disc. Account");
    END;

    LOCAL PROCEDURE UpdateDepreciationBook@30(VAR OldSubtractDiscinPurchInv@1001 : Boolean;SubtractDiscinPurchInv@1002 : Boolean);
    VAR
      DepreciationBook@1000 : Record 5611;
    BEGIN
      DepreciationBook.GET(LibraryFixedAsset.GetDefaultDeprBook);
      OldSubtractDiscinPurchInv := DepreciationBook."Subtract Disc. in Purch. Inv.";
      DepreciationBook.VALIDATE("Subtract Disc. in Purch. Inv.",SubtractDiscinPurchInv);
      DepreciationBook.MODIFY(TRUE);
    END;

    LOCAL PROCEDURE VerifyGLEntry@2(GLAccountNo@1001 : Code[20];Amount@1002 : Decimal;PreAssignedNo@1006 : Code[20]);
    VAR
      GLEntry@1000 : Record 17;
      PurchInvHeader@1005 : Record 122;
      GeneralLedgerSetup@1004 : Record 98;
      Assert@1003 : Codeunit 130000;
    BEGIN
      GeneralLedgerSetup.GET;
      PurchInvHeader.SETRANGE("Pre-Assigned No.",PreAssignedNo);
      PurchInvHeader.FINDFIRST;
      GLEntry.SETRANGE("Document No.",PurchInvHeader."No.");
      GLEntry.SETRANGE("G/L Account No.",GLAccountNo);
      GLEntry.FINDFIRST;
      Assert.AreNearlyEqual(
        Amount,GLEntry.Amount,GeneralLedgerSetup."Amount Rounding Precision",STRSUBSTNO(AmountError,Amount,GLEntry.TABLECAPTION));
    END;

    BEGIN
    {
      // [FEATURE] [Fixed Asset] [Invoice Discount] [Purchase]
      Unsupported version tags:
      NA: Unable to Execute
      ES: Unable to Execute

      Test GL Entry for Purchase Invoice Discount Amount with Fixed Asset.
        1. Verify GL Entry for Purchase Invoice Discount Amount with Fixed Asset.

        Covers Test Cases
        ---------------------------------------------------------------------------
        Test Function Name                            TFS ID
        ---------------------------------------------------------------------------
        PurchaseInvoiceDiscountFA    141519 to 141522,141525,141528,141529
    }
    END.
  }
}

