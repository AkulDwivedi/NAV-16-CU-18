OBJECT Codeunit 113 Vend. Entry-Edit
{
  OBJECT-PROPERTIES
  {
    Date=29-06-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.48822,NAVIN9.00.00.48822;
  }
  PROPERTIES
  {
    TableNo=25;
    Permissions=TableData 25=imd,
                TableData 380=m;
    OnRun=VAR
            VendorLedgerEntry1@1500004 : Record 25;
            GSTGroupRemAmount@1500002 : Decimal;
            InvoiceGSTAmount@1500001 : Decimal;
            AppliedGSTAmount@1500000 : Decimal;
            InvoiceBase@1500003 : Decimal;
          BEGIN
            VendLedgEntry := Rec;
            VendLedgEntry.LOCKTABLE;
            VendLedgEntry.FIND;
            VendLedgEntry."On Hold" := "On Hold";
            IF VendLedgEntry.Open THEN BEGIN
              VendLedgEntry."Due Date" := "Due Date";
              DtldVendLedgEntry.SETCURRENTKEY("Vendor Ledger Entry No.");
              DtldVendLedgEntry.SETRANGE("Vendor Ledger Entry No.",VendLedgEntry."Entry No.");
              DtldVendLedgEntry.MODIFYALL("Initial Entry Due Date","Due Date");
              VendLedgEntry."Pmt. Discount Date" := "Pmt. Discount Date";
              VendLedgEntry."Applies-to ID" := "Applies-to ID";
              VendLedgEntry.VALIDATE("Payment Method Code","Payment Method Code");
              VendLedgEntry.VALIDATE("Remaining Pmt. Disc. Possible","Remaining Pmt. Disc. Possible");
              VendLedgEntry."Pmt. Disc. Tolerance Date" := "Pmt. Disc. Tolerance Date";
              VendLedgEntry.VALIDATE("Max. Payment Tolerance","Max. Payment Tolerance");
              VendLedgEntry.VALIDATE("Accepted Payment Tolerance","Accepted Payment Tolerance");
              VendLedgEntry.VALIDATE("Accepted Pmt. Disc. Tolerance","Accepted Pmt. Disc. Tolerance");
              VendLedgEntry.CALCFIELDS("Remaining Amount");
              IF VendLedgEntry."GST on Advance Payment" AND OnlineApplication AND
                (("Amount to Apply" <> 0)) AND (VendLedgEntry."Remaining Amount" <> 0) THEN
                IF GSTApplicationManagement.FillGSTApplicationBufferPurchase(PurchaseHeader,VendLedgEntry) THEN BEGIN
                    GSTGroupRemAmount := GSTApplicationManagement.GetApplicationRemainingAmount(
                      TransactionType1::Purchase,PurchaseHeader."Document Type",PurchaseHeader."No.",
                      VendLedgEntry."Vendor No.",VendLedgEntry."GST Group Code","Amount to Apply",
                      VendLedgEntry."Remaining Amount",VendLedgEntry."Entry No.",FALSE,InvoiceGSTAmount,AppliedGSTAmount,InvoiceBase);
                     IF "Amount to Apply" = 0 THEN
                       "Amount to Apply" := GSTGroupRemAmount;
                     GSTApplicationManagement.CheckGroupAmount(
                       PurchaseHeader."Document Type",PurchaseHeader."No.","Amount to Apply",InvoiceBase,
                       VendLedgEntry."Remaining Amount",VendLedgEntry."GST Group Code");
                     GSTApplicationManagement.CheckAppliedAmount(
                      TransactionType1::Purchase,VendLedgEntry."Entry No.",VendLedgEntry."Vendor No.",
                      InvoiceBase,"Amount to Apply",VendLedgEntry."GST Group Code",TRUE);
                END;
              IF IsGSTOnAdvancePayment(VendLedgEntry) AND (NOT OnlineApplication) AND
                (ApplyingVendorLedgerEntry."Entry No." > 1)  AND ("Amount to Apply" <> 0)THEN BEGIN
                GSTApplicationManagement.ApplyCurrencyFactorInvoice(TRUE);
                GSTApplicationManagement.GetPurchaseInvoiceAmountOffline(
                  VendLedgEntry,ApplyingVendorLedgerEntry,
                  ApplyingVendorLedgerEntry."Total TDS Including SHE CESS");
                GSTGroupRemAmount :=
                  GSTApplicationManagement.GetApplicationRemainingAmount(
                    TransactionType1::Purchase,ApplyingVendorLedgerEntry."Document Type",
                    ApplyingVendorLedgerEntry."Document No.",VendLedgEntry."Vendor No.",
                    VendLedgEntry."GST Group Code","Amount to Apply",VendLedgEntry."Remaining Amount",
                    VendLedgEntry."Entry No.",FALSE,InvoiceGSTAmount,AppliedGSTAmount,InvoiceBase);
                  IF "Amount to Apply" = 0 THEN
                    "Amount to Apply" := GSTGroupRemAmount;
                  GSTApplicationManagement.CheckGroupAmount(
                    ApplyingVendorLedgerEntry."Document Type",
                    ApplyingVendorLedgerEntry."Document No.","Amount to Apply",InvoiceBase,
                    VendLedgEntry."Remaining Amount",VendLedgEntry."GST Group Code");
                  GSTApplicationManagement.CheckAppliedAmount(
                    TransactionType1::Purchase,VendLedgEntry."Entry No.",VendLedgEntry."Vendor No.",
                    InvoiceBase,"Amount to Apply",VendLedgEntry."GST Group Code",TRUE);
              END;
              ApplyingVendorLedgerEntry.CALCFIELDS("Remaining Amount");
              IF IsGSTOnAdvancePayment(ApplyingVendorLedgerEntry) THEN BEGIN
                GSTApplicationManagement.ApplyCurrencyFactorInvoice(TRUE);
                GSTApplicationManagement.GetPurchaseInvoiceAmountWithPaymentOffline(
                  VendLedgEntry,ApplyingVendorLedgerEntry,ApplyingVendorLedgerEntry."Total TDS Including SHE CESS");
                GSTGroupRemAmount :=
                  GSTApplicationManagement.GetApplicationRemainingAmount(
                    TransactionType1::Purchase,VendLedgEntry."Document Type",
                    VendLedgEntry."Document No.",ApplyingVendorLedgerEntry."Vendor No.",
                    ApplyingVendorLedgerEntry."GST Group Code","Amount to Apply",ApplyingVendorLedgerEntry."Remaining Amount",
                    ApplyingVendorLedgerEntry."Entry No.",FALSE,
                    InvoiceGSTAmount,AppliedGSTAmount,InvoiceBase);
                    IF "Amount to Apply" = 0 THEN
                      "Amount to Apply" := GSTGroupRemAmount * -1;
                  GSTApplicationManagement.CheckGroupAmount(
                    VendLedgEntry."Document Type",VendLedgEntry."Document No.","Amount to Apply",InvoiceBase,
                    ApplyingVendorLedgerEntry."Remaining Amount",ApplyingVendorLedgerEntry."GST Group Code");
              END;
              CheckPurchCreditMemoAppOffline(Rec,ApplyingVendorLedgerEntry);
              VendLedgEntry.VALIDATE("Amount to Apply","Amount to Apply");
              VendLedgEntry.VALIDATE("Applying Entry","Applying Entry");
              VendLedgEntry.VALIDATE("Applies-to Ext. Doc. No.","Applies-to Ext. Doc. No.");
              VendLedgEntry.VALIDATE("Message to Recipient","Message to Recipient");
            END;
            VendLedgEntry.VALIDATE("Exported to Payment File","Exported to Payment File");
            VendLedgEntry.VALIDATE("Creditor No.","Creditor No.");
            VendLedgEntry.VALIDATE("Payment Reference","Payment Reference");
            VendLedgEntry.MODIFY;
            Rec := VendLedgEntry;
          END;

  }
  CODE
  {
    VAR
      VendLedgEntry@1000 : Record 25;
      DtldVendLedgEntry@1001 : Record 380;
      PurchaseHeader@1500004 : Record 38;
      ApplyingVendorLedgerEntry@1500003 : Record 25;
      GSTApplicationManagement@1500002 : Codeunit 16403;
      OnlineApplication@1500001 : Boolean;
      TransactionType1@1500000 : 'Purchase,Sales';

    PROCEDURE SetGSTType@1500000(VAR PurchaseHeader2@1500002 : Record 38;OnlineApplicationL@1500000 : Boolean);
    BEGIN
      PurchaseHeader := PurchaseHeader2;
      OnlineApplication := OnlineApplicationL;
    END;

    PROCEDURE SetGSTTypeDirect@1500001(VAR VendorLedgerEntry@1500002 : Record 25;OnlineApplicationL@1500000 : Boolean);
    BEGIN
      ApplyingVendorLedgerEntry := VendorLedgerEntry;
      OnlineApplication := OnlineApplicationL;
    END;

    LOCAL PROCEDURE IsGSTOnAdvancePayment@1500002(VAR VendorLedgerEntry@1500000 : Record 25) : Boolean;
    BEGIN
      WITH VendorLedgerEntry DO
        EXIT(
          "GST on Advance Payment" AND ("Document Type" = "Document Type"::Payment) AND
          ("Remaining Amount" <> 0));
    END;

    LOCAL PROCEDURE CheckPurchCreditMemoAppOffline@1500022(VendorLedgerEntry@1500001 : Record 25;ApplyingVendorLedgerEntry@1500000 : Record 25);
    VAR
      VendorLedgerEntry1@1170000000 : Record 25;
      GSTApplicationManagement@1500002 : Codeunit 16403;
    BEGIN
      WITH VendorLedgerEntry1 DO BEGIN
        COPY(VendLedgEntry);
        SETCURRENTKEY("Vendor No.","Applies-to ID","Document No.");
        SETRANGE("Vendor No.",ApplyingVendorLedgerEntry."Vendor No.");
        SETRANGE("Applies-to ID",ApplyingVendorLedgerEntry."Applies-to ID");
        SETFILTER("Document No.",'<>%1',ApplyingVendorLedgerEntry."Document No.");
        IF FINDFIRST THEN BEGIN
          IF (ApplyingVendorLedgerEntry."Entry No." > 1) AND ( "Applies-to ID" <> '') THEN
            IF (("Document Type" = "Document Type"::"Credit Memo") AND
              (ApplyingVendorLedgerEntry."Document Type" = ApplyingVendorLedgerEntry."Document Type"::Invoice)) OR
              (("Document Type" = "Document Type"::Invoice) AND
              (ApplyingVendorLedgerEntry."Document Type" = ApplyingVendorLedgerEntry."Document Type"::"Credit Memo"))THEN
                GSTApplicationManagement.CheckGSTPurchCrMemoValidationsOffline(VendorLedgerEntry,
                  ApplyingVendorLedgerEntry,COUNT);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

