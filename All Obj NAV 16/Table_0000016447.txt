OBJECT Table 16447 Location Distribution
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.49326;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               ComponentDistribution.RESET;
               ComponentDistribution.SETRANGE("Distribution No.","Distribution No.");
               ComponentDistribution.SETRANGE("From GST Component Code","GST Component Code");
               ComponentDistribution.SETRANGE("From GST Reg. No.","From GST Reg. No.");
               ComponentDistribution.SETRANGE("To GST Reg. No.","To GST Reg. No.");
               ComponentDistribution.DELETEALL;
             END;

    CaptionML=[ENU=Location Distribution;
               ENN=Location Distribution];
  }
  FIELDS
  {
    { 1   ;   ;Distribution No.    ;Code20        ;CaptionML=[ENU=Distribution No.;
                                                              ENN=Distribution No.];
                                                   Editable=No }
    { 2   ;   ;GST Component Code  ;Code10        ;CaptionML=[ENU=GST Component Code;
                                                              ENN=GST Component Code];
                                                   Editable=No }
    { 3   ;   ;From GST Reg. No.   ;Code15        ;CaptionML=[ENU=From GST Reg. No.;
                                                              ENN=From GST Reg. No.];
                                                   Editable=No }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENN=Line No.] }
    { 5   ;   ;Month               ;Integer       ;CaptionML=[ENU=Month;
                                                              ENN=Month];
                                                   Editable=No }
    { 6   ;   ;Year                ;Integer       ;CaptionML=[ENU=Year;
                                                              ENN=Year];
                                                   Editable=No }
    { 7   ;   ;GST Base Amount     ;Decimal       ;CaptionML=[ENU=GST Base Amount;
                                                              ENN=GST Base Amount];
                                                   Editable=No }
    { 8   ;   ;GST Amount          ;Decimal       ;CaptionML=[ENU=GST Amount;
                                                              ENN=GST Amount];
                                                   Editable=No }
    { 9   ;   ;To GST Reg. No.     ;Code15        ;TableRelation="GST Registration Nos." WHERE (Input Service Distributor=FILTER(No));
                                                   OnValidate=BEGIN
                                                                TESTFIELD("Distribution %",0);
                                                                IF "From GST Reg. No." = "To GST Reg. No." THEN
                                                                  ERROR(SameGSTErr);
                                                                IF "To GST Reg. No." <> '' THEN BEGIN
                                                                  ValidateSameToGSTRegNo;
                                                                  LocationDistribution.RESET;
                                                                  LocationDistribution.SETRANGE("Distribution No.","Distribution No.");
                                                                  LocationDistribution.SETRANGE("GST Component Code","GST Component Code");
                                                                  LocationDistribution.SETRANGE("To GST Reg. No.","To GST Reg. No.");
                                                                  IF LocationDistribution.FINDFIRST THEN
                                                                    IF "Line No." <> LocationDistribution."Line No." THEN
                                                                      ERROR(ToGSTErr,"To GST Reg. No.","Distribution No.","GST Component Code");
                                                                END;

                                                                DistComponentAmount.GET("Distribution No.","GST Component Code");
                                                                "GST Base Amount" := DistComponentAmount."GST Base Amount";
                                                                "GST Amount" := DistComponentAmount."GST Amount";
                                                              END;

                                                   CaptionML=[ENU=To GST Reg. No.;
                                                              ENN=To GST Reg. No.] }
    { 10  ;   ;Distribution %      ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("To GST Reg. No.");
                                                                ValidateSameDistributionPercent;
                                                                LocationDistribution.RESET;
                                                                LocationDistribution.SETRANGE("Distribution No.","Distribution No.");
                                                                LocationDistribution.SETRANGE("GST Component Code","GST Component Code");
                                                                LocationDistribution.SETRANGE("From GST Reg. No.","From GST Reg. No.");
                                                                LocationDistribution.SETFILTER("Line No.",'<>%1',"Line No.");
                                                                LocationDistribution.CALCSUMS("Distribution %");
                                                                IF LocationDistribution."Distribution %" + "Distribution %" > 100 THEN
                                                                  ERROR(DistrPerErr,"Distribution No.","GST Component Code");
                                                                GeneralLedgerSetup.GET;
                                                                IF ("Distribution %" < 0) OR ("Distribution %" > 100) THEN
                                                                  ERROR(DistributionPercentErr,FIELDNAME("Distribution %"));
                                                                IF "Distribution %" <> 0 THEN
                                                                  "Distribution Amount" := ROUND("GST Amount" * "Distribution %" / 100,GeneralLedgerSetup."Amount Rounding Precision")
                                                                ELSE
                                                                  "Distribution Amount" := 0;
                                                              END;

                                                   CaptionML=[ENU=Distribution %;
                                                              ENN=Distribution %] }
    { 11  ;   ;Distribution Amount ;Decimal       ;CaptionML=[ENU=Distribution Amount;
                                                              ENN=Distribution Amount];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Distribution No.,GST Component Code,From GST Reg. No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DistComponentAmount@1500001 : Record 16446;
      LocationDistribution@1500000 : Record 16447;
      ToGSTErr@1500002 : TextConst '@@@="%1 = To GST Reg. No., %2 = Distribution No., %3 = GST Component Code";ENU=To GST Reg. No. %1 already exists for Distribution No. %2 Component %3.;ENN=To GST Reg. No. %1 already exists for Distribution No. %2 Component %3.';
      SameGSTErr@1500003 : TextConst 'ENU=GST Registration No. and To GST Reg. No. cannot be same.;ENN=GST Registration No. and To GST Reg. No. cannot be same.';
      DistributionPercentErr@1500004 : TextConst '@@@="%1 = Field Name";ENU=%1 must be in between 0 and 100.;ENN=%1 must be in between 0 and 100.';
      GeneralLedgerSetup@1500005 : Record 98;
      DistrPerErr@1500006 : TextConst '@@@="%1 = Distribution No., %2 = Component Code";ENU=%1 cannot be more than 100 for Distribution %1 Component %2.;ENN=%1 cannot be more than 100 for Distribution %1 Component %2.';
      ComponentDistribution@1500008 : Record 16448;
      GSTRegNoErr@1500009 : TextConst 'ENU=To GST Reg. No. must be same.;ENN=To GST Reg. No. must be same.';
      DistrErr@1500010 : TextConst 'ENU=Distribution % must be same.;ENN=Distribution % must be same.';

    LOCAL PROCEDURE ValidateSameToGSTRegNo@1500000();
    VAR
      SameLocation@1500000 : Boolean;
    BEGIN
      LocationDistribution.RESET;
      LocationDistribution.SETRANGE("Distribution No.","Distribution No.");
      LocationDistribution.SETFILTER("GST Component Code",'<>%1',"GST Component Code");
      IF LocationDistribution.FINDSET THEN BEGIN
        REPEAT
          IF LocationDistribution."To GST Reg. No." = "To GST Reg. No." THEN
            SameLocation := TRUE;
        UNTIL LocationDistribution.NEXT = 0;
        IF NOT SameLocation THEN
          ERROR(GSTRegNoErr);
      END;
    END;

    LOCAL PROCEDURE ValidateSameDistributionPercent@1500001();
    BEGIN
      LocationDistribution.RESET;
      LocationDistribution.SETRANGE("Distribution No.","Distribution No.");
      LocationDistribution.SETFILTER("GST Component Code",'<>%1',"GST Component Code");
      LocationDistribution.SETRANGE("To GST Reg. No.","To GST Reg. No.");
      IF LocationDistribution.FINDFIRST THEN
        IF LocationDistribution."Distribution %" <> "Distribution %" THEN
          ERROR(DistrErr);
    END;

    BEGIN
    END.
  }
}

