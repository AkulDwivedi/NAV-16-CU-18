OBJECT Table 16301 LC Amended Details
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 LCSetup.GET;
                 LCSetup.TESTFIELD("Amended Nos.");
                 NoSeriesMgt.InitSeries(LCSetup."Amended Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               IF Closed THEN
                 ERROR(Text001);
             END;

    OnDelete=BEGIN
               IF Released THEN
                 ERROR(Text002);
             END;

    CaptionML=[ENU=LC Amended Details;
               ENN=LC Amended Details];
    LookupPageID=Page16320;
    DrillDownPageID=Page16320;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  LCSetup.GET;
                                                                  NoSeriesMgt.TestManual(LCSetup."Amended Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ENN=No.] }
    { 2   ;   ;LC No.              ;Code20        ;CaptionML=[ENU=LC No.;
                                                              ENN=LC No.] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENN=Description] }
    { 4   ;   ;Transaction Type    ;Option        ;CaptionML=[ENU=Transaction Type;
                                                              ENN=Transaction Type];
                                                   OptionCaptionML=[ENU=Sale,Purchase;
                                                                    ENN=Sale,Purchase];
                                                   OptionString=Sale,Purchase }
    { 5   ;   ;Issued To/Received From;Code20     ;TableRelation=IF (Transaction Type=CONST(Sale)) Customer
                                                                 ELSE IF (Transaction Type=CONST(Purchase)) Vendor;
                                                   CaptionML=[ENU=Issued To/Received From;
                                                              ENN=Issued To/Received From] }
    { 6   ;   ;Issuing Bank        ;Code20        ;TableRelation="Bank Account";
                                                   CaptionML=[ENU=Issuing Bank;
                                                              ENN=Issuing Bank] }
    { 7   ;   ;Date of Issue       ;Date          ;CaptionML=[ENU=Date of Issue;
                                                              ENN=Date of Issue] }
    { 8   ;   ;Expiry Date         ;Date          ;OnValidate=BEGIN
                                                                IF "Date of Issue" <> 0D THEN
                                                                  IF "Date of Issue" > "Expiry Date" THEN
                                                                    ERROR(Text003);
                                                                LCDetails.GET("LC No.");
                                                                LCDetails."Expiry Date" := "Expiry Date";
                                                                LCDetails.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Expiry Date;
                                                              ENN=Expiry Date] }
    { 9   ;   ;Type of LC          ;Option        ;CaptionML=[ENU=Type of LC;
                                                              ENN=Type of LC];
                                                   OptionCaptionML=[ENU=Inland,Foreign;
                                                                    ENN=Inland,Foreign];
                                                   OptionString=Inland,Foreign }
    { 10  ;   ;Type of Credit Limit;Option        ;CaptionML=[ENU=Type of Credit Limit;
                                                              ENN=Type of Credit Limit];
                                                   OptionCaptionML=[ENU=Fixed,Revolving;
                                                                    ENN=Fixed,Revolving];
                                                   OptionString=Fixed,Revolving }
    { 11  ;   ;Currency Code       ;Code10        ;TableRelation=IF (Type of LC=CONST(Foreign)) Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ENN=Currency Code] }
    { 12  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ENN=No. Series] }
    { 13  ;   ;LC Value            ;Decimal       ;OnValidate=VAR
                                                                Currency@1280001 : Record 4;
                                                                TotalAmount@1280000 : Decimal;
                                                              BEGIN
                                                                CLEAR(TotalAmount);
                                                                IF "Currency Code" <> '' THEN BEGIN
                                                                  Currency.GET("Currency Code");
                                                                  "LC Value LCY" := ROUND("LC Value" * "Exchange Rate",Currency."Amount Rounding Precision");
                                                                END ELSE
                                                                  "LC Value LCY" := "LC Value";

                                                                IF "Transaction Type" = "Transaction Type"::Purchase THEN BEGIN
                                                                  BankCrLimit.SETRANGE("Bank No.","Issuing Bank");
                                                                  BankCrLimit.SETFILTER("From Date",'<= %1',"Date of Issue");
                                                                  BankCrLimit.SETFILTER("To Date",'>=%1',"Date of Issue");
                                                                  IF BankCrLimit.FINDLAST THEN BEGIN
                                                                    LCDetails.RESET;
                                                                    LCDetails.SETRANGE("Transaction Type","Transaction Type");
                                                                    LCDetails.SETRANGE("Issuing Bank",BankCrLimit."Bank No.");
                                                                    IF BankCrLimit."To Date" <> 0D THEN
                                                                      LCDetails.SETFILTER("Date of Issue",'%1..%2',BankCrLimit."From Date",BankCrLimit."To Date")
                                                                    ELSE
                                                                      LCDetails.SETFILTER("Date of Issue",'>=%1',BankCrLimit."From Date");
                                                                    LCDetails.SETFILTER("No.",'<>%1',"LC No.");
                                                                    IF LCDetails.FIND('-') THEN
                                                                      REPEAT
                                                                        IF LCDetails."Latest Amended Value" = 0 THEN
                                                                          TotalAmount := TotalAmount + LCDetails."LC Value LCY"
                                                                        ELSE
                                                                          TotalAmount := TotalAmount + LCDetails."Latest Amended Value";
                                                                      UNTIL LCDetails.NEXT = 0;
                                                                    IF TotalAmount + "LC Value LCY" > BankCrLimit.Amount THEN
                                                                      ERROR(Text004);
                                                                  END ELSE
                                                                    IF "LC Value" <> 0 THEN
                                                                      ERROR(Text005);
                                                                END;

                                                                CALCFIELDS("Value Utilised");
                                                                IF "LC Value LCY" < "Value Utilised" THEN
                                                                  ERROR(Text006);
                                                                "Remaining Amount" := "LC Value LCY" - "Value Utilised";
                                                                LCDetails.GET("LC No.");
                                                                LCDetails."Latest Amended Value" := "LC Value LCY";
                                                                IF "LC Value" <> 0 THEN
                                                                  LCDetails."LC Value" := "LC Value";
                                                                LCDetails."Remaining Amount" := "LC Value LCY" - "Value Utilised";
                                                                LCDetails."LC Value LCY" := "LC Value LCY";
                                                                LCDetails.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=LC Value;
                                                              ENN=LC Value] }
    { 14  ;   ;Value Utilised      ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("LC Orders"."Order Value" WHERE (LC No.=FIELD(LC No.)));
                                                   CaptionML=[ENU=Value Utilised;
                                                              ENN=Value Utilised];
                                                   Editable=No }
    { 15  ;   ;Remaining Amount    ;Decimal       ;CaptionML=[ENU=Remaining Amount;
                                                              ENN=Remaining Amount];
                                                   Editable=No }
    { 16  ;   ;LC Amended Date     ;Date          ;CaptionML=[ENU=LC Amended Date;
                                                              ENN=LC Amended Date] }
    { 17  ;   ;Released            ;Boolean       ;CaptionML=[ENU=Released;
                                                              ENN=Released];
                                                   Editable=No }
    { 18  ;   ;Closed              ;Boolean       ;CaptionML=[ENU=Closed;
                                                              ENN=Closed];
                                                   Editable=No }
    { 21  ;   ;Exchange Rate       ;Decimal       ;CaptionML=[ENU=Exchange Rate;
                                                              ENN=Exchange Rate] }
    { 22  ;   ;LC Value LCY        ;Decimal       ;CaptionML=[ENU=LC Value LCY;
                                                              ENN=LC Value LCY];
                                                   Editable=No }
    { 23  ;   ;Receiving Bank      ;Code20        ;CaptionML=[ENU=Receiving Bank;
                                                              ENN=Receiving Bank] }
    { 24  ;   ;Bank LC No.         ;Code20        ;CaptionML=[ENU=Bank LC No.;
                                                              ENN=Bank LC No.] }
    { 25  ;   ;Bank Amended No.    ;Code20        ;CaptionML=[ENU=Bank Amended No.;
                                                              ENN=Bank Amended No.] }
    { 26  ;   ;Revolving Cr. Limit Types;Option   ;CaptionML=[ENU=Revolving Cr. Limit Types;
                                                              ENN=Revolving Cr. Limit Types];
                                                   OptionCaptionML=[ENU=" ,Automatic,Manual";
                                                                    ENN=" ,Automatic,Manual"];
                                                   OptionString=[ ,Automatic,Manual] }
    { 27  ;   ;Previous LC Value   ;Decimal       ;CaptionML=[ENU=Previous LC Value;
                                                              ENN=Previous LC Value] }
    { 28  ;   ;Previous Expiry Date;Date          ;CaptionML=[ENU=Previous Expiry Date;
                                                              ENN=Previous Expiry Date] }
  }
  KEYS
  {
    {    ;No.,LC No.                              ;Clustered=Yes }
    {    ;Description                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      LCSetup@1280000 : Record 16303;
      LCDetails@1280002 : Record 16300;
      BankCrLimit@1280008 : Record 16304;
      LCADetails@1280009 : Record 16301;
      NoSeriesMgt@1280001 : Codeunit 396;
      Text001@1500000 : TextConst 'ENU=You cannot modify the Document.;ENN=You cannot modify the Document.';
      Text002@1500001 : TextConst 'ENU=You cannot delete the document.;ENN=You cannot delete the document.';
      Text003@1500002 : TextConst 'ENU=Expiry Date cannot be before Issue Date.;ENN=Expiry Date cannot be before Issue Date.';
      Text004@1500003 : TextConst 'ENU=LC(s) value exceeds the credit limit available for this bank.;ENN=LC(s) value exceeds the credit limit available for this bank.';
      Text005@1500004 : TextConst 'ENU=Banks credit limit is Zero.;ENN=Banks credit limit is Zero.';
      Text006@1500005 : TextConst 'ENU=LC Value cannot be lower than the Value Utilised.;ENN=LC Value cannot be lower than the Value Utilised.';

    PROCEDURE AssistEdit@2(OldLCADetails@1280000 : Record 16301) : Boolean;
    BEGIN
      WITH LCADetails DO BEGIN
        LCADetails := Rec;
        LCSetup.GET;
        LCSetup.TESTFIELD("Amended Nos.");
        IF NoSeriesMgt.SelectSeries(LCSetup."Amended Nos.",OldLCADetails."No. Series","No. Series") THEN BEGIN
          LCSetup.GET;
          LCSetup.TESTFIELD("Amended Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := LCADetails;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

