OBJECT Table 16585 Tax Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Form ID");
             END;

    OnDelete=BEGIN
               TaxJnlLine.SETRANGE("Journal Template Name",Name);
               TaxJnlLine.DELETEALL(TRUE);
               TaxJnlBatch.SETRANGE("Journal Template Name",Name);
               TaxJnlBatch.DELETEALL;
             END;

    CaptionML=[ENU=Tax Journal Template;
               ENN=Tax Journal Template];
    LookupPageID=Page16350;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ENN=Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ENN=Description] }
    { 6   ;   ;Form ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Page));
                                                   OnValidate=BEGIN
                                                                IF "Form ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[ENU=Form ID;
                                                              ENN=Form ID] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::Excise:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."General Journal";
                                                                      "Form ID" := PAGE::"Tax Journal";
                                                                    END;
                                                                  Type::"VAT Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."VAT Adjustment Journal";
                                                                      "Form ID" := PAGE::"VAT Adjustment Journal";
                                                                    END;
                                                                  Type::"TDS Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."TDS Adjustment Journal";
                                                                      "Form ID" := PAGE::"TDS Adjustment Journal";
                                                                    END;
                                                                  Type::"TCS Adjustments":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."TCS Adjustment Journal";
                                                                      "Form ID" := PAGE::"TCS Adjustment Journal";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ENN=Type];
                                                   OptionCaptionML=[ENU=Excise,,VAT Adjustments,,,TDS Adjustments,TCS Adjustments;
                                                                    ENN=Excise,,VAT Adjustments,,,TDS Adjustments,TCS Adjustments];
                                                   OptionString=Excise,,VAT Adjustments,,,TDS Adjustments,TCS Adjustments }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                TaxJnlLine.SETRANGE("Journal Template Name",Name);
                                                                TaxJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              ENN=Source Code] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ENN=Reason Code] }
    { 16  ;   ;Form Name           ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Page),
                                                                                                                Object ID=FIELD(Form ID)));
                                                   CaptionML=[ENU=Form Name;
                                                              ENN=Form Name];
                                                   Editable=No }
    { 19  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              ENN=Bal. Account Type];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account;
                                                                    ENN=G/L Account,Customer,Vendor,Bank Account];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account }
    { 20  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              ENN=Bal. Account No.] }
    { 21  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ENN=No. Series] }
    { 22  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ENN=Posting No. Series] }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      TaxJnlBatch@1500002 : Record 16586;
      TaxJnlLine@1500001 : Record 16587;
      SourceCodeSetup@1500000 : Record 242;
      Text001@1500004 : TextConst 'ENU=must not be %1;ENN=must not be %1';

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1500000 : Code[20]);
    VAR
      GLAcc@1500001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

