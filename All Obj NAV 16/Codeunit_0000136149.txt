OBJECT Codeunit 136149 Chart Configuration Tool
{
  OBJECT-PROPERTIES
  {
    Date=27-09-17;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00,Test;
  }
  PROPERTIES
  {
    Subtype=Test;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Assert@1019 : Codeunit 130000;
      LibraryUtility@1009 : Codeunit 131000;
      SourceIDToSet@1001 : Integer;
      SourceTypeToSet@1002 : Integer;
      ActualFieldsListCount@1004 : Integer;
      DimensionNameToSet@1005 : Text[80];
      ActualTypeFilter@1003 : Text;
      SourceIDValidationError@1010 : TextConst 'ENU=There is no Object within the filter.Filters: Type: %1, ID: %2';
      InvalidFilterFieldError@1011 : TextConst 'ENU=There is no Field within the filter.Filters: TableNo: %1, FieldName: %2';
      DimensionMeasureInvalidError@1006 : TextConst 'ENU=There is no Field within the filter.Filters: TableNo: %1, FieldName: %3, Type: %2, Class: %4';
      DefaultChartDescription@1012 : TextConst 'ENU=This is a test description for the chart';
      NothingInsideTheFilter@1018 : TextConst 'ENU=DB:NothingInsideFilter';
      MaxNoOfMeasures@1007 : TextConst 'ENU=You cannot select more than %1 measures when using the Customize Chart option.';
      TableSalesheader@1008 : TextConst 'ENU=Sales Header';
      DimensionMeasureInvalidErrorType0@1013 : TextConst 'ENU=There is no Field within the filter.Filters: TableNo: %1, FieldName: %2, Class: %3';
      Text000@1032 : TextConst '@@@=NONE;ENU=NONE';
      Text001@1031 : TextConst '@@@=COUNT;ENU=COUNT';
      Text002@1030 : TextConst '@@@=SUM;ENU=SUM';
      Text003@1029 : TextConst '@@@=MIN;ENU=MIN';
      Text004@1028 : TextConst '@@@=MAX;ENU=MAX';
      Text005@1027 : TextConst '@@@=AVERAGE;ENU=AVERAGE';
      RequiredMeasureCaptionTxt@1043 : TextConst 'ENU=Required Measure Caption';
      RequiredMeasureCaptionChangedTxt@1042 : TextConst 'ENU=Required Measure Caption Changed';
      XAxisTitleTxt@1041 : TextConst 'ENU=Cities';
      YAxisTitleTxt@1040 : TextConst 'ENU=Balances';
      ChartNotFoundErr@1039 : TextConst 'ENU=The Chart %1 was not found.';
      NoDataInBlobErr@1038 : TextConst 'ENU=The blob field does not contain any data.';
      DescriptionTxt@1037 : TextConst 'ENU=This chart shows information about the customer balance by City. The text can be up very very long. 0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789';
      TestNameDescriptionTxt@1036 : TextConst 'ENU=Test: Description';
      TestNameRequiredMeasureCaptionTxt@1035 : TextConst 'ENU=Test: Required Caption';
      TestNameEditorLanguageFieldTxt@1034 : TextConst 'ENU=Test: Language Field';
      TestNameEditorTextFieldTxt@1033 : TextConst 'ENU=Test: Text Field';
      DANLanguageCodeTxt@1044 : TextConst 'ENU=DAN';
      DANLanguageMemoTxt@1045 : TextConst 'ENU=Danish';
      ENULanguageCodeTxt@1046 : TextConst 'ENU=ENU';
      ENULanguageMemoTxt@1047 : TextConst 'ENU=English';
      LTHLanguageCodeTxt@1048 : TextConst 'ENU=LTH';
      LTHLanguageMemoTxt@1049 : TextConst 'ENU=Lithuanian';
      MemoTxt@1050 : TextConst 'ENU=Memo';
      ZAxisTestNameTxt@1051 : TextConst 'ENU=Z-Axis';
      TestNameDescriptionTxtLanguageCountTxt@1052 : TextConst 'ENU=Test: Description Language Count';
      OptionalMeasureCaptionTxt@1053 : TextConst 'ENU=Optional Measure Caption';
      TestNameOptionalMeasureColumnTxt@1054 : TextConst 'ENU=Test: Optional Measure Column';
      TestNameOptionalMeasureAggregationTxt@1055 : TextConst 'ENU=Test: Optional Measure Aggregation';
      TestNameOptionalMeasureTypeTxt@1056 : TextConst 'ENU=Test: Optional Measure Type';
      TestNameOptionalMeasureCaptionTxt@1057 : TextConst 'ENU=Test: Optional Measure Caption';
      LanguageCode1Txt@1058 : TextConst 'ENU=DAN';
      LanguageCode1AlternativeTxt@1059 : TextConst 'ENU=ELL';
      LanguageCode2Txt@1060 : TextConst 'ENU=THA';
      LanguageCode2AlternativeTxt@1061 : TextConst 'ENU=BGR';
      LanguageCode3Txt@1062 : TextConst 'ENU=KOR';
      LanguageCode3AlternativeTxt@1063 : TextConst 'ENU=LTH';
      LanguageNotDefinedErr@1064 : TextConst 'ENU=Only %1 languages are supported';
      TestNameRequiredMeasureLanguageCountTxt@1065 : TextConst 'ENU=Test: Required measure language count';
      ChartSetupNotShowingExpectedRecordTxt@1066 : TextConst 'ENU=The ChartSetup page is not open on the expected record.';
      ChartFoundInDBErr@1067 : TextConst 'ENU=There is an entry in the database with the same ID. A test likely did not clean-up correctly';
      TextChartNameTestTxt@1068 : TextConst 'ENU=Name';
      TestNameXAxisTxt@1069 : TextConst 'ENU=Test: X-Axis';
      TestNameShowXAxisTitleTxt@1070 : TextConst 'ENU=Test: Show X-Axis title';
      TestNameXAxisTitleTxt@1071 : TextConst 'ENU=Test: X-Axis title';
      TestNameShowYAxisTitleTxt@1072 : TextConst 'ENU=Test: Show Y-Axis title';
      TestNameYAxisTitleTxt@1073 : TextConst 'ENU=Test: Y-Axis title';
      TestNameHasTableTxt@1074 : TextConst 'ENU=Test: HasTable';
      TestNameTableIDTxt@1075 : TextConst 'ENU=Test: TableID';
      TestNameRequiredMeasureColumnTxt@1076 : TextConst 'ENU=Test: Required Column';
      TestNameRequiredMeasureAggregationTxt@1077 : TextConst 'ENU=Test: Required Aggregation';
      TestNameRequiredMeasureTypeTxt@1078 : TextConst 'ENU=Test: Required Type';
      TextMemoToBeTruncatedMsg@1079 : TextConst 'ENU=The length of the text that you entered is %1. The maximum length is %2. The text has been truncated to this length.';
      MemoTestNameTxt@1080 : TextConst 'ENU=Test: Memo message';
      XAxisDatapointTxt@1081 : TextConst 'ENU=X Data Point';
      ZAxisDatapointTxt@1082 : TextConst 'ENU=Z Data Point';
      TestNameXAxisDatapointTxt@1083 : TextConst 'ENU=Test: X axis Datapoint';
      TestNameZAxisDatapointTxt@1084 : TextConst 'ENU=Test: Z axis Datapoint';
      ColumnDataTypeErr@1000 : TextConst 'ENU="Wrong column data dype "';

    LOCAL PROCEDURE Initialize@6();
    BEGIN
      ClearGlobals;
    END;

    [Test]
    PROCEDURE TestTableChart2Dimensions4Measures@1();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1007 : TEMPORARY Record 9180;
      TempGenericChartFilter@1006 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1005 : TEMPORARY Record 9182;
      TempGenericChartCaptionsBuf@1010 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1011 : TEMPORARY Record 9186;
      GenericChartMgt@1004 : Codeunit 9180;
      ChartBuilder@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      DataMeasureType@1000 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1003 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      HasXDimension@1016 : Boolean;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      HasZDimension@1019 : Boolean;
      ZDimensionID@1022 : Integer;
      ZDimensionName@1021 : Text[80];
      ShowZDimensionTitle@1025 : Boolean;
      CountOfMeasures@1020 : Integer;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      MeasureName@1030 : ARRAY [4] OF Text[50];
      FilterIndex@1032 : Integer;
      FilterFieldName@1038 : Text[30];
      FilterFieldValue@1037 : Text[250];
      FilterFieldId@1036 : Integer;
      FilterCount@1039 : Integer;
      ChartCode@1008 : Code[20];
      RetrievedDescription@1002 : Text;
      ExpectedDescription@1009 : Text[1024];
    BEGIN
      // SETUP: Create a table based chart with X Dimension and 4 measures
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      CountOfMeasures := 4;
      // Y Axis, Index is zero based in chart builder
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      HasXDimension := TRUE;
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // Z Axis
      HasZDimension := FALSE;
      GetDefaultZDimensionsInfoForTable(ZDimensionID,ZDimensionName,ShowZDimensionTitle);

      // FILTERS
      FilterCount := 1;
      FilterIndex := 0;
      GetDefaultFilterInfoForTable(FilterFieldName,FilterFieldValue,FilterFieldId);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.INSERT;

      // Add Measures
      FOR MeasureIndex := 1 TO 4 DO
        AddMeasureToChart(
          TempGenericChartSetup,TempGenericChartYAxis,
          MeasureName[MeasureIndex],MeasureID[MeasureIndex],
          MeasureIndex - 1,DataMeasureType[MeasureIndex],
          DataAggregationType[MeasureIndex]);

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);

      // EXECUTE: Save the Chart and retrieve fields using chart builder.
      // Save
      ExpectedDescription := PADSTR(DefaultChartDescription,1024,'A');
      TempGenericChartMemoBuf.SetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage,ExpectedDescription);
      GenericChartMgt.SaveChanges(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter,
        TempGenericChartCaptionsBuf,TempGenericChartMemoBuf);
      Chart.MODIFY;

      TempGenericChartSetup.DELETEALL;
      TempGenericChartYAxis.DELETEALL;
      TempGenericChartFilter.DELETEALL;

      // Retrieve fields.
      CreateBuilderFromChart(Chart,ChartBuilder);
      FindChartByID(Chart,ChartCode);

      // VERIFY: Retrieve fields using chart builder and verify.
      TempGenericChartMemoBuf.DELETEALL;
      GenericChartMgt.RetrieveXML(
        Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);
      RetrievedDescription := TempGenericChartMemoBuf.GetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage);
      VerifyChartSourceProperties(ChartBuilder,SourceType,SourceName,SourceID,ExpectedDescription);
      VerifyChartSourceProperties(
        ChartBuilder,TempGenericChartSetup."Source Type",SourceName,TempGenericChartSetup."Source ID",RetrievedDescription);

      VerifyChartDimensions(
        ChartBuilder,
        HasXDimension,
        1,XDimensionID,
        XDimensionName,
        ShowXDimensionTitle);

      VerifyChartDimensions(
        ChartBuilder,
        HasXDimension,1,
        TempGenericChartSetup."X-Axis Field ID",
        TempGenericChartSetup."X-Axis Field Name",
        TempGenericChartSetup."X-Axis Show Title");

      VerifyChartDimensions(
        ChartBuilder,HasZDimension,
        2,ZDimensionID,
        ZDimensionName,
        ShowZDimensionTitle);

      VerifyChartDimensions(
        ChartBuilder,HasZDimension,2,
        TempGenericChartSetup."Z-Axis Field ID",
        TempGenericChartSetup."Z-Axis Field Name",
        TempGenericChartSetup."Z-Axis Show Title");

      // Verify Measure
      Assert.AreEqual(CountOfMeasures,ChartBuilder.MeasureCount,'Count of Measures Match');
      FOR MeasureIndex := 1 TO 4 DO BEGIN
        VerifyChartMeasure(ChartBuilder,
          TRUE,MeasureIndex - 1,
          MeasureID[MeasureIndex],
          MeasureName[MeasureIndex],
          DataMeasureType[MeasureIndex],
          DataAggregationType[MeasureIndex]);

        VerifyChartMeasureAgainstTable(ChartBuilder,TempGenericChartYAxis,TRUE,MeasureIndex - 1);
      END;
      VerifyChartFilters(ChartBuilder,FilterIndex,FilterFieldName,FilterFieldValue,FilterFieldId,FilterCount);
      VerifyChartFilters(ChartBuilder,
        FilterIndex,
        TempGenericChartFilter."Filter Field Name",
        TempGenericChartFilter."Filter Value",
        TempGenericChartFilter."Filter Field ID",
        FilterCount);
    END;

    [Test]
    PROCEDURE TestTableChart3Dimensions@115();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1006 : TEMPORARY Record 9180;
      TempGenericChartFilter@1005 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1004 : TEMPORARY Record 9182;
      ChartBuilder@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      DataMeasureType@1000 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1003 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      HasXDimension@1016 : Boolean;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      HasZDimension@1019 : Boolean;
      ZDimensionID@1022 : Integer;
      ZDimensionName@1021 : Text[80];
      ShowZDimensionTitle@1025 : Boolean;
      CountOfMeasures@1020 : Integer;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      MeasureName@1030 : ARRAY [4] OF Text[50];
      FilterIndex@1032 : Integer;
      FilterFieldName@1038 : Text[30];
      FilterFieldValue@1037 : Text[250];
      FilterFieldId@1036 : Integer;
      FilterCount@1039 : Integer;
      ChartCode@1008 : Code[20];
    BEGIN
      // SETUP: Create a table based chart with X and Z dimensions
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      CountOfMeasures := 1;
      // Y Axis
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      HasXDimension := TRUE;
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // Z Axis
      HasZDimension := TRUE;
      GetDefaultZDimensionsInfoForTable(ZDimensionID,ZDimensionName,ShowZDimensionTitle);
      ShowZDimensionTitle := TRUE;

      // FILTERS
      FilterCount := 1;
      FilterIndex := 0;
      GetDefaultFilterInfoForTable(FilterFieldName,FilterFieldValue,FilterFieldId);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      SetChartDimensions(TempGenericChartSetup,2,ZDimensionID,ZDimensionName,ZDimensionName,ShowZDimensionTitle);
      TempGenericChartSetup.INSERT;

      // Add Measures
      MeasureIndex := 1;
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName[MeasureIndex],MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);

      // EXECUTE: Save the Chart and retrieve fields using chart builder.
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;
      CreateBuilderFromChart(Chart,ChartBuilder);

      // VERIFY: Retrieve fields match the passed values.
      VerifyChartSourceProperties(ChartBuilder,SourceType,SourceName,SourceID,DefaultChartDescription);

      // Verify Dimensions
      VerifyChartDimensions(ChartBuilder,HasXDimension,1,XDimensionID,XDimensionName,ShowXDimensionTitle);
      VerifyChartDimensions(ChartBuilder,HasZDimension,2,ZDimensionID,ZDimensionName,ShowZDimensionTitle);

      // Verify Measure
      Assert.AreEqual(CountOfMeasures,ChartBuilder.MeasureCount,'Count of Measures Match');

      FOR MeasureIndex := 1 TO 4 DO
        VerifyChartMeasure(ChartBuilder,
          MeasureIndex = 1,MeasureIndex - 1,
          MeasureID[MeasureIndex],
          MeasureName[MeasureIndex],
          DataMeasureType[MeasureIndex],
          DataAggregationType[MeasureIndex]);

      VerifyChartFilters(ChartBuilder,FilterIndex,FilterFieldName,FilterFieldValue,FilterFieldId,FilterCount);
    END;

    [Test]
    [HandlerFunctions(HandleTableFieldsChooser)]
    PROCEDURE TestTableChart2DimensionsRequiredMeasureOnly@131();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1007 : TEMPORARY Record 9180;
      TempGenericChartFilter@1006 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1005 : TEMPORARY Record 9182;
      Field@1008 : Record 2000000041;
      GenericChartMgt@1004 : Codeunit 9180;
      DataMeasureType@1003 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1000 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      ChartBuilder@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      HasXDimension@1016 : Boolean;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      HasZDimension@1019 : Boolean;
      ZDimensionID@1022 : Integer;
      ZDimensionName@1021 : Text[80];
      ShowZDimensionTitle@1025 : Boolean;
      CountOfMeasures@1020 : Integer;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      MeasureName@1030 : ARRAY [4] OF Text[50];
      FilterIndex@1032 : Integer;
      FilterFieldName@1038 : Text[30];
      FilterFieldValue@1037 : Text[250];
      FilterFieldId@1036 : Integer;
      FilterCount@1039 : Integer;
      DummyString@1009 : Text[80];
      ChartCode@1010 : Code[20];
    BEGIN
      // SETUP: Create a table based chart with required measure only
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      CountOfMeasures := 1;
      // Y Axis
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      HasXDimension := TRUE;
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // Z Axis
      HasZDimension := FALSE;
      GetDefaultZDimensionsInfoForTable(ZDimensionID,ZDimensionName,ShowZDimensionTitle);

      // FILTERS
      FilterCount := 0;
      FilterIndex := 0;
      FilterFieldName := '';
      FilterFieldValue := '';
      FilterFieldId := 0;

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      SourceIDToSet := SourceID;
      DimensionNameToSet := GetFieldNameFromID(TempGenericChartSetup."Source ID",XDimensionID);

      GenericChartMgt.RetrieveFieldColumn(
        TempGenericChartSetup,
        TempGenericChartSetup."X-Axis Field ID",
        TempGenericChartSetup."X-Axis Field Name",
        TempGenericChartSetup."X-Axis Title",1,FALSE); // 1 is For Dimension

      // Verify Count of fields displayed
      GetFieldsForTable(Field,SourceIDToSet,1);
      Assert.AreEqual(ActualFieldsListCount,Field.COUNT,'count of fields match');
      Assert.AreEqual(XDimensionID,TempGenericChartSetup."X-Axis Field ID",'XDimension ID Matches');
      Assert.AreEqual(DimensionNameToSet,TempGenericChartSetup."X-Axis Field Name",'XDimension Name Matches');
      Assert.AreEqual(DimensionNameToSet,TempGenericChartSetup."X-Axis Title",'XDimension Title Matches');
      Assert.AreEqual(Field.GETFILTER(Type),ActualTypeFilter,'Filter String Matches Expected for X Dimension');

      // Add required Measure only
      MeasureIndex := 1;
      SourceIDToSet := SourceID;
      DimensionNameToSet := GetFieldNameFromID(TempGenericChartSetup."Source ID",MeasureID[MeasureIndex]);

      GenericChartMgt.RetrieveFieldColumn(
        TempGenericChartSetup,
        TempGenericChartYAxis."Y-Axis Measure Field ID",
        TempGenericChartYAxis."Y-Axis Measure Field Name",
        DummyString,2,FALSE); // 2 is For Measure

      CLEAR(Field);

      GetFieldsForTable(Field,SourceIDToSet,2);
      Assert.AreEqual(ActualFieldsListCount,Field.COUNT,'count of fields match for Measures');
      Assert.AreEqual(MeasureID[MeasureIndex],TempGenericChartYAxis."Y-Axis Measure Field ID",'Measure ID Matches');
      Assert.AreEqual(DimensionNameToSet,TempGenericChartYAxis."Y-Axis Measure Field Name",'Measure Name Matches');
      Assert.AreEqual(DimensionNameToSet,TempGenericChartYAxis."Y-Axis Measure Field Name",'Measure Title Matches');

      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName[MeasureIndex],MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      // EXECUTE: Save the Chart and retrieve fields using chart builder.
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;
      CreateBuilderFromChart(Chart,ChartBuilder);

      // VERIFY: Retrieve fields match the passed values.
      VerifyChartSourceProperties(ChartBuilder,SourceType,SourceName,SourceID,DefaultChartDescription);

      // Verify Dimensions
      VerifyChartDimensions(ChartBuilder,HasXDimension,1,XDimensionID,XDimensionName,ShowXDimensionTitle);
      VerifyChartDimensions(ChartBuilder,HasZDimension,2,ZDimensionID,ZDimensionName,ShowZDimensionTitle);

      // Verify Measure
      Assert.AreEqual(CountOfMeasures,ChartBuilder.MeasureCount,'Count of Measures Match');

      FOR MeasureIndex := 1 TO 4 DO
        VerifyChartMeasure(ChartBuilder,
          MeasureIndex = 1,MeasureIndex - 1,
          MeasureID[MeasureIndex],
          MeasureName[MeasureIndex],
          DataMeasureType[MeasureIndex],
          DataAggregationType[MeasureIndex]);

      VerifyChartFilters(ChartBuilder,FilterIndex,FilterFieldName,FilterFieldValue,FilterFieldId,FilterCount);
    END;

    [Test]
    [HandlerFunctions(HandleQueryFieldsChooser)]
    PROCEDURE TestQueryChart2DimensionsRequiredMeasure@135();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1007 : TEMPORARY Record 9180;
      TempGenericChartFilter@1006 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1005 : TEMPORARY Record 9182;
      TempGenericChartCaptionsBuf@1004 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1000 : TEMPORARY Record 9186;
      GenericChartMgt@1010 : Codeunit 9180;
      DataMeasureType@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1002 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      ChartBuilder@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      HasXDimension@1016 : Boolean;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      HasZDimension@1019 : Boolean;
      ZDimensionID@1022 : Integer;
      ZDimensionName@1021 : Text[250];
      ShowZDimensionTitle@1025 : Boolean;
      CountOfMeasures@1020 : Integer;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : Integer;
      MeasureName@1030 : Text[50];
      FilterIndex@1032 : Integer;
      FilterFieldName@1038 : Text[30];
      FilterFieldValue@1037 : Text[250];
      FilterFieldId@1036 : Integer;
      FilterCount@1039 : Integer;
      ChartCode@1008 : Code[20];
      RetrievedDescription@1009 : Text;
    BEGIN
      // SETUP: Create a query based chart
      Initialize;
      GetDefaultSourceQueryInfo(SourceName,SourceID,SourceType);
      CountOfMeasures := 1;

      // Y Axis
      MeasureIndex := 0;
      DataMeasureType := DataMeasureType.Line;
      GetDefaultMeasureInfoForQuery(DataAggregationType,MeasureID,MeasureName);

      // X Axis
      HasXDimension := TRUE;
      GetDefaultXDimensionInfoForQuery(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // Z Axis
      HasZDimension := FALSE;
      ZDimensionID := 0;
      ZDimensionName := '';
      ShowZDimensionTitle := FALSE;

      // FILTERS
      FilterCount := 1;
      FilterIndex := 0;
      GetDefaultFilterInfoForQuery(FilterFieldName,FilterFieldValue,FilterFieldId);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      SourceIDToSet := SourceID;
      DimensionNameToSet := XDimensionName;

      GenericChartMgt.RetrieveFieldColumn(
        TempGenericChartSetup,
        TempGenericChartSetup."X-Axis Field ID",
        TempGenericChartSetup."X-Axis Field Name",
        TempGenericChartSetup."X-Axis Title",1,FALSE); // 1 is For Dimension

      Assert.AreEqual(DimensionNameToSet,TempGenericChartSetup."X-Axis Field Name",'XDimension Name Matches');
      Assert.AreEqual(DimensionNameToSet,TempGenericChartSetup."X-Axis Title",'XDimension Title Matches');

      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.INSERT;

      // Add Measures
      MeasureIndex := 1;
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName,MeasureID,
        MeasureIndex - 1,DataMeasureType,
        DataAggregationType);

      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      // Save
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;

      // EXECUTE: Retrieve the properties from the query chart XML
      CreateBuilderFromChart(Chart,ChartBuilder);

      // VERIFY: Chart Properties
      VerifyChartSourceProperties(ChartBuilder,SourceType,SourceName,SourceID,DefaultChartDescription);
      CLEAR(Chart);
      Chart.SETRANGE(ID,ChartCode);
      Chart.FINDFIRST;
      GenericChartMgt.RetrieveXML(
        Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);
      RetrievedDescription := TempGenericChartMemoBuf.GetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage);
      VerifyChartSourceProperties(
        ChartBuilder,TempGenericChartSetup."Source Type",SourceName,TempGenericChartSetup."Source ID",RetrievedDescription);

      // Verify Dimensions
      VerifyChartDimensions(ChartBuilder,HasXDimension,1,XDimensionID,XDimensionName,ShowXDimensionTitle);
      VerifyChartDimensions(ChartBuilder,HasZDimension,2,ZDimensionID,ZDimensionName,ShowZDimensionTitle);

      // Verify Measures
      Assert.AreEqual(CountOfMeasures,ChartBuilder.MeasureCount,'Count of Measures Match');

      FOR MeasureIndex := 1 TO 4 DO
        VerifyChartMeasure(ChartBuilder,
          MeasureIndex = 1,MeasureIndex - 1,
          MeasureID,
          MeasureName,
          DataMeasureType,
          DataAggregationType);

      VerifyChartFilters(ChartBuilder,FilterIndex,FilterFieldName,FilterFieldValue,FilterFieldId,FilterCount);
    END;

    [Test]
    PROCEDURE TestCopyChart@11();
    VAR
      Chart@1001 : Record 2000000078;
      CopiedChart@1004 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      GenericChartMgt@1003 : Codeunit 9180;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      InStream@1002 : InStream;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      MeasureName@1030 : ARRAY [4] OF Text[50];
      TargetChartCode@1000 : Code[20];
      SourceChartXml@1005 : Text;
      TargetChartXml@1006 : Text;
      ChartXmlLine@1007 : Text;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.INSERT;

      // Add Measures
      MeasureIndex := 1;
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName[MeasureIndex],MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      // Save
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;

      // EXECUTE: Copy the source chart to the Target Chart by invoking copychart method.
      TargetChartCode := GenerateRandomChartCode;
      GenericChartMgt.CopyChart(Chart,TargetChartCode,TargetChartCode);
      CopiedChart.SETRANGE(ID,TargetChartCode);
      CopiedChart.FINDFIRST;
      CopiedChart.CALCFIELDS(BLOB);
      CopiedChart.BLOB.CREATEINSTREAM(InStream);
      WHILE NOT InStream.EOS DO BEGIN
        InStream.READTEXT(ChartXmlLine);
        TargetChartXml := TargetChartXml + ChartXmlLine;
      END;
      CLEAR(InStream);
      CLEAR(ChartXmlLine);

      // VERIFY: Retrieve the fields in target chart and verify the changes.
      Chart.CALCFIELDS(BLOB);
      Chart.BLOB.CREATEINSTREAM(InStream);
      WHILE NOT InStream.EOS DO BEGIN
        InStream.READTEXT(ChartXmlLine);
        SourceChartXml := SourceChartXml + ChartXmlLine;
      END;

      Assert.AreEqual(SourceChartXml,TargetChartXml,'The copied chart xml matches source chart xml');
      Assert.AreEqual(TargetChartCode,CopiedChart.Name,'Title of target chart matches what was entered');
    END;

    [Test]
    [HandlerFunctions(HandleLookupPage,HandleTableFieldsChooser)]
    PROCEDURE TestLookupObjectId@14();
    VAR
      Chart@1001 : Record 2000000078;
      Field@1005 : Record 2000000041;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1000 : TEMPORARY Record 9181;
      GenericChartMgt@1003 : Codeunit 9180;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      FilterFieldName@1002 : Text[30];
      FilterFieldID@1004 : Integer;
      ChartCode@1006 : Code[20];
    BEGIN
      // SETUP: Set the field info
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // EXECUTE: Add the fields to chart object by invoking the two methods LookupObjectID and RetrieveFieldColumn
      CreateDefaultChart(Chart,ChartCode);

      TempGenericChartSetup.VALIDATE(ID,Chart.ID);
      TempGenericChartSetup.VALIDATE("Source Type",SourceType);

      SourceIDToSet := SourceID;
      SourceTypeToSet := TempGenericChartSetup."Source Type"::Table;
      GenericChartMgt.LookUpObjectId(
        TempGenericChartSetup."Source Type"::Table,TempGenericChartSetup."Source ID",TempGenericChartSetup."Object Name");

      FilterFieldID := 20;
      FilterFieldName := GetFieldNameFromID(TempGenericChartSetup."Source ID",FilterFieldID);
      DimensionNameToSet := FilterFieldName;

      GenericChartMgt.RetrieveFieldColumn(
        TempGenericChartSetup,
        TempGenericChartFilter."Filter Field ID",
        TempGenericChartFilter."Filter Field Name",
        TempGenericChartFilter."Filter Field Name",0,FALSE);
      GetFieldsForTable(Field,SourceIDToSet,0);

      // VERIFY: The Lookup and Filter set values are set properly
      Assert.AreEqual(SourceID,TempGenericChartSetup."Source ID",'Expected Source ID Matches');
      Assert.AreEqual(SourceName,TempGenericChartSetup."Object Name",'Expected Source ID Matches');
      Assert.AreEqual(ActualFieldsListCount,Field.COUNT,'count of fields match');
      Assert.AreEqual(FilterFieldID,TempGenericChartFilter."Filter Field ID",'Filter ID Matches');
      Assert.AreEqual(FilterFieldName,TempGenericChartFilter."Filter Field Name",'Filter Name Matches');
      Assert.AreEqual(Field.GETFILTER(Type),ActualTypeFilter,'Filter String Matches Expected for Filter');
    END;

    [Test]
    PROCEDURE TestMeasureNonNumericFieldErrorForTable@163();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[50];
      ShowXDimensionTitle@1024 : Boolean;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);
      MeasureIndex := 1;
      MeasureID[MeasureIndex] := XDimensionID;

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // EXECUTE: Save Chart with Measure set to Non-numeric field
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        XDimensionName,MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      // VERIFY: Error is displayed when Measure is saved with a non-numeric field
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      CompareFilterStrings(
        STRSUBSTNO(DimensionMeasureInvalidError,TempGenericChartSetup."Source ID",TypeFilterText(2),XDimensionName,GetFieldClassFilter),
        StripNewLines(GETLASTERRORTEXT),
        'Error is returned when saving an Measure field which is non-numeric');
    END;

    [Test]
    PROCEDURE TestMeasureNonNumericFieldErrorForQuery@20();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1007 : TEMPORARY Record 9180;
      TempGenericChartFilter@1006 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1005 : TEMPORARY Record 9182;
      DataMeasureType@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1002 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[50];
      ShowXDimensionTitle@1024 : Boolean;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : Integer;
      MeasureName@1030 : Text[50];
      FilterFieldName@1038 : Text[30];
      FilterFieldValue@1037 : Text[250];
      FilterFieldId@1036 : Integer;
      ChartCode@1008 : Code[20];
    BEGIN
      // SETUP: Create a query based chart
      Initialize;
      GetDefaultSourceQueryInfo(SourceName,SourceID,SourceType);

      // Y Axis
      MeasureIndex := 0;
      DataMeasureType := DataMeasureType.StackedColumn100;
      GetDefaultMeasureInfoForQuery(DataAggregationType,MeasureID,MeasureName);

      // X Axis
      GetDefaultXDimensionInfoForQuery(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // FILTERS
      GetDefaultFilterInfoForQuery(FilterFieldName,FilterFieldValue,FilterFieldId);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      // SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      // TempGenericChartSetup.INSERT;

      // Add Measures
      MeasureIndex := 1;
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        XDimensionName,XDimensionID,
        MeasureIndex - 1,DataMeasureType,
        DataAggregationType);

      // AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      // Save
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.VerifyFailure(NothingInsideTheFilter,'Error is returned when saving an Measure field which is non-numeric');
    END;

    [Test]
    PROCEDURE TestDimensionsDuplicateFieldsErrorForTable@47();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // X Axis
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      ShowXDimensionTitle := TRUE;
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      // EXECUTE: Set Chart to have duplicate X dimensions
      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      SetChartDimensions(TempGenericChartSetup,2,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.INSERT;

      // VERIFY: Duplicate dimensions can be saved only checks are implemented in the page.
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;
    END;

    [Test]
    PROCEDURE TestDuplicateMeasuresForTable@66();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionName@1018 : Text[50];
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      MeasureName@1000 : ARRAY [4] OF Text[50];
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // EXECUTE: Save Chart with Duplicate Measures
      MeasureIndex := 1;
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        XDimensionName,MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      MeasureIndex := 2;
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName[MeasureIndex],MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName[MeasureIndex],MeasureID[MeasureIndex],
        MeasureIndex,DataMeasureType[MeasureIndex + 1],
        DataAggregationType[MeasureIndex]);

      // VERIFY: Error is not displayed and save is allowed, since validation is done in the page
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
    END;

    [Test]
    PROCEDURE TestInvalidSourceIDError@74();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;

      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      CreateDefaultChart(Chart,ChartCode);

      // EXECUTE: Create a Chart wit an invalid source id
      SourceID := -RANDOM(100);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is displayed and save is not allowed.
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        STRSUBSTNO(SourceIDValidationError,TempGenericChartSetup."Source Type",SourceID),
        StripNewLines(GETLASTERRORTEXT),
        'Source ID is Validated');
    END;

    [Test]
    PROCEDURE TestDimensionsNonExistentFieldNameforTable@80();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      ShowXDimensionTitle := TRUE;
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      // EXECUTE: Save Chart with X dimensions set to negative field id
      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.VALIDATE("X-Axis Field ID",-RANDOM(100));
      TempGenericChartSetup.VALIDATE("X-Axis Field Name",Chart.ID);
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is displayed when dimension X is saved
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      CompareFilterStrings(
        STRSUBSTNO(DimensionMeasureInvalidErrorType0,TempGenericChartSetup."Source ID",Chart.ID,GetFieldClassFilter),
        StripNewLines(GETLASTERRORTEXT),
        'Error is returned when saving an X dimension field which is numeric');

      // EXECUTE: Save Chart with Z dimensions set to negative field id
      TempGenericChartSetup.DELETEALL;
      SetChartDimensions(TempGenericChartSetup,2,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.VALIDATE("Z-Axis Field ID",-RANDOM(100));
      TempGenericChartSetup.VALIDATE("Z-Axis Field Name",Chart.ID);
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is displayed when dimension Z is saved
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      CompareFilterStrings(
        STRSUBSTNO(DimensionMeasureInvalidErrorType0,TempGenericChartSetup."Source ID",Chart.ID,GetFieldClassFilter),
        StripNewLines(GETLASTERRORTEXT),
        'Error is returned when saving an Z dimension field which is numeric');
    END;

    [Test]
    PROCEDURE TestDimensionsNegativeFieldIdForTable@2();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      ShowXDimensionTitle := TRUE;
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      // EXECUTE: Save Chart with X dimensions set to negative field ID
      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.VALIDATE("X-Axis Field ID",-RANDOM(100));
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is not displayed and chart can be successfully saved
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        XDimensionID,TempGenericChartSetup."X-Axis Field ID",'X Axis Field ID Passed in Is overridden with the ID from Field Name');

      // EXECUTE: Save Chart with Z dimensions set to negative field ID
      TempGenericChartSetup.DELETEALL;
      SetChartDimensions(TempGenericChartSetup,2,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.VALIDATE("Z-Axis Field ID",-RANDOM(100));
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is not displayed and chart can be successfully saved
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        XDimensionID,TempGenericChartSetup."Z-Axis Field ID",'Z Axis Field ID Passed in Is overridden with the ID from Field Name');
    END;

    [Test]
    PROCEDURE TestMeasureNonExistentFieldNameForTable@7();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      MeasureName@1000 : Text[50];
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);
      MeasureIndex := 1;

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // EXECUTE: Save Chart with Measure set to Non-Existent field name
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName,MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      TempGenericChartYAxis.VALIDATE("Y-Axis Measure Field Name",Chart.ID);
      TempGenericChartYAxis.MODIFY;

      // VERIFY: Error is displayed when Measure is saved with a non existent field name
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      CompareFilterStrings(
        STRSUBSTNO(DimensionMeasureInvalidError,TempGenericChartSetup."Source ID",TypeFilterText(2),Chart.ID,GetFieldClassFilter),
        StripNewLines(GETLASTERRORTEXT),
        'Error is returned when saving an Measure field which is non-numeric');
    END;

    [Test]
    PROCEDURE TestMeasureNegativeFieldIdForTable@32();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      MeasureName@1000 : Text[50];
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);
      MeasureIndex := 1;

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // EXECUTE: Save Chart with Measure set to Negative field ID
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName,MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      TempGenericChartYAxis.VALIDATE("Y-Axis Measure Field ID",-RANDOM(100));
      TempGenericChartYAxis.MODIFY;

      // VERIFY: Measure can be saved with a negative field id
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        MeasureID[MeasureIndex],TempGenericChartYAxis."Y-Axis Measure Field ID",
        'Measure Field ID Passed in Is overridden with the ID from Field Name');
    END;

    [Test]
    PROCEDURE TestInvalidFilterFieldNameForTable@50();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      FilterIndex@1007 : Integer;
      FilterFieldName@1006 : Text[30];
      FilterFieldValue@1005 : Text[250];
      FilterFieldId@1004 : Integer;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);
      CreateDefaultChart(Chart,ChartCode);

      // Filters
      FilterIndex := 0;
      GetDefaultFilterInfoForTable(FilterFieldName,FilterFieldValue,FilterFieldId);

      // EXECUTE: Create a Chart with an filter containing and invalid field name
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      TempGenericChartFilter.VALIDATE("Filter Field Name",Chart.ID);
      TempGenericChartFilter.MODIFY;

      // VERIFY: Error is displayed and save is not allowed.
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        STRSUBSTNO(InvalidFilterFieldError,TempGenericChartSetup."Source ID",Chart.ID),
        StripNewLines(GETLASTERRORTEXT),
        'Error is returned when saving an Measure field which is non-numeric');
    END;

    [Test]
    PROCEDURE TestNegativeFilterFieldIDForTable@67();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      FilterIndex@1007 : Integer;
      FilterFieldName@1006 : Text[30];
      FilterFieldValue@1005 : Text[250];
      FilterFieldId@1004 : Integer;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);
      CreateDefaultChart(Chart,ChartCode);

      // Filters
      FilterIndex := 0;
      GetDefaultFilterInfoForTable(FilterFieldName,FilterFieldValue,FilterFieldId);

      // EXECUTE: Create a Chart wit an invalid source id
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      TempGenericChartFilter.VALIDATE("Filter Field ID",-RANDOM(100));
      TempGenericChartFilter.MODIFY;

      // VERIFY: Error is not displayed and save is allowed, since validation is done in the page.
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        FilterFieldId,TempGenericChartFilter."Filter Field ID",
        'Filter Field ID Passed in Is overridden with the ID from Field Name (if the id is invalid)');
    END;

    [Test]
    PROCEDURE TestInvalidFilterFieldvalueForTable@76();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      FilterIndex@1007 : Integer;
      FilterFieldName@1006 : Text[30];
      FilterFieldValue@1005 : Text[250];
      FilterFieldId@1004 : Integer;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);
      CreateDefaultChart(Chart,ChartCode);

      // Filters
      FilterIndex := 0;
      GetDefaultFilterInfoForTable(FilterFieldName,FilterFieldValue,FilterFieldId);

      // EXECUTE: Create a Chart with an filter containing and invalid field value
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      TempGenericChartFilter.VALIDATE("Filter Value",Chart.ID);
      TempGenericChartFilter.MODIFY;

      // VERIFY: Save is allowed.
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
    END;

    [Test]
    PROCEDURE TestInvalidFilterFieldNameForQuery@84();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      FilterIndex@1007 : Integer;
      FilterFieldName@1006 : Text[30];
      FilterFieldValue@1005 : Text[250];
      FilterFieldId@1004 : Integer;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceQueryInfo(SourceName,SourceID,SourceType);
      CreateDefaultChart(Chart,ChartCode);

      // Filters
      FilterIndex := 0;
      GetDefaultFilterInfoForQuery(FilterFieldName,FilterFieldValue,FilterFieldId);

      // EXECUTE: Create a Chart with an filter containing and invalid field name
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      TempGenericChartFilter.VALIDATE("Filter Field Name",Chart.ID);
      TempGenericChartFilter.MODIFY;

      // VERIFY: Error is displayed and save is not allowed.
      ASSERTERROR SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.VerifyFailure(NothingInsideTheFilter,'Error is returned when saving an Measure field which is non-numeric');
    END;

    [Test]
    PROCEDURE TestDescription@28();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      TempGenericChartCaptionsBuf@1007 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1006 : TEMPORARY Record 9186;
      GenericChartMgt@1000 : Codeunit 9180;
      ChartBuilder@1002 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      ChartCode@1016 : Code[20];
      RetrievedDescription@1003 : Text;
      Description@1004 : ARRAY [3] OF Text;
      index@1005 : Integer;
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);
      CreateDefaultChart(Chart,ChartCode);

      // EXECUTE: Create a Chart with an filter containing and invalid field value
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      Description[1] := '';
      Description[2] := ' ' + DefaultChartDescription + ' ';
      Description[3] := PADSTR(DefaultChartDescription,100,'') + '>CDATA[&';

      FOR index := 1 TO ARRAYLEN(Description) DO BEGIN
        // VERIFY: Save is allowed. Retrieve and verify the saved value
        TempGenericChartMemoBuf.SetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage,Description[index]);
        GenericChartMgt.SaveChanges(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter,
          TempGenericChartCaptionsBuf,TempGenericChartMemoBuf);
        Chart.MODIFY;

        TempGenericChartSetup.DELETEALL;
        TempGenericChartYAxis.DELETEALL;
        TempGenericChartFilter.DELETEALL;

        // Retrieve fields.
        CreateBuilderFromChart(Chart,ChartBuilder);

        // VERIFY: Retrieve fields using chart builder and verify.
        GenericChartMgt.RetrieveXML(Chart,TempGenericChartSetup,TempGenericChartYAxis,
          TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);
        RetrievedDescription := TempGenericChartMemoBuf.GetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage);
        Assert.AreEqual(
          Description[index],
          RetrievedDescription,
          STRSUBSTNO('Index: %1 Retrieved Descripton from XML Matches',index));
        Assert.AreEqual(
          Description[index],
          ChartBuilder.Description,
          STRSUBSTNO('Index: %1 Retrieved Descripton from ChartBuilder Matches',index));
      END;
    END;

    [Test]
    PROCEDURE TestTableChartCountMeasureBlankFieldName@88();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1007 : TEMPORARY Record 9180;
      TempGenericChartFilter@1006 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1005 : TEMPORARY Record 9182;
      TempGenericChartCaptionsBuf@1010 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1002 : TEMPORARY Record 9186;
      GenericChartMgt@1004 : Codeunit 9180;
      ChartBuilder@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      DataMeasureType@1000 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1003 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      CountOfMeasures@1020 : Integer;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      MeasureName@1030 : ARRAY [4] OF Text[50];
      ChartCode@1008 : Code[20];
    BEGIN
      // SETUP: Create a table based chart with X Dimension and 4 measures
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      CountOfMeasures := 4;
      // Y Axis, Index is zero based in chart builder
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // Add Measures
      FOR MeasureIndex := 1 TO 4 DO BEGIN
        AddMeasureToChart(
          TempGenericChartSetup,TempGenericChartYAxis,
          MeasureName[MeasureIndex],MeasureID[MeasureIndex],
          MeasureIndex - 1,DataMeasureType[MeasureIndex],
          DataAggregationType[MeasureIndex]);
        IF MeasureIndex = 1 THEN BEGIN
          DataAggregationType[MeasureIndex] := DataAggregationType[MeasureIndex].Count;
          MeasureName[MeasureIndex] := '';
          MeasureID[MeasureIndex] := 0;
          TempGenericChartYAxis."Y-Axis Measure Field ID" := MeasureID[MeasureIndex];
          TempGenericChartYAxis."Y-Axis Measure Field Name" := MeasureName[MeasureIndex];
          TempGenericChartYAxis.Aggregation := ConvertAggregationToOption(DataAggregationType[MeasureIndex]);
          TempGenericChartYAxis.MODIFY;
        END;
      END;

      // EXECUTE: Save the Chart and retrieve fields using chart builder.
      // Save
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;

      TempGenericChartSetup.DELETEALL;
      TempGenericChartYAxis.DELETEALL;
      TempGenericChartFilter.DELETEALL;

      // Retrieve fields.
      CreateBuilderFromChart(Chart,ChartBuilder);
      FindChartByID(Chart,ChartCode);

      // VERIFY: Retrieve fields using chart builder and verify.
      GenericChartMgt.RetrieveXML(
        Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);
      // Verify Measure
      Assert.AreEqual(CountOfMeasures,ChartBuilder.MeasureCount,'Count of Measures Match');
      FOR MeasureIndex := 1 TO 4 DO BEGIN
        VerifyChartMeasure(ChartBuilder,
          TRUE,MeasureIndex - 1,
          MeasureID[MeasureIndex],
          MeasureName[MeasureIndex],
          DataMeasureType[MeasureIndex],
          DataAggregationType[MeasureIndex]);
        VerifyChartMeasureAgainstTable(ChartBuilder,TempGenericChartYAxis,TRUE,MeasureIndex - 1);
      END;
    END;

    [Test]
    PROCEDURE TestDimensionsIdAndNameMismatchForTable@44();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      XDimensionID@1017 : Integer;
      XDimensionName@1018 : Text[80];
      ShowXDimensionTitle@1024 : Boolean;
      MeasureID@1028 : ARRAY [4] OF Integer;
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
      ZDimensionID@1004 : Integer;
      ZDimensionName@1003 : Text[80];
      ShowZDimensionTitle@1000 : Boolean;
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);

      // X Axis
      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // Z Axis
      GetDefaultZDimensionsInfoForTable(ZDimensionID,ZDimensionName,ShowZDimensionTitle);

      ShowXDimensionTitle := TRUE;
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);

      // EXECUTE: Save Chart with X dimensions set to negative field ID
      SetChartDimensions(TempGenericChartSetup,1,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.VALIDATE("X-Axis Field Name",ZDimensionName);
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is not displayed and chart can be successfully saved
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        XDimensionName,TempGenericChartSetup."X-Axis Field Name",
        'X Axis Field Name Passed in Is overridden with the Name from Field ID');

      // EXECUTE: Save Chart with Z dimensions set to negative field ID
      TempGenericChartSetup.DELETEALL;
      SetChartDimensions(TempGenericChartSetup,2,XDimensionID,XDimensionName,XDimensionName,ShowXDimensionTitle);
      TempGenericChartSetup.VALIDATE("Z-Axis Field Name",ZDimensionName);
      TempGenericChartSetup.INSERT;

      // VERIFY: Error is not displayed and chart can be successfully saved
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        XDimensionName,TempGenericChartSetup."Z-Axis Field Name",
        'Z Axis Field Name Passed in Is overridden with the Name from Field ID');
    END;

    [Test]
    PROCEDURE TestMeasureFieldIdAndNameMismatchForTable@57();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      DataMeasureType@1012 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      DataAggregationType@1011 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      MeasureIndex@1027 : Integer;
      MeasureID@1028 : ARRAY [4] OF Integer;
      MeasureName@1000 : Text[50];
      HasMeasure@1029 : ARRAY [4] OF Boolean;
      ChartCode@1016 : Code[20];
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);

      // Y Axis, Index is Zero based
      MeasureIndex := 0;
      GetTableMeasures(HasMeasure,MeasureID,DataMeasureType,DataAggregationType);
      MeasureIndex := 1;

      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // EXECUTE: Save Chart with Measure set to Negative field ID
      AddMeasureToChart(
        TempGenericChartSetup,TempGenericChartYAxis,
        MeasureName,MeasureID[MeasureIndex],
        MeasureIndex - 1,DataMeasureType[MeasureIndex],
        DataAggregationType[MeasureIndex]);

      TempGenericChartYAxis.VALIDATE(
        "Y-Axis Measure Field Name",GetFieldNameFromID(TempGenericChartSetup."Source ID",MeasureID[MeasureIndex + 1]));
      TempGenericChartYAxis.MODIFY;

      // VERIFY: Measure can be saved with a negative field id
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        MeasureName,TempGenericChartYAxis."Y-Axis Measure Field Name",
        'Measure Field Name Passed in Is overridden with the Name from Field ID');
    END;

    [Test]
    PROCEDURE TestFilterFieldIDAndNameMismatchForTable@92();
    VAR
      Chart@1001 : Record 2000000078;
      TempGenericChartSetup@1010 : TEMPORARY Record 9180;
      TempGenericChartFilter@1009 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      SourceName@1013 : Text[30];
      SourceID@1014 : Integer;
      SourceType@1015 : Option;
      FilterIndex@1007 : Integer;
      FilterFieldName@1006 : Text[30];
      FilterFieldValue@1005 : Text[250];
      FilterFieldId@1004 : Integer;
      ChartCode@1016 : Code[20];
      XDimensionID@1003 : Integer;
      XDimensionName@1002 : Text[80];
      ShowXDimensionTitle@1000 : Boolean;
    BEGIN
      // SETUP: Create a Source Chart
      Initialize;
      GetDefaultSourceTableInfo(SourceName,SourceID,SourceType);
      CreateDefaultChart(Chart,ChartCode);

      // Filters
      FilterIndex := 0;
      GetDefaultFilterInfoForTable(FilterFieldName,FilterFieldValue,FilterFieldId);

      GetDefaultXDimensionsInfoForTable(XDimensionID,XDimensionName,ShowXDimensionTitle);

      // EXECUTE: Create a Chart wit an invalid source id
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType,SourceID,SourceName);
      TempGenericChartSetup.INSERT;

      // Add Filter
      AddFiltersToChart(TempGenericChartSetup,TempGenericChartFilter,FilterFieldId,FilterFieldName,FilterIndex,FilterFieldValue);
      TempGenericChartFilter.VALIDATE("Filter Field Name",COPYSTR(XDimensionName,1,30));
      TempGenericChartFilter.MODIFY;

      // VERIFY: Error is not displayed and save is allowed, since validation is done in the page.
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Chart.MODIFY;
      RetrieveChartWithoutDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      Assert.AreEqual(
        FilterFieldName,TempGenericChartFilter."Filter Field Name",
        'Filter Field ID Passed in Is overridden with the ID from Field Name (if the id is invalid)');
    END;

    [Test]
    PROCEDURE TestRetrieveFieldColumnIdFromNameTable@23();
    VAR
      Field@1005 : Record 2000000041;
      GenericChartMgt@1000 : Codeunit 9180;
      ObjType@1001 : ' ,Table,Query';
      ObjID@1003 : Integer;
      FoundID@1002 : Integer;
      Step@1006 : Integer;
    BEGIN
      Field.SETRANGE(TableNo,DATABASE::"Sales Header");
      Field.FINDSET;
      Step := RANDOM(Field.COUNT);
      IF Step = Field.COUNT THEN
        Step := Field.COUNT - 1;
      Field.NEXT(Step);
      ObjType := ObjType::Table;
      ObjID := DATABASE::"Sales Header";
      GenericChartMgt.RetrieveFieldColumnIDFromName(ObjType,ObjID,FoundID,Field.FieldName);
      Assert.AreEqual(Field."No.",FoundID,'Retrieving name of field in a table based on table no and field no');
    END;

    [Test]
    PROCEDURE TestRetrieveFieldColumnIdFromNameQuery@29();
    VAR
      GenericChartMgt@1004 : Codeunit 9180;
      ObjType@1003 : ' ,Table,Query';
      ObjID@1002 : Integer;
      FoundID@1000 : Integer;
    BEGIN
      ObjType := ObjType::Query;
      ObjID := QUERY::"Trailing Sales Order Qry";
      GenericChartMgt.RetrieveFieldColumnIDFromName(ObjType,ObjID,FoundID,'ShipmentDate');
      Assert.AreEqual(2,FoundID,'Retrieving name of column in a query based on table no and field no');
      GenericChartMgt.RetrieveFieldColumnIDFromName(ObjType,ObjID,FoundID,'Status');
      Assert.AreEqual(3,FoundID,'Retrieving name of column in a query based on table no and field no');
    END;

    [Test]
    PROCEDURE TestBuildFilterText@22();
    VAR
      GenericChartMgt@1005 : Codeunit 9180;
      FText@1000 : Text[250];
      Inp@1001 : Text[100];
      ExpectedText@1002 : Text[250];
    BEGIN
      MakeRandomTextString(RANDOM(100),FText);
      MakeRandomTextString(RANDOM(100),Inp);
      ExpectedText := COPYSTR(FText + ' ; ' + Inp,1,MAXSTRLEN(ExpectedText));
      GenericChartMgt.BuildFilterText(FText,Inp);
      Assert.AreEqual(ExpectedText,FText,'Building filter text without exceeding maximum length');
      MakeRandomTextString(240,FText);
      MakeRandomTextString(20,Inp);
      ExpectedText := COPYSTR(FText + ',...',1,MAXSTRLEN(ExpectedText));
      GenericChartMgt.BuildFilterText(FText,Inp);
      Assert.AreEqual(ExpectedText,FText,'Building filter text length 240 with exceeding of maximum length');
      MakeRandomTextString(248,FText);
      MakeRandomTextString(20,Inp);
      ExpectedText := COPYSTR(FText + '..',1,MAXSTRLEN(ExpectedText));
      GenericChartMgt.BuildFilterText(FText,Inp);
      Assert.AreEqual(ExpectedText,FText,'Building filter text length 248 with exceeding of maximum length');
    END;

    [Test]
    PROCEDURE TestGetDescriptionFunction@37();
    VAR
      TempGenericChartSetup@1003 : TEMPORARY Record 9180;
      TempGenericChartYAxis@1004 : TEMPORARY Record 9182;
      TempGenericChartFilter@1005 : TEMPORARY Record 9181;
      Chart@1000 : Record 2000000078;
      GenericChartMgt@1002 : Codeunit 9180;
      ChartCode@1001 : Code[20];
      SourceType@1006 : ' ,Table,Query';
      ActualMsg@1008 : Text;
      ExpectedMsg@1009 : Text[250];
    BEGIN
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType::Table,DATABASE::"Sales Header",TableSalesheader);
      TempGenericChartSetup.INSERT;
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      ExpectedMsg := DefaultChartDescription;
      ActualMsg := GenericChartMgt.GetDescription(Chart);
      Assert.AreEqual(ExpectedMsg,ActualMsg,'Function GenericChartMgt.GetDescription');
    END;

    [Test]
    [HandlerFunctions(HandleTooManyMeasuresMessage)]
    PROCEDURE TestChartCustomization@30();
    VAR
      TempGenericChartSetup@1009 : TEMPORARY Record 9180;
      TempGenericChartYAxis@1008 : TEMPORARY Record 9182;
      TempGenericChartFilter@1007 : TEMPORARY Record 9181;
      Chart@1006 : Record 2000000078;
      TempChart@1001 : TEMPORARY Record 2000000078;
      GenericChartMgt@1005 : Codeunit 9180;
      ChartCode@1004 : Code[20];
      SourceType@1003 : ' ,Table,Query';
      OutputVal@1010 : Boolean;
    BEGIN
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType::Table,DATABASE::"Sales Header",TableSalesheader);
      TempGenericChartSetup.INSERT;
      AddTooManyMeasuresToChart(Chart.ID,TempGenericChartYAxis);
      SaveChartWithDefaultDescription(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter);
      TempChart := Chart;
      OutputVal := GenericChartMgt.ChartCustomization(TempChart);
      Assert.IsFalse(OutputVal,'Return value from ChartCustomization');
    END;

    [Test]
    PROCEDURE TestGetQueryCountColumnName@45();
    VAR
      TempGenericChartSetup@1002 : TEMPORARY Record 9180;
      Chart@1001 : Record 2000000078;
      GenericChartMgt@1006 : Codeunit 9180;
      ChartCode@1000 : Code[20];
      SourceType@1003 : ' ,Table,Query';
      CountColumnName@1005 : Text[50];
    BEGIN
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(Chart,TempGenericChartSetup,SourceType::Query,QUERY::"Acc. Sched. Line Desc. Count",TableSalesheader);
      TempGenericChartSetup.INSERT;
      CountColumnName := GenericChartMgt.GetQueryCountColumnName(TempGenericChartSetup);
      Assert.AreEqual('Count_',CountColumnName,'Retrieval of name of column with aggregation = Count');
    END;

    [Test]
    PROCEDURE TestSetAggregationType@46();
    VAR
      TempGenericChartQueryColumn@1000 : TEMPORARY Record 9183;
    BEGIN
      CLEAR(TempGenericChartQueryColumn);
      WITH TempGenericChartQueryColumn DO BEGIN
        INSERT;
        SetAggregationType('none');
        Assert.IsTrue("Aggregation Type" = "Aggregation Type"::None,'Setting aggregation type for query column to None');
        SetAggregationType('count');
        Assert.IsTrue("Aggregation Type" = "Aggregation Type"::Count,'Setting aggregation type for query column to Count');
        SetAggregationType('sum');
        Assert.IsTrue("Aggregation Type" = "Aggregation Type"::Sum,'Setting aggregation type for query column to Sum');
        SetAggregationType('min');
        Assert.IsTrue("Aggregation Type" = "Aggregation Type"::Min,'Setting aggregation type for query column to Min');
        SetAggregationType('max');
        Assert.IsTrue("Aggregation Type" = "Aggregation Type"::Max,'Setting aggregation type for query column to Max');
        SetAggregationType('average');
        Assert.IsTrue("Aggregation Type" = "Aggregation Type"::Avg,'Setting aggregation type for query column to Average');
      END;
    END;

    [Test]
    PROCEDURE TestSetTitle@35();
    VAR
      GenericChartSetup@1000 : Record 9180;
    BEGIN
      GenericChartSetup.INIT;
      GenericChartSetup.VALIDATE(Name,'TestName');
    END;

    [Test]
    PROCEDURE TestYAxisMeasureFieldID@48();
    VAR
      TempGenericChartYAxis@1000 : TEMPORARY Record 9182;
    BEGIN
      //TAB9182
      WITH TempGenericChartYAxis DO BEGIN
        INIT;
        "Y-Axis Measure Field ID" := 1;
        INSERT(TRUE);
        DELETE;
      END;
    END;

    [Test]
    PROCEDURE TestAggregations@49();
    VAR
      GenericChartQueryColumn@1018 : Record 9183;
    BEGIN
      //TAB9183
      WITH GenericChartQueryColumn DO BEGIN
        SetAggregationType(Text000);
        TESTFIELD("Aggregation Type","Aggregation Type"::None);
        SetAggregationType(Text001);
        TESTFIELD("Aggregation Type","Aggregation Type"::Count);
        SetAggregationType(Text002);
        TESTFIELD("Aggregation Type","Aggregation Type"::Sum);
        SetAggregationType(Text003);
        TESTFIELD("Aggregation Type","Aggregation Type"::Min);
        SetAggregationType(Text004);
        TESTFIELD("Aggregation Type","Aggregation Type"::Max);
        SetAggregationType(Text005);
        TESTFIELD("Aggregation Type","Aggregation Type"::Avg);
      END;
    END;

    [Test]
    PROCEDURE TestFieldTypeMapping@53();
    VAR
      GenericChartQueryColumn@1018 : Record 9183;
      Field@1000 : Record 2000000041;
    BEGIN
      WITH GenericChartQueryColumn DO BEGIN
        SetAndVerifyColumnDataType(Field.Type::Date,"Column Data Type"::Date);
        SetAndVerifyColumnDataType(Field.Type::Time,"Column Data Type"::Time);
        SetAndVerifyColumnDataType(Field.Type::DateFormula,"Column Data Type"::DateFormula);
        SetAndVerifyColumnDataType(Field.Type::Decimal,"Column Data Type"::Decimal);
        SetAndVerifyColumnDataType(Field.Type::Text,"Column Data Type"::Text);
        SetAndVerifyColumnDataType(Field.Type::Code,"Column Data Type"::Code);
        SetAndVerifyColumnDataType(Field.Type::Binary,"Column Data Type"::Binary);
        SetAndVerifyColumnDataType(Field.Type::Boolean,"Column Data Type"::Boolean);
        SetAndVerifyColumnDataType(Field.Type::Integer,"Column Data Type"::Integer);
        SetAndVerifyColumnDataType(Field.Type::Option,"Column Data Type"::Option);
        SetAndVerifyColumnDataType(Field.Type::BigInteger,"Column Data Type"::BigInteger);
        SetAndVerifyColumnDataType(Field.Type::DateTime,"Column Data Type"::DateTime);
      END;
    END;

    [Test]
    PROCEDURE TestFilters@51();
    VAR
      TempGenericChartFilter@1000 : TEMPORARY Record 9181;
      GenericChartFilters@1001 : Page 9181;
      i@1002 : Integer;
      j@1003 : Integer;
    BEGIN
      j := RANDOM(20);
      FOR i := 1 TO j DO BEGIN
        TempGenericChartFilter.ID := '';
        TempGenericChartFilter."Line No." := 10000 * i;
        TempGenericChartFilter."Filter Field ID" := i;
        TempGenericChartFilter."Filter Field Name" := FORMAT(i);
        TempGenericChartFilter."Filter Value" := FORMAT(i);
        TempGenericChartFilter.INSERT;
      END;
      GenericChartFilters.SetFilters(TempGenericChartFilter);
      TempGenericChartFilter.DELETEALL;
      GenericChartFilters.GetFilters(TempGenericChartFilter);
      Assert.AreEqual(j,TempGenericChartFilter.COUNT,'Number of TempGenericChart filter records retrieved.');
      TempGenericChartFilter.FINDSET;
      i := 0;
      REPEAT
        i += 1;
        TempGenericChartFilter.TESTFIELD(ID,'');
        TempGenericChartFilter.TESTFIELD("Line No.",10000 * i);
        TempGenericChartFilter.TESTFIELD("Filter Field ID",i);
        TempGenericChartFilter.TESTFIELD("Filter Field Name",FORMAT(i));
        TempGenericChartFilter.TESTFIELD("Filter Value",FORMAT(i));
      UNTIL TempGenericChartFilter.NEXT = 0;
    END;

    [Test]
    PROCEDURE TestCompanyFilter@52();
    VAR
      GenericCharts@1000 : TestPage 9182;
    BEGIN
      // Test of Onopen trigger
      GenericCharts.OPENVIEW;
      GenericCharts.CLOSE;
    END;

    [Test]
    [HandlerFunctions(HandleMemoTooLongMessage)]
    PROCEDURE TestMemo@83();
    VAR
      TempGenericChartMemoBuf@1000 : TEMPORARY Record 9186;
      TooLongText@1001 : Text;
    BEGIN
      TempGenericChartMemoBuf.Code := '';
      TempGenericChartMemoBuf."Language Code" := DANLanguageCodeTxt;
      TempGenericChartMemoBuf.SetMemoText(DANLanguageMemoTxt);
      TempGenericChartMemoBuf.INSERT;
      TempGenericChartMemoBuf."Language Code" := ENULanguageCodeTxt;
      TempGenericChartMemoBuf.SetMemoText(ENULanguageMemoTxt);
      TempGenericChartMemoBuf.INSERT;
      TempGenericChartMemoBuf."Language Code" := LTHLanguageCodeTxt;
      TempGenericChartMemoBuf.SetMemoText(LTHLanguageMemoTxt);
      TempGenericChartMemoBuf.INSERT;
      TempGenericChartMemoBuf.GET('',ENULanguageCodeTxt);
      Assert.AreEqual(FORMAT(ENULanguageMemoTxt),TempGenericChartMemoBuf.GetMemoText,MemoTxt);

      TooLongText := PADSTR(TooLongText,2501,'A');
      TempGenericChartMemoBuf.SetMemoText(TooLongText);
    END;

    [Test]
    PROCEDURE TestCustomerBalanceByCityAndCurrency@81();
    VAR
      Cust@1000 : Record 18;
      Chart@1001 : Record 2000000078;
      ChartSetup@1002 : TestPage 9183;
      ChartBuilder@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      ChartName@1004 : Text[30];
      ChartID@1005 : Code[20];
    BEGIN
      ChartID := 'TST-ALLFIELDS-1';
      ChartName := 'Customer Balance By City';
      ValidateEntryDoesNotExist(ChartID);
      OpenAndInitializeCustomerBalanceByCity(ChartSetup,ChartID,ChartName);
      ChartSetup.Description.SETVALUE(DescriptionTxt);
      ChartSetup."Z-Axis Field".SETVALUE(Cust.FIELDNAME("Currency Code"));
      ChartSetup."Data Point Z Label".SETVALUE(ZAxisDatapointTxt);
      CommitCustomerBalanceByCity(ChartSetup,ChartID,ChartName);

      GetChartFromDB(Chart,ChartID);
      ValidateChartEntryExist(Chart,ChartID,ChartName);
      GetChartBuilder(Chart,ChartBuilder);
      ValidateStandardCustomerBalanceByCityValues(ChartBuilder);
      Assert.AreEqual(FORMAT(DescriptionTxt),ChartBuilder.Description,TestNameDescriptionTxt);
      Assert.AreEqual(Cust.FIELDNAME("Currency Code"),ChartBuilder.ZDimensionName,ZAxisTestNameTxt);
      Assert.AreEqual(FORMAT(ZAxisDatapointTxt),ChartBuilder.ZDimensionCaption,TestNameZAxisDatapointTxt);

      RemoveChartFromDB(ChartID);
    END;

    [Test]
    [HandlerFunctions(MemoEditorRunModalHandler)]
    PROCEDURE TestCustomerBalanceByCityAndDescriptionWithAssistEdit@79();
    VAR
      Chart@1000 : Record 2000000078;
      GenericChartMgt@1001 : Codeunit 9180;
      ChartSetup@1002 : TestPage 9183;
      ChartBuilder@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      ChartName@1004 : Text[30];
      ChartID@1005 : Code[20];
      Index@1006 : Integer;
    BEGIN
      ChartID := 'ENU=TST-MEMO-1';
      ChartName := 'Memo Test Chart';
      ValidateEntryDoesNotExist(ChartID);
      OpenAndInitializeCustomerBalanceByCity(ChartSetup,ChartID,ChartName);
      Assert.AreEqual('',ChartSetup.Description.VALUE,TestNameDescriptionTxt);
      ChartSetup.Description.ASSISTEDIT;
      CommitCustomerBalanceByCity(ChartSetup,ChartID,ChartName);
      Assert.AreEqual(FORMAT(DescriptionTxt),ChartSetup.Description.VALUE,TestNameDescriptionTxt);

      GetChartFromDB(Chart,ChartID);
      ValidateChartEntryExist(Chart,ChartID,ChartName);
      GetChartBuilder(Chart,ChartBuilder);
      ValidateStandardCustomerBalanceByCityValues(ChartBuilder);

      Assert.AreEqual(4,ChartBuilder.GetMultilanguageDescription.Count,TestNameDescriptionTxtLanguageCountTxt);
      Assert.AreEqual(FORMAT(DescriptionTxt),
        ChartBuilder.GetMultilanguageDescription.GetText(GenericChartMgt.GetUserLanguage),TestNameDescriptionTxt);
      FOR Index := 1 TO GetMaxLanguageCount DO
        Assert.AreEqual(FORMAT(DescriptionTxt + GetLanguage(Index)),
          ChartBuilder.GetMultilanguageDescription.GetText(GetLanguage(Index)),TestNameDescriptionTxt + ' - ' + FORMAT(Index));

      RemoveChartFromDB(ChartID);
    END;

    [Test]
    [HandlerFunctions(CaptionEditorRunModalHandler)]
    PROCEDURE TestCustomerBalanceByCityAndMeasureCaptionWithAssistEdit@78();
    VAR
      Chart@1001 : Record 2000000078;
      GenericChartMgt@1002 : Codeunit 9180;
      ChartSetup@1000 : TestPage 9183;
      ChartBuilder@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      MultilanguageText@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartMultiLanguageText";
      ChartName@1005 : Text[30];
      ChartID@1006 : Code[20];
      Index@1007 : Integer;
    BEGIN
      ChartID := 'TST-CAP-1';
      ChartName := 'Test Measure Caption Name';
      ValidateEntryDoesNotExist(ChartID);
      OpenAndInitializeCustomerBalanceByCity(ChartSetup,ChartID,ChartName);
      Assert.AreEqual(
        FORMAT(RequiredMeasureCaptionTxt),FORMAT(ChartSetup.RequiredMeasureCaption.VALUE),TestNameRequiredMeasureCaptionTxt);
      ChartSetup.RequiredMeasureCaption.ASSISTEDIT;
      CommitCustomerBalanceByCity(ChartSetup,ChartID,ChartName);
      Assert.AreEqual(FORMAT(RequiredMeasureCaptionChangedTxt),
        FORMAT(ChartSetup.RequiredMeasureCaption.VALUE),TestNameRequiredMeasureCaptionTxt);

      ChartSetup.RequiredMeasureCaption.SETVALUE(RequiredMeasureCaptionTxt);
      CommitCustomerBalanceByCity(ChartSetup,ChartID,ChartName);

      GetChartFromDB(Chart,ChartID);
      ValidateChartEntryExist(Chart,ChartID,ChartName);
      GetChartBuilder(Chart,ChartBuilder);
      ValidateStandardCustomerBalanceByCityValues(ChartBuilder);

      MultilanguageText := ChartBuilder.GetMultilanguageMeasureCaption(0);
      Assert.AreEqual(4,MultilanguageText.Count,TestNameRequiredMeasureLanguageCountTxt);
      Assert.AreEqual(FORMAT(RequiredMeasureCaptionTxt),
        MultilanguageText.GetText(GenericChartMgt.GetUserLanguage),TestNameRequiredMeasureCaptionTxt);
      FOR Index := 1 TO GetMaxLanguageCount DO
        Assert.AreEqual(FORMAT(RequiredMeasureCaptionChangedTxt + GetLanguage(Index)),
          MultilanguageText.GetText(GetLanguage(Index)),TestNameRequiredMeasureCaptionTxt + ' - ' + FORMAT(Index));

      RemoveChartFromDB(ChartID);
    END;

    [Test]
    PROCEDURE TestCustomerBalanceByCityUsing6Measures@71();
    VAR
      GenericChartYAxis@1000 : Record 9182;
      Chart@1001 : Record 2000000078;
      Cust@1002 : Record 18;
      ChartSetup@1003 : TestPage 9183;
      ChartBuilder@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";
      AggregationType@1005 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      MeasureType@1006 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
      ChartName@1007 : Text[30];
      ChartID@1008 : Code[20];
    BEGIN
      ChartName := 'Customer Balance By City';
      ChartID := 'TST-ALLFIELDS-1';

      ValidateEntryDoesNotExist(ChartID);
      OpenAndInitializeCustomerBalanceByCity(ChartSetup,ChartID,ChartName);
      ChartSetup.Description.SETVALUE(DescriptionTxt);

      ChartSetup.OptionalMeasureColumn1.SETVALUE(Cust.FIELDNAME("Budgeted Amount"));
      ChartSetup.OptionalMeasureAggregation1.SETVALUE(GenericChartYAxis.Aggregation::Min);
      ChartSetup.OptionalMeasureType1.SETVALUE(GenericChartYAxis."Chart Type"::Area);
      ChartSetup.OptionalMeasureCaption1.SETVALUE(OptionalMeasureCaptionTxt + '1');
      ChartSetup.OptionalMeasureColumn2.SETVALUE(Cust.FIELDNAME("Credit Limit (LCY)"));
      ChartSetup.OptionalMeasureAggregation2.SETVALUE(GenericChartYAxis.Aggregation::Max);
      ChartSetup.OptionalMeasureType2.SETVALUE(GenericChartYAxis."Chart Type"::Point);
      ChartSetup.OptionalMeasureCaption2.SETVALUE(OptionalMeasureCaptionTxt + '2');
      ChartSetup.OptionalMeasureColumn3.SETVALUE(Cust.FIELDNAME("Sales (LCY)"));
      ChartSetup.OptionalMeasureAggregation3.SETVALUE(GenericChartYAxis.Aggregation::Avg);
      ChartSetup.OptionalMeasureType3.SETVALUE(GenericChartYAxis."Chart Type"::Line);
      ChartSetup.OptionalMeasureCaption3.SETVALUE(OptionalMeasureCaptionTxt + '3');
      ChartSetup.OptionalMeasureColumn4.SETVALUE(Cust.FIELDNAME("Profit (LCY)"));
      ChartSetup.OptionalMeasureAggregation4.SETVALUE(GenericChartYAxis.Aggregation::Sum);
      ChartSetup.OptionalMeasureType4.SETVALUE(GenericChartYAxis."Chart Type"::Range);
      ChartSetup.OptionalMeasureCaption4.SETVALUE(OptionalMeasureCaptionTxt + '4');
      ChartSetup.OptionalMeasureColumn5.SETVALUE(Cust.FIELDNAME("Payments (LCY)"));
      ChartSetup.OptionalMeasureAggregation5.SETVALUE(GenericChartYAxis.Aggregation::Min);
      ChartSetup.OptionalMeasureType5.SETVALUE(GenericChartYAxis."Chart Type"::Range);
      ChartSetup.OptionalMeasureCaption5.SETVALUE(OptionalMeasureCaptionTxt + '5');

      CommitCustomerBalanceByCity(ChartSetup,ChartID,ChartName);

      GetChartFromDB(Chart,ChartID);
      ValidateChartEntryExist(Chart,ChartID,ChartName);
      GetChartBuilder(Chart,ChartBuilder);
      ValidateStandardCustomerBalanceByCityValues(ChartBuilder);
      Assert.AreEqual(FORMAT(DescriptionTxt),ChartBuilder.Description,TestNameDescriptionTxt);

      Assert.AreEqual(Cust.FIELDNAME("Budgeted Amount"),ChartBuilder.GetMeasureName(1),TestNameOptionalMeasureColumnTxt);
      Assert.AreEqual(FORMAT(AggregationType.Min),FORMAT(ChartBuilder.GetMeasureOperator(1)),TestNameOptionalMeasureAggregationTxt);
      Assert.AreEqual(FORMAT(MeasureType.Area),FORMAT(ChartBuilder.GetMeasureChartType(1)),TestNameOptionalMeasureTypeTxt);
      Assert.AreEqual(FORMAT(OptionalMeasureCaptionTxt + '1'),FORMAT(ChartBuilder.GetMeasureCaption(1)),
        TestNameOptionalMeasureCaptionTxt);
      Assert.AreEqual(Cust.FIELDNAME("Credit Limit (LCY)"),ChartBuilder.GetMeasureName(2),TestNameOptionalMeasureColumnTxt);
      Assert.AreEqual(FORMAT(AggregationType.Max),FORMAT(ChartBuilder.GetMeasureOperator(2)),TestNameOptionalMeasureAggregationTxt);
      Assert.AreEqual(FORMAT(MeasureType.Point),FORMAT(ChartBuilder.GetMeasureChartType(2)),TestNameOptionalMeasureTypeTxt);
      Assert.AreEqual(FORMAT(OptionalMeasureCaptionTxt + '2'),FORMAT(ChartBuilder.GetMeasureCaption(2)),
        TestNameOptionalMeasureCaptionTxt);
      Assert.AreEqual(Cust.FIELDNAME("Sales (LCY)"),ChartBuilder.GetMeasureName(3),TestNameOptionalMeasureColumnTxt);
      Assert.AreEqual(FORMAT(AggregationType.Avg),FORMAT(ChartBuilder.GetMeasureOperator(3)),TestNameOptionalMeasureAggregationTxt);
      Assert.AreEqual(FORMAT(MeasureType.Line),FORMAT(ChartBuilder.GetMeasureChartType(3)),TestNameOptionalMeasureTypeTxt);
      Assert.AreEqual(FORMAT(OptionalMeasureCaptionTxt + '3'),FORMAT(ChartBuilder.GetMeasureCaption(3)),
        TestNameOptionalMeasureCaptionTxt);
      Assert.AreEqual(Cust.FIELDNAME("Profit (LCY)"),ChartBuilder.GetMeasureName(4),TestNameOptionalMeasureColumnTxt);
      Assert.AreEqual(FORMAT(AggregationType.Sum),FORMAT(ChartBuilder.GetMeasureOperator(4)),TestNameOptionalMeasureAggregationTxt);
      Assert.AreEqual(FORMAT(MeasureType.Range),FORMAT(ChartBuilder.GetMeasureChartType(4)),TestNameOptionalMeasureTypeTxt);
      Assert.AreEqual(FORMAT(OptionalMeasureCaptionTxt + '4'),FORMAT(ChartBuilder.GetMeasureCaption(4)),
        TestNameOptionalMeasureCaptionTxt);
      Assert.AreEqual(Cust.FIELDNAME("Payments (LCY)"),ChartBuilder.GetMeasureName(5),TestNameOptionalMeasureColumnTxt);
      Assert.AreEqual(FORMAT(AggregationType.Min),FORMAT(ChartBuilder.GetMeasureOperator(5)),TestNameOptionalMeasureAggregationTxt);
      Assert.AreEqual(FORMAT(MeasureType.Range),FORMAT(ChartBuilder.GetMeasureChartType(5)),TestNameOptionalMeasureTypeTxt);
      Assert.AreEqual(FORMAT(OptionalMeasureCaptionTxt + '5'),FORMAT(ChartBuilder.GetMeasureCaption(5)),
        TestNameOptionalMeasureCaptionTxt);

      RemoveChartFromDB(ChartID);
    END;

    [Test]
    PROCEDURE TestDuplicatedMultilanguageDescriptionAndCaption@55();
    VAR
      Chart@1000 : Record 2000000078;
      TempGenericChartSetup@1007 : TEMPORARY Record 9180;
      TempGenericChartFilter@1006 : TEMPORARY Record 9181;
      TempGenericChartYAxis@1005 : TEMPORARY Record 9182;
      TempGenericChartCaptionsBuf@1004 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1003 : TEMPORARY Record 9186;
      GLAccount@1008 : Record 15;
      GenericChartMgt@1002 : Codeunit 9180;
      ChartCode@1001 : Code[20];
    BEGIN
      // [SCENARIO 378072] RetriveXML for Chart where tags with the same language code are used for Description and Caption
      // [GIVEN] Chart with <ENU> language for Description and Caption
      CreateDefaultChart(Chart,ChartCode);
      CreateGeneratorFromChart(
        Chart,TempGenericChartSetup,TempGenericChartSetup."Source Type"::Table,DATABASE::"G/L Account",GLAccount.TABLENAME);
      TempGenericChartSetup."X-Axis Field ID" := GLAccount.FIELDNO("No.");
      TempGenericChartSetup."X-Axis Show Title" := TRUE;
      TempGenericChartSetup.INSERT;

      TempGenericChartMemoBuf.Code := GenericChartMgt.DescriptionCode;
      TempGenericChartMemoBuf."Language Code" := GenericChartMgt.GetUserLanguage;
      TempGenericChartMemoBuf.Memo1 := LibraryUtility.GenerateGUID;
      TempGenericChartMemoBuf.INSERT;

      TempGenericChartCaptionsBuf.Code := GenericChartMgt.XAxisCaptionCode;
      TempGenericChartCaptionsBuf."Language Code" := GenericChartMgt.GetUserLanguage;
      TempGenericChartCaptionsBuf.Caption := LibraryUtility.GenerateGUID;
      TempGenericChartCaptionsBuf.INSERT;

      GenericChartMgt.SaveChanges(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter,
        TempGenericChartCaptionsBuf,TempGenericChartMemoBuf);
      Chart.MODIFY;
      TempGenericChartCaptionsBuf.DELETEALL;
      TempGenericChartMemoBuf.DELETEALL;

      // [GIVEN] Update Xml schema with copy of lines with <ENU> language for xml elements: <Text ID="ENU">Translation</Text>
      UpdateXMLForChart(Chart);

      // [WHEN] Retrieve XML for Chart
      GenericChartMgt.RetrieveXML(
        Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);

      // [THEN] One record created per each Description and Caption buffer
      Assert.RecordCount(TempGenericChartCaptionsBuf,1);
      Assert.RecordCount(TempGenericChartMemoBuf,1);
    END;

    LOCAL PROCEDURE OpenAndInitializeCustomerBalanceByCity@95(VAR ChartSetup@1000 : TestPage 9183;ChartID@1005 : Text;ChartName@1006 : Text);
    VAR
      GenericChartSetup@1004 : Record 9180;
      GenericChartYAxis@1003 : Record 9182;
      Cust@1001 : Record 18;
    BEGIN
      ChartSetup.OPENNEW;
      ChartSetup.Name.VALUE := ChartName;
      ChartSetup.ID.VALUE := ChartID;
      ChartSetup."Source Type".SETVALUE(GenericChartSetup."Source Type"::Table);
      ChartSetup."Source ID".SETVALUE(DATABASE::Customer);
      ChartSetup.RequiredMeasureColumn.SETVALUE(Cust.FIELDNAME(Balance));
      ChartSetup.RequiredMeasureAggregation.SETVALUE(GenericChartYAxis.Aggregation::Sum);
      ChartSetup.RequiredMeasureType.SETVALUE(GenericChartYAxis."Chart Type"::ColumnStacked);
      ChartSetup.RequiredMeasureCaption.SETVALUE(RequiredMeasureCaptionTxt);
      ChartSetup."X-Axis Field".SETVALUE(Cust.FIELDNAME(City));
      ChartSetup."Show X-Axis Title".SETVALUE(TRUE);
      ChartSetup."X-Axis Title".SETVALUE(XAxisTitleTxt);
      ChartSetup."Data Point X Label".SETVALUE(XAxisDatapointTxt);
      ChartSetup."Show Y-Axis Title".SETVALUE(TRUE);
      ChartSetup."Y-Axis Title".SETVALUE(YAxisTitleTxt);
    END;

    LOCAL PROCEDURE CommitCustomerBalanceByCity@93(VAR ChartSetup@1002 : TestPage 9183;ChartID@1000 : Code[20];ChartName@1001 : Text[30]);
    BEGIN
      // It is necessary to move next/previous to get the ChartDefinition commited to blob in the DB.
      ChartSetup.NEXT; // This line does the actual commit to the DB.
      ChartSetup.GOTOKEY(ChartID); // Getting back to the original record.
      // Ensure that we are actually on the expected record.
      Assert.AreEqual(ChartName,ChartSetup.Name.VALUE,ChartSetupNotShowingExpectedRecordTxt);
      Assert.AreEqual(ChartID,ChartSetup.ID.VALUE,ChartSetupNotShowingExpectedRecordTxt);
    END;

    LOCAL PROCEDURE GetChartFromDB@91(VAR Chart@1000 : Record 2000000078;ChartID@1001 : Code[20]);
    BEGIN
      Chart.SETRANGE(ID,ChartID);
      IF NOT Chart.FINDFIRST THEN
        Assert.Fail(STRSUBSTNO(ChartNotFoundErr,ChartID))
    END;

    LOCAL PROCEDURE RemoveChartFromDB@89(ChartID@1000 : Code[20]);
    VAR
      Chart@1001 : Record 2000000078;
    BEGIN
      Chart.SETRANGE(ID,ChartID);
      IF Chart.FINDFIRST THEN
        Chart.DELETE
    END;

    LOCAL PROCEDURE GetChartBuilder@87(VAR Chart@1000 : Record 2000000078;VAR chartBuilder@1001 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder") : Boolean;
    VAR
      InStream@1002 : InStream;
    BEGIN
      IF NOT Chart.BLOB.HASVALUE THEN
        EXIT(FALSE);
      Chart.CALCFIELDS(BLOB);
      Chart.BLOB.CREATEINSTREAM(InStream);
      chartBuilder := chartBuilder.FromStream(InStream);
      EXIT(TRUE)
    END;

    LOCAL PROCEDURE GetLanguage@86(Number@1000 : Integer) LanguageCode : Code[3];
    VAR
      GenericChartMgt@1001 : Codeunit 9180;
    BEGIN
      IF Number = 1 THEN BEGIN
        LanguageCode := LanguageCode1Txt;
        IF GenericChartMgt.GetUserLanguage = LanguageCode THEN
          LanguageCode := LanguageCode1AlternativeTxt
      END;

      IF Number = 2 THEN BEGIN
        LanguageCode := LanguageCode2Txt;
        IF GenericChartMgt.GetUserLanguage = LanguageCode THEN
          LanguageCode := LanguageCode2AlternativeTxt
      END;

      IF Number = 3 THEN BEGIN
        LanguageCode := LanguageCode3Txt;
        IF GenericChartMgt.GetUserLanguage = LanguageCode THEN
          LanguageCode := LanguageCode3AlternativeTxt
      END;

      IF Number > GetMaxLanguageCount THEN
        Assert.Fail(STRSUBSTNO(LanguageNotDefinedErr,GetMaxLanguageCount))
    END;

    LOCAL PROCEDURE GetMaxLanguageCount@85() : Integer;
    BEGIN
      EXIT(3);
    END;

    LOCAL PROCEDURE AddFiltersToChart@64(VAR TempGenericChartSetup@1010 : TEMPORARY Record 9180;VAR TempGenericChartFilter@1009 : TEMPORARY Record 9181;FilterFieldID@1007 : Integer;FilterFieldName@1004 : Text[30];FilterIndex@1006 : Integer;FilterValue@1000 : Text[250]);
    BEGIN
      TempGenericChartFilter.VALIDATE(ID,TempGenericChartSetup.ID);
      TempGenericChartFilter.VALIDATE("Line No.",(FilterIndex + 1) * 10000);
      IF TempGenericChartSetup."Source Type" = TempGenericChartSetup."Source Type"::Table THEN BEGIN
        TempGenericChartFilter.VALIDATE("Filter Field ID",FilterFieldID);
        TempGenericChartFilter.VALIDATE("Filter Field Name",GetFieldNameFromID(TempGenericChartSetup."Source ID",FilterFieldID));
      END ELSE
        TempGenericChartFilter.VALIDATE("Filter Field Name",FilterFieldName);

      TempGenericChartFilter.VALIDATE("Filter Value",FilterValue);
      GetFieldColumnNoName(
        TempGenericChartSetup."Source Type",TempGenericChartSetup."Source ID",TempGenericChartFilter."Filter Field ID",
        TempGenericChartFilter."Filter Field Name");
      TempGenericChartFilter.INSERT;
    END;

    LOCAL PROCEDURE AddMeasureToChart@4(VAR TempGenericChartSetup@1000 : TEMPORARY Record 9180;VAR TempGenericChartYAxis@1001 : TEMPORARY Record 9182;VAR MeasureName@1002 : Text[50];MeasureFieldID@1006 : Integer;MeasureIndex@1003 : Integer;DataMeasureType@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";DataAggregationType@1005 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType");
    BEGIN
      TempGenericChartYAxis.VALIDATE(ID,TempGenericChartSetup.ID);
      TempGenericChartYAxis.VALIDATE("Line No.",(MeasureIndex + 1) * 10000);
      TempGenericChartYAxis.VALIDATE("Y-Axis Measure Field ID",MeasureFieldID);
      IF TempGenericChartSetup."Source Type" = TempGenericChartSetup."Source Type"::Table THEN
        MeasureName := GetFieldNameFromID(TempGenericChartSetup."Source ID",MeasureFieldID);
      TempGenericChartYAxis.VALIDATE("Y-Axis Measure Field Name",MeasureName);
      TempGenericChartYAxis.VALIDATE(Aggregation,ConvertAggregationToOption(DataAggregationType));
      TempGenericChartYAxis.VALIDATE("Chart Type",ConvertMeasureTypeToOption(DataMeasureType));
      TempGenericChartYAxis.INSERT;
    END;

    LOCAL PROCEDURE ClearGlobals@5();
    BEGIN
      CLEAR(SourceIDToSet);
      CLEAR(SourceTypeToSet);
      CLEAR(ActualFieldsListCount);
      CLEAR(DimensionNameToSet);
      CLEAR(ActualTypeFilter);
    END;

    LOCAL PROCEDURE ConvertAggregationToOption@41(DataAggregationType@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType") : Integer;
    VAR
      GenericChartYAxis@1002 : Record 9182;
    BEGIN
      CASE FORMAT(DataAggregationType) OF
        'None':
          EXIT(GenericChartYAxis.Aggregation::None);
        'Count':
          EXIT(GenericChartYAxis.Aggregation::Count);
        'Sum':
          EXIT(GenericChartYAxis.Aggregation::Sum);
        'Min':
          EXIT(GenericChartYAxis.Aggregation::Min);
        'Max':
          EXIT(GenericChartYAxis.Aggregation::Max);
        'Avg':
          EXIT(GenericChartYAxis.Aggregation::Avg);
      END;
    END;

    LOCAL PROCEDURE ConvertMeasureTypeToOption@42(DataMeasureType@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType") : Integer;
    VAR
      i@1001 : Integer;
    BEGIN
      i := DataMeasureType;
      CASE i OF
        10: // Column
          EXIT(0);
        0:
          EXIT(1);
        3:
          EXIT(2);
        11:
          EXIT(3);
        12:
          EXIT(4);
        13:
          EXIT(5);
        15:
          EXIT(6);
        16:
          EXIT(7);
        5:
          EXIT(8);
        17:
          EXIT(9);
        18:
          EXIT(10);
        21:
          EXIT(11);
        25:
          EXIT(12);
        33:
          EXIT(13);
      END;
    END;

    LOCAL PROCEDURE GetDefaultSourceQueryInfo@58(VAR SourceName@1002 : Text[30];VAR SourceID@1001 : Integer;VAR SourceType@1000 : Option);
    VAR
      GenericChartSetup@1003 : Record 9180;
    BEGIN
      SourceName := 'Trailing Sales Order Qry';
      SourceID := 760;
      SourceType := GenericChartSetup."Source Type"::Query;
    END;

    LOCAL PROCEDURE GetDefaultXDimensionInfoForQuery@56(VAR XDimensionID@1000 : Integer;VAR XDimensionName@1001 : Text[80];VAR ShowXDimensionTitle@1002 : Boolean);
    BEGIN
      XDimensionID := 6;
      XDimensionName := 'CurrencyCode';
      ShowXDimensionTitle := TRUE;
    END;

    LOCAL PROCEDURE GetDefaultMeasureInfoForQuery@61(VAR DataAggregationType@1005 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";VAR MeasureID@1004 : Integer;VAR MeasureName@1003 : Text[50]);
    BEGIN
      MeasureID := 30; // Amount
      MeasureName := 'Amount';
      DataAggregationType := DataAggregationType.Count;
    END;

    LOCAL PROCEDURE GetFieldNameFromID@70(SourceID@1000 : Integer;FieldNo@1001 : Integer) : Text[30];
    VAR
      SourceFieldRef@1003 : FieldRef;
      SourceRecordRef@1002 : RecordRef;
    BEGIN
      SourceRecordRef.OPEN(SourceID);
      SourceFieldRef := SourceRecordRef.FIELD(FieldNo);
      EXIT(SourceFieldRef.NAME);
    END;

    LOCAL PROCEDURE GetTableMeasures@97(VAR HasMeasure@1001 : ARRAY [4] OF Boolean;VAR MeasureID@1002 : ARRAY [4] OF Integer;VAR DataMeasureType@1003 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";VAR DataAggregationType@1004 : ARRAY [4] OF DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType");
    VAR
      MeasureIndex@1000 : Integer;
    BEGIN
      MeasureIndex := 1;
      HasMeasure[MeasureIndex] := TRUE;
      DataMeasureType[MeasureIndex] := DataMeasureType[MeasureIndex].Line;
      MeasureID[MeasureIndex] := 60; // Amount
      DataAggregationType[MeasureIndex] := DataAggregationType[MeasureIndex].Count;

      // Measure Index 1
      MeasureIndex := 2;
      HasMeasure[MeasureIndex] := TRUE;
      DataMeasureType[MeasureIndex] := DataMeasureType[MeasureIndex].Area;
      MeasureID[MeasureIndex] := 61; // Amount Including VAT
      DataAggregationType[MeasureIndex] := DataAggregationType[MeasureIndex].Sum;

      // Measure Index 2
      MeasureIndex := 3;
      HasMeasure[MeasureIndex] := TRUE;
      DataMeasureType[MeasureIndex] := DataMeasureType[MeasureIndex].Column;
      MeasureID[MeasureIndex] := 130; // Prepayment %
      DataAggregationType[MeasureIndex] := DataAggregationType[MeasureIndex].Max;

      // Measure Index 3
      MeasureIndex := 4;
      HasMeasure[MeasureIndex] := TRUE;
      DataMeasureType[MeasureIndex] := DataMeasureType[MeasureIndex].Point;
      MeasureID[MeasureIndex] := 140; // Prepmt. Payment Discount %
      DataAggregationType[MeasureIndex] := DataAggregationType[MeasureIndex].None;
    END;

    LOCAL PROCEDURE GetDefaultFilterInfoForTable@123(VAR FilterFieldName@1002 : Text[30];VAR FilterFieldValue@1001 : Text[250];VAR FilterFieldId@1000 : Integer);
    BEGIN
      FilterFieldName := 'Posting Date';
      FilterFieldValue := '<> TODAY';
      FilterFieldId := 20;
    END;

    LOCAL PROCEDURE GetDefaultFilterInfoForQuery@129(VAR FilterFieldName@1002 : Text[30];VAR FilterFieldValue@1001 : Text[250];VAR FilterFieldId@1000 : Integer);
    BEGIN
      FilterFieldName := 'CurrencyCode';
      FilterFieldValue := '<> USD';
      FilterFieldId := 6;
    END;

    LOCAL PROCEDURE CreateDefaultChart@69(VAR Chart@1001 : Record 2000000078;VAR ChartCode@1000 : Code[20]);
    BEGIN
      Chart.INIT;
      IF ChartCode = '' THEN
        ChartCode := GenerateRandomChartCode;
      Chart.VALIDATE(ID,ChartCode);
      Chart.VALIDATE(Name,Chart.ID);
      Chart.INSERT;
    END;

    LOCAL PROCEDURE CreateGeneratorFromChart@82(VAR Chart@1001 : Record 2000000078;VAR TempGenericChartSetup@1000 : TEMPORARY Record 9180;SourceType@1002 : Option;SourceID@1003 : Integer;SourceName@1004 : Text[30]);
    BEGIN
      TempGenericChartSetup.VALIDATE(ID,Chart.ID);
      TempGenericChartSetup.VALIDATE("Source Type",SourceType);
      TempGenericChartSetup.VALIDATE("Source ID",SourceID);
      TempGenericChartSetup.VALIDATE("Object Name",SourceName);
    END;

    LOCAL PROCEDURE CreateBuilderFromChart@90(VAR Chart@1001 : Record 2000000078;VAR ChartBuilder@1002 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder");
    VAR
      InStream@1000 : InStream;
    BEGIN
      Chart.CALCFIELDS(BLOB);
      Chart.BLOB.CREATEINSTREAM(InStream);
      ChartBuilder := ChartBuilder.FromStream(InStream);
    END;

    LOCAL PROCEDURE GetDefaultSourceTableInfo@36(VAR SourceName@1002 : Text[30];VAR SourceID@1001 : Integer;VAR SourceType@1000 : Option);
    VAR
      GenericChartSetup@1003 : Record 9180;
    BEGIN
      SourceName := 'Sales Header';
      SourceID := 36;
      SourceType := GenericChartSetup."Source Type"::Table;
    END;

    LOCAL PROCEDURE GetDefaultXDimensionsInfoForTable@33(VAR XDimensionID@1000 : Integer;VAR XDimensionName@1001 : Text[80];VAR ShowXDimensionTitle@1002 : Boolean);
    BEGIN
      XDimensionID := 3;
      XDimensionName := 'No.';
      ShowXDimensionTitle := TRUE;
    END;

    LOCAL PROCEDURE GetDefaultZDimensionsInfoForTable@43(VAR ZDimensionID@1000 : Integer;VAR ZDimensionName@1001 : Text[80];VAR ShowZDimensionTitle@1002 : Boolean);
    BEGIN
      ZDimensionID := 6;
      ZDimensionName := 'Bill-to Name 2';
      ShowZDimensionTitle := FALSE;
    END;

    LOCAL PROCEDURE GenerateRandomChartCode@9() : Text[20];
    VAR
      Chart@1000 : Record 2000000078;
    BEGIN
      EXIT(COPYSTR(LibraryUtility.GenerateRandomCode(Chart.FIELDNO(ID),DATABASE::Chart),1,20));
    END;

    LOCAL PROCEDURE GetFieldsForTable@13(VAR Field@1000 : Record 2000000041;SourceTableNo@1001 : Integer;FilterType@1002 : Integer);
    BEGIN
      Field.SETRANGE(TableNo,SourceTableNo);
      CASE FilterType OF
        0:
          Field.SETRANGE(Type);
        1,2:
          Field.SETFILTER(Type,TypeFilterText(FilterType));
      END;
      Field.SETFILTER(Class,'<>%1',Field.Class::FlowFilter);
      Field.FINDSET;
    END;

    LOCAL PROCEDURE GetFieldColumnNoName@12(SourceType@1004 : ' ,Table,Query';SourceNo@1000 : Integer;VAR FieldColNo@1001 : Integer;VAR FieldColName@1003 : Text);
    VAR
      TempGenericChartQueryColumn@1006 : TEMPORARY Record 9183;
      Field@1002 : Record 2000000041;
      GenericChartMgt@1007 : Codeunit 9180;
      Found@1005 : Boolean;
    BEGIN
      CASE SourceType OF
        SourceType::Table:
          WITH Field DO
            IF FieldColNo > 0 THEN BEGIN
              FieldColName := '';
              IF GET(SourceNo,FieldColNo) THEN
                FieldColName := FieldName;
            END ELSE BEGIN
              SETRANGE(TableNo,SourceNo);
              IF FINDSET THEN
                REPEAT
                  IF UPPERCASE(FieldName) = UPPERCASE(FieldColName) THEN BEGIN
                    Found := TRUE;
                    FieldColNo := "No.";
                  END;
                UNTIL (NEXT = 0) OR Found;
            END;
        SourceType::Query:
          BEGIN
            GenericChartMgt.GetQueryColumnList(TempGenericChartQueryColumn,SourceNo,0,FALSE);
            WITH TempGenericChartQueryColumn DO
              IF FieldColNo > 0 THEN BEGIN
                FieldColName := '';
                SETRANGE("Query Column No.",FieldColNo);
                IF FINDFIRST THEN
                  FieldColName := "Column Name";
              END ELSE BEGIN
                IF FINDSET THEN
                  REPEAT
                    IF UPPERCASE("Column Name") = UPPERCASE(FieldColName) THEN BEGIN
                      Found := TRUE;
                      FieldColNo := "Query Column No.";
                    END;
                  UNTIL (NEXT = 0) OR Found;
              END;
          END;
      END;
    END;

    LOCAL PROCEDURE FindChartByID@94(VAR Chart@1001 : Record 2000000078;ChartCode@1002 : Code[20]);
    BEGIN
      CLEAR(Chart);
      Chart.SETRANGE(ID,ChartCode);
      Chart.FINDFIRST;
    END;

    LOCAL PROCEDURE SaveChartWithDefaultDescription@8(VAR Chart@1004 : Record 2000000078;VAR TempGenericChartSetup@1003 : TEMPORARY Record 9180;VAR TempGenericChartYAxis@1001 : TEMPORARY Record 9182;VAR TempGenericChartFilter@1002 : TEMPORARY Record 9181);
    VAR
      TempGenericChartCaptionsBuf@1006 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1000 : TEMPORARY Record 9186;
      GenericChartMgt@1005 : Codeunit 9180;
    BEGIN
      TempGenericChartMemoBuf.SetMemo(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage,DefaultChartDescription);
      GenericChartMgt.SaveChanges(Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartFilter,
        TempGenericChartCaptionsBuf,TempGenericChartMemoBuf);
    END;

    LOCAL PROCEDURE RetrieveChartWithoutDescription@24(VAR Chart@1004 : Record 2000000078;VAR TempGenericChartSetup@1003 : TEMPORARY Record 9180;VAR TempGenericChartYAxis@1001 : TEMPORARY Record 9182;VAR TempGenericChartFilter@1002 : TEMPORARY Record 9181);
    VAR
      TempGenericChartCaptionsBuf@1006 : TEMPORARY Record 9185;
      TempGenericChartMemoBuf@1000 : TEMPORARY Record 9186;
      GenericChartMgt@1005 : Codeunit 9180;
    BEGIN
      TempGenericChartSetup.DELETEALL;
      TempGenericChartYAxis.DELETEALL;
      TempGenericChartFilter.DELETEALL;
      GenericChartMgt.RetrieveXML(
        Chart,TempGenericChartSetup,TempGenericChartYAxis,TempGenericChartCaptionsBuf,TempGenericChartMemoBuf,TempGenericChartFilter);
    END;

    LOCAL PROCEDURE SetChartDimensions@18(VAR TempGenericChartSetup@1004 : TEMPORARY Record 9180;DimensionType@1000 : Integer;DimensionFieldId@1001 : Integer;DimensionFieldName@1005 : Text[80];DimensionCaption@1002 : Text[80];ShowDimensiontitle@1003 : Boolean);
    BEGIN
      IF TempGenericChartSetup."Source Type" = TempGenericChartSetup."Source Type"::Table THEN
        DimensionFieldName := GetFieldNameFromID(TempGenericChartSetup."Source ID",DimensionFieldId);
      IF DimensionType = 1 THEN BEGIN
        TempGenericChartSetup.VALIDATE("X-Axis Field ID",DimensionFieldId);
        TempGenericChartSetup.VALIDATE("X-Axis Field Name",DimensionFieldName);
        TempGenericChartSetup.VALIDATE("X-Axis Title",DimensionCaption);
        TempGenericChartSetup.VALIDATE("X-Axis Show Title",ShowDimensiontitle);
      END ELSE
        IF DimensionType = 2 THEN BEGIN
          TempGenericChartSetup.VALIDATE("Z-Axis Field ID",DimensionFieldId);
          TempGenericChartSetup.VALIDATE("Z-Axis Field Name",DimensionFieldName);
          TempGenericChartSetup.VALIDATE("Z-Axis Title",DimensionCaption);
          TempGenericChartSetup.VALIDATE("Z-Axis Show Title",ShowDimensiontitle);
        END;
    END;

    LOCAL PROCEDURE StripNewLines@3(InputString@1001 : Text) : Text;
    VAR
      Newline@1000 : ARRAY [2] OF Char;
    BEGIN
      Newline[1] := 10;
      Newline[2] := 13;

      EXIT(DELCHR(DELCHR(InputString,'=',FORMAT(Newline[1])),'=',FORMAT(Newline[2])));
    END;

    LOCAL PROCEDURE SetAndVerifyColumnDataType@54(FieldType@1000 : Option;ExpectedColumnDataType@1001 : Option);
    VAR
      GenericChartQueryColumn@1002 : Record 9183;
    BEGIN
      GenericChartQueryColumn.SetColumnDataType(FieldType);
      Assert.AreEqual(ExpectedColumnDataType,GenericChartQueryColumn."Column Data Type",ColumnDataTypeErr);
    END;

    LOCAL PROCEDURE VerifyChartSourceProperties@27(VAR ChartBuilder@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";SourceType@1001 : Option;SourceName@1002 : Text[30];SourceID@1003 : Integer;Description@1005 : Text);
    VAR
      GenericChartSetup@1000 : Record 9180;
    BEGIN
      IF SourceType = GenericChartSetup."Source Type"::Table THEN BEGIN
        Assert.AreEqual(SourceID,ChartBuilder.TableId,'Table ID Matches');
        Assert.AreEqual(SourceName,ChartBuilder.TableName,'Table Name Matches');
        Assert.AreEqual(TRUE,ChartBuilder.HasTable,'SourceType is Table');
        Assert.AreEqual(FALSE,ChartBuilder.HasQuery,'SourceType is not Query');
      END ELSE BEGIN
        Assert.AreEqual(SourceID,ChartBuilder.QueryId,'Query ID Matches');
        Assert.AreEqual(SourceName,ChartBuilder.QueryName,'Query Name Matches');
        Assert.AreEqual(FALSE,ChartBuilder.HasTable,'SourceType is not Table');
        Assert.AreEqual(TRUE,ChartBuilder.HasQuery,'SourceType is Query');
      END;

      Assert.AreEqual(Description,ChartBuilder.Description,'Description Matches');
    END;

    LOCAL PROCEDURE VerifyChartDimensions@31(VAR ChartBuilder@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";IsDimensionDefined@1006 : Boolean;DimensionType@1005 : Integer;DimensionFieldId@1003 : Integer;DimensionCaption@1002 : Text[250];ShowDimensiontitle@1001 : Boolean);
    BEGIN
      IF DimensionType = 1 THEN
        IF IsDimensionDefined THEN BEGIN
          Assert.AreEqual(DimensionFieldId,ChartBuilder.XDimensionId,'XDimension ID Matches');
          Assert.AreEqual(DimensionCaption,ChartBuilder.XDimensionName,'XDimensionName Matches');
          Assert.AreEqual(TRUE,ChartBuilder.HasXDimension,'Has XDimension matches');
          Assert.AreEqual(ShowDimensiontitle,ChartBuilder.ShowXDimensionTitle,'Show X Dimension Title matches');
        END;

      IF DimensionType = 2 THEN
        IF IsDimensionDefined THEN BEGIN
          Assert.AreEqual(DimensionFieldId,ChartBuilder.ZDimensionId,'ZDimension ID Matches');
          Assert.AreEqual(DimensionCaption,ChartBuilder.ZDimensionName,'ZDimensionName Matches');
          Assert.AreEqual(TRUE,ChartBuilder.HasZDimension,'Has ZDimension matches');
          Assert.AreEqual(ShowDimensiontitle,ChartBuilder.ShowZDimensionTitle,'Show Z Dimension Title matches');
        END;
    END;

    LOCAL PROCEDURE VerifyChartMeasure@38(VAR ChartBuilder@1001 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";IsMeasureDefinedAtIndex@1000 : Boolean;MeasureIndex@1003 : Integer;MeasureFieldID@1006 : Integer;MeasureName@1007 : Text[50];DataMeasureType@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";DataAggregationType@1005 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType");
    BEGIN
      Assert.AreEqual(
        IsMeasureDefinedAtIndex,ChartBuilder.HasMeasureField(MeasureIndex),
        STRSUBSTNO('Property: HasMeasure for measure field:%1 matches',MeasureIndex));
      IF IsMeasureDefinedAtIndex THEN BEGIN
        Assert.AreEqual(
          FORMAT(DataMeasureType),FORMAT(ChartBuilder.GetMeasureChartType(MeasureIndex)),
          STRSUBSTNO('Chart Type for Measure:%1 matches',MeasureIndex));
        Assert.AreEqual(MeasureFieldID,ChartBuilder.GetMeasureId(MeasureIndex),STRSUBSTNO('ID for Measure:%1 matches',MeasureIndex));
        Assert.AreEqual(
          FORMAT(DataAggregationType),FORMAT(ChartBuilder.GetMeasureOperator(MeasureIndex)),
          STRSUBSTNO('Aggregation Type for Measure:%1 matches',MeasureIndex));
        Assert.AreEqual(MeasureName,ChartBuilder.GetMeasureName(MeasureIndex),STRSUBSTNO('Name for Measure:%1 matches',MeasureIndex));
      END;
    END;

    LOCAL PROCEDURE VerifyChartMeasureAgainstTable@65(VAR ChartBuilder@1001 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";VAR TempGenericChartYAxis@1002 : TEMPORARY Record 9182;IsMeasureDefinedAtIndex@1000 : Boolean;MeasureIndex@1003 : Integer);
    BEGIN
      Assert.AreEqual(
        IsMeasureDefinedAtIndex,ChartBuilder.HasMeasureField(MeasureIndex),
        STRSUBSTNO('Property: HasMeasure for measure field:%1 matches',MeasureIndex));
      TempGenericChartYAxis.SETRANGE("Line No.",(MeasureIndex + 1) * 10000);
      TempGenericChartYAxis.FINDFIRST;
      IF IsMeasureDefinedAtIndex THEN BEGIN
        Assert.AreEqual(
          FORMAT(TempGenericChartYAxis."Chart Type"),FORMAT(ChartBuilder.GetMeasureChartType(MeasureIndex)),
          STRSUBSTNO('Chart Type for Measure:%1 matches',MeasureIndex));
        Assert.AreEqual(
          TempGenericChartYAxis."Y-Axis Measure Field ID",ChartBuilder.GetMeasureId(MeasureIndex),
          STRSUBSTNO('ID for Measure:%1 matches',MeasureIndex));
        Assert.AreEqual(
          FORMAT(TempGenericChartYAxis.Aggregation),FORMAT(ChartBuilder.GetMeasureOperator(MeasureIndex)),
          STRSUBSTNO('Aggregation Type for Measure:%1 matches',MeasureIndex));
        Assert.AreEqual(
          TempGenericChartYAxis."Y-Axis Measure Field Name",ChartBuilder.GetMeasureName(MeasureIndex),
          STRSUBSTNO('Name for Measure:%1 matches',MeasureIndex));
      END;
    END;

    LOCAL PROCEDURE VerifyChartFilters@40(VAR ChartBuilder@1004 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder";FilterIndex@1006 : Integer;FilterFieldName@1013 : Text[30];FilterFieldValue@1012 : Text[250];FilterFieldId@1011 : Integer;FilterCount@1000 : Integer);
    VAR
      ActualFilterFieldName@1005 : Text[30];
      ActualFilterFieldValue@1003 : Text[250];
      ActualFilterFieldId@1002 : Integer;
      ActualFilterCount@1001 : Integer;
    BEGIN
      IF ChartBuilder.HasQuery THEN BEGIN
        ActualFilterFieldId := ChartBuilder.GetQueryFilterFieldId(FilterIndex);
        ActualFilterFieldName := ChartBuilder.GetQueryFilterFieldName(FilterIndex);
        ActualFilterFieldValue := ChartBuilder.GetQueryFilterValue(FilterIndex);
        ActualFilterCount := ChartBuilder.QueryFilterCount;
        Assert.AreEqual(0,ChartBuilder.TableFilterCount,'Table filter count is zero when source type is set to query');
      END ELSE BEGIN
        ActualFilterFieldId := ChartBuilder.GetTableFilterFieldId(FilterIndex);
        ActualFilterFieldName := ChartBuilder.GetTableFilterFieldName(FilterIndex);
        ActualFilterFieldValue := ChartBuilder.GetTableFilterValue(FilterIndex);
        ActualFilterCount := ChartBuilder.TableFilterCount;
        Assert.AreEqual(0,ChartBuilder.QueryFilterCount,'Query filter count is zero when source type is set to Table');
      END;

      IF ActualFilterCount > 0 THEN BEGIN
        Assert.AreEqual(FilterFieldId,ActualFilterFieldId,STRSUBSTNO('Filter field ID for Index:%1 matches',FilterIndex));
        Assert.AreEqual(FilterFieldName,ActualFilterFieldName,STRSUBSTNO('Filter field Name for Index:%1 matches',FilterIndex));
        Assert.AreEqual(FilterFieldValue,ActualFilterFieldValue,STRSUBSTNO('Filter field Value for Index:%1 matches',FilterIndex));
        Assert.AreEqual(FilterCount,ActualFilterCount,'Filter count value matches');
      END ELSE
        Assert.AreEqual(0,ActualFilterCount,'Filters don''t exist ');
    END;

    LOCAL PROCEDURE TypeFilterText@15(Category@1000 : Integer) : Text;
    VAR
      DummyField@1001 : Record 2000000041;
    BEGIN
      WITH DummyField DO
        CASE Category OF
          1:
            EXIT(
              STRSUBSTNO(
                '%1|%2|%3|%4|%5|%6|%7',Type::Date,Type::Time,Type::DateFormula,Type::Text,Type::Code,Type::Option,Type::DateTime));
          2:
            EXIT(STRSUBSTNO('%1|%2|%3|%4|%5',Type::Decimal,Type::Binary,Type::Integer,Type::BigInteger,Type::Duration));
        END;
    END;

    LOCAL PROCEDURE Min@16(A@1000 : Integer;B@1001 : Integer) : Integer;
    BEGIN
      IF A < B THEN
        EXIT(A);
      EXIT(B);
    END;

    LOCAL PROCEDURE GetFieldClassFilter@17() : Text;
    BEGIN
      EXIT('<>FlowFilter');
    END;

    LOCAL PROCEDURE CompareFilterStrings@21(ExpectedStr@1000 : Text;ActualStr@1001 : Text;ErrMsg@1003 : Text);
    VAR
      CompLength@1002 : Integer;
    BEGIN
      ExpectedStr := COPYSTR(ExpectedStr,46);
      ActualStr := COPYSTR(ActualStr,46);
      CompLength := Min(STRLEN(ExpectedStr),STRLEN(ActualStr));
      Assert.AreEqual(COPYSTR(ExpectedStr,1,CompLength),COPYSTR(ActualStr,1,CompLength),COPYSTR(ErrMsg,1,1024)); // Max length for Msg in AreEqual
    END;

    LOCAL PROCEDURE MakeRandomTextString@25(Length@1000 : Integer;VAR Output@1001 : Text[250]);
    VAR
      AllowedChars@1002 : Text[36];
      i@1003 : Integer;
    BEGIN
      AllowedChars := 'abcdefghijklmnopqrstuvwxyz0123456789';
      Output := '';
      FOR i := 1 TO Length DO
        Output := Output + COPYSTR(AllowedChars,RANDOM(MAXSTRLEN(AllowedChars)),1);
    END;

    LOCAL PROCEDURE AddTooManyMeasuresToChart@34(ChartID@1005 : Code[20];VAR TempGenericChartYAxis@1000 : TEMPORARY Record 9182);
    VAR
      Field@1002 : Record 2000000041;
      LineNo@1006 : Integer;
    BEGIN
      Field.SETRANGE(TableNo,DATABASE::"Sales Header");
      Field.SETRANGE(Type,Field.Type::Decimal);
      Field.FINDSET;
      TempGenericChartYAxis.DELETEALL;
      REPEAT
        LineNo += 10000;
        CLEAR(TempGenericChartYAxis);
        TempGenericChartYAxis.ID := ChartID;
        TempGenericChartYAxis."Line No." := LineNo;
        TempGenericChartYAxis."Y-Axis Measure Field ID" := Field."No.";
        TempGenericChartYAxis."Y-Axis Measure Field Name" := Field.FieldName;
        TempGenericChartYAxis.INSERT;
      UNTIL Field.NEXT = 0;
    END;

    LOCAL PROCEDURE ValidateEntryDoesNotExist@99(ChartID@1001 : Code[20]);
    VAR
      Chart@1000 : Record 2000000078;
    BEGIN
      Chart.SETRANGE(ID,ChartID);
      IF Chart.FINDFIRST THEN
        Assert.Fail(ChartFoundInDBErr)
    END;

    LOCAL PROCEDURE ValidateChartEntryExist@98(VAR Chart@1002 : Record 2000000078;ChartID@1001 : Code[20];ChartName@1000 : Text[30]);
    BEGIN
      Assert.AreEqual(FORMAT(ChartName),Chart.Name,TextChartNameTestTxt);
      Assert.AreEqual(FORMAT(ChartID),Chart.ID,TextChartNameTestTxt);
      IF NOT Chart.BLOB.HASVALUE THEN
        Assert.Fail(NoDataInBlobErr)
    END;

    LOCAL PROCEDURE ValidateStandardCustomerBalanceByCityValues@96(VAR ChartBuilder@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartBuilder");
    VAR
      Cust@1006 : Record 18;
      AggregationType@1008 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataAggregationType";
      MeasureType@1007 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
    BEGIN
      Assert.IsTrue(ChartBuilder.HasTable,TestNameHasTableTxt);
      Assert.AreEqual(DATABASE::Customer,ChartBuilder.TableId,TestNameTableIDTxt);
      Assert.AreEqual(Cust.FIELDNAME(Balance),ChartBuilder.GetMeasureName(0),TestNameRequiredMeasureColumnTxt);
      Assert.AreEqual(FORMAT(AggregationType.Sum),FORMAT(ChartBuilder.GetMeasureOperator(0)),TestNameRequiredMeasureAggregationTxt);
      Assert.AreEqual(FORMAT(MeasureType.StackedColumn),FORMAT(ChartBuilder.GetMeasureChartType(0)),TestNameRequiredMeasureTypeTxt);
      Assert.AreEqual(FORMAT(RequiredMeasureCaptionTxt),FORMAT(ChartBuilder.GetMeasureCaption(0)),TestNameRequiredMeasureCaptionTxt);
      Assert.AreEqual(Cust.FIELDNAME(City),ChartBuilder.XDimensionName,TestNameXAxisTxt);
      Assert.AreEqual(FORMAT(XAxisDatapointTxt),ChartBuilder.XDimensionCaption,TestNameXAxisDatapointTxt);
      Assert.IsTrue(ChartBuilder.ShowXDimensionTitle,TestNameShowXAxisTitleTxt);
      Assert.AreEqual(FORMAT(XAxisTitleTxt),ChartBuilder.XDimensionTitle,TestNameXAxisTitleTxt);
      Assert.IsTrue(ChartBuilder.ShowYAxisTitle,TestNameShowYAxisTitleTxt);
      Assert.AreEqual(FORMAT(YAxisTitleTxt),ChartBuilder.YAxisTitle,TestNameYAxisTitleTxt);
    END;

    LOCAL PROCEDURE UpdateXMLForChart@183(VAR Chart@1000 : Record 2000000078);
    VAR
      XMLDOMManagement@1004 : Codeunit 6224;
      XMLDocOut@1008 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument";
      XMLNode@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNode";
      XMLNodeList@1003 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlNodeList";
      InStream@1002 : InStream;
      OutStream@1001 : OutStream;
    BEGIN
      Chart.BLOB.CREATEINSTREAM(InStream);
      XMLDOMManagement.LoadXMLDocumentFromInStream(InStream,XMLDocOut);
      XMLNodeList := XMLDocOut.GetElementsByTagName('Text');

      XMLNode := XMLNodeList.ItemOf(0);
      XMLNode.ParentNode.AppendChild(XMLNode.Clone);

      XMLNode := XMLNodeList.ItemOf(2);
      XMLNode.ParentNode.AppendChild(XMLNode.Clone);

      CLEAR(Chart.BLOB);
      Chart.BLOB.CREATEOUTSTREAM(OutStream);
      XMLDocOut.Save(OutStream);
      Chart.MODIFY;
    END;

    [ModalPageHandler]
    PROCEDURE HandleLookupPage@19(VAR ObjectsPage@1000 : TestPage 358);
    VAR
      SourceID@1002 : Integer;
      SourceType@1003 : ' ,Table,Query';
    BEGIN
      SourceID := SourceIDToSet;
      EVALUATE(SourceType,ObjectsPage.FILTER.GETFILTER("Object Type"));
      Assert.AreEqual(SourceTypeToSet,SourceType,'Verify that filter is set correctly');
      ObjectsPage.FILTER.SETFILTER("Object ID",FORMAT(SourceID));
      ObjectsPage.OK.INVOKE;
    END;

    [ModalPageHandler]
    PROCEDURE HandleTableFieldsChooser@10(VAR FieldsList@1000 : TestPage 6218);
    BEGIN
      Assert.AreEqual(FORMAT(SourceIDToSet),FieldsList.FILTER.GETFILTER(TableNo),'Table No matches');
      ActualTypeFilter := FieldsList.FILTER.GETFILTER(Type);
      ActualFieldsListCount := 0;
      REPEAT
        ActualFieldsListCount += 1;
      UNTIL NOT FieldsList.NEXT;
      FieldsList.FIRST;
      FieldsList.FILTER.SETFILTER(FieldName,DimensionNameToSet);
      Assert.AreEqual(DimensionNameToSet,FORMAT(FieldsList.FieldName),'Verify the correct Dimension Name has been selected');
      FieldsList.OK.INVOKE;
    END;

    [ModalPageHandler]
    PROCEDURE HandleQueryFieldsChooser@26(VAR QueryColumnList@1000 : TestPage 9186);
    BEGIN
      Assert.AreEqual(FORMAT(SourceIDToSet),QueryColumnList.FILTER.GETFILTER("Query No."),'Table No matches');
      ActualTypeFilter := '';
      ActualFieldsListCount := 0;
      REPEAT
        ActualFieldsListCount += 1;
      UNTIL NOT QueryColumnList.NEXT;
      QueryColumnList.FILTER.SETFILTER("Column Name",DimensionNameToSet);
      Assert.AreEqual(DimensionNameToSet,FORMAT(QueryColumnList."Column Name"),'Verify the correct Dimension Name has been selected');
      QueryColumnList.OK.INVOKE;
    END;

    [MessageHandler]
    PROCEDURE HandleTooManyMeasuresMessage@39(MsgText@1000 : Text[250]);
    BEGIN
      Assert.AreEqual(STRSUBSTNO(MaxNoOfMeasures,6),MsgText,'Message when trying to customize with too many measures');
    END;

    [ModalPageHandler]
    PROCEDURE MemoEditorRunModalHandler@101(VAR Editor@1000 : TestPage 9189);
    VAR
      GenericChartMgt@1001 : Codeunit 9180;
    BEGIN
      Editor.FIRST;
      IF NOT Editor.GOTOKEY(GenericChartMgt.DescriptionCode,GenericChartMgt.GetUserLanguage) THEN
        Editor.NEW;
      Editor."Language Code".SETVALUE(GenericChartMgt.GetUserLanguage);
      Editor.MemoText.SETVALUE(DescriptionTxt);

      Assert.AreEqual(GenericChartMgt.GetUserLanguage,Editor."Language Code".VALUE,TestNameEditorLanguageFieldTxt);
      Assert.AreEqual(FORMAT(DescriptionTxt),Editor.MemoText.VALUE,TestNameEditorTextFieldTxt);

      Editor.NEW;
      Editor."Language Code".SETVALUE(GetLanguage(1));
      Editor.MemoText.SETVALUE(DescriptionTxt + GetLanguage(1));
      Editor.NEW;
      Editor."Language Code".SETVALUE(GetLanguage(2));
      Editor.MemoText.SETVALUE(DescriptionTxt + GetLanguage(2));
      Editor.NEW;
      Editor."Language Code".SETVALUE(GetLanguage(3));
      Editor.MemoText.SETVALUE(DescriptionTxt + GetLanguage(3));

      Assert.AreEqual(GetLanguage(3),Editor."Language Code".VALUE,TestNameEditorLanguageFieldTxt);
      Assert.AreEqual(FORMAT(DescriptionTxt + GetLanguage(3)),Editor.MemoText.VALUE,TestNameEditorTextFieldTxt);

      Editor.NEW;
    END;

    [ModalPageHandler]
    PROCEDURE CaptionEditorRunModalHandler@100(VAR Editor@1000 : TestPage 9185);
    VAR
      GenericChartMgt@1001 : Codeunit 9180;
    BEGIN
      IF NOT Editor.GOTOKEY(GenericChartMgt.RequiredMeasureCode,GenericChartMgt.GetUserLanguage) THEN
        Editor.NEW;

      Editor."Language Code".SETVALUE(GenericChartMgt.GetUserLanguage);
      Editor.Text.VALUE := RequiredMeasureCaptionChangedTxt;

      Assert.AreEqual(GenericChartMgt.GetUserLanguage,Editor."Language Code".VALUE,TestNameEditorLanguageFieldTxt);
      Assert.AreEqual(FORMAT(RequiredMeasureCaptionChangedTxt),FORMAT(Editor.Text.VALUE),TestNameEditorTextFieldTxt);

      Editor.NEW;
      Editor."Language Code".SETVALUE(GetLanguage(1));
      Editor.Text.SETVALUE(RequiredMeasureCaptionChangedTxt + GetLanguage(1));
      Editor.NEW;
      Editor."Language Code".SETVALUE(GetLanguage(2));
      Editor.Text.SETVALUE(RequiredMeasureCaptionChangedTxt + GetLanguage(2));
      Editor.NEW;
      Editor."Language Code".SETVALUE(GetLanguage(3));
      Editor.Text.SETVALUE(RequiredMeasureCaptionChangedTxt + GetLanguage(3));

      Assert.AreEqual(GetLanguage(3),Editor."Language Code".VALUE,TestNameEditorLanguageFieldTxt);
      Assert.AreEqual(FORMAT(RequiredMeasureCaptionChangedTxt + GetLanguage(3)),FORMAT(Editor.Text.VALUE),TestNameEditorTextFieldTxt);

      Editor.NEW;
    END;

    [MessageHandler]
    PROCEDURE HandleMemoTooLongMessage@59(Message@1000 : Text[1024]);
    BEGIN
      Assert.AreEqual(STRSUBSTNO(TextMemoToBeTruncatedMsg,2501,2500),Message,MemoTestNameTxt)
    END;

    BEGIN
    {
      // [FEATURE] [Chart]
      Test Scenarios:
      1. Verify a chart based on a table defined with 4 measures and filters can be saved correctly, Retrieve the proeprties from XML and verify they match.
      2. Verify a chart based on a table defined with Z dimension can be saved correctly, Retrieve the proeprties from XML and verify they match.
      3. Verify that table chart with only a required measure can save chart values correctly.
      4. Verify a chart based on a query defined with X dimension and required measure can be saved correctly,
         Retrieve the properties from XML and verify they match.
      5. Test a chart can be copied successfully.
      6. Test the methods lookupobjectid and retrievefieldcolumn.
      7. Verify that Error is thrown when X Axis or Z Axis is numeric for a table based chart.
      8. Verify that Error is thrown when Y Axis is not numeric for a table based chart.
      9. Verify that Error is thrown when Y Axis is not numeric for a Query based chart.
      10. Verify that Error is thrown when X Axis is numeric for a Query based chart.
      11. Verify that Error is thrown when Y Axis Measure is duplicate.
      12. Verify that Error is thrown when Z Axis Measure is duplicate of X.
      13. Verify that Error is thrown when saving an Source ID that does not exist.
      14. Verify that Error is thrown when saving an X and Z Dimension with Field name that does not exist.
      15. Verify that Error is not thrown when saving an X and Z Dimension with valid field name and field id that's negative.
      16. Verify that Error is thrown when saving an Y Measure with Field name that does not exist.
      17. Verify that Error is not thrown when saving an Y Measure with valid field name and field id that's negative.
      18. Verify that Error is thrown when saving an filter field name that does not exist for a table chart.
      19. Verify that Error is not thrown when saving an negative filter field id for a table chart.
      20. Verify that Error is not thrown when saving an invalid filter field value for a table chart.
      21. Verify that Error is thrown when saving an filter field name that does not exist for a Query chart.
      22. Test Description field with special character, blank, leading spaces
      23. Save Chart with Measure aggregation set to count and field blanked out.
      24. Verify Setting and Getting texts out of the memo.
      25. PageTest: Test 3D Chart - setting all required fields including caption and description (Customer Balance by City and Currency).
      26. PageTest: Verify multilanguage on memo field (Optional measure).
      27. PageTest: Verify multilanguage on caption field (Required measure).
      28. PageTest: Verify using all measures.
    }
    END.
  }
}

