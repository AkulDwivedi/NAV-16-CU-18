OBJECT Report 13721 Update Challan Details
{
  OBJECT-PROPERTIES
  {
    Date=24-04-16;
    Time=12:00:00 PM;
    Version List=NAVIN9.00.00.45778;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Update Challan Details;
               ENN=Update Challan Details];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  CompanyInformation@1500000 : Record 79;
                BEGIN
                  CompanyInformation.GET;
                  IF PayTDSDocNo = '' THEN
                    ERROR(Text16501);
                  IF ChallanNo = '' THEN
                    ERROR(Text16502);
                  IF ChallanDate = 0D THEN
                    ERROR(Text16503);
                  IF BankName = '' THEN
                    ERROR(Text16505);
                  IF BSRCode = '' THEN
                    IF CompanyInformation."Company Status" <> CompanyInformation."Company Status"::Government THEN
                      ERROR(Text16504);
                  IF STRLEN(BSRCode) < 7 THEN
                    IF CompanyInformation."Company Status" <> CompanyInformation."Company Status"::Government THEN
                      ERROR(Text16500);
                  IF MinorHeadCode = MinorHeadCode::" " THEN
                    ERROR(Text16510);
                  UpdateTDSRegister;
                END;

    UseRequestPage=Yes;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   MinorHeadCode := MinorHeadCode::"200";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ENN=Options] }

      { 1280001;2;Field     ;
                  CaptionML=[ENU=Pay TDS Document No.;
                             ENN=Pay TDS Document No.];
                  SourceExpr=PayTDSDocNo }

      { 1280002;2;Field     ;
                  CaptionML=[ENU=Challan No.;
                             ENN=Challan No.];
                  SourceExpr=ChallanNo }

      { 1280004;2;Field     ;
                  CaptionML=[ENU=Challan Date;
                             ENN=Challan Date];
                  SourceExpr=ChallanDate }

      { 1280007;2;Field     ;
                  CaptionML=[ENU=Bank Name;
                             ENN=Bank Name];
                  SourceExpr=BankName }

      { 1280009;2;Field     ;
                  CaptionML=[ENU=BSR Code;
                             ENN=BSR Code];
                  SourceExpr=BSRCode }

      { 1500000;2;Field     ;
                  CaptionML=[ENU=Cheque No.;
                             ENN=Cheque No.];
                  SourceExpr=CheckNo }

      { 1500002;2;Field     ;
                  CaptionML=[ENU=Cheque Date;
                             ENN=Cheque Date];
                  SourceExpr=CheckDate }

      { 1500013;2;Field     ;
                  CaptionML=[ENU=Minor Head Code;
                             ENN=Minor Head Code];
                  SourceExpr=MinorHeadCode }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TDSEntry@1280003 : Record 13729;
      FormManagement@1500009 : Codeunit 16400;
      ChallanNo@1500013 : Code[5];
      ChallanDate@1280017 : Date;
      BankName@1280018 : Text[100];
      PayTDSDocNo@1500014 : Code[20];
      Text002@1280001 : TextConst 'ENU=There are no records with this document no.;ENN=There are no records with this document no.';
      BSRCode@1280002 : Text[7];
      Text16500@1500000 : TextConst 'ENU=BSR code must have at least 7 digits.;ENN=BSR code must have at least 7 digits.';
      CheckNo@1500001 : Code[20];
      CheckDate@1500002 : Date;
      Text16501@1500003 : TextConst 'ENU=Enter Pay TDS Document No.;ENN=Enter Pay TDS Document No.';
      Text16502@1500004 : TextConst 'ENU=Enter Challan No.;ENN=Enter Challan No.';
      Text16503@1500005 : TextConst 'ENU=Enter Challan Date.;ENN=Enter Challan Date.';
      Text16504@1500006 : TextConst 'ENU=Enter BSR Code.;ENN=Enter BSR Code.';
      Text16505@1500007 : TextConst 'ENU=Enter Bank Name.;ENN=Enter Bank Name.';
      Text16506@1500008 : TextConst 'ENU=Challan No already filed.;ENN=Challan No already filed.';
      Text16507@1500010 : TextConst 'ENU=Cannot calculate a date filter because you have not set up quarterly income tax periods for the income tax accounting period that the specified TDS entry was posted in.;ENN=Cannot calculate a date filter because you have not set up quarterly income tax periods for the income tax accounting period that the specified TDS entry was posted in.';
      Text16508@1500011 : TextConst 'ENU=Cannot calculate a date filter because you have not set up an income tax accounting period for the posting date of the specified TDS entry.;ENN=Cannot calculate a date filter because you have not set up an income tax accounting period for the posting date of the specified TDS entry.';
      Text16509@1500012 : TextConst 'ENU=The Challan number that has been generated is longer than expected. Make sure that the No. Series for the TDS Nil Challan number in the General Ledger Setup window does not contain more than %1 characters.;ENN=The Challan number that has been generated is longer than expected. Make sure that the No. Series for the TDS Nil Challan number in the General Ledger Setup window does not contain more than %1 characters.';
      MinorHeadCode@1500015 : ' ,200,400';
      Text16510@1500016 : TextConst 'ENU=Minor Head Code must not be Blank.;ENN=Minor Head Code must not be Blank.';

    LOCAL PROCEDURE UpdateTDSRegister@1500007();
    BEGIN
      TDSEntry.RESET;
      TDSEntry.SETRANGE("Pay TDS Document No.",PayTDSDocNo);
      TDSEntry.SETRANGE("Challan No.",'');
      IF TDSEntry.FINDSET THEN BEGIN
        REPEAT
          TDSEntry."Challan No." := ChallanNo;
          TDSEntry."Challan Date" := ChallanDate;
          TDSEntry."Bank Name" := BankName;
          TDSEntry."BSR Code" := BSRCode;
          TDSEntry."Check/DD No." := CheckNo;
          TDSEntry."Check Date" := CheckDate;
          TDSEntry."Minor Head Code" := MinorHeadCode;
          TDSEntry.MODIFY;
          UpdateForm26Q27Q;
        UNTIL TDSEntry.NEXT = 0;
        UpdateChallanRegister;
        UpdateReportingEntries(TDSEntry,TRUE);
      END ELSE
        MESSAGE(Text002);
    END;

    PROCEDURE SetDocumentNo@1280000(DocumentNo@1280000 : Code[20]);
    BEGIN
      PayTDSDocNo := DocumentNo;
    END;

    PROCEDURE UpdateChallanRegister@1500000();
    VAR
      ChallanRegister@1500005 : Record 16504;
      TDSAccountingSetup@1500004 : Record 13724;
      GenJnlPosLine@1500003 : Codeunit 12;
      Form26Q27QEntry@1500002 : Record 16505;
      TDSEntry1@1500001 : Record 13729;
      CompanyInfo@1500000 : Record 79;
    BEGIN
      ChallanRegister.RESET;
      ChallanRegister.SETRANGE("Challan No.",ChallanNo);
      ChallanRegister.SETRANGE("Challan Date",ChallanDate);
      IF ChallanRegister.FINDFIRST THEN BEGIN
        IF ChallanRegister.Filed OR ChallanRegister.Revised THEN
          ERROR(Text16506);
        Form26Q27QEntry.SETRANGE("Challan No.",ChallanRegister."Challan No.");
        Form26Q27QEntry.SETRANGE("Challan Date",ChallanRegister."Challan Date");
        Form26Q27QEntry.SETRANGE("Pay TDS Document No.",PayTDSDocNo);
        IF Form26Q27QEntry.FINDFIRST THEN BEGIN
          Form26Q27QEntry.MODIFYALL("Challan-Detail Record No.",ChallanRegister."Challan-Detail Record No.");
          Form26Q27QEntry.MODIFYALL("Challan Register Entry No.",ChallanRegister."Entry No.");
        END;
        EXIT;
      END;

      CompanyInfo.GET;
      ChallanRegister.RESET;
      ChallanRegister.SETRANGE("Pay TDS Document No.",PayTDSDocNo);
      TDSEntry.SETCURRENTKEY("Pay TDS Document No.","Posting Date");
      TDSEntry.SETRANGE("Challan No.",ChallanNo);
      TDSEntry.FIND('+');
      ChallanRegister.SETRANGE("TDS Payment Date",TDSEntry."TDS Payment Date");
      IF ChallanRegister.FINDFIRST THEN BEGIN
        ChallanRegister."Last Bank Challan No." := ChallanRegister."Challan No.";
        ChallanRegister."Last Bank-Branch Code" := ChallanRegister."BSR Code";
        ChallanRegister."Last Date of Challan No." := ChallanRegister."Last Date of Challan No.";
        ChallanRegister."Challan No." := ChallanNo;
        ChallanRegister."BSR Code" := BSRCode;
        ChallanRegister."Challan Date" := ChallanDate;
        ChallanRegister."Bank Name" := BankName;
        ChallanRegister."Minor Head Code" := MinorHeadCode;
        IF CompanyInfo."Company Status" = CompanyInfo."Company Status"::Government THEN BEGIN
          ChallanRegister."Last Transfer Voucher No." := ChallanRegister."Transfer Voucher No.";
          ChallanRegister."Transfer Voucher No." := ChallanNo;
        END;
        IF ChallanRegister.Filed OR (ChallanRegister.Revised AND (NOT ChallanRegister."Correction-C3"))
        THEN BEGIN
          IF ChallanRegister.Filed THEN
            ChallanRegister."No. of Revision" := ChallanRegister."No. of Revision" + 1;
          ChallanRegister."Correction-C2" := TRUE;
          ChallanRegister.Filed := FALSE;
          ChallanRegister.Revised := TRUE;
        END;
        ChallanRegister.MODIFY;
      END ELSE BEGIN
        ChallanRegister.INIT;
        ChallanRegister."Entry No." := TDSAccountingSetup.GetLastEntryNo(DATABASE::"Challan Register") + 1;
        ChallanRegister."Challan No." := ChallanNo;
        ChallanRegister."Last Bank Challan No." := ChallanNo;
        ChallanRegister."BSR Code" := COPYSTR(BSRCode,1,7);
        ChallanRegister."Last Bank-Branch Code" := COPYSTR(BSRCode,1,7);
        ChallanRegister."Challan Date" := ChallanDate;
        ChallanRegister."Last Date of Challan No." := ChallanDate;
        ChallanRegister."Bank Name" := BankName;
        ChallanRegister."Pay TDS Document No." := PayTDSDocNo;
        ChallanRegister."Minor Head Code" := MinorHeadCode;
        ChallanRegister."TDS Payment Date" := ChallanDate;
        ChallanRegister."Non Resident Payment" := TDSEntry."Non Resident Payments";
        ChallanRegister."T.A.N. No." := TDSEntry."T.A.N. No.";
        ChallanRegister."TDS Section" := TDSEntry."TDS Section";
        ChallanRegister."Check / DD No." := TDSEntry."Check/DD No.";
        ChallanRegister."Check / DD Date" := TDSEntry."Check Date";
        ChallanRegister."TDS Group" := TDSEntry."TDS Group";
        ChallanRegister."Challan-Detail Record No." :=
          GenJnlPosLine.GetChallanDetailRecNo(
            FormManagement.GetQuarter(TDSEntry."Posting Date"),
            FormManagement.GetFinancialYear(TDSEntry."Posting Date"),TDSEntry."Non Resident Payments",TDSEntry."T.A.N. No.");
        IF CheckFiledEntries(
             FormManagement.GetQuarter(TDSEntry."Posting Date"),
             FormManagement.GetFinancialYear(TDSEntry."Posting Date"),TDSEntry."Non Resident Payments")
        THEN BEGIN
          ChallanRegister."Correction-C9" := TRUE;
          ChallanRegister.Revised := TRUE;
          ChallanRegister."No. of Revision" += 1;
        END;
        ChallanRegister."Financial Year" := FormManagement.GetFinancialYear(TDSEntry."Posting Date");
        ChallanRegister.Quarter := FormManagement.GetQuarter(TDSEntry."Posting Date");
        ChallanRegister."Assessment Year" := FormManagement.GetAssessmentYear(TDSEntry."Posting Date");
        IF CompanyInfo."Company Status" = CompanyInfo."Company Status"::Government THEN
          ChallanRegister."Transfer Voucher No." := ChallanNo;
        ChallanRegister."Last Tot Deposit Amt/ Challan" := ChallanRegister."Total TDS Including SHE Cess" +
          ChallanRegister."Oltas Interest";
        ChallanRegister."User ID" := USERID;
        ChallanRegister."Nil Challan Indicator" := TDSEntry."NIL Challan Indicator";
        ChallanRegister.INSERT;
        Form26Q27QEntry.SETRANGE("Challan No.",ChallanRegister."Challan No.");
        Form26Q27QEntry.SETRANGE("Challan Date",ChallanRegister."Challan Date");
        Form26Q27QEntry.SETRANGE("Pay TDS Document No.",PayTDSDocNo);
        IF Form26Q27QEntry.FINDFIRST THEN BEGIN
          Form26Q27QEntry.MODIFYALL("Challan-Detail Record No.",ChallanRegister."Challan-Detail Record No.");
          Form26Q27QEntry.MODIFYALL("Challan Register Entry No.",ChallanRegister."Entry No.");
        END;
        TDSEntry1.RESET;
        TDSEntry1.SETRANGE("Challan No.",ChallanRegister."Challan No.");
        TDSEntry1.MODIFYALL("Challan Register Entry No.",ChallanRegister."Entry No.");
      END;
    END;

    PROCEDURE CheckFiledEntries@1500001(Quarter@1500000 : Integer;FinancialYear@1500001 : Code[6];NRIPayment@1500002 : Boolean) : Boolean;
    VAR
      ChallanRegister@1500003 : Record 16504;
    BEGIN
      ChallanRegister.SETRANGE(Quarter,Quarter);
      ChallanRegister.SETRANGE("Financial Year",FinancialYear);
      ChallanRegister.SETRANGE("Non Resident Payment",NRIPayment);
      ChallanRegister.SETRANGE(Filed,TRUE);
      IF NOT ChallanRegister.FINDFIRST THEN BEGIN
        ChallanRegister.SETRANGE(Filed);
        ChallanRegister.SETRANGE(Revised,TRUE);
        EXIT(ChallanRegister.FINDFIRST);
      END;
      EXIT(TRUE);
    END;

    PROCEDURE UpdateForm26Q27Q@1500002();
    VAR
      Form26Q27QEntry@1500000 : Record 16505;
    BEGIN
      Form26Q27QEntry.RESET;
      Form26Q27QEntry.SETRANGE("TDS Entry No.",TDSEntry."Entry No.");
      IF Form26Q27QEntry.FINDFIRST THEN BEGIN
        Form26Q27QEntry."Challan No." := TDSEntry."Challan No.";
        Form26Q27QEntry."BSR Code" := TDSEntry."BSR Code";
        Form26Q27QEntry."Challan Date" := TDSEntry."Challan Date";
        Form26Q27QEntry."Bank Name" := TDSEntry."Bank Name";
        Form26Q27QEntry."Check / DD No." := TDSEntry."Check/DD No.";
        Form26Q27QEntry."Check / DD Date" := TDSEntry."Check Date";
        Form26Q27QEntry."TDS Payment Date" := ChallanDate;
        Form26Q27QEntry."Minor Head Code" := TDSEntry."Minor Head Code";
        IF Form26Q27QEntry."Pay TDS Document No." = '' THEN BEGIN
          Form26Q27QEntry."Pay TDS Document No." := PayTDSDocNo;
          Form26Q27QEntry."TDS Paid" := TRUE;
        END;
        Form26Q27QEntry.MODIFY(TRUE);
      END;
    END;

    PROCEDURE UpdateReportingEntries@1500003(TDSEntry@1500003 : Record 13729;SetChallanFilter@1500004 : Boolean);
    VAR
      DateFilterCalc@1500002 : Codeunit 358;
      TDSEntry2@1500006 : Record 13729;
      AccountingPeriodFilter@1500001 : Text[30];
    BEGIN
      WITH TDSEntry DO BEGIN
        RESET;
        IF SetChallanFilter THEN
          SETRANGE("Challan No.",ChallanNo);
        IF FINDSET THEN
          REPEAT
            DateFilterCalc.CreateTDSAccountingDateFilter(AccountingPeriodFilter,FALSE,"Posting Date",0);
            IF ("TDS Base Amount" <> "TDS Line Amount") AND NOT "NIL Challan Indicator" THEN BEGIN
              TDSEntry2.SETCURRENTKEY("Party Type","Party Code","Posting Date","TDS Nature of Deduction");
              TDSEntry2.SETRANGE("Party Type","Party Type");
              TDSEntry2.SETRANGE("Party Code","Party Code");
              TDSEntry2.SETFILTER("Posting Date",AccountingPeriodFilter);
              TDSEntry2.SETRANGE("TDS Nature of Deduction","TDS Nature of Deduction");
              TDSEntry2.SETFILTER("Entry No.",'<%1',"Entry No.");
              TDSEntry2.SETRANGE("Total TDS Including SHE CESS",0);
              IF TDSEntry2.FINDSET THEN
                REPEAT
                  UpdateForm26Q27QEntries(
                    TDSEntry2."Entry No.","Posting Date",
                    "Challan Register Entry No.",GetChallanDetailRecordNo("Entry No."));
                UNTIL TDSEntry2.NEXT = 0;
            END;
            UpdateForm26Q27QEntries(
              "Entry No.","Posting Date",
              "Challan Register Entry No.",GetChallanDetailRecordNo("Entry No."));
          UNTIL NEXT = 0;
      END;
    END;

    PROCEDURE UpdateForm26Q27QEntries@1500004(TDSEntryNo@1500000 : Integer;ReportingDate@1500003 : Date;ReportingEntryNo@1500004 : Integer;ChallanDetailRecordNo@1500005 : Integer);
    VAR
      GenJnlPostLine@1500001 : Codeunit 12;
      Form26Q27QEntry@1500002 : Record 16505;
    BEGIN
      WITH Form26Q27QEntry DO BEGIN
        SETRANGE("TDS Entry No.",TDSEntryNo);
        LOCKTABLE;
        IF FINDFIRST THEN BEGIN
          "Reporting Quarter" := FormManagement.GetQuarter(ReportingDate);
          "Reporting Challan Entry No." := ReportingEntryNo;
          "Challan-Detail Record No." := ChallanDetailRecordNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE GetChallanDetailRecordNo@1500005(TDSEntryNo@1500000 : Integer) : Integer;
    VAR
      Form26Q27QEntry@1500001 : Record 16505;
    BEGIN
      WITH Form26Q27QEntry DO BEGIN
        SETRANGE("TDS Entry No.",TDSEntryNo);
        IF FINDFIRST THEN
          EXIT("Challan-Detail Record No.");
      END;
    END;

    PROCEDURE UpdateNilChallanEntries@1500008(TANNo@1500006 : Code[10];Quarter@1500008 : Option;FinancialYear@1500007 : Code[6];Resident@1500000 : Boolean);
    VAR
      TDSEntry@1500002 : Record 13729;
      TDSEntryLoc@1500005 : Record 13729;
      GeneralLedgerSetup@1500001 : Record 98;
      NoSeriesManagement@1500004 : Codeunit 396;
    BEGIN
      GeneralLedgerSetup.GET;
      GeneralLedgerSetup.TESTFIELD("TDS Nil Challan Nos.");
      GeneralLedgerSetup.TESTFIELD("Nil Pay TDS Document Nos.");
      TDSEntry.SETCURRENTKEY("TDS Section");
      TDSEntry.SETFILTER("Posting Date",GenerateDateFilterFromQuarter(Quarter,FinancialYear));
      ChallanDate := TDSEntry.GETRANGEMAX(TDSEntry."Posting Date");
      TDSEntry.SETRANGE("Non Resident Payments",NOT Resident);
      TDSEntry.SETRANGE("T.A.N. No.",TANNo);
      TDSEntry.SETRANGE("TDS Paid",FALSE);
      TDSEntry.SETRANGE("NIL Challan Indicator",TRUE);
      TDSEntry.SETRANGE(Reversed,FALSE);

      IF TDSEntry.FINDSET(TRUE,FALSE) THEN
        REPEAT
          IF IsEndOfTDSGroup(TDSEntry) THEN BEGIN
            PayTDSDocNo := NoSeriesManagement.GetNextNo(GeneralLedgerSetup."Nil Pay TDS Document Nos.",WORKDATE,TRUE);
            ChallanNo := GetNewNilChallanNo(GeneralLedgerSetup."TDS Nil Challan Nos.");
            TDSEntryLoc.COPY(TDSEntry);
            TDSEntryLoc.SETRANGE("TDS Section",TDSEntry."TDS Section");
            TDSEntryLoc.MODIFYALL("Pay TDS Document No.",PayTDSDocNo,TRUE);
            TDSEntryLoc.MODIFYALL("TDS Paid",TRUE,TRUE);
            UpdateTDSRegister;
          END;
        UNTIL TDSEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE IsEndOfTDSGroup@1500009(VAR TDSEntry@1500001 : Record 13729) : Boolean;
    VAR
      TDSEntryCopy@1500000 : Record 13729;
    BEGIN
      TDSEntryCopy.COPY(TDSEntry);
      IF TDSEntryCopy.NEXT = 0 THEN
        EXIT(TRUE);
      EXIT(TDSEntryCopy."TDS Section" <> TDSEntry."TDS Section");
    END;

    LOCAL PROCEDURE GenerateDateFilterFromQuarter@1500006(Quarter@1500001 : Option;FinancialYear@1500000 : Code[6]) : Text;
    VAR
      IncomeTaxAccountingPeriod@1500003 : Record 13724;
      IncomeTaxPeriodSetup@1500004 : Record 16501;
    BEGIN
      IncomeTaxAccountingPeriod.SETRANGE("Financial Year for eTDS/TCS",FinancialYear);
      IF IncomeTaxAccountingPeriod.FINDFIRST THEN BEGIN
        IF NOT IncomeTaxPeriodSetup.GET(IncomeTaxAccountingPeriod."No.",Quarter) THEN
          ERROR(Text16507);
        EXIT(STRSUBSTNO('%1..%2',IncomeTaxPeriodSetup."Starting Date",IncomeTaxPeriodSetup."Ending Date"));
      END ELSE
        ERROR(Text16508);
    END;

    PROCEDURE InitializeRequest@1500010(NewPayTDSDocumentNo@1500000 : Code[20];NewChallanNo@1500001 : Code[5];NewChallanDate@1500002 : Date;NewBankName@1500003 : Text;NewBSRCode@1500004 : Text;NewChequeNo@1500005 : Code[20];NewChequeDate@1500006 : Date;NewMinorHeadCode@1500013 : Option);
    BEGIN
      PayTDSDocNo := NewPayTDSDocumentNo;
      ChallanNo := NewChallanNo;
      ChallanDate := NewChallanDate;
      BankName := NewBankName;
      BSRCode := NewBSRCode;
      CheckNo := NewChequeNo;
      CheckDate := NewChequeDate;
      MinorHeadCode := NewMinorHeadCode;
    END;

    LOCAL PROCEDURE GetNewNilChallanNo@1500020(NoSeries@1500001 : Code[10]) : Code[5];
    VAR
      ChallanRegister@1500007 : Record 16504;
      NoSeriesManagement@1500002 : Codeunit 396;
      CandidateChallanNo@1500000 : Code[20];
    BEGIN
      CandidateChallanNo := NoSeriesManagement.GetNextNo(NoSeries,WORKDATE,TRUE);
      IF STRLEN(CandidateChallanNo) > MAXSTRLEN(ChallanRegister."Challan No.") THEN
        ERROR(STRSUBSTNO(Text16509,MAXSTRLEN(ChallanRegister."Challan No.")));
      EXIT(CandidateChallanNo);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

