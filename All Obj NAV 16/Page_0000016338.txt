OBJECT Page 16338 Adjust Service Tax Credit
{
  OBJECT-PROPERTIES
  {
    Date=07-09-12;
    Time=12:00:00 PM;
    Version List=NAVIN7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Adjust Service Tax Credit;
               ENN=Adjust Service Tax Credit];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table16474;
    PageType=List;
    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1500019 ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unction;
                                 ENN=F&unction];
                      Image=Action }
      { 1500020 ;2   ;Action    ;
                      CaptionML=[ENU=Adjust Credit;
                                 ENN=Adjust Credit];
                      Image=EditAdjustments;
                      OnAction=BEGIN
                                 CLEARALL;
                                 TempSvcTaxEntryDetail.DELETEALL;
                                 TempSvcTaxEntryDetail2.DELETEALL;
                                 GetSelectedEntries;
                                 CheckAdjustmentEntries;
                                 AdjustEntries;
                                 MESSAGE(Text16501);
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1500000;1;Group     ;
                GroupType=Repeater }

    { 1500001;2;Field     ;
                SourceExpr="Entry No." }

    { 1500026;2;Field     ;
                SourceExpr="Service Tax Entry No." }

    { 1500003;2;Field     ;
                SourceExpr=Type }

    { 1500028;2;Field     ;
                SourceExpr="Document Type" }

    { 1500005;2;Field     ;
                SourceExpr="Document No." }

    { 1500022;2;Field     ;
                SourceExpr="Service Tax Registration No." }

    { 1500024;2;Field     ;
                SourceExpr="Service Tax Group Code" }

    { 1500007;2;Field     ;
                SourceExpr="Posting Date" }

    { 1500009;2;Field     ;
                SourceExpr="GL Account" }

    { 1500013;2;Field     ;
                SourceExpr="Remaining Credit" }

    { 1500015;2;Field     ;
                SourceExpr="Remaining Credit (eCess)" }

    { 1500017;2;Field     ;
                SourceExpr="Remaining Credit (SHECess)" }

  }
  CODE
  {
    VAR
      SvcTaxEntryDetail@1500000 : Record 16474;
      TempSvcTaxEntryDetail@1500001 : TEMPORARY Record 16474;
      Text16500@1500003 : TextConst 'ENU=Please select combination of document type Invoice/Payment and Credit Memo/Refund.;ENN=Please select combination of document type Invoice/Payment and Credit Memo/Refund.';
      TempSvcTaxEntryDetail2@1500004 : TEMPORARY Record 16474;
      Text16501@1500002 : TextConst 'ENU=Service Tax Credit adjusted successfully.;ENN=Service Tax Credit adjusted successfully.';

    PROCEDURE GetSelectedEntries@1500000();
    BEGIN
      CurrPage.SETSELECTIONFILTER(SvcTaxEntryDetail);
      SvcTaxEntryDetail.MARKEDONLY(TRUE);
      IF SvcTaxEntryDetail.FIND('-') THEN
        REPEAT
          IF (SvcTaxEntryDetail."Remaining Credit" > 0) OR (SvcTaxEntryDetail."Remaining Credit (eCess)" > 0) OR
             (SvcTaxEntryDetail."Remaining Credit (SHECess)" > 0)
          THEN BEGIN
            TempSvcTaxEntryDetail.TRANSFERFIELDS(SvcTaxEntryDetail);
            TempSvcTaxEntryDetail.INSERT;
          END ELSE BEGIN
            TempSvcTaxEntryDetail2.TRANSFERFIELDS(SvcTaxEntryDetail);
            TempSvcTaxEntryDetail2.INSERT;
          END;
        UNTIL SvcTaxEntryDetail.NEXT = 0;
    END;

    PROCEDURE CheckAdjustmentEntries@1500002();
    BEGIN
      IF NOT TempSvcTaxEntryDetail.FINDFIRST THEN
        ERROR(Text16500);
      IF NOT TempSvcTaxEntryDetail2.FINDFIRST THEN
        ERROR(Text16500);
    END;

    PROCEDURE AdjustEntries@1500010();
    VAR
      ServiceTaxEntryDetail@1500000 : Record 16474;
      ServiceTaxEntryDetail2@1500001 : Record 16474;
      AppliedAmount@1500002 : Decimal;
    BEGIN
      IF TempSvcTaxEntryDetail.FINDSET THEN
        REPEAT
          IF TempSvcTaxEntryDetail2.FINDSET THEN
            REPEAT
              CompareAmt(TempSvcTaxEntryDetail."Remaining Credit",TempSvcTaxEntryDetail2."Remaining Credit",AppliedAmount);
              TempSvcTaxEntryDetail."Applied S.T. Credit" += AppliedAmount;
              CompareAmt(TempSvcTaxEntryDetail."Remaining Credit (eCess)",TempSvcTaxEntryDetail2."Remaining Credit (eCess)",
                AppliedAmount);
              TempSvcTaxEntryDetail."Applied S.T. eCess Credit" += AppliedAmount;
              CompareAmt(TempSvcTaxEntryDetail."Remaining Credit (SHECess)",TempSvcTaxEntryDetail2."Remaining Credit (SHECess)",
                AppliedAmount);
              TempSvcTaxEntryDetail."Applied S.T. SHECess Credit" += AppliedAmount;
              CompareAmt(TempSvcTaxEntryDetail."Remaining S.T. Base Amount",TempSvcTaxEntryDetail2."Remaining S.T. Base Amount",
                AppliedAmount);
              TempSvcTaxEntryDetail."Applied S.T. Base Amount" += AppliedAmount;
              TempSvcTaxEntryDetail2.Closed := TempSvcTaxEntryDetail2."Remaining Credit" = 0;
              TempSvcTaxEntryDetail2."Closed (eCess)" := TempSvcTaxEntryDetail2."Remaining Credit (eCess)" = 0;
              TempSvcTaxEntryDetail2."Closed (SHECess)" := TempSvcTaxEntryDetail2."Remaining Credit (SHECess)" = 0;
              TempSvcTaxEntryDetail2.MODIFY;
            UNTIL TempSvcTaxEntryDetail2.NEXT = 0;
          TempSvcTaxEntryDetail.Closed := TempSvcTaxEntryDetail."Remaining Credit" = 0;
          TempSvcTaxEntryDetail."Closed (eCess)" := TempSvcTaxEntryDetail."Remaining Credit (eCess)" = 0;
          TempSvcTaxEntryDetail."Closed (SHECess)" := TempSvcTaxEntryDetail."Remaining Credit (SHECess)" = 0;
          TempSvcTaxEntryDetail.MODIFY;
        UNTIL TempSvcTaxEntryDetail.NEXT = 0;
      IF TempSvcTaxEntryDetail.FINDSET THEN
        REPEAT
          ServiceTaxEntryDetail := TempSvcTaxEntryDetail;
          ServiceTaxEntryDetail.MODIFY;
        UNTIL TempSvcTaxEntryDetail.NEXT = 0;
      IF TempSvcTaxEntryDetail2.FINDSET THEN
        REPEAT
          ServiceTaxEntryDetail2 := TempSvcTaxEntryDetail2;
          ServiceTaxEntryDetail2.MODIFY;
        UNTIL TempSvcTaxEntryDetail2.NEXT = 0;
    END;

    PROCEDURE CompareAmt@1500016(VAR CompareAmtWith@1500000 : Decimal;VAR CompareAmtTo@1500001 : Decimal;VAR AppliedAmount@1500002 : Decimal);
    BEGIN
      IF (CompareAmtWith = 0) OR (CompareAmtTo = 0) THEN
        EXIT;
      IF CompareAmtWith = ABS(CompareAmtTo) THEN BEGIN
        AppliedAmount := CompareAmtWith;
        CompareAmtWith := 0;
        CompareAmtTo := 0;
      END;
      IF CompareAmtWith > ABS(CompareAmtTo) THEN BEGIN
        AppliedAmount := ABS(CompareAmtTo);
        CompareAmtWith -= ABS(CompareAmtTo);
        CompareAmtTo := 0;
      END;
      IF CompareAmtWith < ABS(CompareAmtTo) THEN BEGIN
        AppliedAmount := CompareAmtWith;
        CompareAmtTo += CompareAmtWith;
        CompareAmtWith := 0;
      END;
    END;

    BEGIN
    END.
  }
}

